SUBROUTINE READ_CLIM(KR,IM,JM,KM,TEMP,SAL)

!.. READ STATIC BACKGROUND-ERROR VALUES.
!..
!.. ANDREA STORTO *INGV* 2009-06-11

USE NETCDF
USE SET_KND
USE GRD_STR
USE IOUNITS

IMPLICIT NONE

INTEGER         ,INTENT(IN) :: KR,IM,JM,KM
REAL(R8) , INTENT(OUT) :: TEMP(IM,JM,KM),SAL(IM,JM,KM)
REAL(R8) :: ZT1(IM,JM,KM), ZT2(IM,JM,KM), ZR
INTEGER(I4) :: STAT,NCID,IDVAR,IT1, IT2, MM, DD, DDE(12)
CHARACTER(LEN=99) :: CFILE

MM=(KR/100)
DD= KR-MM*100

DDE=(/31,28,31,30,31,30,31,31,30,31,30,31/)

IT1=MM
IF( REAL(DD) .GE. REAL(DDE(IT1))/2._R8) THEN
    IT2=IT1+1
    IF(IT2==13) IT2=1
ELSE
    IT2=IT1-1
    IF(IT2==0) IT2=12
ENDIF

ZR=ABS( REAL(DD) - REAL(DDE(IT1))/2._R8) / &
   ABS( REAL(DDE(MM))/2._R8 + REAL(DDE(IT2))/2._R8)

WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) ' READ_CLIM  - DATE (MMDD,MM,DD):',KR,MM,DD
WRITE(IOUNLOG,*) ' RECORDS AND ASSOCIATED COEFFICIENTS:'
WRITE(IOUNLOG,*) ' RECORD1 - ',IT1,1._R8-ZR
WRITE(IOUNLOG,*) ' RECORD2 - ',IT2,ZR
WRITE(IOUNLOG,*) 

CFILE='tem_clim.nc'
STAT=NF90_OPEN(CFILE,NF90_NOWRITE, NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (NCID, 'votemper', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZT1,&
& START=(/GRD%I0,GRD%J0,1,IT1/),COUNT=(/GRD%IM,GRD%JM,GRD%KM,1/)  )
STAT = NF90_GET_VAR (NCID,IDVAR,ZT2,&
& START=(/GRD%I0,GRD%J0,1,IT2/),COUNT=(/GRD%IM,GRD%JM,GRD%KM,1/)  )
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

TEMP = (1._R8-ZR)*ZT1 + ZR*ZT2

STAT=NF90_CLOSE(NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

CFILE='sal_clim.nc'
STAT=NF90_OPEN(CFILE,NF90_NOWRITE, NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (NCID, 'vosaline', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (NCID,IDVAR,ZT1 ,&
& START=(/GRD%I0,GRD%J0,1,IT1/),COUNT=(/GRD%IM,GRD%JM,GRD%KM,1/)  )
STAT = NF90_GET_VAR (NCID,IDVAR,ZT2 ,&
& START=(/GRD%I0,GRD%J0,1,IT2/),COUNT=(/GRD%IM,GRD%JM,GRD%KM,1/)  )
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

SAL = (1._R8-ZR)*ZT1 + ZR*ZT2

STAT=NF90_CLOSE(NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

END SUBROUTINE READ_CLIM
