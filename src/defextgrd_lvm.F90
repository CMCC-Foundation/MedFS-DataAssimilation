SUBROUTINE DEFEXTGRD_LVM
USE RECFILTER

!-- DEFINE EXTENDED GRID

USE GRD_STR
USE MYFRTPROF

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY,IERR
INTEGER(KIND=I4) :: I,J,KK,JLEV

CALL MYFRTPROF_WALL('DEFEXTGRD_LVM: DEFINE EXTENDED GRID',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// DEFINE EXTENDED GRID'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF( .NOT.ALLOCATED(GRD%DX).OR..NOT.ALLOCATED(GRD%DY).OR.&
  & .NOT.ALLOCATED(GRD%MSR)) THEN
   WRITE(IOUNERR,*) 'DEFEXTGRD CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN DEFEXTGRD, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('DEFEXTGRD CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM

ALLOCATE(ISTPNSZ(NX,NY),JSTPNSZ(NX,NY),STAT=IERR)
IF(IERR.NE.0) THEN
   WRITE(IOUNERR,*) 'CANNOT ALLOCATE ISTPNZ,JSTPNZ'
   CALL ABOR1('DEFEXTGRD: ALLOCATION PROBLEMS')
ENDIF
ALLOCATE(INXZ(NX,NY),JNXZ(NX,NY),STAT=IERR)
IF(IERR.NE.0) THEN
   WRITE(IOUNERR,*) 'CANNOT ALLOCATE INXZ,JNXZ'
   CALL ABOR1('DEFEXTGRD: ALLOCATION PROBLEMS')
ENDIF

WRITE(IOUNLOG,*) 'VECTORS HAVE BEEN ALLOCATED' ; CALL FLUSH(IOUNLOG)

!... MOVED TO RDGRDS

   ISTPNSZ(:,:) = NINT(CRMX(:,:)*RF_EFC / NEWDX(:,:) )+1
   JSTPNSZ(:,:) = NINT(CRMY(:,:)*RF_EFC / NEWDY(:,:) )+1

IMAXZ = 0
JMAXZ = 0

   IMXZ = 0
   DO J=1,NY
      KK=ISTPNSZ(1,J)
      IF( LLMSRM(1,J) ) KK=KK+1
      INXZ(1,J) = KK
      DO I=2,NX
         IF(.NOT.LLMSRM(I,J).AND.LLMSRM(I-1,J)) THEN
            KK=KK+ISTPNSZ(I,J)
         ELSEIF(LLMSRM(I,J).AND..NOT.LLMSRM(I-1,J))THEN
            KK=KK+ISTPNSZ(I,J)+1
         ELSEIF(LLMSRM(I,J)) THEN
            KK=KK+1
         ENDIF
         INXZ(I,J) = KK
      ENDDO
      IMXZ = MAX( IMXZ, KK+ISTPNSZ(NX,J))
   ENDDO
   IMAXZ  = MAX(IMAXZ,IMXZ)

   JMXZ = 0
   DO I=1,NX
      KK=JSTPNSZ(I,1)
      IF( LLMSRM(I,1) ) KK=KK+1
      JNXZ(I,1) = KK
      DO J=2,NY
         IF(.NOT.LLMSRM(I,J).AND.LLMSRM(I,J-1)) THEN
            KK=KK+JSTPNSZ(I,J)
         ELSEIF(LLMSRM(I,J).AND..NOT.LLMSRM(I,J-1))THEN
            KK=KK+JSTPNSZ(I,J)+1
         ELSEIF(LLMSRM(I,J)) THEN
            KK=KK+1
         ENDIF
         JNXZ(I,J) = KK
      ENDDO
      JMXZ = MAX( JMXZ, KK+JSTPNSZ(I,NY))
   ENDDO
   JMAXZ = MAX(JMAXZ,JMXZ)

WRITE(IOUNLOG,*) 'IMAXZ,JMAXZ = ',IMAXZ,JMAXZ

CALL MYFRTPROF_WALL('DEFEXTGRD_LVM: DEFINE EXTENDED GRID',1)

END SUBROUTINE DEFEXTGRD_LVM
