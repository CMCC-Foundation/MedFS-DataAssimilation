SUBROUTINE VER_HOR

!-----------------------------------------------------------------------
!                                                                      !
! APPLY HORIZONTAL FILTER                                              !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
! VERSION 2: S.DOBRICIC 2007                                           !
!     SYMMETRIC CALCULATION IN PRESENCE OF COASTAL BOUNDARIES          !
!     ETA_AD, TEM_AD, AND SAL_AD ARE HERE TEMPORARY ARRAYS             !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE EOF_STR
 USE RECFILTER, ONLY : SCXNS,SCYNS,FCT,LLTSAPART,SCXNZ,SCYNZ, NTOTV,LL_NOSRF_RF
 USE DRV_STR
 USE CTL_STR
 USE IOUNITS
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE RUN, ONLY : LLVARMDT
 USE RECFILTER, ONLY : LLRFSR
 USE DEBUGTMP
 USE LBCLNK
 USE MPIREL
 USE MYNETCDF
 USE BAL_AIRSEA
 USE EOFS3D
 USE HYBRID

 IMPLICIT NONE

 REAL(R8), DIMENSION (GRD%IM,GRD%JM)  :: UD, VD
 INTEGER(I4)    :: I,J,K,K1,NTOTV2
 CHARACTER(LEN=20) :: CFILE
 REAL(R8), ALLOCATABLE :: PSV_ADH(:,:,:)

CALL MYFRTPROF_WALL('VER_HOR: CONTROL TO PHYSICAL SPACE',0)

 IF( LL_NOSRF_RF ) THEN
    NTOTV2 = 2*GRD%KM*COUNT( (/LL_TS,LL_UV/) )
 ELSE
    NTOTV2 = NTOTV
 ENDIF

! ---
! VERTICAL EOFS
    DT_CSTRIN='START VER_HOR' ; IF( LL_DEBUGTMP ) CALL CALLDT
    CALL VEOF
    DT_CSTRIN='AFTER VEOF'    ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! LOAD TEMPORARY ARRAYS
    IF(DRV%MASK(DRV%KTR) .GT. 1) THEN
       PSV_AD = PSV
       IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT
       IF(LL_DIAH) THEN
          ALLOCATE(PSV_ADH(GRD%IM,GRD%JM,NTOTV))
          PSV_ADH = PSV_DH
       ENDIF
    ENDIF

! ---
! X DIRECTION

    IF(LLVARMDT) CALL RCFL_XZ( GRD%IM,GRD%JM,GRD%CMDT)

    CALL RCFL_X(GRD%IM,GRD%JM,NTOTV2,PSV)
    IF( LL_LBCLNK ) CALL LBC_LNK( PSV )

    IF( LL_DIAH ) CALL RCFL_X(GRD%IM,GRD%JM,NTOTV2,PSV_DH)

    DT_CSTRIN='AFTER RCFL_X'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT = GRD%CMDT * SCXNZ

    PSV=PSV*SCXNS
    DT_CSTRIN='AFTER SCAL X'  ; IF( LL_DEBUGTMP ) CALL CALLDT

    IF( LL_DIAH ) PSV_DH=PSV_DH*SCXNS 

! ---
! Y DIRECTION
    IF(LLVARMDT) CALL RCFL_YZ(GRD%IM,GRD%JM,GRD%CMDT)

    CALL RCFL_Y(GRD%IM,GRD%JM,NTOTV2,PSV)
    DT_CSTRIN='AFTER RCFL_Y'  ; IF( LL_DEBUGTMP ) CALL CALLDT

    IF( LL_DIAH ) CALL RCFL_Y(GRD%IM,GRD%JM,NTOTV2,PSV_DH)
! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT = GRD%CMDT * SCYNZ

    PSV= PSV*SCYNS
    DT_CSTRIN='AFTER SCAL Y'  ; IF( LL_DEBUGTMP ) CALL CALLDT

    IF( LL_DIAH ) PSV_DH=PSV_DH*SCYNS 

! ---
! TRANSPOSE CALCULATION IN THE PRESENSE OF COASTAL BOUNDARIES
 IF(DRV%MASK(DRV%KTR) .GT. 1) THEN

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT_AD * SCYNZ

    PSV_AD = PSV_AD*SCYNS

    IF( LL_DIAH ) PSV_ADH=PSV_ADH*SCYNS 

    DT_CSTRIN='AFTER SCAL Y AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! Y DIRECTION
    IF(LLVARMDT) CALL RCFL_YZ_AD( GRD%IM, GRD%JM,GRD%CMDT_AD)

    CALL RCFL_Y_AD(GRD%IM,GRD%JM,NTOTV2,PSV_AD)
    DT_CSTRIN='AFTER RCFL_Y_AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT
    IF( LL_DIAH ) CALL RCFL_Y_AD(GRD%IM,GRD%JM,NTOTV2,PSV_ADH)

! ---
! SCALE BY THE SCALING FACTOR
    IF(LLVARMDT) GRD%CMDT_AD = GRD%CMDT_AD * SCXNZ

    PSV_AD=PSV_AD*SCXNS
    IF( LL_DIAH ) PSV_ADH=PSV_ADH*SCXNS

    DT_CSTRIN='AFTER SCAL X AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! X DIRECTION
    IF(LLVARMDT) CALL RCFL_XZ_AD(GRD%IM,GRD%JM,GRD%CMDT_AD)

    IF( LL_LBCLNK ) CALL LBC_LNK( PSV_AD )
    CALL RCFL_X_AD(GRD%IM,GRD%JM,NTOTV2,PSV_AD)

    IF( LL_DIAH ) CALL RCFL_X_AD(GRD%IM,GRD%JM,NTOTV2,PSV_ADH)

    DT_CSTRIN='AFTER RCFL_X_AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! AVERAGE
    PSV = ( PSV + PSV_AD ) * 0.5_R8
    IF(LLVARMDT) GRD%CMDT = (GRD%CMDT + GRD%CMDT_AD ) * 0.5_R8

    IF( LL_DIAH ) THEN 
       PSV_DH = ( PSV_DH + PSV_ADH ) * 0.5_R8
       DEALLOCATE ( PSV_ADH )
    ENDIF

 ENDIF

  DT_CSTRIN='AFTER AVERAGING'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE FOR BOUNDARIES
    PSV = PSV * FCT

    IF( LL_DIAH ) THEN
        PSV_DH = PSV_DH * FCT
        CALL WRITE_VARNCDF('ANINCR_HYBRDIA.NC',GRD%IM,GRD%JM,NTOTV,PSV_DH,'psv_hybr','mixed','Hybrid Analysis Increments')
        DEALLOCATE(PSV_DH)
        LL_DIAH = .FALSE.
    ENDIF

    IF(LLVARMDT)  GRD%CMDT = GRD%CMDT  * FCT(:,:,1)

    DT_CSTRIN='AFTER RESCALING'  ; IF( LL_DEBUGTMP ) CALL CALLDT

    IF(LL_HYBRID_CR) THEN
        PSV = ZHYB_A*PSV + ZHYB_B*PSVF
    ENDIF

    IF ( LL_EOFS3D ) CALL EOF3D

! ---
! SIMPLIFIED BALANCE
    IF(DRV%BAL(DRV%KTR) .EQ. 1) &
       CALL BALANCE

! ---
! SIMPLIFIED BALANCE AT 2M
    IF(DRV%BA2(DRV%KTR) .EQ. 1) &
       CALL BAL2M_TL

! ---
! BOUYANCY FORCE
    IF(DRV%BMD(DRV%KTR) .EQ. 1) &
       CALL GET_BYG

! ---
! BAROTROPIC MODEL
    IF(DRV%BMD(DRV%KTR) .EQ. 1) &
       CALL BAR_MOD

! ---
! VELOCITY OPERATOR
    IF(LL_UV .AND. (DRV%BMD(DRV%KTR)+DRV%BAL(DRV%KTR)) .GT. 0) &
       CALL GET_VEL

! ---
! DIVERGENCE DAMPING
    IF(DRV%DDA(DRV%KTR) .EQ. 1 .AND. LL_UV) &
       CALL DIV_DMP

CALL FLUSH(IOUNLOG)

CALL MYFRTPROF_WALL('VER_HOR: CONTROL TO PHYSICAL SPACE',1)
END SUBROUTINE VER_HOR
