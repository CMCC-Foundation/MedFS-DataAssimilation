SUBROUTINE VER_HORF

!-----------------------------------------------------------------------
!                                                                      !
! APPLY HORIZONTAL FILTER                                              !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
! VERSION 2: S.DOBRICIC 2007                                           !
!     SYMMETRIC CALCULATION IN PRESENCE OF COASTAL BOUNDARIES          !
!     ETA_AD, TEM_AD, AND SAL_AD ARE HERE TEMPORARY ARRAYS             !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE EOF_STR
 USE RECFILTER
 USE DRV_STR
 USE CTL_STR
 USE IOUNITS
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE DEBUGTMP
 USE LBCLNK
 USE MPIREL
 USE MYNETCDF

 IMPLICIT NONE

 REAL(R8), DIMENSION (GRD%IM,GRD%JM)  :: UD, VD
 INTEGER(I4)    :: I,J,K,K1,NTOTV2
 CHARACTER(LEN=20) :: CFILE

CALL MYFRTPROF_WALL('VER_HORF: CONTROL TO PHYSICAL SPACE',0)

 IF( LL_NOSRF_RF ) THEN
    NTOTV2 = 2*GRD%KM*COUNT( (/LL_TS,LL_UV/) )
 ELSE
    NTOTV2 = NTOTV
 ENDIF

! ---
! VERTICAL EOFS
    DT_CSTRIN='START VER_HOR' ; IF( LL_DEBUGTMP ) CALL CALLDT
    CALL VEOFF
    DT_CSTRIN='AFTER VEOF'    ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! LOAD TEMPORARY ARRAYS
    IF(DRV%MASK(DRV%KTR) .GT. 1) THEN
       PSVF_AD = PSVF
    ENDIF

! ---
! X DIRECTION
    CALL RCFLF_X(GRD%IM,GRD%JM,NTOTV2,PSVF)
    IF( LL_LBCLNK ) CALL LBC_LNK( PSVF )

    DT_CSTRIN='AFTER RCFL_X'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    PSVF=PSVF*SCXNSF
    DT_CSTRIN='AFTER SCAL X'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! Y DIRECTION
    CALL RCFLF_Y(GRD%IM,GRD%JM,NTOTV2,PSVF)
    DT_CSTRIN='AFTER RCFL_Y'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    PSVF= PSVF*SCYNSF
    DT_CSTRIN='AFTER SCAL Y'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! TRANSPOSE CALCULATION IN THE PRESENSE OF COASTAL BOUNDARIES
 IF(DRV%MASK(DRV%KTR) .GT. 1) THEN

! ---
! SCALE BY THE SCALING FACTOR
    PSVF_AD = PSVF_AD*SCYNSF
    DT_CSTRIN='AFTER SCAL Y AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! Y DIRECTION
    CALL RCFLF_Y_AD(GRD%IM,GRD%JM,NTOTV2,PSVF_AD)
    DT_CSTRIN='AFTER RCFL_Y_AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE BY THE SCALING FACTOR
    PSVF_AD=PSVF_AD*SCXNSF
    DT_CSTRIN='AFTER SCAL X AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! X DIRECTION
    IF( LL_LBCLNK ) CALL LBC_LNK( PSVF_AD )
    CALL RCFLF_X_AD(GRD%IM,GRD%JM,NTOTV2,PSVF_AD)

    DT_CSTRIN='AFTER RCFL_X_AD'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! AVERAGE
    PSVF = ( PSVF + PSVF_AD ) * 0.5_R8

 ENDIF

  DT_CSTRIN='AFTER AVERAGING'  ; IF( LL_DEBUGTMP ) CALL CALLDT

! ---
! SCALE FOR BOUNDARIES
    PSVF = PSVF * FCT

    DT_CSTRIN='AFTER RESCALING'  ; IF( LL_DEBUGTMP ) CALL CALLDT

CALL FLUSH(IOUNLOG)

CALL MYFRTPROF_WALL('VER_HORF: CONTROL TO PHYSICAL SPACE',1)
END SUBROUTINE VER_HORF
