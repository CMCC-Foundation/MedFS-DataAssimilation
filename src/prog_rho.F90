PROGRAM RHO

!.. TEST APPLICABILITY OF "LOCAL HYDROSTATIC ADJUSTMENT"
!.. OVER ALL THE GLOBE, USING MODEL DEPARTURES.
!..
!.. EXTERNAL : LIBOCEANVAR
!..
!.. ANDREA STORTO *INGV* 2009-03-26

USE SET_KND
USE GRD_STR
USE DRV_STR
USE OCEANTOOLS , ONLY : RHO_UNESCO, RHO_UNESCOTL

IMPLICIT NONE

REAL(R4), ALLOCATABLE, DIMENSION(:,:,:) :: ETA
REAL(R8), ALLOCATABLE, DIMENSION(:,:,:,:) :: T4D, S4D
REAL(R8), ALLOCATABLE, DIMENSION(:,:,:) :: T3B, S3B, T3P, S3P
REAL(R8), ALLOCATABLE, DIMENSION(:,:)   :: RHOTL
REAL(R8)   ::  RHO0, RHOTEMP,ETATEMP
INTEGER(I4), PARAMETER :: NOUT=21
INTEGER(I4), PARAMETER :: NTSTEPS=14
INTEGER(I4), PARAMETER :: INCR=2
INTEGER(I4) :: ITIME,JX,JY,JD,KLEVNM
LOGICAL, ALLOCATABLE, DIMENSION(:,:) :: LAPPCLIB

!... INIT
RHO0=RHO_UNESCO(35.0_R8 ,0.0_R8 ,0.0_R8 ,.FALSE.)
OPEN(NOUT,FILE='ETADIFF.DAT')
DRV%NTR = 1
ALLOCATE( DRV%GRID (DRV%NTR))
ALLOCATE( DRV%RATIO(DRV%NTR))
ALLOCATE( DRV%MASK (DRV%NTR))
ALLOCATE( DRV%BMD(DRV%NTR))
ALLOCATE( DRV%DDA(DRV%NTR))
ALLOCATE( DRV%DDI(DRV%NTR))
ALLOCATE( DRV%CGRID(DRV%NTR))
DRV%GRID (1:DRV%NTR) = 1
DRV%RATIO(1:DRV%NTR) = 1.
DRV%MASK (1:DRV%NTR) = 1
DRV%BMD  (1:DRV%NTR) = 0
DRV%DDA  (1:DRV%NTR) = 0
DRV%DDI  (1:DRV%NTR) = 0
DRV%CGRID(1:DRV%NTR) = 'GLO0231'

!... SET UP MODEL GRID AND DATA ALLOCATION
CALL RDGRDS('GLO0231')
ALLOCATE( ETA(GRD%IM,GRD%JM,NTSTEPS) , &
        & T4D(GRD%IM,GRD%JM,GRD%KM,NTSTEPS) , &
        & S4D(GRD%IM,GRD%JM,GRD%KM,NTSTEPS) , &
        & RHOTL(GRD%IM,GRD%JM) , &
        & T3B(GRD%IM,GRD%JM,GRD%KM) , &
        & S3B(GRD%IM,GRD%JM,GRD%KM) , &
        & T3P(GRD%IM,GRD%JM,GRD%KM) , &
        & S3P(GRD%IM,GRD%JM,GRD%KM) )

!... READ IN MODEL SSH
CALL READFGETAB(ETA)

!... READ IN T,S
CALL READFG4DF('VOTEMPER',T4D,NTSTEPS)
CALL READFG4DF('VOSALINE',S4D,NTSTEPS)

!... CHECK LEVEL OF NO MOTION FOR EACH OBS
  WRITE(*,*) 'NO MOTION LEVEL IS'
  DO JD=1,GRD%KM
   IF(GRD%DEP(JD) .GT. 1000._R8) THEN
        KLEVNM = JD
        EXIT
   ENDIF
  ENDDO
  WRITE(*,*) '        ',KLEVNM

!... CHECK POINTS
     ALLOCATE(LAPPCLIB(GRD%IM,GRD%JM))
     LAPPCLIB=.TRUE.
     DO JY=1,GRD%JM
       DO JX=1,GRD%IM
         IF( GRD%MSK(JX,JY,KLEVNM).LT.0.999_R8) &
         & LAPPCLIB(JX,JY) =.FALSE.
       ENDDO
     ENDDO


!... START FROM TIME 0, EACH SECOND DAY COMPUTE
DO ITIME=1,NTSTEPS-INCR,INCR

     T3P=T4D(:,:,:,ITIME+INCR) - T4D(:,:,:,ITIME)
     S3P=S4D(:,:,:,ITIME+INCR) - S4D(:,:,:,ITIME)
     T3B=T4D(:,:,:,ITIME)
     S3B=S4D(:,:,:,ITIME)
     RHOTL(:,:) = 0._R8

     DO JD=1,GRD%KM
       DO JY=1,GRD%JM
         POINTS : DO JX=1,GRD%IM

!... COMPUTE TL OF RHO FROM MODEL BACKGROUND AND TRAJECTORY

           IF(.NOT.LAPPCLIB(JX,JY)) CYCLE POINTS
           RHOTL(JX,JY) = RHOTL(JX,JY) + &
           & RHO_UNESCOTL(S3B(JX,JY,JD),T3B(JX,JY,JD),&
           & S3P(JX,JY,JD),T3P(JX,JY,JD),0._R8,.FALSE.)*GRD%DEP(JD)

         ENDDO POINTS
       ENDDO
     ENDDO

!... COMPUTE DIFFERENCE (DSSH -DETA) AS A FUNCTION OF LATITUDE
!    AND WRITE IT DOWN

     DO JY=1,GRD%JM
         POINTS2 : DO JX=1,GRD%IM
           IF(.NOT.LAPPCLIB(JX,JY)) CYCLE POINTS2
           RHOTEMP = -1._R8*RHOTL(JX,JY)/RHO0
           ETATEMP = ETA(JX,JY,ITIME+INCR) - ETA(JX,JY,ITIME)
           WRITE(NOUT,'(I3,I5,I5,X,2(F11.6,X),E15.6)') &
           & ITIME,JX,JY,GRD%LON(JX,JY),GRD%LAT(JX,JY),&
           & REAL(ETATEMP,KIND=R8)-RHOTEMP
       ENDDO POINTS2
     ENDDO
ENDDO
CLOSE(NOUT)
END PROGRAM RHO
