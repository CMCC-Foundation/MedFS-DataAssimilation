SUBROUTINE READ_AVHRR_SST_NC(DT1,DT2,CFILE,MAXOBS,NTOBS,ZDATA,&
          & ZLLON,ZLLAT,ZZTIM,ZERR)

USE SET_KND
USE NETCDF
USE IOUNITS

IMPLICIT NONE

REAL   (DP), INTENT(IN) :: DT1, DT2
CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXOBS
INTEGER(I4), INTENT(OUT) :: NTOBS
REAL   (DP), DIMENSION(MAXOBS), INTENT(OUT) :: ZZTIM
REAL   (R8), DIMENSION(MAXOBS), INTENT(OUT) :: ZDATA,&
          & ZLLON,ZLLAT,ZERR

INTEGER(I4), PARAMETER :: NX=1440, NY=720
REAL(DP) :: ZOTIM(NX,NY,2)
REAL(R4) :: ZOLON(NX),ZOLAT(NY),ZOSST(NX,NY,2),ZOERR(NX,NY,2)
INTEGER(I4) :: STAT,IDVAR,IDF,JX,JY,JT

WRITE(IOUNOUT,*) ' :: OPENING AVHRR FILE '//TRIM(CFILE)
WRITE(IOUNLOG,*) ' :: OPENING AVHRR FILE '//TRIM(CFILE)

STAT=NF90_OPEN(CFILE,NF90_NOWRITE, IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLON)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLAT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'sst', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOSST)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'sst_err', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'days', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOTIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT=NF90_CLOSE(IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

NTOBS=0

DO JT=1,2
 DO JY=1,NY
  DO JX=1,NX
        IF(ZOSST(JX,JY,JT).GE.-3._R4 .AND. ZOSST(JX,JY,JT).LT.35._R4 .AND. &
        & ZOERR(JX,JY,JT) .GT. 0._R8 .AND. ZOERR(JX,JY,JT) .LT. 1.5_R8 .AND. &
        & ZOTIM(JX,JY,JT).GE.DT1  .AND. ZOTIM(JX,JY,JT).LE.DT2) THEN
          NTOBS=NTOBS+1
          IF(NTOBS.GT.MAXOBS) &
          & CALL ABOR1('READ_AVHRR_NC   : MAXOBS TO INCREASE')
          ZDATA(NTOBS) = REAL(ZOSST(JX,JY,JT),KIND=R8)
          ZLLON(NTOBS) = REAL(ZOLON(JX),KIND=R8)
          ZLLAT(NTOBS) = REAL(ZOLAT(JY),KIND=R8)
          ZZTIM(NTOBS) = REAL(ZOTIM(JX,JY,JT),KIND=8)
          ZERR(NTOBS)  = REAL(ZOERR(JX,JY,JT),KIND=R8)
        ENDIF
  ENDDO
 ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NTOBS,' OBSERVATIONS'

END SUBROUTINE READ_AVHRR_SST_NC
