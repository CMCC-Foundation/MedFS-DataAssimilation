SUBROUTINE READ_SACD(CFILE,MAXSS,NO,ZDATA,&
           & ZLLON,ZLLAT,ZBIAS)

USE SET_KND
USE NETCDF
USE IOUNITS

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXSS
INTEGER(I4), INTENT(OUT) :: NO
REAL(R8), INTENT(OUT), DIMENSION(MAXSS) :: ZDATA,&
           & ZLLON,ZLLAT
REAL(R8), INTENT(OUT), DIMENSION(MAXSS,3) :: ZBIAS
REAL(R8), ALLOCATABLE :: ZTLO(:),ZTAUX(:,:,:),ZTLA(:),ZTVA(:,:)
INTEGER(I4) :: STAT,IDVAR,IDF,JX,JY,NX,NY
INTEGER(I4),PARAMETER :: NBEAMS=3

WRITE(IOUNOUT,*) ' :: OPENING SACD FILE '//TRIM(CFILE)
WRITE(IOUNLOG,*) ' :: OPENING SACD FILE '//TRIM(CFILE)

STAT=NF90_OPEN(CFILE,NF90_NOWRITE, IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_DIMID(IDF, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION(IDF, IDVAR, LEN = NX)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_DIMID(IDF, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_DIMENSION(IDF, IDVAR, LEN = NY)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

ALLOCATE(ZTLO(NX),ZTLA(NY),ZTVA(NX,NY),ZTAUX(NX,NY,3))

STAT = NF90_INQ_VARID (IDF, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTLO)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTLA)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'SSS', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTVA)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'anc_sst', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTAUX(:,:,1))
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'density', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTAUX(:,:,2))
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'scat_wind_speed', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZTAUX(:,:,3))
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT=NF90_CLOSE(IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

NO=0
DO JY=1,NY
   DO JX=1,NX
      IF( ZTVA(JX,JY).LT.40._R4 .AND. ZTVA(JX,JY).GT.0.1_R4 ) THEN
         NO = NO + 1
         ZDATA(NO) = ZTVA(JX,JY)
         ZLLON(NO) = ZTLO(JX)
         ZLLAT(NO) = ZTLA(JY)
         ZBIAS(NO,1:3) = ZTAUX(JX,JY,1:3)
      ENDIF
   ENDDO
ENDDO

WRITE(IOUNLOG,*) ' FILE : ', TRIM(CFILE), ' FOUND ',NO,' SSS OBS'

DEALLOCATE(ZTLO,ZTLA,ZTVA,ZTAUX)
RETURN

END SUBROUTINE READ_SACD
