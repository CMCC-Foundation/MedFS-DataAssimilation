SUBROUTINE POSNAM(KULNAM,CDNAML)

!**** *POSNAM* - POSITION NAMELIST FILE FOR READING

!     AUTHOR.
!     -------
!        MATS HAMRUD *ECMWF*

!     MODIFICATIONS.
!     --------------
!        ORIGINAL : 93-06-22
!        M.HAMRUD      01-OCT-2003 CY28 CLEANING
!        M.HAMRUD      01-DEC-2003 CY28R1 CLEANING
!      R. EL KHATIB 04-08-10 APPLY NORMS + PROPER ABORT IF NAMELIST IS MISSING
!        A.STORTO      FEB-2009 MODIFICATIONS FOR LOCAL USE
!     --------------------------------------------------------------

USE SET_KND  ,ONLY : I4
USE IOUNITS

IMPLICIT NONE

INTEGER(KIND=I4),INTENT(IN)    :: KULNAM
CHARACTER(LEN=*)  ,INTENT(IN)    :: CDNAML


CHARACTER (LEN = 40) ::  CLINE
CHARACTER (LEN =  1) ::  CLTEST

INTEGER(KIND=I4) :: ILEN, IND1, ISTATUS, ISCAN

CLINE='                                        '
REWIND(KULNAM)
ILEN=LEN(CDNAML)
ISTATUS=0
ISCAN=0
DO WHILE (ISTATUS==0 .AND. ISCAN==0)
  READ(KULNAM,'(A)',IOSTAT=ISTATUS) CLINE
  SELECT CASE (ISTATUS)
  CASE (:-1)
    CLINE='POSNAM:CANNOT LOCATE '//CDNAML//' '
    CALL ABOR1(CLINE)
  CASE (0)
    IF (INDEX(CLINE(1:10),'&') == 0) THEN
      ISCAN=0
    ELSE
      IND1=INDEX(CLINE,'&'//CDNAML)
      IF (IND1 == 0) THEN
        ISCAN=0
      ELSE
        CLTEST=CLINE(IND1+ILEN+1:IND1+ILEN+1)
        IF (   (LGE(CLTEST,'0').AND.LLE(CLTEST,'9')) &
         & .OR.(LGE(CLTEST,'A').AND.LLE(CLTEST,'Z')) ) THEN
          ISCAN=0
        ELSE
          ISCAN=1
        ENDIF
      ENDIF
    ENDIF
  CASE (1:)
    CLINE='POSNAM:READ ERROR IN NAMELIST FILE'
    CALL ABOR1(CLINE)
  END SELECT
ENDDO
BACKSPACE(KULNAM)

END SUBROUTINE POSNAM
