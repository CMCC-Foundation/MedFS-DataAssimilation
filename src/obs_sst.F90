#undef SHARED_MEMORY
SUBROUTINE OBS_SST

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR SST DATA                            !
!                                                                      !
! VERSION 1: A.STORTO 2009                                             !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE READFG
 USE CTL_STR
 USE IOUNITS , ONLY : IOUNERR,IOUNLOG,IOUNOUT
 USE RUN, ONLY : RTIMES1950,NTSTEPS
 USE READFG
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)   ::  I, J, D, KK,JTSTEP,KSTEP,JP,FIRSTTS
 INTEGER(I4)   ::  KNUM

CALL MYFRTPROF_WALL('OBS_SST: COMPUTE SST BACKGROUND VALUES',0)

 WRITE(IOUNOUT,*) ' ENTERING OBS_SST'

FIRST : DO JTSTEP=1,NTSTEPS
  IF( LL_FGASSIM(JTSTEP) ) THEN
      FIRSTTS=JTSTEP
      EXIT FIRST
  ENDIF
ENDDO FIRST

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(KK,KSTEP,JTSTEP,I,J,JP)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
 CYOBS : DO KK = 1,SST%NO

  IF(SST%FLC(KK).NE.1 ) CYCLE CYOBS

  KSTEP = MINLOC( ABS(RTIMES1950(1:NTSTEPS)-SST%TIM(KK) ), DIM=1 ) - (FIRSTTS-1)
  IF(KSTEP .LT. 1 .OR. KSTEP .GT. NFGTSTEPS) THEN
     WRITE(IOUNOUT,*) ' OBS_SST: FOUND KSTEP EQUAL TO ',KSTEP
     WRITE(IOUNOUT,*) ' OBS_SST: TIME EQUAL TO ',SST%TIM(KK)
     WRITE(IOUNOUT,*) ' LL_FGASSIM : ',LL_FGASSIM
     WRITE(IOUNOUT,*) ' RTIMES1950 : ',RTIMES1950(1:NTSTEPS)
     CALL ABOR1('OBS_SST: KSTEP OUT OF RANGE')
  ENDIF

  SST%BAC(KK) = 0._R8
  DO JP=1,NPQ
       SST%BAC(KK) = SST%BAC(KK) + SST%PQ(KK,JP)*GRD%TEMB(SST%IB(KK,JP),SST%JB(KK,JP),1,KSTEP)
  ENDDO

  SST%RES(KK) = SST%VAL(KK) - SST%BAC(KK)

 ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) ' *** SST OBSERVATIONS OBSOP COMPLETED'

CALL MYFRTPROF_WALL('OBS_SST: COMPUTE SST BACKGROUND VALUES',1)
END SUBROUTINE OBS_SST
