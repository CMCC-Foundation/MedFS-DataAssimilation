SUBROUTINE READICOADS

!.. INTERFACE FOR READING INS FILES
!.. CALLING READEN3FILE, WHICH IS INTENDED
!.. FOR A MORE GENERIC (READ: POTENTIALLY EXTERNAL)
!.. READING OF OBSERVATIONAL NETCDF FILES
!..
!.. ANDREA STORTO, *INGV* 2009-03-23

USE SET_KND
USE OBS_STR, ONLY : SST,INS,SLA,OBS,SSS
USE GRD_STR
USE IOUNITS
USE ALLOCOBS
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE RUN, ONLY : ZANJUL1950

IMPLICIT NONE

CHARACTER(LEN=20) :: CFILE
INTEGER(I4), PARAMETER :: NMAX = 1500000,MAXPROFS = 20000
INTEGER(I4) :: NOBS , IFILE, KOSUM, NLEVS
INTEGER(I4) :: STOBS,ENOBS,TOTOBS
INTEGER(I4), DIMENSION(NMAX) :: OTYPE, OPAR, OPROF
REAL   (R8), DIMENSION(NMAX) :: ODEP,  OVAL
REAL   (DP), DIMENSION(NMAX) :: OTIME
REAL   (R8), DIMENSION(NMAX) :: OLON,  OLAT
CHARACTER(LEN=8), DIMENSION(NMAX) :: OPLA
LOGICAL :: LPROCEED, LFLAGS(10), LDUPL, LDUPLA(MAXPROFS)
INTEGER(I4) :: KPRC, KDUPL, JOBS,JPROF,JPROF2, KPIND, KPIND2,UPPROF,K,JT
INTEGER(I4), PARAMETER :: NICOADSTYPES=1

  WRITE(IOUNOUT,*) ' READING IN IN-SITU OBSERVATIONS'

  IF( .NOT. ALLOCATED(INS%VAL) ) THEN
    INS%NO = NMAX
    INS%NC = NMAX
    CALL ALLOCINS(INS%NO,.TRUE.)
    INS%FLC(:)=0
    INS%FLG(:)=1
    TOTOBS=0
    UPPROF=0
  ELSE
    TOTOBS=INS%NO
    UPPROF=INS%PROF(INS%NO)
  ENDIF


   WRITE(IOUNLOG,*) 'READING ICOADS INSTYPE '

   LPROCEED=.TRUE.
   IFILE=1

  !... LOOP OVER EXISTING INS FILES
   DO WHILE(LPROCEED)
      WRITE(CFILE,'(A,I2.2)' ) 'ICOADS_',IFILE
      INQUIRE(FILE=CFILE,EXIST=LPROCEED)
      IF(LPROCEED) THEN
          WRITE(IOUNLOG,*)
          WRITE(IOUNLOG,*) ' /// READING INS FILE ',TRIM(CFILE)
          WRITE(IOUNLOG,*)

          CALL READICOADSFILE(CFILE,IOUNLOG,NMAX,&
          & NOBS,OTYPE,OPAR,ODEP,OVAL,OTIME,OLON,OLAT,OPROF,OPLA)

   !... STORE OBS

          IF(NOBS.GT.0) THEN
            IF(TOTOBS+NOBS .GT. NMAX ) &
            & CALL ABOR1('READICOADS : NMAX MUST BE INCREASED')

            STOBS = TOTOBS +1
            ENOBS = TOTOBS + NOBS

            INS%OTYPE(STOBS:ENOBS) = OTYPE(1:NOBS)
            INS%  PAR(STOBS:ENOBS) = OPAR (1:NOBS)
            INS%  TIM(STOBS:ENOBS) = OTIME(1:NOBS)
            INS%  DPT(STOBS:ENOBS) = ODEP (1:NOBS)
            INS%  VAL(STOBS:ENOBS) = OVAL (1:NOBS)
            INS%  LON(STOBS:ENOBS) = OLON (1:NOBS)
            INS%  LAT(STOBS:ENOBS) = OLAT (1:NOBS)
            INS% PROF(STOBS:ENOBS) = OPROF(1:NOBS) + UPPROF
            INS% PLNO(STOBS:ENOBS)(1:8) = OPLA (1:NOBS)(1:8)

            TOTOBS = TOTOBS + NOBS
            UPPROF = INS% PROF(ENOBS)

          ENDIF
      ELSE

          IF(IFILE.EQ.1) &
          & WRITE(IOUNERR,*) '*** WARNING : NO ICOADS INSITU FILE'

      ENDIF
      IFILE=IFILE+1
  ENDDO

 INS%NO=TOTOBS
 INS%FLC(1:INS%NO) = 1

 INS%DSRC(1:INS%NO) = 'ICO'

END SUBROUTINE READICOADS
