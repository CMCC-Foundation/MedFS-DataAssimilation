MODULE CERES

USE GRD_STR
USE SET_KND
USE IOUNITS
USE MYNETCDF

IMPLICIT NONE

LOGICAL  :: LL_CERES = .FALSE.
REAL(R8) :: ZCVAL,ZCBAC,ZCERR,HC1,HC2,AT
REAL(R8), PARAMETER :: ZCOEFF = 1020._R8*4000._R8
INTEGER(I4) :: NTB,NDAYS

CONTAINS

SUBROUTINE CERES_INIT

USE NETCDF

IMPLICIT NONE

WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) '/////////// CERES TOA DATA ///////////'
WRITE(IOUNLOG,*)

OPEN(UNIT=201,FILE='CERES_val.dat')
READ(201,*) ZCBAC,HC1,HC2,ZCVAL,ZCERR,AT,NDAYS
CLOSE(201)

! bb<-(hc2-hc1)/(at*nd*86400)

WRITE(IOUNLOG,*) 'OBSERVED    VALUE: ',ZCVAL
WRITE(IOUNLOG,*) 'OBSERVATION ERROR: ',ZCERR
WRITE(IOUNLOG,*) 'BACKGROUND  VALUE: ',ZCBAC

WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) '//////////////////////////////////////'
WRITE(IOUNLOG,*)

END SUBROUTINE CERES_INIT

SUBROUTINE CERES_OO_TL(NX,NY,NZ,TEM,CER_TL)

IMPLICIT NONE

INTEGER(I4),INTENT(IN) :: NX,NY,NZ
REAL(R8),INTENT(IN) :: TEM(NX,NY,NZ)
REAL(R8),INTENT(OUT) :: CER_TL

REAL(R8) :: HC_TL
INTEGER(I4) :: JI,JJ,JK

HC_TL=0._R8
DO JK=1,NZ
 DO JJ=1,NY
  DO JI=1,NX
   HC_TL=HC_TL+ZCOEFF*TEM(JI,JJ,JK)*GRD%MSK(JI,JJ,JK)* &
   & GRD%DX(JI,JJ)*GRD%DY(JI,JJ)*GRD%DZ(JK)
  ENDDO
 ENDDO
ENDDO

CER_TL=HC_TL/(AT*NDAYS*86400._R8)

END SUBROUTINE CERES_OO_TL

SUBROUTINE CERES_OO_AD(NX,NY,NZ,TEM_AD,CER_AD)

IMPLICIT NONE

INTEGER(I4), INTENT(IN) :: NX,NY,NZ
REAL(R8), INTENT(INOUT) :: CER_AD
REAL(R8), INTENT(INOUT) :: TEM_AD(NX,NY,NZ)

REAL(R8) :: HC_AD
INTEGER(I4) :: JI,JJ,JK
  
HC_AD = CER_AD / (AT*NDAYS*86400._R8)
DO JK=NZ,1,-1
  DO JJ=NY,1,-1
    DO JI=NX,1,-1
      TEM_AD(JI,JJ,JK)=TEM_AD(JI,JJ,JK) + ZCOEFF*HC_AD* &
      & GRD%MSK(JI,JJ,JK)*GRD%DX(JI,JJ)*GRD%DY(JI,JJ)*GRD%DZ(JK)
    END DO
  END DO
END DO
CER_AD = 0._R8

END SUBROUTINE CERES_OO_AD

SUBROUTINE CERES_OO_TLF(NX,NY,NZ,TEM)

IMPLICIT NONE

INTEGER(I4),INTENT(IN) :: NX,NY,NZ
REAL(R8),INTENT(IN) :: TEM(NX,NY,NZ)
REAL(R8) :: CER_TL

REAL(R8) :: HC_TL
INTEGER(I4) :: JI,JJ,JK

HC_TL=0._R8
DO JK=1,NZ
 DO JJ=1,NY
  DO JI=1,NX
   HC_TL=HC_TL+ZCOEFF*TEM(JI,JJ,JK)*GRD%MSK(JI,JJ,JK)* &
   & GRD%DX(JI,JJ)*GRD%DY(JI,JJ)*GRD%DZ(JK)
  ENDDO
 ENDDO
ENDDO

CER_TL=HC_TL/(AT*NDAYS*86400._R8)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) '/////////// CERES TOA DATA ///////////'
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) 'OBSERVED    VALUE: ',ZCVAL
WRITE(IOUNLOG,*) 'OBSERVATION ERROR: ',ZCERR
WRITE(IOUNLOG,*) 'BACKGROUND  VALUE: ',ZCBAC
WRITE(IOUNLOG,*) 'FINAL INCR. VALUE: ',CER_TL
WRITE(IOUNLOG,*) 'ANALYSIS    VALUE: ',CER_TL+ZCBAC
WRITE(IOUNLOG,*) 'RESDIUAL    VALUE: ',CER_TL+ZCBAC-ZCVAL
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) '//////////////////////////////////////'
WRITE(IOUNLOG,*)

OPEN(202,FILE='CERES_new.dat')
WRITE(202,'(4F10.6)') ZCVAL,ZCERR,ZCBAC,CER_TL 
CLOSE(202)

END SUBROUTINE CERES_OO_TLF

END MODULE CERES
