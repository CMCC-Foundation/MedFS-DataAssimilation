MODULE SDL_MODULE

!    INTERFACE BETWEEN USER APPLICATIONS AND SYSTEM-DEPENDENT INTRINSIC
!    ROUTINES, PROVIDED BY THE COMPUTER VENDORS.

!    ALL ROUTINES WHICH WISH TO CALL THESE ROUTINES MUST CONTAIN:
!    USE SDL_MODULE

! AUTHOR :
! ------
!   11-APR-2005 R. EL KHATIB  *METEO-FRANCE*
!   26-APR-2006 S.T.SAARINEN  DR.HOOK TRACE, CALLS TO EC_RAISE, INTEL/IFORT TRACEBACK

USE SET_KND  ,ONLY : I4  ,R8
#ifndef NOMPI
USE MPI
#endif

IMPLICIT NONE

SAVE

PRIVATE

INTEGER, PARAMETER :: SIGABRT = 6 ! HARDCODED

PUBLIC :: SDL_SRLABORT, SDL_DISABORT, SDL_TRACEBACK

CONTAINS

!-----------------------------------------------------------------------------
SUBROUTINE SDL_TRACEBACK(KTID)

! PURPOSE :
! -------
!   TRACEBACK

!   KTID : THREAD

INTEGER(KIND=I4), INTENT(IN), OPTIONAL :: KTID
INTEGER(KIND=I4) ITID, IPRINT_OPTION, ILEVEL
#ifdef NECSX
CHARACTER(LEN=*), PARAMETER :: CLNECMSG = '*** CALLING NEC TRACEBACK ***'
#endif

IF (PRESENT(KTID)) THEN
  ITID = KTID
ELSE
  ITID = OML_MY_THREAD()
ENDIF

#ifdef VPP
  CALL ERRTRA
  IF (PRESENT(KTID)) CALL SLEEP(28)
#elif RS6K
  WRITE(0,*)'SDL_TRACEBACK: CALLING XL_TRBK, THRD = ',ITID
  CALL XL__TRBK()
  WRITE(0,*)'SDL_TRACEBACK: DONE XL_TRBK, THRD = ',ITID
#elif __INTEL_COMPILER
  WRITE(0,*)'SDL_TRACEBACK: CALLING INTEL_TRBK, THRD = ',ITID
  CALL INTEL_TRBK() ! SEE IFSAUX/UTILITIES/GENTRBK.F90
  WRITE(0,*)'SDL_TRACEBACK: DONE INTEL_TRBK, THRD = ',ITID
#elif defined(LINUX) || defined(SUN4)
  WRITE(0,*)'SDL_TRACEBACK: CALLING LINUX_TRBK, THRD = ',ITID
  CALL LINUX_TRBK() ! SEE IFSAUX/UTILITIES/LINUXTRBK.C
  WRITE(0,*)'SDL_TRACEBACK: DONE LINUX_TRBK, THRD = ',ITID
#elif defined(NECSX)
! MESPUT WRITES OUT ONTO UNIT 6
  WRITE(6,*)'SDL_TRACEBACK: CALLING NEC/MESPUT, THRD = ',ITID
  CALL NECSX_TRBK(CLNECMSG)
  CALL FLUSH(6)
  WRITE(6,*)'SDL_TRACEBACK: DONE NEC/MESPUT, THRD = ',ITID
#else
  WRITE(0,*)'SDL_TRACEBACK: NO PROPER TRACEBACK IMPLEMENTED.'
  ! A TRACEBACK USING DBX-DEBUGGER, IF AVAILABLE AND
  ! ACTIVATED VIA 'EXPORT DBXDEBUGGER=1'
  WRITE(0,*)'SDL_TRACEBACK: CALLING DBX_TRBK, THRD = ',ITID
  CALL DBX_TRBK() ! SEE IFSAUX/UTILITIES/LINUXTRBK.C
  WRITE(0,*)'SDL_TRACEBACK: DONE DBX_TRBK, THRD = ',ITID
#endif

END SUBROUTINE SDL_TRACEBACK
!-----------------------------------------------------------------------------
SUBROUTINE SDL_SRLABORT

! PURPOSE :
! -------
!   TO ABORT IN SERIAL ENVIRONMENT

CALL ABORT
STOP 'SDL_SRLABORT'

END SUBROUTINE SDL_SRLABORT
!-----------------------------------------------------------------------------
SUBROUTINE SDL_DISABORT(KCOMM)

! PURPOSE :
! -------
!   TO ABORT IN DISTRIBUTED ENVIRONMENT

!   KCOMM : COMMUNICATOR

INTEGER(KIND=I4), INTENT(IN) :: KCOMM

INTEGER(KIND=I4) :: IRETURN_CODE,IERROR

#ifdef VPP

CALL VPP_ABORT()

#else

IRETURN_CODE=1
#ifdef NOMPI
CALL ABORT
#else
CALL MPI_ABORT(KCOMM,IRETURN_CODE,IERROR)
#endif
#endif

CALL ABORT
STOP 'SDL_DISABORT'

END SUBROUTINE SDL_DISABORT
!-----------------------------------------------------------------------------
FUNCTION OML_MY_THREAD()
INTEGER(KIND=I4) :: OML_MY_THREAD
!$ INTEGER(KIND=I4) OMP_GET_THREAD_NUM
OML_MY_THREAD = 1
!$ OML_MY_THREAD = OMP_GET_THREAD_NUM() + 1
END FUNCTION OML_MY_THREAD
!-----------------------------------------------------------------------------

END MODULE SDL_MODULE
