SUBROUTINE READ_AMSR_DAY(FILENAME,IDIFF,MOBS,NOBS,ZOSST,ZTIME,ZLLON,ZLLAT)

!       THIS ROUTINE READS VERSION-5 RSS AMSR-E OR AMSR-J DAILY FILES

!       INPUT
!       FILENAME  WITH PATH IN FORM SATNAME_YYYYMMDD_V5.GZ
!        WHERE SATNAME  = NAME OF SATELLITE (AMSRE OR AMSR)
!              YYYY             = YEAR
!                  MM           = MONTH
!                  DD           = DAY OF MONTH
!
!       OUTPUT
!       AMSR_DATA  (A 1440X720X6X2 ARRAY OF DATA)
!       THE 6 ELEMENTS OF AMSR_DATA CORRESPOND TO:
!       1:TIME          TIME OF MEASUREMENT IN FRACTIONAL HOURS GMT
!       2:SST           SEA SURFACE TEMPERATURE IN DEG CELCIUS
!       3:WIND          10M SURFACE WIND IN METERS/SECOND
!       4:VAPOR         COLUMNAR WATER VAPOR IN MILLIMETERS
!       5:CLOUD         CLOUD LIQUID WATER IN MILLIMETERS
!       6:RAIN          RAIN RATE IN MILLIMETERS/HOUR
!
!       LONGITUDE  IS 0.25*XDIM-0.125    DEGREES EAST
!       LATITUDE   IS 0.25*YDIM-90.125

USE SET_KND
USE IOUNITS
USE RUN, ONLY : ZJULSTART,ZJULEND

IMPLICIT NONE

INTEGER(I4),INTENT(IN) :: MOBS,IDIFF
CHARACTER(LEN=*), INTENT(IN) :: FILENAME
INTEGER(I4),INTENT(OUT) :: NOBS
REAL(KIND=R8), DIMENSION(MOBS), INTENT(OUT) :: &
& ZOSST,ZTIME,ZLLON,ZLLAT

CHARACTER(1) :: ABUF(1440,720,6)
REAL(R4), DIMENSION(1440,720,6,2) :: AMSR_DATA
REAL(R4) :: XSCALE(6), XMINVAL(6)
REAL(DP) :: ZVT
LOGICAL :: LEXIST
INTEGER(I4) :: IA, IV, JX, JY
DATA XSCALE/0.1,0.15,0.2,0.3,0.01,0.1/
DATA XMINVAL/0.,-3.,0.,0.,0.,0./

!     SET DATA ARRAYS TO MISSING
AMSR_DATA=254.

INQUIRE(FILE=FILENAME,EXIST=LEXIST)
IF(.NOT.LEXIST) THEN
     WRITE(IOUNERR,*) 'FILE ',TRIM(FILENAME),' NOT FOUND'
     CALL ABOR1('AMSR-E FILE NOT FOUND')
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' :: READING AMSR-E FILE: ', TRIM(FILENAME)

OPEN(3,FILE=FILENAME,STATUS='OLD',RECL=6220800,&
& ACCESS='DIRECT',FORM='UNFORMATTED')
DO IA=1,2
     READ(3,REC=IA) ABUF
     DO IV=1,6
         AMSR_DATA(:,:,IV,IA)=REAL(ICHAR(ABUF(:,:,IV)))
         WHERE(AMSR_DATA(:,:,IV,IA)<=250)
              AMSR_DATA(:,:,IV,IA)=&
           &  AMSR_DATA(:,:,IV,IA)*XSCALE(IV)+XMINVAL(IV)
         ELSEWHERE
              AMSR_DATA(:,:,IV,IA)= -99999.
         ENDWHERE
     ENDDO
ENDDO
CLOSE(3)

NOBS=0
DO JY=1,720,4
  DO JX=1,1440,4
     DO IA=1,2
        ZVT= REAL(IDIFF,KIND=DP) + REAL(AMSR_DATA(JX,JY,1,IA),KIND=DP)/&
        & REAL(24,KIND=DP)
        IF(AMSR_DATA(JX,JY,2,IA).GT.-99998. ) THEN
        !  .AND. ZVT.GE.ZJULSTART .AND. &
        ! & ZVT.LE.ZJULEND) THEN
          NOBS=NOBS+1
          ZOSST(NOBS) = REAL(AMSR_DATA(JX,JY,2,IA),KIND=R8)
          ZTIME(NOBS) = ZVT
          ZLLON(NOBS) = 0.25_R8*REAL(JX,KIND=R8)-0.125_R8
          ZLLAT(NOBS) = 0.25_R8*REAL(JY,KIND=R8)-90.125_R8
        ENDIF
     ENDDO
  ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NOBS,' OBSERVATIONS'

RETURN
END SUBROUTINE READ_AMSR_DAY
