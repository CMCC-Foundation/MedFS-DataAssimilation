SUBROUTINE RF_INIT

! A.S. INITIALIZE REC FILTER COMMONS

USE SET_KND
USE GRD_STR
USE RUN, ONLY : LL_ICERFMASK
USE ICE
USE EXTGRID
USE RECFILTER, ONLY : LLMSR, LLMSR2, NEWDX, NEWDY, LLRFMASK
USE MYFRTPROF

IMPLICIT NONE

INTEGER(I4) :: I, J, K, KF
REAL(KIND=R8) :: DXMIN, DXMAX
REAL(KIND=R8) :: DYMIN, DYMAX

CALL MYFRTPROF_WALL('RF_INIT: INITIALIZE RF',0)

IF( RCF_TYPE .EQ. 1 ) THEN
    WRITE(IOUNOUT,*) ' FIRST-ORDER RECURSIVE FILTER'
ELSEIF( RCF_TYPE .EQ. 2 ) THEN
    WRITE(IOUNOUT,*) ' THIRD-ORDER RECURSIVE FILTER'
    RF_NTR = 1
ENDIF

NTOTV = PSV3D*GRD%KM+PSV2D

ALLOCATE( LLMSR(GRD%IM,GRD%JM,NTOTV ), &
        & LLMSR2(GRD%IM,GRD%JM) )

KF=0
IF(LL_TS) THEN
 LLMSR(:,:,KF+1:KF+GRD%KM) = (GRD%MSR.GT.0.9_R8)
 KF=KF+GRD%KM
 LLMSR(:,:,KF+1:KF+GRD%KM) = (GRD%MSR.GT.0.9_R8)
 KF=KF+GRD%KM
ENDIF
IF(LL_UV) THEN
 LLMSR(:,:,KF+1:KF+GRD%KM) = (GRD%MSR.GT.0.9_R8)
 KF=KF+GRD%KM
 LLMSR(:,:,KF+1:KF+GRD%KM) = (GRD%MSR.GT.0.9_R8)
 KF=KF+GRD%KM
ENDIF
IF(LL_SSH) THEN
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
ENDIF
IF(LL_SST) THEN
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
ENDIF
IF(LL_SSS) THEN
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
ENDIF
IF(LL_TQ2) THEN
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
ENDIF
IF(LL_ICE) THEN
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
 LLMSR(:,:,KF+1) = (GRD%MSR(:,:,1).GT.0.9_R8)
 KF=KF+1
ENDIF

IF( LLRFMASK ) THEN
  DO K=1,NTOTV
    LLMSR2 = LLMSR(:,:,K)
    DO J=2,GRD%JM-1
      DO I=2,GRD%IM-1
       IF( .NOT. LLMSR(I,J,K) ) THEN
         IF( COUNT( (/LLMSR2(I-1,J),LLMSR2(I+1,J),LLMSR2(I,J-1),&
          &  LLMSR2(I,J+1)/) ) .GT. 0 ) LLMSR(I,J,K) = .TRUE.
       ENDIF
      ENDDO
    ENDDO
  ENDDO
  IF( NWRAPS .EQ. 0 ) THEN
    LLMSR(1,:,:) = LLMSR(GRD%IM-1,:,:)
    LLMSR(GRD%IM,:,:) = LLMSR(2,:,:)
  ENDIF
ENDIF

LLMSR2 = LLMSR(:,:,1)

IF ( LLVARMDT .OR. NCONF.EQ.202) THEN

    ALLOCATE( LLMSRM(GRD%IM,GRD%JM) )
    LLMSRM = LLMSR2
    DO J=2,GRD%JM-1
      DO I=2,GRD%IM-1
       IF( .NOT. LLMSRM(I,J) ) THEN
         IF( COUNT( (/LLMSRM(I-1,J), LLMSRM(I+1,J), &
         & LLMSRM(I,J-1), LLMSRM(I,J+1)/) ) .GT. 0 ) LLMSRM(I,J) = .TRUE.
       ENDIF
      ENDDO
    ENDDO
    LLMSRM(1,:) = LLMSRM(GRD%IM-1,:)
    LLMSRM(GRD%IM,:) = LLMSRM(2,:)
    IF( LLVARMDT ) WHERE ( ABS( GRD%CMDT_STDEV ) .GT. 0.08_R8 ) GRD%CMDT_STDEV = 0.08_R8

ENDIF

ALLOCATE( NEWDX(GRD%IM, GRD%JM) )
ALLOCATE( NEWDY(GRD%IM, GRD%JM) )

DXMIN = MINVAL(GRD%DX,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
DXMAX = MAXVAL(GRD%DX,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
DYMIN = MINVAL(GRD%DY,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))
DYMAX = MAXVAL(GRD%DY,MASK=(INT(GRD%MSK(:,:,1)).EQ.1))

WHERE ( GRD%DX .GT. DXMIN )
        NEWDX = GRD%DX
ELSEWHERE
        NEWDX = DXMIN
ENDWHERE

WHERE ( GRD%DY .GT. DYMIN ) 
        NEWDY = GRD%DY
ELSEWHERE
        NEWDY = DYMIN
ENDWHERE

IF( LL_ICERFMASK .AND. NCONF.NE.202) CALL ICERFMASK

 CALL MYFRTPROF_WALL('RF_INIT: INITIALIZE RF',1)

END SUBROUTINE RF_INIT
