SUBROUTINE READ_REMSS_MWOISST_NC(CFILE,MAXOBS,NTOBS,ZDATA,&
          & ZLLON,ZLLAT)

USE SET_KND
USE NETCDF
USE IOUNITS

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXOBS
INTEGER(I4), INTENT(OUT) :: NTOBS
REAL   (R8), DIMENSION(MAXOBS), INTENT(OUT) :: ZDATA,&
          & ZLLON,ZLLAT

INTEGER(I4), PARAMETER :: NX=1440, NY=720
REAL(R4) :: ZOLON(NX),ZOLAT(NY),ZOSST(NX,NY),ZOTIM(NX,NY)
INTEGER(I4) :: STAT,IDVAR,IDF,JX,JY

WRITE(IOUNOUT,*) ' :: OPENING MWOI-SST FILE '//TRIM(CFILE)
WRITE(IOUNLOG,*) ' :: OPENING MWOI-SST FILE '//TRIM(CFILE)

STAT=NF90_OPEN(CFILE,NF90_NOWRITE, IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLON)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLAT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'analysed_sst', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOSST)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT=NF90_CLOSE(IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

ZOSST = (ZOSST*0.01_R4)

NTOBS=0

DO JY=1,NY,4
  DO JX=1,NX,4
        IF(ZOSST(JX,JY).GE.-3._R4 ) THEN
          NTOBS=NTOBS+1
          IF(NTOBS.GT.MAXOBS) &
          & CALL ABOR1('READ_REMSS_MWOISST: MAXOBS TO INCREASE')
          ZDATA(NTOBS) = REAL(ZOSST(JX,JY),KIND=R8)
          ZLLON(NTOBS) = REAL(ZOLON(JX),KIND=R8)
          ZLLAT(NTOBS) = REAL(ZOLAT(JY),KIND=R8)
        ENDIF
  ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NTOBS,' OBSERVATIONS'

END SUBROUTINE READ_REMSS_MWOISST_NC
