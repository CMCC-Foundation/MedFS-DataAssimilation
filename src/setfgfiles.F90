SUBROUTINE SETFGFILES(NTF)

USE SET_KND
USE IOUNITS, ONLY : IOUNERR,IOUNOUT,IOUNLOG
USE READFG
USE MPIREL

IMPLICIT NONE

INTEGER(I4), INTENT(IN) :: NTF
INTEGER(I4) :: JF, JP_START
LOGICAL     :: FE

IF(NTF.GT.MAXFILES) THEN
   WRITE(IOUNERR,*) &
   & 'MOD_READFG: NUMBER OF FIRST GUESS FILES EXCEEDS MAXIMUM ALLOWED'
   WRITE(IOUNERR,*) 'MAX ALLOWED =',MAXFILES,',  ACTUAL NO =',NTF
   CALL ABOR1('MOD_READFG: TOO MANY FG FILES')
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// SETUP FIRST-GUESS FILENAMES'

IF(LMPION) THEN
 JP_START = MP_SDST(MYPROC) - 1
ELSE
 JP_START = 0
ENDIF

IF(NTF.LE.0) THEN
   NFGFILES=1
   CFGFILES(1) = 'OPA_FIRSTGUESS'
   INQUIRE(FILE='OPA_FIRSTGUESS.nc', EXIST=FE)
   IF(FE) CFGFILES(1) = 'OPA_FIRSTGUESS.nc'
ELSE
   NFGFILES=NTF
   DO JF=1,NFGFILES
      WRITE(CFGFILES(JF),'(A,I4.4,A)') 'OPA_FIRSTGUESS_',JP_START+JF-1,'.nc'
   ENDDO
ENDIF

LLMPF = (NFGFILES.GT.1)

WRITE(IOUNLOG,*) ' NUMBER OF FG FILES:',NFGFILES
WRITE(IOUNLOG,*) ' MULTI-PROC FILES  :',LLMPF
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' -------- NAMES OF FG FILES --------'
DO JF=1,NFGFILES
   WRITE(IOUNLOG,*) JF,' '//TRIM(CFGFILES(JF))
ENDDO
WRITE(IOUNLOG,*)

CALL FLUSH(IOUNLOG)
END SUBROUTINE SETFGFILES
