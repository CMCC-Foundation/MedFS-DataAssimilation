SUBROUTINE WRITE_NCEOF(CFOUT,LLSQUARE)

USE MYFRTPROF
USE SET_KND
USE NETCDF
USE EOF_STR
USE GRD_STR
USE IOUNITS

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFOUT
LOGICAL, INTENT(IN) :: LLSQUARE

INTEGER :: NCID
CHARACTER(LEN=8) :: CDATE
CHARACTER(LEN=10) :: CTIME
INTEGER :: XDIM,YDIM,ZDIM,EDIM,RDIM,X2DIM,Y2DIM
INTEGER :: NRID,EAID,EVID

!----------- CREATE NETCDF FILE FORMATTED FOR 3DVAR
!--------------------------------------------------

WRITE(IOUNLOG,*) ' CREATING FILE ', TRIM(CFOUT)
WRITE(IOUNOUT,*) ' CREATING FILE ', TRIM(CFOUT)

CALL CHECK(NF90_CREATE(TRIM(CFOUT), NF90_CLOBBER, NCID))

! DEFINE DIMENSIONS
WRITE(IOUNLOG,*) ' DEFINING DIMS '
CALL CHECK(NF90_DEF_DIM(NCID, 'x', ROS%EOGNX, XDIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'y', ROS%EOGNY, YDIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'z', ROS%KMT, ZDIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'eof', ROS%NEOF, EDIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'reg', ROS%NREG, RDIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'rx', GRD%IM , X2DIM))
CALL CHECK(NF90_DEF_DIM(NCID, 'ry', GRD%JM , Y2DIM))


! DEFINE VARS AND LONGNAMES
WRITE(IOUNLOG,*) ' DEFINING VARS '
CALL CHECK(NF90_DEF_VAR(NCID, 'nreg', NF90_INT,(/X2DIM,Y2DIM/),NRID))
CALL CHECK(NF90_PUT_ATT(NCID, NRID, 'long_name', 'Full-grid EOF region') )
CALL CHECK(NF90_DEF_VAR(NCID, 'eigenvalues', NF90_REAL,(/RDIM,EDIM/),EAID))
CALL CHECK(NF90_PUT_ATT(NCID, EAID, 'long_name', 'EOF Eigenvalues') )
CALL CHECK(NF90_DEF_VAR(NCID, 'eigenvectors', NF90_REAL,(/RDIM,ZDIM,EDIM/),EVID))
CALL CHECK(NF90_PUT_ATT(NCID, EVID, 'long_name', 'EOF Eigenvectors') )

CALL CHECK(NF90_PUT_ATT(NCID, NF90_GLOBAL, 'author', 'andrea.storto@cmcc.it') )
CALL DATE_AND_TIME(DATE=CDATE, TIME=CTIME)
CALL CHECK(NF90_PUT_ATT(NCID, NF90_GLOBAL, 'date_creation', TRIM(CDATE)//' '//TRIM(CTIME) ))

CALL CHECK(NF90_ENDDEF(NCID))

WRITE(IOUNLOG,*) ' PUTTING VARS '
CALL CHECK(NF90_PUT_VAR(NCID, NRID, GRD%REG) )
IF( LLSQUARE ) THEN
    CALL CHECK(NF90_PUT_VAR(NCID, EAID, ROS%EVA*ROS%EVA) )
ELSE
    CALL CHECK(NF90_PUT_VAR(NCID, EAID, ROS%EVA) )
ENDIF
CALL CHECK(NF90_PUT_VAR(NCID, EVID, ROS%EVC) )

CALL CHECK(NF90_CLOSE(NCID))

END SUBROUTINE WRITE_NCEOF
