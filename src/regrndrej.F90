SUBROUTINE REGRNDREJ(NSOBS)

USE OBS_STR
USE GRD_STR
USE MYNETCDF
USE IOUNITS

IMPLICIT NONE

INTEGER(I4),INTENT(IN) :: NSOBS
INTEGER(I4) :: JO,JR,KO,NOK,NKO,IERR
REAL(R8) :: ZLA, ZLS
INTEGER(I4), PARAMETER :: MAXR=99, MOPR=9000000
INTEGER(I4) :: ILIMITS(MAXR,4),IB,JB,NR,ITMP(4)
INTEGER(I4), ALLOCATABLE :: CNT(:),OIND(:,:),IUNI(:)
REAL(R8), ALLOCATABLE :: RUNI(:)

#include "obs_events.h"

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// RANDOMIZED REJECTION : SUBSAMPLING'

OPEN(512,FILE='regs.dat',STATUS='OLD')
NR=0
DO JR=1,MAXR
  READ(512,*,IOSTAT=IERR) ITMP(:)
  IF(IERR.EQ.0) THEN
    NR=NR+1
    ILIMITS(NR,:) = ITMP(:)
  ENDIF
ENDDO
CLOSE(512)

WRITE(IOUNLOG,*) ' NUMBER OF REGS :',NR
WRITE(IOUNLOG,*) ' NUMBER OF OBS  :',NSOBS

CALL INIT_RANDOM_SEED()
ALLOCATE(CNT(NR))
ALLOCATE(OIND(NR,MOPR))
CNT = 0

ALLOCATE(RUNI(NSOBS))
ALLOCATE(IUNI(NSOBS))

! INSITU
IF(INS%NO.GT.0) THEN
   DO JO=1,INS%NO
     IB=INS%IB(JO,1)
     JB=INS%JB(JO,1)
     INNER : DO JR=1,NR
        IF( IB.GE.ILIMITS(JR,1) .AND. IB.LE.ILIMITS(JR,2) &
       .AND.JB.GE.ILIMITS(JR,3) .AND. JB.LE.ILIMITS(JR,4) ) THEN
          CNT(JR) = CNT(JR)+1
          IF(CNT(JR).GT.MOPR) CALL ABOR1('REGRNDREJ: MOPR TO INCREASE')
          OIND(JR,CNT(JR)) = JO
          EXIT INNER
        ENDIF
     ENDDO INNER
   ENDDO
   DO JR=1,NR
      NOK=0
      NKO=0
      CALL RANDOM_NUMBER(RUNI)
      IUNI = INT(RUNI*CNT(JR))+1
      DO JO=1,CNT(JR)
        IF( ANY(IUNI .EQ. JO ) ) THEN
            NOK=NOK+1
        ELSE
            KO=OIND(JR,JO)
            INS%FLC(KO) = 0
            INS%EVE(KO) = KEVE_REGR
            NKO=NKO+1
        ENDIF
      ENDDO
      WRITE(IOUNLOG,*) ' RANDOM SUBSAMPLING, REGION :',JR
      WRITE(IOUNLOG,*) ' OK AND REJ :',NOK,NKO
   ENDDO
ENDIF
! SLA
IF(SLA%NO.GT.0) THEN
  CALL ABOR1('REGRNDREJ : SLA NO > 0 ')
ENDIF
! SST
IF(SST%NO.GT.0) THEN
  CALL ABOR1('REGRNDREJ : SST NO > 0 ')
ENDIF
! SSS
IF(SSS%NO.GT.0) THEN
  CALL ABOR1('REGRNDREJ : SSS NO > 0 ')
ENDIF

WRITE(IOUNLOG,*)

DEALLOCATE(CNT, OIND, RUNI, IUNI)

END SUBROUTINE REGRNDREJ
