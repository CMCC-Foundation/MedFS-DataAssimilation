SUBROUTINE INI_CFN

!-----------------------------------------------------------------------
!                                                                      !
! INITIALISE THE MINIMISATION                                          !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
!-----------------------------------------------------------------------

 USE OBS_STR
 USE GRD_STR
 USE EOF_STR
 USE CTL_STR
 USE IOUNITS, ONLY : IOUNLOG, IOUNOUT, IOUNERR
 USE RANDOM_NUMBERS
 USE MYFRTPROF, ONLY: MYFRTPROF_WALL
 USE MYNETCDF
 USE RUN, ONLY : LLVARMDT, NCONF, IDOUBLEDOM
 USE WEAKLY
 USE HYBRID
 USE EOFS3D

 IMPLICIT NONE

 INTEGER :: I,J,K,KK
 REAL(R8), ALLOCATABLE :: ZXR(:)
 LOGICAL :: LLPERTBG
 INTEGER(I4)    ::  KSEED
 TYPE(RANDOMNUMBERSTREAM) :: GOLUM

 INTEGER(I4) :: IZS, INDIC, NITER
 REAL(R8) :: RZS
 REAL(R8) :: DZS
 REAL(R8), DIMENSION(:), ALLOCATABLE    :: XGJ
 REAL(R8)       :: XJS,XJE,Z

#include "costf.h"

 CALL MYFRTPROF_WALL('INI_CFN: INITIALIZE COST FUCNTION',0)

! ---
! ALLOCATE MEMORY FOR OPTIMIZATION ARRAYS

    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,*) ' ------ INITIALIZATION OF J AND GRAD(J)'

    CTL%N = GRD%IM * GRD%JM * ROS%NEOF
    IF(NCONF .EQ. 202 ) CTL%N = GRD%IM * GRD%JM

    CVB = CTL%N

    IF(LLVARMDT) THEN
       CVM_S = CTL%N+1
       CTL%N = CTL%N + GRD%IM * GRD%JM
       CVM_E = CTL%N
       WRITE(IOUNLOG,*) ' VARMDT  :  FROM',CVM_S,' TO',CVM_E
    ENDIF

    IF(LL_HYBRID) THEN
       CVH_S = CTL%N+1
       CTL%N = CTL%N + GRD%IM * GRD%JM * ROSH%NEOF
       CVH_E = CTL%N
       WRITE(IOUNLOG,*) ' HYBRID  :  FROM',CVH_S,' TO',CVH_E
    ENDIF

    IF(LL_HYBRID .AND. LL_ALPHA_CTL) THEN
       CVA_S = CTL%N+1
       CTL%N = CTL%N + NNA
       CVA_E = CTL%N
       WRITE(IOUNLOG,*) ' ALPHA   :  FROM',CVA_S,' TO',CVA_E
    ENDIF

    IF(LL_EOFS3D) THEN
       CVE_S = CTL%N + 1
       CTL%N = CTL%N + NN_EOFS3D
       CVE_E = CTL%N + 1
       WRITE(IOUNLOG,*) ' 3DEOFS  :  FROM',CVE_S,' TO',CVE_E
    ENDIF

    IF(LL_WEAKLY) THEN
       CVWC4_S = CTL%N+1
       CTL%N = CTL%N + NN_WEAKLY
       CVWC4_E = CTL%N
       WRITE(IOUNLOG,*) ' WEAK-C  :  FROM',CVWC4_S,' TO',CVWC4_E
    ENDIF

    WRITE(IOUNLOG,*)
    WRITE(IOUNLOG,*) ' SIZE OF THE CONTROL VECTOR: ',CTL%N

    ALLOCATE( CTL%NBD(CTL%N), CTL%IWA(3*CTL%N))
    ALLOCATE( CTL%X_C(CTL%N), CTL%G_C(CTL%N))
    ALLOCATE( CTL%L_C(CTL%N), CTL%U_C(CTL%N))
    ALLOCATE( CTL%WA(8*CTL%M), CTL%SG(CTL%M), CTL%SGO(CTL%M), CTL%YG(CTL%M), CTL%YGO(CTL%M))
    ALLOCATE( CTL%WS(CTL%N,CTL%M), CTL%WY(CTL%N,CTL%M))
    ALLOCATE( CTL%SY(CTL%M,CTL%M), CTL%SS(CTL%M,CTL%M), CTL%YY(CTL%M,CTL%M))
    ALLOCATE( CTL%WT(CTL%M,CTL%M), CTL%WN(2*CTL%M,2*CTL%M), CTL%SND(2*CTL%M,2*CTL%M))
    ALLOCATE( CTL%Z_C(CTL%N), CTL%R_C(CTL%N), CTL%D_C(CTL%N), CTL%T_C(CTL%N))
    ALLOCATE( CTL%WA3(2*CTL%M*CTL%N + 5*CTL%N + 11*CTL%M*CTL%M + 8*CTL%M) )

    CTL%TASK = 'START'

    CTL%IPRINT = 1

    CTL%FACTR=1.0E+7_R8

    CTL%GIPRINT(1) = 1
    CTL%GIPRINT(2) = 1
    CTL%FINISH = .FALSE.
    CTL%IFLAG  = 0
    ALLOCATE( CTL%GW(CTL%N), CTL%GOLD(CTL%N) )
    CTL%GOLD = 0._R8
    CTL%GW = 0._R8

! ---
! INITIALISE THE ARRAYS
      DO I=1,CTL%N,2
         CTL%NBD(I)=0
         CTL%L_C(I)=-1.0E+3_R8
         CTL%U_C(I)=1.0E+3_R8
      ENDDO

      DO I=2,CTL%N,2
         CTL%NBD(I)=0
         CTL%L_C(I)=-1.0E+3_R8
         CTL%U_C(I)=1.0E+3_R8
      ENDDO

      DO I=1,CTL%N
         CTL%X_C(I)=0._R8
      ENDDO
      
!      IF( LL_HYBRID .AND. LL_ALPHA_CTL) THEN
!         CTL%L_C(CVA_S:CVA_E)=-1._R8
!         CTL%U_C(I)=1._R8
!      ENDIF

 IF( IDOUBLEDOM .EQ. 2 ) THEN
  
   WRITE(IOUNLOG,*) ' IDOUBLEDOM 2 :: REINITIALIZING CONTROL VECTOR'

   KK=0
   DO K=1,ROS%NEOF
    DO J=1,GRD%JM
     DO I=1,GRD%IM
       KK = KK+1
       CTL%X_C(KK) = GRD%RO(I,J,K)
     ENDDO
    ENDDO
   ENDDO

   CALL CNV_INN

   WRITE(IOUNLOG,*) ' INITIALIZING PHYSICAL SPACE CONTROL VECTOR'

   CALL FLUSH(IOUNLOG)

   CTL%TASK = 'START'
   ALLOCATE( XGJ(CTL%N) )
   WRITE(IOUNLOG,*) ' RECOMPUTATION OF INITIAL COST FUNCTION'
   CALL COSTF(INDIC,CTL%N,CTL%X_C,XJS,XGJ,NITER,IZS,RZS,DZS)
   WRITE(IOUNLOG,*) ' PERFORMED'
   CALL FLUSH(IOUNLOG)
   DEALLOCATE( XGJ )

 ENDIF

!.... EVENTUALLY PERTURB FIRST GUESS IN CONTROL SPACE

LLPERTBG=.FALSE.

IF(LLPERTBG) THEN

    KSEED=1
    ALLOCATE(ZXR(CTL%N))
    CALL INITIALIZE_RANDOM_NUMBERS(KSEED,GOLUM)
    CALL GAUSSIAN_DISTRIBUTION(ZXR,GOLUM)
    DO I=1,CTL%N
        CTL%X_C(I) = CTL%X_C(I) + ZXR(I)
    ENDDO
    DEALLOCATE(ZXR)

ENDIF

CALL MYFRTPROF_WALL('INI_CFN: INITIALIZE COST FUCNTION',1)
END SUBROUTINE INI_CFN
