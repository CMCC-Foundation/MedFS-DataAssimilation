SUBROUTINE MIN_CFN

!-----------------------------------------------------------------------
!                                                                      !
! MINIMISE THE COST FUNCTION                                           !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
!-----------------------------------------------------------------------


 USE SET_KND
 USE DRV_STR
 USE OBS_STR
 USE GRD_STR
 USE EOF_STR
 USE CTL_STR
 USE RUN
 USE IOUNITS, ONLY : IOUNERR, IOUNOUT, IOUNLOG
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)     :: CNTR, K,NITER
 REAL(R8)        :: MAXPG

 INTEGER(I4) :: IZS, INDIC
 REAL(R8) :: RZS
 REAL(R8) :: DZS
 REAL(R8), DIMENSION(CTL%N)    :: ZY, X, XGJ
 REAL(R8)       :: XJS, XJE,Z

 CHARACTER(LEN=3) :: NORMTYPE
 REAL(R8), ALLOCATABLE :: RZ(:)
 INTEGER(I4) :: IZ(5),NRZ,IMODE(3),OMODE,IO,IMPRES,REVERSE
 REAL(R8) :: DF1,DXMIN,DF10

 EXTERNAL :: SIMUL_RC,EUCLID,CTONBE,CTCABE

#include "costf.h"

  CALL MYFRTPROF_WALL('MIN_CFN: MINIMIZE COST FUNCTION',0)

  CNTR = 0

  IF(IDOUBLEDOM .EQ. 2)THEN
       MAXPG = MAXVAL(ABS(CTL%G_C(1:CTL%N)))
       CTL%PGTOL = MAXPG * CTL%PGPER
       CNTR = 1
  ENDIF

  CTL%KITER = 0
  NITER = 0
  IF( LL_BFEND .AND. DRV%MASK(DRV%KTR) .GT. 1 ) THEN
      WRITE(IOUNLOG,*) 'LL_BFEND: FORCING DRV_MASK TO 1'
      DRV%MASK(DRV%KTR) = 1
  ENDIF

  IF(CTL%ALG(1:3) .EQ. 'CG+'  ) CALL COSTF(INDIC,CTL%N,CTL%X_C,XJS,XGJ,NITER,IZS,RZS,DZS)
  IF(CTL%ALG(1:5) .EQ. 'M1QN3') THEN
       CALL COSTF(INDIC,CTL%N,CTL%X_C,XJS,XGJ,NITER,IZS,RZS,DZS)
       DF10=CTL%PGTOL*XJS
       NORMTYPE = 'two'
       IMPRES=3
       IO=IOUNLOG
       IMODE=(/0,0,0/)
       IZ=0
       NRZ  = 4*CTL%N+(2*CTL%N+1)
       ALLOCATE(RZ(NRZ))
       INDIC=1
  ENDIF

  DO WHILE( (CTL%TASK(1:5).EQ.'NEW_X' .OR. CTL%TASK(1:2).EQ.'FG' .OR. &
  & CTL%TASK(1:5).EQ.'START') .AND. &
  & NITER .LE. CTL%NIMAX .AND. CTL%KITER .LE. CTL%NSMAX )

    NITER = NITER +1
    WRITE(IOUNLOG,*) 'ITERATION NO ',CTL%KITER, ' TASK IS ', TRIM(CTL%TASK)

    IF(CTL%ALG(1:8) .EQ. 'L-BFGS-B' ) THEN
#ifdef LBFGSB3
    CALL SETULB(CTL%N, CTL%M, CTL%X_C, CTL%L_C, CTL%U_C, CTL%NBD, CTL%F_C, &
                CTL%G_C,    &
                CTL%FACTR, CTL%PGTOL, CTL%WA3, CTL%IWA, &
                CTL%TASK, CTL%IPRINT,  CTL%CSAVE, CTL%LSAVE, CTL%ISAVE, &
                CTL%DSAVE)
#else
    CALL SETULB(CTL%N, CTL%M, CTL%X_C, CTL%L_C, CTL%U_C, CTL%NBD, CTL%F_C, CTL%G_C,    &
                CTL%FACTR, CTL%PGTOL, CTL%WS, CTL%WY, CTL%SY, CTL%SS, CTL%YY, CTL%WT,  &
                CTL%WN, CTL%SND, CTL%Z_C, CTL%R_C, CTL%D_C, CTL%T_C, CTL%WA, CTL%SG,   &
                CTL%SGO, CTL%YG, CTL%YGO, CTL%IWA,                                     &
                CTL%TASK, CTL%IPRINT,  CTL%CSAVE, CTL%LSAVE, CTL%ISAVE, CTL%DSAVE)
#endif

   ELSEIF (CTL%ALG(1:3) .EQ. 'CG+' ) THEN

       WRITE(IOUNLOG,*) ' USING CG+,CTL%EPS,CTL%IFLAG,CTL%IREST,CTL%GMETHOD,CTL%FINISH'
       WRITE(IOUNLOG,*)             CTL%EPS,CTL%IFLAG,CTL%IREST,CTL%GMETHOD,CTL%FINISH
       CALL CGFAM(CTL%N,CTL%X_C,CTL%F_C,CTL%G_C,CTL%D_C,CTL%GOLD,CTL%GIPRINT,CTL%EPS,CTL%GW,&
                  CTL%IFLAG,CTL%IREST,CTL%GMETHOD,CTL%FINISH )
       WRITE(IOUNLOG,*) ' CG+ IFLAG : ',CTL%IFLAG
       IF(CTL%IFLAG.EQ.0) CTL%TASK(1:3) = 'END'
       IF(CTL%IFLAG.EQ.1) CTL%TASK(1:2) = 'FG'
       IF(CTL%IFLAG.EQ.2) THEN
           CTL%TASK(1:5) = 'NEW_X'
           IF(ALL(ABS(CTL%G_C) .LE. CTL%EPS*ABS(CTL%F_C) ) ) CTL%FINISH = .TRUE.
       ENDIF
       IF(CTL%IFLAG.LT.0) THEN
           WRITE(IOUNERR,*) 'ERROR ', CTL%IFLAG, ' IN CG+'
           CALL ABOR1('CG+ FAILED')
       ENDIF

   ELSEIF (CTL%ALG(1:5) .EQ. 'M1QN3' ) THEN

       REVERSE=1
       DF1=DF10
       DXMIN=SQRT(EPSILON(Z))
       CALL M1QN3(SIMUL_RC,EUCLID,CTONBE,CTCABE,CTL%N,CTL%X_C,CTL%F_C,CTL%G_C,DXMIN,DF1, &
          & CTL%PGTOL,NORMTYPE,IMPRES,IO,IMODE,OMODE,CTL%NIMAX,CTL%NSMAX,IZ,RZ,NRZ, &
          & REVERSE,INDIC,IZS,RZS,DZS)
  
       IF(REVERSE.LT.0) THEN
            CTL%TASK(1:3) = 'END'
            WRITE(IOUNOUT,*) 'M1QN3 ENDED WITH OMODE ',OMODE
       ELSE
            CTL%TASK(1:2) = 'FG'
       ENDIF

   ELSE
       CALL ABOR1('MIN_CFN : ALG NOT SUPPORTED')
   ENDIF

    WRITE(IOUNLOG,*) 'AFTER SETULB TASK IS ', TRIM(CTL%TASK),' AT ITER', NITER

    IF (CTL%TASK(1:2) .EQ. 'FG') THEN

! CALCULATE THE COST FUNCTION AND ITS GRADIENT
          CALL COSTF(INDIC,CTL%N,CTL%X_C,XJS,XGJ,NITER,IZS,RZS,DZS)

! MODIFY THE STOPPING CRITERIA
         IF(CNTR.EQ.0 .AND. CTL%PGPER.NE.0._R8 .AND. DRV%KTR.EQ.1 )THEN
            MAXPG = MAXVAL(ABS(CTL%G_C(1:CTL%N)))
            CTL%PGTOL = MAXPG * CTL%PGPER
            CNTR = 1
         ENDIF

      ENDIF

      ! EVENTUALLY SWITCH ON BOUNDARIES FILTERING
      IF ( ( (CTL%TASK(1:5).NE.'NEW_X' .AND. CTL%TASK(1:2).NE.'FG' .AND. &
      & CTL%TASK(1:5).NE.'START') .OR. NITER .GT. CTL%NIMAX ) .AND. &
      & LL_BFEND .AND. DRV%MASK(DRV%KTR) .EQ. 1 ) THEN

          WRITE(IOUNLOG,*) 'REINITIALIZING MINIMIZATION WITH BOUNDARIES FILTERING'

          CTL%TASK = 'START'
          DRV%MASK(DRV%KTR) = 3
          LL_BFEND =.FALSE.
          REVERSE = 1

      ENDIF


  ENDDO !WHILE

  IF( CTL%ALG(1:5) .EQ. 'M1QN3') DEALLOCATE(RZ)

  CALL MYFRTPROF_WALL('MIN_CFN: MINIMIZE COST FUNCTION',1)

END SUBROUTINE MIN_CFN
