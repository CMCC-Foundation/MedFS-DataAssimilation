SUBROUTINE READOBS103DD

!.. READ OBS IN BINARY OR NETCDF FORMAT

USE SET_KND
USE RUN , ONLY : NSUBDOMAINS, IO_OBSFORMAT, LL_ONEPROC103
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE ALLOCOBS
USE EXTGRID
USE MYNETCDF
USE STATS

IMPLICIT NONE

INTEGER(I4) :: KKCNT(NOFAMS), NTOT,NNF
INTEGER(I4) :: KKCNT2(NOFAMS)
INTEGER(I4) :: IAUX,IST,IEN,NMAXOBS,JO,NOINT,KOBS,JSAT,K,J,JI
INTEGER(I4) :: NCID, DIMID, VARID, JOBS, MAXINS, MAXSLA
CHARACTER(LEN=30) :: CFILE
TYPE(INS_T) :: AUX
REAL(R8), ALLOCATABLE :: AUX_TB(:,:)
REAL(R8), ALLOCATABLE :: AUX_SB(:,:)
REAL(R8) :: ZICO

#include "io_obs.h"

CALL MYFRTPROF_WALL('READOBS103DD: READ OBS FILES FOR IDOUBLEDOM 2',0)

!.. GET OBS VECT INDEX

CFILE = 'OBSSTAT_CR.NC'
CALL GETNCVAR(CFILE,'NOBS',NOFAMS,KKCNT)
CALL GETNCDIM(CFILE,'OBS',NTOT)

WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) ' *** READOBS103DD'
WRITE(IOUNLOG,*) ' OBSERVATION PER TYPE '
DO JO=1,NOFAMS
  WRITE(IOUNLOG,*) ' ',CCFAMS(JO), ' = ', KKCNT(JO)
ENDDO
WRITE(IOUNLOG,*) ' -------------'
WRITE(IOUNLOG,*) ' TOT = ', NTOT
WRITE(IOUNLOG,*) 

ZICO = 0.2_R8
WRITE(IOUNLOG,*) ' OBS VECTOR EXTENSION FACTOR: ', ZICO

INS%NO = KKCNT(NNINS)
INS%NC = INS%NO

IF(INS%NO.GT.0) THEN
  CALL ALLOCINS(INS%NO+INT(INS%NO*ZICO),.FALSE.)
  INS%FLC = 1
  INS%FLG = 1
ENDIF

SLA%NO=KKCNT(NNSLA)
SLA%NC=SLA%NO

IF(SLA%NO.GT.0) THEN
  CALL ALLOCSLA(SLA%NO+INT(SLA%NO*ZICO),.FALSE.)
  SLA%FLC = 1
  SLA%FLG = 1
ENDIF

SST%NO=KKCNT(NNSST)
SST%NC=SST%NO

IF(SST%NO.GT.0) THEN
  CALL ALLOCSST(SST%NO+INT(SST%NO*ZICO),.FALSE.)
  SST%FLC = 1
  SST%FLG = 1
ENDIF

SSS%NO=KKCNT(NNSSS)
SSS%NC=SSS%NO

IF(SSS%NO.GT.0) THEN
  CALL ALLOCSSS(SSS%NO+INT(SSS%NO*ZICO),.FALSE.)
  SSS%FLC = 1
  SSS%FLG = 1
ENDIF

CALL FLUSH(IOUNLOG)

CALL ALLOCINSB(NTOT,.FALSE.,AUX)
ALLOCATE( AUX_TB(NTOT,GRD%KM) )
ALLOCATE( AUX_SB(NTOT,GRD%KM) )

WRITE(IOUNLOG,*) ' FETCHING OBS IN FILE ',TRIM(CFILE)
CALL FLUSH(IOUNLOG)

CALL GETNCVAR(CFILE,'LON',NTOT,AUX%LON)
CALL GETNCVAR(CFILE,'LAT',NTOT,AUX%LAT)
CALL GETNCVAR(CFILE,'TIME',NTOT,AUX%TIM)
CALL GETNCVAR(CFILE,'DEPTH',NTOT,AUX%DPT)
CALL GETNCVAR(CFILE,'VALUE',NTOT,AUX%VAL)
CALL GETNCVAR(CFILE,'ERROR',NTOT,AUX%ERR)
CALL GETNCVAR(CFILE,'OMG',NTOT,AUX%RES)
CALL GETNCVAR(CFILE,'INC',NTOT,AUX%INC)
CALL GETNCVAR(CFILE,'TYPE',NTOT,AUX%OTYPE)
CALL GETNCVAR(CFILE,'PARAM',NTOT,AUX%PAR)
CALL GETNCVAR(CFILE,'INST',NTOT,AUX%INST)
CALL GETNCVAR(CFILE,'BIAS',NTOT,AUX%BIA)
CALL GETNCVAR(CFILE,'IB',NTOT,NPQ,AUX%IB)
CALL GETNCVAR(CFILE,'JB',NTOT,NPQ,AUX%JB)
CALL GETNCVAR(CFILE,'KB',NTOT,AUX%KB)
CALL GETNCVAR(CFILE,'BGERR',NTOT,AUX%BGERR)
CALL GETNCVAR(CFILE,'PQ',NTOT,NPQ,AUX%PQ)
CALL GETNCVAR(CFILE,'TB',NTOT,GRD%KM,AUX_TB)
CALL GETNCVAR(CFILE,'SB',NTOT,GRD%KM,AUX_SB)

KKCNT2 = 0

DO JO=1,NTOT

   SELECT CASE(AUX%PAR(JO))
     CASE(21) 
       NNF = NNSLA
     CASE(20) 
       NNF = NNSST
     CASE(19) 
       NNF = NNSSS
     CASE DEFAULT 
       NNF = NNINS
   ENDSELECT

   KKCNT2(NNF) = KKCNT2(NNF)+1
   JI = KKCNT2(NNF)

   IF( NNF .EQ. NNSLA ) THEN
     SLA%ERR(JI) = AUX%ERR(JO)
     SLA%INC(JI) = AUX%INC(JO)
     SLA%RES(JI) = AUX%RES(JO)
     SLA%LON(JI) = AUX%LON(JO)
     SLA%LAT(JI) = AUX%LAT(JO)
     SLA%TIM(JI) = AUX%TIM(JO)
     SLA%VAL(JI) = AUX%VAL(JO)
     SLA%IB (JI,:) = AUX%IB(JO,:)
     SLA%JB (JI,:) = AUX%JB(JO,:)
     SLA%PQ (JI,1:NPQ) = AUX%PQ(JO,1:NPQ)
     SLA%BOT(JI) = AUX%KB(JO)
     SLA%KSAT(JI) = AUX%INST(JO)
     SLA%BIA(JI) = AUX%BIA(JO)
     SLA%BGERR(JI) = AUX%BGERR(JO)
     SLA%TB(JI,:) = AUX_TB(JO,:)
     SLA%SB(JI,:) = AUX_SB(JO,:)
   ELSEIF( NNF .EQ. NNINS) THEN
     INS%ERR(JI) = AUX%ERR(JO)
     INS%INC(JI) = AUX%INC(JO)
     INS%RES(JI) = AUX%RES(JO)
     INS%OTYPE(JI) = AUX%OTYPE(JO)
     INS%PAR(JI) = AUX%PAR(JO)
     INS%PLNO(JI) = TRANSFER(AUX%INST(JO),INS%PLNO(JI))
     INS%LON(JI) = AUX%LON(JO)
     INS%LAT(JI) = AUX%LAT(JO)
     INS%TIM(JI) = AUX%TIM(JO)
     INS%VAL(JI) = AUX%VAL(JO)
     INS%DPT(JI) = AUX%DPT(JO)
     INS%IB (JI,:) = AUX%IB(JO,:)
     INS%JB (JI,:) = AUX%JB(JO,:)
     INS%PQ (JI,1:2*NPQ) = AUX%PQ(JO,1:2*NPQ)
     INS%KB(JI) = AUX%KB(JO)
     INS%BIA(JI) = AUX%BIA(JO)
     INS%BGERR(JI) = AUX%BGERR(JO)
    ELSEIF( NNF .EQ. NNSST) THEN
     SST%ERR(JI) = AUX%ERR(JO)
     SST%INC(JI) = AUX%INC(JO)
     SST%RES(JI) = AUX%RES(JO)
     SST%LON(JI) = AUX%LON(JO)
     SST%LAT(JI) = AUX%LAT(JO)
     SST%TIM(JI) = AUX%TIM(JO)
     SST%VAL(JI) = AUX%VAL(JO)
     SST%IB (JI,:) = AUX%IB(JO,:)
     SST%JB (JI,:) = AUX%JB(JO,:)
     SST%PQ (JI,1:NPQ) = AUX%PQ(JO,1:NPQ)
     SST%KSAT(JI) = AUX%INST(JO)
     SST%BIA(JI) = AUX%BIA(JO)
     SST%BGERR(JI) = AUX%BGERR(JO)
   ELSEIF( NNF .EQ. NNSSS) THEN
     SSS%ERR(JI) = AUX%ERR(JO)
     SSS%INC(JI) = AUX%INC(JO)
     SSS%RES(JI) = AUX%RES(JO)
     SSS%LON(JI) = AUX%LON(JO)
     SSS%LAT(JI) = AUX%LAT(JO)
     SSS%TIM(JI) = AUX%TIM(JO)
     SSS%VAL(JI) = AUX%VAL(JO)
     SSS%IB (JI,:) = AUX%IB(JO,:)
     SSS%JB (JI,:) = AUX%JB(JO,:)
     SSS%PQ (JI,1:NPQ) = AUX%PQ(JO,1:NPQ)
     SSS%KSAT(JI) = AUX%INST(JO)
     SSS%BIA(JI) = AUX%BIA(JO)
     SSS%BGERR(JI) = AUX%BGERR(JO)
   ENDIF

ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SLA SATELLITES'
DO JSAT=1,NOSLASATS
      SLA%ISLAOBS(JSAT) = COUNT( SLA%KSAT(1:SLA%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSLASATID(JSAT)),&
      & '   NO OF OBS:',SLA%ISLAOBS(JSAT)
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SST SATELLITES'
DO JSAT=1,NOSSTSATS
      SST%ISSTOBS(JSAT) = COUNT( SST%KSAT(1:SST%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSSTSATID(JSAT)),&
      & '   NO OF OBS:',SST%ISSTOBS(JSAT)
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' COUNTS FOR SSS SATELLITES'
DO JSAT=1,NOSSSSATS
      SSS%ISSSOBS(JSAT) = COUNT( SSS%KSAT(1:SSS%NO) .EQ. JSAT)
      WRITE(IOUNLOG,*) ' SAT:',JSAT,' ',TRIM(CSSSSATID(JSAT)),&
      & '   NO OF OBS:',SSS%ISSSOBS(JSAT)
ENDDO

CALL FLUSH(IOUNLOG)
CALL FLUSH(IOUNOUT)

CALL MYFRTPROF_WALL('READOBS103DD: READ OBS FILES FOR IDOUBLEDOM 2',1)
END SUBROUTINE READOBS103DD
