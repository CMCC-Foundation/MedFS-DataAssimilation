#ifdef NECSX
#undef SHARED_MEMORY
SUBROUTINE THINOUT_SLA(ITER,ZIN1,ZIN2)

!.. THINNING OF SLA OBSERVATIONS
!..
!.. FOR EACH SATELLITE DEFINE 2D THINNING
!.. BOXES, WITHIN WHICH ONLY THE OBS CLOSER TO
!.. ANALYSIS TIME IS RETAINED.
!..
!.. ANDREA STORTO *INGV* 2009-04-6

USE SET_KND
USE MYNETCDF
USE OBSDEF
USE OBS_STR, ONLY : SLA, NPQ
USE GRD_STR
USE IOUNITS
USE RUN, ONLY : LL_SAVETHINOUTSLA
USE MYFRTPROF

IMPLICIT NONE

 INTEGER(I4),INTENT(IN)   ::  ITER
 REAL(R8),   INTENT(IN)   ::  ZIN1, ZIN2
 REAL(R8) :: XRES,YRES,ZAUX,ZM
 INTEGER(I4)   ::  K,JOBS,IIND,JIND,KOKTH,KKOTH,NX,NY,MAXOBS,OIND
 INTEGER(I4)   ::  JX,JY,JSAT,KEVE_HTHN,PKN
 REAL(DP), ALLOCATABLE :: ZDIST(:,:)
 INTEGER(I4), ALLOCATABLE :: KCOUNTTH(:,:),KGOOD(:,:),KOBS(:,:,:)

#include "obs_events.h"

 CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',0)

  IF(SLA%NO.LE.0) THEN
    CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',1)
    RETURN
  ENDIF

  WRITE(IOUNOUT,*) ' *** SLA THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' *** SLA THINNING - ITERATION ',ITER

  KOKTH=0
  KKOTH=0
  XRES=ZIN1
  YRES=ZIN2

  IF(ITER.EQ.1) THEN
     PKN=1
     KEVE_HTHN = KEVE_HTH1
  ELSEIF(ITER.EQ.2) THEN
     PKN=2
     KEVE_HTHN = KEVE_HTH2
  ELSE
     CALL ABOR1('THINOUT: UNSUPPORTED NUMBER OF ITERATION')
  ENDIF

  NX = INT(GRD%IM/ZIN1) + 1
  NY = INT(GRD%JM/ZIN2) + 1

  WRITE(IOUNLOG,*) '*** SLA THINNING PRE-ALLOCATION'
  WRITE(IOUNLOG,*) '    ZIN1, ZIN2  == ', ZIN1 ,ZIN2
  WRITE(IOUNLOG,*) '    PKN         == ', PKN
  WRITE(IOUNLOG,*) '    XRES, YRES  == ', XRES ,YRES
  WRITE(IOUNLOG,*) '    NX, NY      == ', NX, NY
  CALL FLUSH(IOUNLOG)

  ALLOCATE(KCOUNTTH(NX,NY),  &
         & ZDIST(NX,NY),  &
         & KGOOD(NX,NY) )

 SLASAT : DO JSAT=1,NOSLASATS

  IF(SLA%ISLAOBS(JSAT) .LE. 0 ) CYCLE SLASAT

  WRITE(IOUNOUT,*) 'THINNING FOR SATELLITE '//TRIM(CSLASATID(JSAT))
  WRITE(IOUNLOG,*) 'THINNING FOR SATELLITE '//TRIM(CSLASATID(JSAT)),&
  & ', FOUND',SLA%ISLAOBS(JSAT),' OBS'

  MAXOBS = COUNT(SLA%FLC(1:SLA%NO).EQ.1 .AND. SLA%KSAT(1:SLA%NO) .EQ. JSAT)

  WRITE(IOUNLOG,*) '    SAT ', JSAT, ' MAXOBS = ',MAXOBS
  MAXOBS = MAX( NINT(MAXOBS/10._R8), 100)
  WRITE(IOUNLOG,*) '    ALLOCATION = ',MAXOBS

  IF( MAXOBS .EQ. 0 ) CYCLE SLASAT
  ALLOCATE( KOBS(NX,NY,MAXOBS) )

  KCOUNTTH=0
  ZDIST = 1000000._DP
  KGOOD=0

  CYOBS5: DO K=1,SLA%NO

       IF(SLA%FLC(K).NE.1 .OR. SLA%KSAT(K) .NE. JSAT) CYCLE CYOBS5

       !... COORDINATES ON PROJECTED PLAN
       IIND = INT( (REAL(SLA%IB(K,PKN),KIND=R8))/XRES) + 1
       JIND = INT( (REAL(SLA%JB(K,PKN),KIND=R8))/YRES) + 1

       IF( IIND .GT. NX .OR. JIND .GT. NY ) THEN
         WRITE(IOUNERR,*) ' IB, JB, SAT ::',IIND,JIND,JSAT
         WRITE(IOUNERR,*) ' I, J ::',SLA%IB(K,1), SLA%JB(K,1)
         CALL ABOR1('THINOUT: WRONG I,J INDEXES')
       ENDIF

       KCOUNTTH(IIND,JIND) = KCOUNTTH(IIND,JIND) + 1
       OIND = KCOUNTTH(IIND,JIND)
       IF(OIND.GT. MAXOBS) THEN
         WRITE(IOUNERR,*) ' I, J ::',IIND,JIND
         WRITE(IOUNERR,*) ' OIND VS MAXOBS :: ',OIND, MAXOBS
         CALL ABOR1('THINOUT: MAXOBS MUST BE INCREASED')
       ENDIF
       KOBS(IIND,JIND,OIND) = K

       IF(ABS(SLA%TDIST(K)) .LT. ZDIST(IIND,JIND) ) THEN
              ZDIST(IIND,JIND) = ABS(SLA%TDIST(K))
              KGOOD(IIND,JIND) = K
       ENDIF

  ENDDO CYOBS5

  DO JY=1,NY
    CYX : DO JX=1,NX

      IF(KCOUNTTH(JX,JY) .GT. 0 ) THEN
          IF (KGOOD(JX,JY).EQ.0 ) &
          & CALL ABOR1('SLA THINNING, PROBLEMS WITH MINIMUM DIST')

            DO K= 1, KCOUNTTH(JX,JY)
               IF( KGOOD(JX,JY) .NE. KOBS(JX,JY,K) ) THEN
                   JOBS = KOBS(JX,JY,K)
                   SLA%FLC(JOBS) = 0
                   SLA%EVE(JOBS) = KEVE_HTHN
                   KKOTH = KKOTH +1
               ELSE
                   KOKTH = KOKTH + 1
               ENDIF
            ENDDO

      ENDIF

    ENDDO CYX
  ENDDO

  IF(ITER.EQ.1 .AND. LL_SAVETHINOUTSLA ) &
  CALL WRITE_VARNCDF('thinout_'//TRIM(CSLASATID(JSAT))//'.nc',NX,NY,&
       KCOUNTTH,'occurr','','Number of Occurrences')

  DEALLOCATE( KOBS )

 ENDDO SLASAT

  WRITE(IOUNOUT,*) ' END SLA THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** THINNING REPORT - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' THINNING GRID : ',NX,' X',NY
  WRITE(IOUNLOG,*) ' FILTERED-OUT  : ',KKOTH
  WRITE(IOUNLOG,*) ' RETAINED      : ',KOKTH
  WRITE(IOUNLOG,*) ' TOTAL         : ',KOKTH + KKOTH
  WRITE(IOUNLOG,*) ' FLAGS COUNT   : ',SUM(SLA%FLC(1:SLA%NO))

  SLA%NC = KOKTH

  DEALLOCATE(KCOUNTTH,  &
         & ZDIST,  &
         & KGOOD )

 CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',1)

END SUBROUTINE THINOUT_SLA
#else
SUBROUTINE THINOUT_SLA(ITER,ZIN1,ZIN2)

!.. THINNING OF SLA OBSERVATIONS
!..
!.. FOR EACH SATELLITE DEFINE 2D THINNING
!.. BOXES, WITHIN WHICH ONLY THE VALID OBS CLOSER TO
!.. ANALYSIS TIME IS RETAINED.
!..
!.. ANDREA STORTO *INGV* 2009-04-6

USE SET_KND
USE MYNETCDF
USE OBSDEF
USE OBS_STR, ONLY : SLA, NPQ
USE GRD_STR
USE IOUNITS
USE RUN, ONLY : LL_SAVETHINOUTSLA
USE MYFRTPROF
#ifdef SHARED_MEMORY
USE OMP_LIB
#endif

IMPLICIT NONE

 INTEGER(I4),INTENT(IN)   ::  ITER
 REAL(R8),   INTENT(IN)   ::  ZIN1, ZIN2
 REAL(R8) :: XRES,YRES,ZAUX,ZM,ZDIST
 INTEGER(I4)   ::  K,JOBS,IIND,JIND,KOKTH(64),KKOTH(64),NX,NY,MAXOBS,OIND
 INTEGER(I4)   ::  JX,JY,JSAT,KEVE_HTHN,PKN,MYT,KGOOD
 INTEGER(I4), ALLOCATABLE :: KCOUNTTH(:,:),KOBS(:,:,:)

#include "obs_events.h"

 CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',0)

  IF(SLA%NO.LE.0) THEN
    CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',1)
    RETURN
  ENDIF

  WRITE(IOUNOUT,*) ' *** SLA THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' *** SLA THINNING - ITERATION ',ITER

  KOKTH=0
  KKOTH=0
  XRES=ZIN1
  YRES=ZIN2

  IF(ITER.EQ.1) THEN
     PKN=1
     KEVE_HTHN = KEVE_HTH1
  ELSEIF(ITER.EQ.2) THEN
     PKN=2
     KEVE_HTHN = KEVE_HTH2
  ELSE
     CALL ABOR1('THINOUT: UNSUPPORTED NUMBER OF ITERATION')
  ENDIF

  NX = INT(GRD%IM/ZIN1) + 1
  NY = INT(GRD%JM/ZIN2) + 1

  WRITE(IOUNLOG,*) '*** SLA THINNING PRE-ALLOCATION'
  WRITE(IOUNLOG,*) '    ZIN1, ZIN2  == ', ZIN1 ,ZIN2
  WRITE(IOUNLOG,*) '    PKN         == ', PKN
  WRITE(IOUNLOG,*) '    XRES, YRES  == ', XRES ,YRES
  WRITE(IOUNLOG,*) '    NX, NY      == ', NX, NY
  CALL FLUSH(IOUNLOG)

  ALLOCATE(KCOUNTTH(NX,NY) )

 SLASAT : DO JSAT=1,NOSLASATS

  IF(SLA%ISLAOBS(JSAT) .LE. 0 ) CYCLE SLASAT

  WRITE(IOUNOUT,*) 'THINNING FOR SATELLITE '//TRIM(CSLASATID(JSAT))
  WRITE(IOUNLOG,*) 'THINNING FOR SATELLITE '//TRIM(CSLASATID(JSAT)),&
  & ', FOUND',SLA%ISLAOBS(JSAT),' OBS'

  MAXOBS = COUNT(SLA%FLC(1:SLA%NO).EQ.1 .AND. SLA%KSAT(1:SLA%NO) .EQ. JSAT)

  WRITE(IOUNLOG,*) '    SAT ', JSAT, ' MAXOBS = ',MAXOBS
  MAXOBS = MAX( NINT(MAXOBS/10._R8), 100)
  WRITE(IOUNLOG,*) '    ALLOCATION = ',MAXOBS

  IF( MAXOBS .EQ. 0 ) CYCLE SLASAT
  ALLOCATE( KOBS(NX,NY,MAXOBS) )

  KCOUNTTH=0
  KGOOD=0

  CYOBS5: DO K=1,SLA%NO

       IF(SLA%FLC(K).NE.1 .OR. SLA%KSAT(K) .NE. JSAT) CYCLE CYOBS5

       !... COORDINATES ON PROJECTED PLAN
       IIND = INT( (REAL(SLA%IB(K,PKN),KIND=R8))/XRES) + 1
       JIND = INT( (REAL(SLA%JB(K,PKN),KIND=R8))/YRES) + 1

#ifndef NECSX
       IF( IIND .GT. NX .OR. JIND .GT. NY ) THEN
         WRITE(IOUNERR,*) ' IB, JB, SAT ::',IIND,JIND,JSAT
         WRITE(IOUNERR,*) ' I, J ::',SLA%IB(K,1), SLA%JB(K,1)
         CALL ABOR1('THINOUT: WRONG I,J INDEXES')
       ENDIF
#endif

       KCOUNTTH(IIND,JIND) = KCOUNTTH(IIND,JIND) + 1
       OIND = KCOUNTTH(IIND,JIND)
#ifndef NECSX
       IF(OIND.GT. MAXOBS) THEN
         WRITE(IOUNERR,*) ' I, J ::',IIND,JIND
         WRITE(IOUNERR,*) ' OIND VS MAXOBS :: ',OIND, MAXOBS
         CALL ABOR1('THINOUT: MAXOBS MUST BE INCREASED')
       ENDIF
#endif
       KOBS(IIND,JIND,OIND) = K

  ENDDO CYOBS5

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(JY,JX,K,JOBS,MYT,ZDIST,KGOOD)
MYT=OMP_GET_THREAD_NUM()+1
!$OMP DO SCHEDULE(DYNAMIC,1)
#else
MYT=1
#endif
  DO JY=1,NY
    CYX : DO JX=1,NX

      IF(KCOUNTTH(JX,JY) .EQ. 0 ) CYCLE CYX
          ZDIST = 1000000._R8
          KGOOD = 0
          DO K= 1, KCOUNTTH(JX,JY)
             IF( ABS(SLA%TDIST(KOBS(JX,JY,K))) .LT. ZDIST ) THEN
               ZDIST=ABS(SLA%TDIST(KOBS(JX,JY,K)))
               KGOOD=K
             ENDIF
          ENDDO

          IF (KGOOD.EQ.0 ) &
          & CALL ABOR1('SLA THINNING, PROBLEMS WITH MINIMUM DIST')

            DO K= 1, KCOUNTTH(JX,JY)
               IF( KGOOD .NE. K ) THEN
                   JOBS = KOBS(JX,JY,K)
                   SLA%FLC(JOBS) = 0
                   SLA%EVE(JOBS) = KEVE_HTHN
                   KKOTH(MYT) = KKOTH(MYT) +1
               ELSE
                   KOKTH(MYT) = KOKTH(MYT) + 1
               ENDIF
            ENDDO

    ENDDO CYX
  ENDDO
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

  IF(ITER.EQ.1 .AND. LL_SAVETHINOUTSLA ) &
  CALL WRITE_VARNCDF('thinout_'//TRIM(CSLASATID(JSAT))//'.nc',NX,NY,&
       KCOUNTTH,'occurr','','Number of Occurrences')

  DEALLOCATE( KOBS )

 ENDDO SLASAT

  WRITE(IOUNOUT,*) ' END SLA THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** THINNING REPORT - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' THINNING GRID : ',NX,' X',NY
  WRITE(IOUNLOG,*) ' FILTERED-OUT  : ',SUM(KKOTH)
  WRITE(IOUNLOG,*) ' RETAINED      : ',SUM(KOKTH)
  WRITE(IOUNLOG,*) ' TOTAL         : ',SUM(KOKTH) + SUM(KKOTH)
  WRITE(IOUNLOG,*) ' FLAGS COUNT   : ',SUM(SLA%FLC(1:SLA%NO))

  SLA%NC = SUM(KOKTH)

  DEALLOCATE(KCOUNTTH)

 CALL MYFRTPROF_WALL('THINOUT_SLA: SLA DATA THINNING',1)

END SUBROUTINE THINOUT_SLA
#endif
