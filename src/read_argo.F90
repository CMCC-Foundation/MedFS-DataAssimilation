SUBROUTINE READ_ARGO(CFILENAME,NUMLEVS,IOUN,MAXOBS,&
         & LLSTRICTPOS,LLSTRICTTIM,LSTRICTPRE,LSTRICTPSU,&
         & LSTRICTTEM,LGLOBTQF,LGLOBSQF,LGLOBPQF,LLCHECKTIME,LLGREYL,&
         & NTOTOBS,OBSPAR,OBSDEP,OBSVAL, OBSTIME, &
         & OBSLON,OBSLAT,OBSPROF,OBSPLAT)

!
! -> READ IN OBSERVATIONS FROM ARGO NETWORK (R OR D)
!    FROM NATIVE NETCDF FORMAT
!
!    ANDREA STORTO 2012-03
!

USE SET_KND
USE OBSDEF
USE RUN
USE NETCDF
USE CALENDAR, ONLY : TIME_DIFF,YMDS2JU

IMPLICIT NONE

CHARACTER(LEN=*) , INTENT(IN) :: CFILENAME
INTEGER(I4),INTENT(IN) :: NUMLEVS,IOUN, MAXOBS
INTEGER(I4),INTENT(OUT) :: NTOTOBS
!... SWITCHES
LOGICAL, INTENT(IN) :: LLSTRICTPOS,LLSTRICTTIM,LSTRICTPRE,LSTRICTPSU,&
         & LSTRICTTEM,LGLOBTQF,LGLOBSQF,LGLOBPQF,LLCHECKTIME,LLGREYL

REAL   (DP)  :: DATE_START, DATE_END, ZJULREF
INTEGER(I4) :: IYY,IMM,IDD,IHH,IMI,ISS
REAL   (DP)  :: ZSS,ZANDATELOC

INTEGER(I4) :: KERR(99),KOK(5),KCO(10)
INTEGER(I4) :: STAT, NCID, IDVAR, JOBS
INTEGER(I4) :: NDTSTR,NPROFS,NPARAMS,NLEVELS,KDTSTR
INTEGER(I4) :: JLEV,JPROF,ITYPE,KPROF

!... OBS STORAGE
INTEGER(I4), PARAMETER :: MAXPROFS = 50000
INTEGER(I4), DIMENSION(MAXOBS),INTENT(OUT) :: OBSPAR, OBSPROF
REAL   (DP), DIMENSION(MAXOBS),INTENT(OUT) :: OBSTIME
REAL   (R8), DIMENSION(MAXOBS),INTENT(OUT) :: OBSDEP,  OBSVAL, OBSLON,OBSLAT
CHARACTER(LEN=8 ), DIMENSION(MAXOBS),INTENT(OUT) :: OBSPLAT
CHARACTER(LEN=40) :: CREFDT

REAL   (R8)      , ALLOCATABLE, DIMENSION(:,:) :: TEMP,SALC,DEPTHC
REAL   (R8)      , ALLOCATABLE, DIMENSION(:,:) :: TEMP_A,SALC_A,DEPTHC_A
REAL   (R8)      , ALLOCATABLE, DIMENSION(:)   :: LATS,LONS
REAL   (DP)      , ALLOCATABLE, DIMENSION(:)   :: TIME
CHARACTER(LEN=4 ), ALLOCATABLE, DIMENSION(:)   :: INST
CHARACTER(LEN=8 ), ALLOCATABLE, DIMENSION(:)   :: PLANO
CHARACTER(LEN=NUMLEVS), DIMENSION(MAXPROFS) :: PQC, TQC, SQC
CHARACTER(LEN=NUMLEVS), DIMENSION(MAXPROFS) :: PQC_A, TQC_A, SQC_A
CHARACTER(LEN=MAXPROFS) :: TGQC,SGQC,POSQC,PRGQC,DATM,TIMQC

INTEGER(4)  :: IDIFFER1, IDIFFER2,IRETC
INTEGER(4), PARAMETER  :: MAXTYP=17
INTEGER(4)  :: CNTTYPES(MAXTYP), KTYPE, NTYPES(MAXTYP)

LOGICAL :: LPROFT, LPROFS, LPROFP, LOBS, LLPROFOK

INTEGER(4)  :: NNGL, JS
INTEGER(4)  :: NPARGL(MAXOBS), STDDGL(MAXOBS), ENDDGL(MAXOBS)
CHARACTER(LEN=8 ), DIMENSION(MAXOBS) :: OBSPLAT_GL

!  INITIALIZE
KCO=0
KERR=0
KOK =0
JOBS=0
CNTTYPES=0

NTYPES = (/831,840,841,842,845,846,847,850,851,&
         & 852,855,856,857,858,859,860,861/)

! READ GREY LIST
IF( LLGREYL ) THEN
   OPEN(761, FILE='ar_greylist.dat')
   READ(761,*) NNGL
   DO JS=1,NNGL
     READ(761,'(A8,X,I1,X,I5,X,I5)') OBSPLAT_GL(JS)(1:8), &
     & NPARGL(JS), STDDGL(JS), ENDDGL(JS)
     OBSPLAT_GL(JS)(1:8) = ADJUSTL( OBSPLAT_GL(JS)(1:8) )
   ENDDO
   CLOSE(761)
ENDIF

WRITE(IOUN,*)
WRITE(IOUN,*) '*** READ_ARGO : READING NETCDF FILE '//TRIM(CFILENAME)
WRITE(IOUN,*)

    STAT = NF90_OPEN(CFILENAME, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_INQ_DIMID (NCID, 'DATE_TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = KDTSTR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_PROF', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NPROFS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_PARAM', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NPARAMS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_DIMID (NCID, 'N_LEVELS', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NLEVELS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'NETCDF DIMENSIONS READ'

    IF(NPROFS.GT.MAXPROFS) &
    & CALL ABOR1('READ_ARGO : MAXPROFS MUST BE INCREASED!')
    IF(NLEVELS.GT.NUMLEVS) &
    & CALL ABOR1('READ_ARGO : NUMLEVS DOES NOT MATCH!')

    STAT = NF90_INQ_VARID (NCID, 'REFERENCE_DATE_TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,CREFDT(1:KDTSTR))
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'REFERENCE_DATE_TIME READ'

    IF(LLCHECKTIME) THEN

      WRITE(IOUN,*) 'REF DATE IS ', CREFDT(1:KDTSTR)

      READ(CREFDT(1:4),*) IYY
      READ(CREFDT(5:6),*) IMM
      READ(CREFDT(7:8),*) IDD
      READ(CREFDT(9:10),*) IHH
      READ(CREFDT(11:12),*) IMI
      READ(CREFDT(13:14),*) ISS

      ZSS = REAL(IHH,KIND=DP)/REAL(24,KIND=DP)  + &
      & REAL(IMI,KIND=DP)/REAL(24*60,KIND=DP)   + &
      & REAL(ISS,KIND=DP)/REAL(24*60*60,KIND=DP)

      CALL YMDS2JU(IYY,IMM,IDD,ZSS,ZJULREF)

      DATE_START = ZJULSTART - ZJULREF
      DATE_END   = ZJULEND   - ZJULREF

      WRITE(IOUN,*) ' DATESTART : ',DATE_START
      WRITE(IOUN,*) ' DATEEND   : ',DATE_END
      WRITE(IOUN,*) ' ... JULIAN DAYS SINCE '//CREFDT(1:10)

    ENDIF

    ALLOCATE(LATS(NPROFS),LONS(NPROFS),TIME(NPROFS),INST(NPROFS),&
           & PLANO(NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'LATITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LATS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LATITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'LONGITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LONS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LONGITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'JULD', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIME)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'JULD READ'

    STAT = NF90_INQ_VARID (NCID, 'WMO_INST_TYPE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,INST)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'WMO_INST_TYPE READ'

    STAT = NF90_INQ_VARID (NCID, 'PLATFORM_NUMBER', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,PLANO)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PLATFORM_NUMBER READ'

    IF(LLSTRICTPOS) THEN
          STAT = NF90_INQ_VARID (NCID, 'POSITION_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,POSQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'POSITION_QC READ'
    ENDIF

    IF(LLSTRICTTIM) THEN
          STAT = NF90_INQ_VARID (NCID, 'JULD_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TIMQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'POSITION_QC READ'
    ENDIF

    IF(LGLOBPQF) THEN
          STAT = NF90_INQ_VARID (NCID, 'PROFILE_PRES_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,PRGQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PROFILE_PRES_QC READ'
    ENDIF

    IF(LGLOBTQF) THEN
          STAT = NF90_INQ_VARID (NCID, 'PROFILE_TEMP_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TGQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PROFILE_TEMP_QC READ'
    ENDIF

    IF(LGLOBSQF) THEN
          STAT = NF90_INQ_VARID (NCID, 'PROFILE_PSAL_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,SGQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PROFILE_PSAL_QC READ'
    ENDIF

    STAT = NF90_INQ_VARID (NCID, 'DATA_MODE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DATM(1:NPROFS))
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'DATA_MODE READ'

    ALLOCATE(TEMP(NLEVELS,NPROFS),SALC(NLEVELS,NPROFS),DEPTHC(NLEVELS,NPROFS))
    ALLOCATE(TEMP_A(NLEVELS,NPROFS),SALC_A(NLEVELS,NPROFS),DEPTHC_A(NLEVELS,NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'TEMP', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TEMP)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'TEMP READ'

    STAT = NF90_INQ_VARID (NCID, 'TEMP_ADJUSTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TEMP_A)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'TEMP_ADJUSTED READ'

    STAT = NF90_INQ_VARID (NCID, 'PSAL', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,SALC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PSAL READ'

    STAT = NF90_INQ_VARID (NCID, 'PSAL_ADJUSTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,SALC_A)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PSAL_ADJUSTED READ'

    STAT = NF90_INQ_VARID (NCID, 'PRES', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DEPTHC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PRES READ'

    STAT = NF90_INQ_VARID (NCID, 'PRES_ADJUSTED', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DEPTHC_A)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PRES_ADJUSTED READ'

    IF(LSTRICTPSU) THEN
          STAT = NF90_INQ_VARID (NCID, 'PSAL_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,SQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PSAL_QC READ'

          STAT = NF90_INQ_VARID (NCID, 'PSAL_ADJUSTED_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,SQC_A(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PSAL_ADJUSTED_QC READ'
    ENDIF

    IF(LSTRICTTEM) THEN
          STAT = NF90_INQ_VARID (NCID, 'TEMP_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'TEMP_QC READ'

          STAT = NF90_INQ_VARID (NCID, 'TEMP_ADJUSTED_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,TQC_A(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'TEMP_ADJUSTED_QC READ'
    ENDIF

    IF(LSTRICTPRE) THEN
          STAT = NF90_INQ_VARID (NCID, 'PRES_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,PQC(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PRES_QC READ'

          STAT = NF90_INQ_VARID (NCID, 'PRES_ADJUSTED_QC', IDVAR)
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          STAT = NF90_GET_VAR (NCID,IDVAR,PQC_A(1:NPROFS))
          IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
          WRITE(IOUN,*) 'PRES_ADJUSTED_QC READ'
    ENDIF

    STAT = NF90_CLOSE(NCID)

!========    OBS SELECTION    ========!

    KPROF=0

    CYCLE_PROF : DO JPROF = 1, NPROFS

       LLPROFOK = .FALSE.
       LPROFT=.TRUE.
       LPROFS=.TRUE.
       LPROFP=.TRUE.

       PLANO(JPROF)(1:8) = ADJUSTL(PLANO(JPROF)(1:8))

       IF( LLGREYL ) THEN
           DO JS=1,NNGL
             IF( PLANO(JPROF)(1:8) .EQ. OBSPLAT_GL(JS)(1:8) .AND. &
                 TIME(JPROF) .GE. REAL(STDDGL(JS),DP) .AND. &
                 TIME(JPROF) .LE. REAL(ENDDGL(JS),DP) ) THEN
               KERR(18) = KERR(18) + 1
               IF( NPARGL(JS) .EQ. KKTEMP ) LPROFT = .FALSE. 
               IF( NPARGL(JS) .EQ. KKSAL  ) LPROFS = .FALSE. 
               IF( NPARGL(JS) .EQ. KKPRE  ) LPROFP = .FALSE. 
             ENDIF
           ENDDO
       ENDIF

       !... CHECK TIME
       IF(LLCHECKTIME) THEN
           IF( TIME(JPROF) .LT. DATE_START .OR. &
                TIME(JPROF) .GT. DATE_END   ) THEN
               KERR(15) = KERR(15) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF

       !... CHECK LATLON
       IF ( LATS(JPROF) .LT. -89.999999 .OR. LATS(JPROF) .GT. 89.999999 .OR. &
          & LONS(JPROF) .LT.-179.999999 .OR. LONS(JPROF) .GT.179.999999 ) THEN
           KERR(1) = KERR(1) + 1
           CYCLE CYCLE_PROF
       ENDIF
       IF ( LLSTRICTPOS ) THEN
           IF( ISNOGOOD( POSQC(JPROF:JPROF) ) ) THEN
               KERR(4) = KERR(4) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF

       !... CHECK DATE
       IF (TIME(JPROF) .LE. 0._DP  ) THEN
           KERR(2) = KERR(2) + 1
           CYCLE CYCLE_PROF
       ENDIF
       IF ( LLSTRICTTIM ) THEN
           IF( ISNOGOOD( TIMQC(JPROF:JPROF) ) ) THEN
               KERR(17) = KERR(17) + 1
               CYCLE CYCLE_PROF
           ENDIF
       ENDIF

       KTYPE=ARG_TYPE(INST(JPROF)(2:4))

       CNTTYPES(KTYPE) = CNTTYPES(KTYPE) +1

       IF(LGLOBTQF) THEN
            IF(TGQC(JPROF:JPROF) .EQ. 'F' ) THEN
               KERR(7) = KERR(7) +1
               LPROFT=.FALSE.
            ENDIF
       ENDIF

       IF(LGLOBSQF) THEN
            IF(SGQC(JPROF:JPROF) .EQ. 'F' ) THEN
               KERR(8) = KERR(8) +1
               LPROFS=.FALSE.
            ENDIF
       ENDIF

       IF(LGLOBPQF) THEN
            IF(PRGQC(JPROF:JPROF) .EQ. 'F' ) THEN
               KERR(8) = KERR(8) +1
               LPROFP=.FALSE.
            ENDIF
       ENDIF

       IF(.NOT.LPROFT .AND. .NOT.LPROFS .OR. .NOT.LPROFP) THEN
            KERR(5) = KERR(5) + 1
            CYCLE CYCLE_PROF
       ENDIF

       KOK(4) = KOK(4) + 1

       CYCLE_LEV : DO JLEV = 1,NLEVELS

          IF(DEPTHC(JLEV,JPROF) .GT. 15000. ) THEN
                 KERR(6) = KERR(6) +1
                 CYCLE CYCLE_LEV
          ENDIF
          IF( LSTRICTPRE ) THEN
            IF( ( ISREALT( DATM,JPROF) .AND. ISNOGOOD( PQC(JPROF)(JLEV:JLEV) )  ) .OR. &
                ( ISDELAT( DATM,JPROF) .AND. ISNOGOOD( PQC_A(JPROF)(JLEV:JLEV) )  ) ) THEN
                 KERR(16) = KERR(16) +1
                 CYCLE CYCLE_LEV
            ENDIF
          ENDIF

        !... TEMPERATURE
          IF(LPROFT) THEN
            IF (TEMP(JLEV,JPROF).LE. 40._R8 .AND. TEMP(JLEV,JPROF) .GE. -2._R8 ) THEN
                  LOBS=.TRUE.
                  IF(LSTRICTTEM) THEN
                    IF( ISNOGOOD( TQC(JPROF)(JLEV:JLEV) ) .AND. ISREALT( DATM,JPROF) ) LOBS=.FALSE.
                    IF( ISNOGOOD( TQC_A(JPROF)(JLEV:JLEV) ) .AND. ISDELAT( DATM,JPROF) ) LOBS=.FALSE.
                  ENDIF
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READ_ARGO : MAXOBS MUST BE INCREASED!')
                    OBSPAR(JOBS) = KKTEMP
                    IF( ISREALT( DATM,JPROF) ) THEN
                      OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                      OBSVAL(JOBS) = TEMP(JLEV,JPROF)
                    ELSE
                      OBSDEP(JOBS) = DEPTHC_A(JLEV,JPROF)
                      OBSVAL(JOBS) = TEMP_A(JLEV,JPROF)
                    ENDIF
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    IF( SALC(JLEV,JPROF) .LE. 40._R8 .AND. SALC(JLEV,JPROF) .GT.  0._R8 ) THEN
                      IF( SALC_A(JLEV,JPROF) .LE. 40._R8 .AND. SALC_A(JLEV,JPROF) .GT.  0._R8 ) THEN
                        OBSVAL(JOBS) = POTEMP( SALC_A(JLEV,JPROF), OBSVAL(JOBS),&
                        & DEP_TO_P(DEPTHC(JLEV,JPROF),LATS(JPROF)), 0._R8 )
                      ELSE
                        OBSVAL(JOBS) = POTEMP( SALC(JLEV,JPROF), OBSVAL(JOBS),&
                        & DEP_TO_P(DEPTHC(JLEV,JPROF),LATS(JPROF)), 0._R8 )
                      ENDIF
                    ELSE
                      OBSVAL(JOBS) = OBSVAL(JOBS) + 200._R8
                    ENDIF
                    KOK(1) = KOK(1) + 1
                  ELSE
                    KERR(9) = KERR(9) +1
                  ENDIF
            ELSE
                  KERR(10) = KERR(10) +1
            ENDIF
          ENDIF

        !... SALINITY
          IF(LPROFS) THEN
            IF (SALC(JLEV,JPROF).LE. 60._R8 .AND. SALC(JLEV,JPROF) .GE. 0._R8 ) THEN
                  LOBS=.TRUE.
                  IF(LSTRICTPSU) THEN
                    IF( ISNOGOOD( SQC(JPROF)(JLEV:JLEV) ) .AND. ISREALT( DATM,JPROF) ) LOBS=.FALSE.
                    IF( ISNOGOOD( SQC_A(JPROF)(JLEV:JLEV) ) .AND. ISDELAT( DATM,JPROF) ) LOBS=.FALSE.
                  ENDIF
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READ_ARGO : MAXOBS MUST BE INCREASED!')
                    OBSPAR(JOBS) = KKSAL
                    IF( ISREALT( DATM,JPROF) ) THEN
                      OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                      OBSVAL(JOBS) = SALC(JLEV,JPROF)
                    ELSE
                      OBSDEP(JOBS) = DEPTHC_A(JLEV,JPROF)
                      OBSVAL(JOBS) = SALC_A(JLEV,JPROF)
                    ENDIF
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    KOK(2) = KOK(2) + 1
                  ELSE
                    KERR(11) = KERR(11) +1
                  ENDIF
            ELSE
                  KERR(12) = KERR(12) +1
            ENDIF
          ENDIF

       ENDDO CYCLE_LEV

    ENDDO CYCLE_PROF

  NTOTOBS=JOBS

  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** REPORT OF READ_ARGO : ',&
  & 'READING OF ARGO PROFILING FLOAT OBSERVATIONS'
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // OPTIONS'
  WRITE(IOUN,*) ' IS TIME OF PROFILE CHECKED ?        : ',LLCHECKTIME
  WRITE(IOUN,*) ' IS QUALITY OF POSITION CHECKED ?    : ',LLSTRICTPOS
  WRITE(IOUN,*) ' IS QUALITY OF DATETIME CHECKED ?    : ',LLSTRICTTIM
  WRITE(IOUN,*) ' IS QUALITY OF SALINITY CHECKED ?    : ',LSTRICTPSU
  WRITE(IOUN,*) ' IS QUALITY OF TEMPERAT CHECKED ?    : ',LSTRICTTEM
  WRITE(IOUN,*) ' IS QUALITY OF PRESSURE CHECKED ?    : ',LSTRICTPRE
  WRITE(IOUN,*) ' IS PROFILE QUALITY OF TEMP USED ?   : ',LGLOBTQF
  WRITE(IOUN,*) ' IS PROFILE QUALITY OF SALI USED ?   : ',LGLOBSQF
  WRITE(IOUN,*) ' IS PROFILE QUALITY OF PRES USED ?   : ',LGLOBPQF
  WRITE(IOUN,*) ' IS GREY LIST USED TO REJECT PROFS ? : ',LLGREYL
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // DIMENSION'
  WRITE(IOUN,*) ' LEVELS      : ',NLEVELS
  WRITE(IOUN,*) ' PROFILES    : ',NPROFS
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // ARGO PROFILES BY TYPE '
  DO KTYPE=1,MAXTYP
     WRITE(IOUN,*) NTYPES(KTYPE),'   :',CNTTYPES(KTYPE)
  ENDDO
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // REPORT'
  WRITE(IOUN,*) ' TOTAL OBS STORED                :', NTOTOBS
  WRITE(IOUN,*) ' PROFILES WITH TIME OUT OF WINDOW:', KERR(15)
  WRITE(IOUN,*) ' PROFILES WITH POSITION PROBLEMS :', KERR(1)
  WRITE(IOUN,*) ' PROFILES WITH DATETIME PROBLEMS :', KERR(2)
  WRITE(IOUN,*) ' PROFILES WITH UNKNOWN INSTRUMENT:', KERR(3)
  WRITE(IOUN,*) ' POSITION QUALITY CHECK NEGATIVE :', KERR(4)
  WRITE(IOUN,*) ' TIME     QUALITY CHECK NEGATIVE :', KERR(17)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR TEMPER. :', KERR(7)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR SALINITY:', KERR(8)
  WRITE(IOUN,*) ' PROFILE QC NEGATIVE FOR T AND S :', KERR(5)
  WRITE(IOUN,*) ' NOT VALID DEPTHS                :', KERR(6)

  WRITE(IOUN,*) ' PRFOILE REJ FOR GREYLIST        :', KERR(18)

  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR T   :', KERR(9)
  WRITE(IOUN,*) ' NOT VALID VALUE FOR T           :', KERR(10)
  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR S   :', KERR(11)
  WRITE(IOUN,*) ' NOT VALID VALUE FOR S           :', KERR(12)
  WRITE(IOUN,*) ' OBSERVATION QC NEGATIVE FOR PRES:', KERR(16)

  WRITE(IOUN,*) ' VALID PROFILES                  :', KOK (4)
  WRITE(IOUN,*) ' EXTRACTED OBS OF SALINITY       :', KOK (2)
  WRITE(IOUN,*) ' EXTRACTED OBS OF TEMPERATURE    :', KOK (1)
  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** END OF READ_ARGO'
  WRITE(IOUN,*)

  DEALLOCATE(LATS,LONS,TIME,INST,PLANO)
  DEALLOCATE(TEMP,SALC,DEPTHC)
  DEALLOCATE(TEMP_A,SALC_A,DEPTHC_A)

CONTAINS 

LOGICAL FUNCTION ISNOGOOD( CHIN )
IMPLICIT NONE
CHARACTER(LEN=1) :: CHIN
ISNOGOOD = .FALSE.
IF( CHIN(1:1) .EQ. '3' .OR. CHIN(1:1) .EQ. '4' ) ISNOGOOD = .TRUE.
END FUNCTION ISNOGOOD

LOGICAL FUNCTION ISREALT( CHIN,IPOS )
IMPLICIT NONE
CHARACTER(LEN=MAXPROFS) :: CHIN
INTEGER :: IPOS
ISREALT = .FALSE.
IF( CHIN(IPOS:IPOS) .EQ. 'R' ) ISREALT = .TRUE.
END FUNCTION ISREALT

LOGICAL FUNCTION ISDELAT( CHIN,IPOS )
IMPLICIT NONE
CHARACTER(LEN=MAXPROFS) :: CHIN
INTEGER :: IPOS
ISDELAT = ( .NOT. ISREALT(CHIN,IPOS) )
END FUNCTION ISDELAT

INTEGER FUNCTION ARG_TYPE(CIN)
IMPLICIT NONE
CHARACTER(LEN=3) :: CIN
INTEGER :: NIN, PC
READ(CIN,*) NIN
   SELECT CASE( NIN )
     CASE(831)
             PC=1
     CASE(840)
             PC=2
     CASE(841)
             PC=3
     CASE(842)
             PC=4
     CASE(845)
             PC=5
     CASE(846)
             PC=6
     CASE(847)
             PC=7
     CASE(850)
             PC=8
     CASE(851)
             PC=9
     CASE(852)
             PC=10
     CASE(855)
             PC=11
     CASE(856)
             PC=12
     CASE(857)
             PC=13
     CASE(858)
             PC=14
     CASE(859)
             PC=15
     CASE(860)
             PC=16
     CASE(861)
             PC=17
     CASE DEFAULT
             PC=0
   ENDSELECT
ARG_TYPE=PC
END FUNCTION ARG_TYPE

   REAL(KIND=R8) FUNCTION dep_to_p( p_dep, p_lat )
      REAL(KIND=R8), INTENT(IN) :: p_dep    ! Depth in meters
      REAL(KIND=R8), INTENT(IN) :: p_lat    ! Latitude in degrees
      REAL(KIND=R8) :: z_x
      REAL(KIND=R8) :: z_c1
      REAL(KIND=R8) :: z_c2
      REAL(KIND=R8) :: z_d

      z_x = SIN( p_lat / 57.29578 )
      z_x = z_x * z_x
      z_c1 = ( 5.92  + 5.25 * z_x ) * 1e-3
      z_c2 = 2.21e-6
      z_d = ( z_c1 - 1 ) * ( z_c1 - 1  ) - 4 * z_c2 * p_dep
      dep_to_p = (( 1 - z_c1 ) - SQRT( z_d )) / ( 2 * z_c2 )
   END FUNCTION dep_to_p

   REAL(KIND=R8) FUNCTION potemp( ps, pt, pp, ppr )

      REAL(KIND=R8), INTENT(IN) :: ps
      REAL(KIND=R8), INTENT(IN) :: pt
      REAL(KIND=R8), INTENT(IN) :: pp
      REAL(KIND=R8), INTENT(IN) :: ppr

      REAL(KIND=R8) :: zpol
      REAL(KIND=R8), PARAMETER :: a1 =  1.067610e-05
      REAL(KIND=R8), PARAMETER :: a2 = -1.434297e-06
      REAL(KIND=R8), PARAMETER :: a3 = -7.566349e-09
      REAL(KIND=R8), PARAMETER :: a4 = -8.535585e-06
      REAL(KIND=R8), PARAMETER :: a5 =  3.074672e-08
      REAL(KIND=R8), PARAMETER :: a6 =  1.918639e-08
      REAL(KIND=R8), PARAMETER :: a7 =  1.788718e-10

      zpol = a1 + a2 * ps + a3 * ( pp + ppr ) + a4 * pt &
         & + a5 * ps * pt + a6 * pt * pt + a7 * pt * ( pp + ppr )

      potemp = pt + ( pp - ppr ) * zpol

   END FUNCTION potemp
END SUBROUTINE READ_ARGO
