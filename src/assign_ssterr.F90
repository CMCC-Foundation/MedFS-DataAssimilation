SUBROUTINE ASSIGN_SSTERR

!.. OBS ERRORS ASSIGNEMENT FOR SPACE-BORNE SST OBS
!..
!.. ANDREA STORTO, 2009


USE SET_KND
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE OBSHANDLING
USE MYFRTPROF
USE MYNETCDF

IMPLICIT NONE

INTEGER(I4) :: JK,ISAT
REAL(R8) :: ZREP(GRD%IM,GRD%JM), ZR
REAL(DP) :: TD, TD2, OETIM, ZT2

CALL MYFRTPROF_WALL('ASSIGN_SSTERR: ASSIGN SST OBS ERROR',0)

!... FOR SST, CYCLE THE OBS

IF(LL_SST_REPRERR ) THEN
   CALL GETNCVAR('SST_repres.nc','sst_stdev',GRD%IM,GRD%JM,ZREP)
ELSE
   ZREP = 0._R8
ENDIF

CYOBS : DO JK=1,SST%NO

     IF(SST%FLC(JK) .EQ. 0 ) CYCLE CYOBS

     ! FOR SWATH DATA WE USE THE ERROR ESTIMATION
     ! FROM THE RETRIEVAL ALGORITHM, REASONABLY BOUNDED

     IF(SST%KSAT(JK) .EQ. 4 .OR. SST%KSAT(JK) .EQ. 5 ) THEN
       SST%ERR(JK) = MAX(MIN(SST%ERR(JK),1.2_R8),0.5_R8)
     ELSEIF(SST%KSAT(JK) .EQ. 7 .OR. SST%KSAT(JK) .EQ. 8 .OR. &
     & SST%KSAT(JK) .EQ. 9 .OR. SST%KSAT(JK) .EQ. 10) THEN
       SST%ERR(JK) = MAX(MIN(SST%ERR(JK),1.5_R8),0.1_R8)
     ELSEIF(SST%KSAT(JK) .EQ. 6) THEN ! NOAA/REYNOLDS DATA
       SST%ERR(JK) = MIN(MAX(0.1_R8,SST%ERR(JK)), 3.0_R8 )  
     ELSE
       SST%ERR(JK) = SST_ERRS(SST%KSAT(JK))
     ENDIF

     TD=MIN(ZTIME_WEIGTH,ABS(SST%TDIST(JK)))
     TD2=TD*TD
     ZT2=ZTIME_WEIGTH*ZTIME_WEIGTH
     OETIM = 1._DP / EXP( -TD2 / ZT2 )

     ZR = OSUM( SST%PQ(JK,1:NPQ),ZREP(SST%IB(JK,1:NPQ),SST%JB(JK,1:NPQ)) )

     SST%ERR(JK) = SQRT(SST%ERR(JK)**2+ZR**2)*REAL(OETIM,R8)

ENDDO CYOBS

CALL MYFRTPROF_WALL('ASSIGN_SSTERR: ASSIGN SST OBS ERROR',1)

END SUBROUTINE ASSIGN_SSTERR
