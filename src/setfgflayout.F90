#undef SHARED_MEMORY
SUBROUTINE SETFGFLAYOUT

USE SET_KND
USE IOUNITS, ONLY : IOUNERR,IOUNOUT,IOUNLOG
USE NETCDF
USE GRD_STR
USE READFG
USE RUN
USE MPIREL

IMPLICIT NONE

INTEGER(I4) :: NCID, STAT, IDVAR, JF, IAUX(2), IAUX2(2), IERR

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// SETUP FIRST-GUESS LAYOUT'

LLMPF = ( NSUBDOMAINS .GT. 1 )

WRITE(IOUNLOG,*) ' MULTIDOMAIN READING : ',LLMPF

IF( NSUBDOMAINS .GT. MAXFILES ) CALL ABOR1('MAXFILES TO INCREASE')

IF(.NOT.LLMPF) THEN
    KDPF(1,1)=1
    KDPF(1,2)=1
    KDPL(1,1)=GRD%IM
    KDPL(1,2)=GRD%JM
    KDHSS(1,1)=0
    KDHSS(1,2)=0
    KDHSE(1,1)=0
    KDHSE(1,2)=0
    KDSL(1,1)=GRD%IM
    KDSL(1,2)=GRD%JM

    KDNS=KDPF+KDHSS
    KDNE=KDPL-KDHSE
    KDLS=1+KDHSS
    KDLE=KDSL-KDHSE

ELSE
  IF(LLAYOUTFILE) THEN
     IF(LMPION) THEN
       OPEN(712,FILE='domains.dat'//'.'//TRIM(CMPIDOM),STATUS='OLD',IOSTAT=IERR)
     ELSE
       OPEN(712,FILE='domains.dat',STATUS='OLD',IOSTAT=IERR)
     ENDIF
     IF(IERR.NE.0) CALL ABOR1('SETFGFLAYOUT: CANNOT OPEN DOMAINS.DAT')
     DO JF=1,NSUBDOMAINS
        READ(712,*,IOSTAT=IERR) IAUX(1:2),KDPF(JF,1:2),KDPL(JF,1:2),KDHSS(JF,1:2),&
        & KDHSE(JF,1:2),KDSL(JF,1:2),IAUX2(1:2),KDNS(JF,1:2),KDNE(JF,1:2),&
        & KDLS(JF,1:2),KDLE(JF,1:2)
        IF(IERR.NE.0) CALL ABOR1('SETFGFLAYOUT: CANNOT READ DOMAINS.DAT')
     ENDDO
     CLOSE(712)
  ELSE
! ...BEWARE OF POSSIBLE SPURIOUS VALUES WHEN NF90_GET_ATT READ ARRAYS...
#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(JF,STAT,IAUX)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
    DO JF=1,NSUBDOMAINS
       WRITE(IOUNLOG,*) ' ABOUT TO OPEN FILE '//TRIM(CFGFILES(JF))
       STAT = NF90_OPEN(CFGFILES(JF), NF90_NOWRITE, NCIDFG(JF))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_NUMBER_TOTAL',IAUX(1))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       IF(IAUX(1).NE.NSUBDOMAINS) THEN
          WRITE(IOUNERR,*) &
          & 'FILE ',TRIM(CFGFILES(JF)),' CONTAINS WRONG DOMAIN_NUMBER_TOTAL'
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_NUMBER_TOTAL')
       ENDIF

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_POSITION_FIRST',KDPF(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_POSITION_LAST',KDPL(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_HALO_SIZE_START',KDHSS(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_HALO_SIZE_END',KDHSE(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_SIZE_LOCAL',KDSL(JF,1:2))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT=NF90_GET_ATT(NCIDFG(JF),NF90_GLOBAL,'DOMAIN_SIZE_GLOBAL',IAUX)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

       WRITE(IOUNLOG,*) 'DOMAIN',JF,NCIDFG(JF),':',&
       & KDPF(JF,:),KDPL(JF,:),KDHSS(JF,:),KDHSE(JF,:),KDSL(JF,:),IAUX(:)
       CALL FLUSH(IOUNLOG)

       IF(IAUX(1).NE.GRD%IM .OR.IAUX(2).NE.GRD%JM) THEN
          WRITE(IOUNERR,*) '/// DOMAIN',JF,' FILE',JF-1
          WRITE(IOUNERR,*) 'SETFGFLAYOUT: GLOBAL DIMS MISMATCH'
          WRITE(IOUNERR,*) 'FOUND    :',IAUX(1),IAUX(2)
          WRITE(IOUNERR,*) 'EXPECTED :',GRD%IM,GRD%JM
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_SIZE_GLOBAL')
       ENDIF
       IF( KDPL(JF,1)-KDPF(JF,1)+1.NE.KDSL(JF,1) .OR. &
         & KDPL(JF,2)-KDPF(JF,2)+1.NE.KDSL(JF,2) ) THEN
          WRITE(IOUNERR,*) '/// DOMAIN',JF,' FILE',JF-1
          WRITE(IOUNERR,*) 'SETFGFLAYOUT: LOCAL DIMS MISMATCH'
          WRITE(IOUNERR,*) 'FOUND    :',KDSL(JF,1:2)
          WRITE(IOUNERR,*) 'EXPECTED :',KDPL(JF,1)-KDPF(JF,1)+1,&
          & KDPL(JF,2)-KDPF(JF,2)+1
          CALL ABOR1('SETFGFLAYOUT: WRONG DOMAIN_SIZE_LOCAL')
       ENDIF

       STAT = NF90_CLOSE(NCIDFG(JF))
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ENDDO
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

    KDNS=KDPF+KDHSS
    KDNE=KDPL-KDHSE
    KDLS=1+KDHSS
    KDLE=KDSL-KDHSE

  ENDIF
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' LAYOUT FILE READING'
DO JF=1,MAX(1,NSUBDOMAINS)
  WRITE(IOUNLOG,*) ' DOM ',JF, ' :: ',KDPF(JF,1:2),KDPL(JF,1:2),KDHSS(JF,1:2),&
        & KDHSE(JF,1:2),KDSL(JF,1:2),KDNS(JF,1:2),KDNE(JF,1:2),&
        & KDLS(JF,1:2),KDLE(JF,1:2)
ENDDO

CALL FLUSH(IOUNLOG)
END SUBROUTINE SETFGFLAYOUT
