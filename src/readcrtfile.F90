SUBROUTINE READCRTFILE(CFILENAME,NUMLEVS,IOUN,MAXOBS,&
         & NTOTOBS,OBSTYPE, OBSPAR,OBSDEP,OBSVAL, OBSTIME, &
         & OBSLON,OBSLAT,OBSPROF,OBSPLAT)

!
! -> READ IN OBSERVATIONS FROM ENSEMBLES INS
!    FROM NATIVE NETCDF FORMAT
!
!    ANDREA STORTO _2009-03_
!

USE SET_KND
USE OBSDEF
USE RUN
USE NETCDF
USE CALENDAR, ONLY : TIME_DIFF,YMDS2JU

IMPLICIT NONE

CHARACTER(LEN=*) , INTENT(IN) :: CFILENAME
INTEGER(I4),INTENT(IN) :: NUMLEVS,IOUN, MAXOBS
INTEGER(I4),INTENT(OUT) :: NTOTOBS

REAL   (DP)  :: DATE_START, DATE_END, ZJULREF
INTEGER(I4) :: IYY,IMM,IDD,IHH,IMI,ISS
REAL   (DP)  :: ZSS,ZANDATELOC

INTEGER(I4) :: KERR(59),KOK(5),KCO(10)
INTEGER(I4) :: STAT, NCID, IDVAR, JOBS
INTEGER(I4) :: NDTSTR,NPROFS,NPARAMS,NLEVELS,KDTSTR
INTEGER(I4) :: JLEV,JPROF,ITYPE

!... OBS STORAGE
INTEGER(I4), PARAMETER :: MAXPROFS = 99000
INTEGER(I4), DIMENSION(MAXOBS),INTENT(OUT) :: OBSTYPE, OBSPAR, OBSPROF
REAL   (DP), DIMENSION(MAXOBS),INTENT(OUT) :: OBSTIME
REAL   (R8), DIMENSION(MAXOBS),INTENT(OUT) :: OBSDEP,  OBSVAL, OBSLON,OBSLAT
CHARACTER(LEN=8 ), DIMENSION(MAXOBS),INTENT(OUT) :: OBSPLAT
CHARACTER(LEN=40) :: CREFDT

REAL   (R8)      , ALLOCATABLE, DIMENSION(:,:) :: TEMP,SALC,DEPTHC
REAL   (R8)      , ALLOCATABLE, DIMENSION(:)   :: LATS,LONS
REAL   (DP)      , ALLOCATABLE, DIMENSION(:)   :: TIME
INTEGER(I4),       ALLOCATABLE, DIMENSION(:)   :: INST
CHARACTER(LEN=32), ALLOCATABLE, DIMENSION(:)   :: PLANO
CHARACTER(LEN=NUMLEVS), DIMENSION(MAXPROFS) :: TQC, SQC
CHARACTER(LEN=MAXPROFS) :: TGQC,SGQC,POSQC,TIMQC

INTEGER(4)  :: IDIFFER1, IDIFFER2,IRETC

INTEGER(4)  :: KPROF

LOGICAL :: LPROFT, LPROFS, LOBS, LLPROFOK

!  INITIALIZE
KCO=0
KERR=0
KOK =0
JOBS=0

WRITE(IOUN,*)
WRITE(IOUN,*) '*** READCRTFILE : READING NETCDF FILE '//TRIM(CFILENAME)
WRITE(IOUN,*)

    STAT = NF90_OPEN(CFILENAME, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_INQ_DIMID (NCID, 'TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = KDTSTR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    NPROFS = KDTSTR

    STAT = NF90_INQ_DIMID (NCID, 'DEPTH', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NLEVELS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'NETCDF DIMENSIONS READ'

    IF(NPROFS.GT.MAXPROFS) &
    & CALL ABOR1('READCRTFILE : MAXPROFS MUST BE INCREASED!')
    IF(NLEVELS.GT.NUMLEVS) &
    & CALL ABOR1('READCRTFILE : NUMLEVS DOES NOT MATCH!')

    ALLOCATE(LATS(NPROFS),LONS(NPROFS),TIME(NPROFS),INST(NPROFS),&
           & PLANO(NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'LATITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LATS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LATITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'LONGITUDE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LONS)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'LONGITUDE READ'

    STAT = NF90_INQ_VARID (NCID, 'TIME', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIME)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'TIME READ'

    CALL YMDS2JU(1950,1,1,0._DP,ZJULREF)

    DATE_START = ZJULSTART - ZJULREF
    DATE_END   = ZJULEND   - ZJULREF

    WRITE(IOUN,*) ' DATESTART : ',DATE_START
    WRITE(IOUN,*) ' DATEEND   : ',DATE_END
    WRITE(IOUN,*) ' ... JULIAN DAYS SINCE 19500101000000'

    STAT = NF90_INQ_VARID (NCID, 'WMO_INST_TYPE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,INST)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'WMO_INST_TYPE READ'

    STAT = NF90_INQ_VARID (NCID, 'DC_REFERENCE', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,PLANO)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'DC_REFERENCE'

    ALLOCATE(TEMP(NLEVELS,NPROFS),SALC(NLEVELS,NPROFS),DEPTHC(NLEVELS,NPROFS))

    STAT = NF90_INQ_VARID (NCID, 'TEMP', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TEMP)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'TEMP READ'

    STAT = NF90_INQ_VARID (NCID, 'PSAL', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,SALC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'PSAL READ'

    STAT = NF90_INQ_VARID (NCID, 'DEPH', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DEPTHC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    WRITE(IOUN,*) 'DEPH READ'

    STAT = NF90_CLOSE(NCID)


!========    OBS SELECTION    ========!

    KPROF=0

    CYCLE_PROF : DO JPROF = 1, NPROFS

       LLPROFOK = .FALSE.
       PLANO(JPROF)(1:32) = ADJUSTL(PLANO(JPROF)(1:32))

       IF( TIME(JPROF) .LT. DATE_START .OR. &
           TIME(JPROF) .GT. DATE_END   ) THEN
           CYCLE CYCLE_PROF
       ENDIF


!... A.S.: OBS TYPE DEFINITION IS IN ACCORDANCE WITH
!    UKMO SIMPLIFIED VERSION OF WMO 1770 TABLE

       SELECT CASE(INST(JPROF))

            CASE(401)   ! XBT/MBT
               ITYPE=KKXBT
               KCO(1) = KCO(1) + 1
            CASE(741)   ! TESAC
               ITYPE=KKTESAC
               KCO(2) = KCO(2) + 1
            CASE(831)   ! ARGO
               ITYPE=KKARGO
               KCO(3) = KCO(3) + 1
            CASE(820)   ! BUOYS
               ITYPE=KKBUOY
               KCO(4) = KCO(4) + 1
            CASE DEFAULT
               WRITE(IOUN,*) &
               & 'READCRTFILE : FOUND OBSTYPE = ',INST(JPROF),&
               & ' WHICH IS UNKNOWN!'
               KERR(3) = KERR(3) + 1
               CYCLE CYCLE_PROF
       ENDSELECT


       LPROFT=.TRUE.
       LPROFS=.TRUE.

       KOK(4) = KOK(4) + 1

       CYCLE_LEV : DO JLEV = 1,NLEVELS

        !... TEMPERATURE
          IF(LPROFT) THEN
            IF (TEMP(JLEV,JPROF).LE. 40._R4 .AND. TEMP(JLEV,JPROF) .GE. -3._R4 ) THEN
                  LOBS=.TRUE.
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READCRTFILE : MAXOBS MUST BE INCREASED!')
                    OBSTYPE(JOBS) = ITYPE
                    OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                    OBSPAR(JOBS) = KKTEMP
                    OBSVAL(JOBS) = TEMP(JLEV,JPROF)
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    KOK(1) = KOK(1) + 1
                  ELSE
                    KERR(9) = KERR(9) +1
                  ENDIF
            ELSE
                  KERR(10) = KERR(10) +1
            ENDIF
          ENDIF

        !... SALINITY
          IF(LPROFS) THEN
            IF (SALC(JLEV,JPROF).LE. 60._R4 .AND. SALC(JLEV,JPROF) .GE. 0._R4 ) THEN
                  LOBS=.TRUE.
                  IF(LOBS) THEN
                    IF( .NOT. LLPROFOK ) THEN
                       LLPROFOK=.TRUE.
                       KPROF=KPROF+1
                    ENDIF
                    JOBS=JOBS+1
                    IF(JOBS.GT.MAXOBS) &
                    & CALL ABOR1('READCRTFILE : MAXOBS MUST BE INCREASED!')
                    OBSTYPE(JOBS) = ITYPE
                    OBSDEP(JOBS) = DEPTHC(JLEV,JPROF)
                    OBSPAR(JOBS) = KKSAL
                    OBSVAL(JOBS) = SALC(JLEV,JPROF)
                    OBSTIME(JOBS) = TIME(JPROF)
                    OBSLON(JOBS) = LONS(JPROF)
                    OBSLAT(JOBS) = LATS(JPROF)
                    OBSPROF(JOBS) = KPROF
                    OBSPLAT(JOBS) = PLANO(JPROF)
                    KOK(2) = KOK(2) + 1
                  ELSE
                    KERR(11) = KERR(11) +1
                  ENDIF
            ELSE
                  KERR(12) = KERR(12) +1
            ENDIF
          ENDIF

       ENDDO CYCLE_LEV

    ENDDO CYCLE_PROF

  NTOTOBS=JOBS

  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** REPORT OF READCRTFILE : ',&
  & 'READING OF ENSEMBLES INS OBSERVATIONS'
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // DIMENSION'
  WRITE(IOUN,*) ' LEVELS      : ',NLEVELS
  WRITE(IOUN,*) ' PROFILES    : ',NPROFS
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // OBS PROFILES (WITHOUT UNVALID TIME/POSITION PROFILES)'
  WRITE(IOUN,*) ' XBT/MBT     : ',KCO(1)
  WRITE(IOUN,*) ' TESAC       : ',KCO(2)
  WRITE(IOUN,*) ' ARGO        : ',KCO(3)
  WRITE(IOUN,*) ' BUOYS       : ',KCO(4)
  WRITE(IOUN,*)
  WRITE(IOUN,*) ' // REPORT'
  WRITE(IOUN,*) ' TOTAL OBS STORED                :', NTOTOBS
  WRITE(IOUN,*) ' VALID PROFILES                  :', KOK (4)
  WRITE(IOUN,*) ' EXTRACTED OBS OF SALINITY       :', KOK (2)
  WRITE(IOUN,*) ' EXTRACTED OBS OF TEMPERATURE    :', KOK (1)
  WRITE(IOUN,*) ' EXTRACTED OBS OF POT. TEMPER.   :', KOK (3)
  WRITE(IOUN,*)
  WRITE(IOUN,*) '*** END OF READCRTFILE'
  WRITE(IOUN,*)

  DEALLOCATE(LATS,LONS,TIME,INST,PLANO)
  DEALLOCATE(TEMP,SALC,DEPTHC)

END SUBROUTINE READCRTFILE
