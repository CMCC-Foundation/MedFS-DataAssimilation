SUBROUTINE READ_SLAMIS(SLAA,ID,NP,MAXO,NOB,NOBSLIB,DDIR,LREM)

USE OBSDEF
USE NETCDF
USE OBS_STR
USE GRD_STR

IMPLICIT NONE

TYPE(SLA_T) :: SLAA
INTEGER(I4), INTENT(IN ) :: ID, NP,  MAXO
INTEGER(I4), INTENT(IN ) :: NOBSLIB
INTEGER(I4), INTENT(INOUT) :: NOB
CHARACTER(LEN=*), INTENT(IN) :: DDIR
LOGICAL, INTENT(IN) :: LREM
INTEGER(I4)  :: NOT, NCID, IDVAR, KK, STAT
INTEGER(I4)  :: JST, JEN, JP, IAUX(4), JO
CHARACTER(LEN=300) :: CF, CFAUX
CHARACTER :: CTMP
REAL(R8) :: ZBIAS
REAL(R8), ALLOCATABLE, DIMENSION(:,:) :: SLAA_TB, SLAA_SB, SLAA_BCP

WRITE(*,*) '>>>>  DATA DIR IS ',TRIM(DDIR), ' LIBRARY IS ',NOBSLIB

IF( NOBSLIB .EQ. 1 ) THEN

   WRITE(CF,'(A,A,I10,A)') TRIM(DDIR),'/OBSSTAT_',ID,'.NC'

   WRITE(*,*) '>>>>>>>>>>>>  READING FILE ',TRIM(CF)
   CALL CHECK( NF90_OPEN(CF,NF90_NOWRITE,NCID) )
   CALL CHECK( NF90_INQ_DIMID (NCID, 'OBS', IDVAR) )
   CALL CHECK( NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NOT) )

   JST = NOB+1
   JEN = NOB+NOT

   IF( JEN .GT. MAXO ) CALL ABOR1('MAXO TO INCREASE')

   CALL CHECK( NF90_INQ_VARID (NCID, 'LON', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%LON(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'LAT', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%LAT(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'DEPTH', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%DPT(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'TIME', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%TIM(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'ERROR', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%ERR(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'INST', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%KSAT(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'OMG', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%RES(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'OMA', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%INC(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'IB', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%IB(JST:JEN,:) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'JB', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%JB(JST:JEN,:) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'KB', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%BOT(JST:JEN) ) )
   CALL CHECK( NF90_INQ_VARID (NCID, 'BGERR', IDVAR) )
   CALL CHECK( NF90_GET_VAR (NCID,IDVAR,SLAA%BGERR(JST:JEN) ) )
   CALL CHECK( NF90_CLOSE (NCID) )

ELSEIF ( NOBSLIB .EQ. 2 .OR. NOBSLIB .EQ. 3 ) THEN

   IF(ID.EQ.0) THEN
     WRITE(CFAUX,'(A,A)') TRIM(DDIR),'/OBS_TAB.BIN'
     IF( NOBSLIB .EQ. 2 ) THEN
        WRITE(CF,'(A,A)') TRIM(DDIR),'/SLAMIS_ALL.bin'
     ELSE
        WRITE(CF,'(A,A)') TRIM(DDIR),'/SLAOBS_ALL.bin'
     ENDIF
   ELSE
     WRITE(CFAUX,'(A,A,I10,A)') TRIM(DDIR),'/OBS_TAB_',ID,'.BIN'
     IF( NOBSLIB .EQ. 2 ) THEN
        WRITE(CF,'(A,A,I10,A)') TRIM(DDIR),'/SLAMIS_ALL_',ID,'.bin'
     ELSE
        WRITE(CF,'(A,A,I10,A)') TRIM(DDIR),'/SLAOBS_ALL_',ID,'.bin'
     ENDIF
   ENDIF

   OPEN(71,FILE=CFAUX, STATUS='OLD')
   READ(71,*) IAUX(1:4)
   CLOSE(71)

   NOT = IAUX(2)

   JST = NOB+1
   JEN = NOB+NOT

   ALLOCATE( SLAA_TB(JEN-JST+1,GRD%KM) )
   ALLOCATE( SLAA_SB(JEN-JST+1,GRD%KM) )
   ALLOCATE( SLAA_BCP(JEN-JST+1,NPRD_SLA) )

   WRITE(*,*) '>>>>>>>>>>>>  READING FILE ',TRIM(CF), ' OBS = ',NOT

   OPEN(31,FILE=CF,FORM='UNFORMATTED',ACCESS='SEQUENTIAL')
   READ(31) SLAA%FLC(JST:JEN)
   READ(31) SLAA%NIND(JST:JEN)
   READ(31) SLAA%INO(JST:JEN)
   READ(31) SLAA%TRACK(JST:JEN)
   READ(31) SLAA%EVE(JST:JEN)
   READ(31) SLAA%KSAT(JST:JEN)
   READ(31) SLAA%BOT(JST:JEN)
   READ(31) SLAA%TDIST(JST:JEN)
   READ(31) SLAA%LON(JST:JEN)
   READ(31) SLAA%LAT(JST:JEN)
   READ(31) SLAA%TIM(JST:JEN)
   READ(31) SLAA%VAL(JST:JEN)
   READ(31) SLAA%BIA(JST:JEN)
   READ(31) SLAA%IB(JST:JEN,:)
   READ(31) SLAA%JB(JST:JEN,:)
   READ(31) SLAA%PQ(JST:JEN,:)
   READ(31) SLAA%DPT(JST:JEN)
   READ(31) SLAA%BAC(JST:JEN)
   READ(31) SLAA_TB 
   READ(31) SLAA_SB 
   READ(31) SLAA_BCP
   READ(31) SLAA%RES(JST:JEN)
   CLOSE(31)

   DEALLOCATE( SLAA_TB  )
   DEALLOCATE( SLAA_SB  )
   DEALLOCATE( SLAA_BCP )

   IF (LREM) THEN
    ZBIAS = 0._R8
    KK = 0
      DO JO=JST,JEN
         IF( ABS(SLAA%RES(JO)) .LT. 5._R8 ) THEN
             ZBIAS = ZBIAS + SLAA%RES(JO)
             KK = KK + 1 
         ENDIF
      ENDDO
    ZBIAS = ZBIAS / REAL(KK)
    SLAA%RES(JST:JEN) = SLAA%RES(JST:JEN) - ZBIAS
    WRITE(*,*) '      >>>>> BIAS REMOVED = ', ZBIAS
   ENDIF

ELSE
   CALL ABOR1('YOU MUST DEFINE A PROPER OBS FILE LIBRARY MODE')
ENDIF

NOB = JEN

END SUBROUTINE READ_SLAMIS
