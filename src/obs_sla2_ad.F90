SUBROUTINE OBS_SLA2_AD

 USE SET_KND
 USE GRD_STR
 USE OBS_STR
 USE RUN, ONLY : RTIMES1950, KLEVNM, RHO0,NTSTEPS,LLVARMDT,NCONF
 USE OCEANTOOLS , ONLY : RHO_UNESCOAD
 USE MYNETCDF
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE TLAD_VARS
 USE WEAKLY, ONLY : LL_WEAKLY, TERR_AD, SERR_AD

 IMPLICIT NONE

 INTEGER(I4)   ::  I, JP, K, KSTEP, JVL, JTSTEP,I2,OBS_K,KBOT
 INTEGER(I4)   ::  XIND1
 REAL(R8) :: ZC, TB, SB, S_AD, T_AD, RTDIFF,NO(SLA%NO)

 CALL MYFRTPROF_WALL('OBS_SLA2_AD: ADJOINT OF SLA OPERATOR',0)

 IF( SLA%NO .EQ. 0 ) THEN
   CALL MYFRTPROF_WALL('OBS_SLA2_AD: ADJOINT OF SLA OPERATOR',1)
   RETURN
 ENDIF

 ZC = -1._R8/RHO0

 IF( .NOT. LL_SLAAD_INIT .AND. NCONF .NE. 202) THEN
     ALLOCATE( SAUX_AD(SLA%NO,GRD%KM), TAUX_AD(SLA%NO,GRD%KM) )
 ENDIF

IF(NCONF .NE. 202 ) THEN
CYOBS : DO K=1,SLA%NO

 IF( .NOT. LL_4DVAR .OR. ( LL_4DVAR .AND. SLA%TATS(K) .EQ. TLAD_TS )) THEN

  OBS%K=OBS%K+1
  OBS_K=OBS%K
  IF( LL_4DVAR ) OBS_K = SLA%OBIN(K)

  KBOT=SLA%BOT(K)

  DO JVL = KBOT,1,-1

        TB      = SLA%TB(K,JVL) 
        SB      = SLA%SB(K,JVL)

        S_AD=0._R8
        T_AD=0._R8

    CALL RHO_UNESCOAD(OBS%GRA(OBS_K),SB,TB,S_AD,T_AD)

    DO JP=1,NPQ
      GRD%TEM_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) = &
    & GRD%TEM_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) + ZC*GRD%DZ(JVL)*SLA%PQ(K,JP)*T_AD
      GRD%SAL_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) = &
    & GRD%SAL_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) + ZC*GRD%DZ(JVL)*SLA%PQ(K,JP)*S_AD
    ENDDO
    IF( LL_WEAKLY ) THEN
     DO JP=1,NPQ
      TERR_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) = &
    & TERR_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) + ZC*GRD%DZ(JVL)*SLA%PQ(K,JP)*T_AD
      SERR_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) = &
    & SERR_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL) + ZC*GRD%DZ(JVL)*SLA%PQ(K,JP)*S_AD
!      IF( .NOT. ABS(TERR_AD(SLA%IB(K,JP),SLA%JB(K,JP),JVL)) .LT. 1.E+36 ) THEN
!          WRITE(1080,*) 'XX',K,JVL,JP,ZC,GRD%DZ(JVL),SLA%PQ(K,JP),T_AD,OBS%GRA(OBS_K),&
!          & SB,TB,S_AD,OBS_K,OBS%GRA(OBS_K),OBS%AMO(OBS_K),OBS%ERR(OBS_K),OBS%INC(K),OBS%RES(K)
!      ENDIF

     ENDDO
    ENDIF

  ENDDO

 ENDIF

ENDDO CYOBS
ENDIF

IF(LLVARMDT .OR. NCONF.EQ.202) THEN
  CYOBS2 : DO K=1,SLA%NO
   IF( .NOT. LL_4DVAR .OR. ( LL_4DVAR .AND. SLA%TATS(K) .EQ. TLAD_TS )) THEN
      OBS_K = K
      IF( LL_4DVAR ) OBS_K = SLA%OBIN(K)
      DO JP=1,NPQ
        GRD%CMDT_AD(SLA%IB(K,JP),SLA%JB(K,JP)) = &
        & GRD%CMDT_AD(SLA%IB(K,JP),SLA%JB(K,JP)) + SLA%PQ(K,JP)*OBS%GRA(OBS_K)
      ENDDO
    ENDIF
  ENDDO CYOBS2
ENDIF

IF( .NOT. LL_SLAAD_INIT ) LL_SLAAD_INIT = .TRUE.

CALL MYFRTPROF_WALL('OBS_SLA2_AD: ADJOINT OF SLA OPERATOR',1)
END SUBROUTINE OBS_SLA2_AD
