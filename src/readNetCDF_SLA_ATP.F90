#define FCC_CDFP_ATPGETTRACKDATAFOR1CYCLE FCC_CDFP_ATPGTDF1C

SUBROUTINE READNETCDF_SLA_ATP(CINIFILE,VARNAME,DATEST,DATEEN, &
                              & IMAXO,NTOTOBS,&
                              & PLON,PLAT,PTIM,PVAL,TRACK,&
                              & IONUMB)

!... READ ALONGTRACK SLA PRODUCT IN NETCDF FORMAT
!
!    ADAPTED FROM AVISO PUBLICREAD SOFTWARE
!    ANDREASTORTO

USE SET_KND
USE MYFRTPROF
USE OBSHANDLING, ONLY : LLFILTERSLA,NLANC_KN,ZLANC_CL

IMPLICIT NONE

CHARACTER (LEN=*),  INTENT(IN) :: CINIFILE         ! INPUT IFILE
CHARACTER (LEN=*),  INTENT(IN) :: VARNAME          ! VAR TO EXTRACT
INTEGER(4),INTENT(IN)          :: IONUMB,IMAXO     ! UNIT FOR DIGNOSTIC OUTPUT
INTEGER(4),INTENT(OUT)         :: NTOTOBS          ! VALID OBS
REAL(R8),INTENT(IN)            :: DATEST, DATEEN
REAL(R8),DIMENSION(IMAXO),INTENT(OUT) :: PLON,PLAT,PTIM,PVAL
INTEGER(4),INTENT(OUT) :: TRACK(IMAXO)

!.............................................................................

REAL(R8) ,PARAMETER :: DC_CDFP_DEFREAL8 = 18446744073709551616.0E+00_R8
INTEGER(I4),PARAMETER :: DC_CDFP_DEFINTEGER32 = 2147483647

INTEGER(I4)  :: IIJ

INTEGER(I4)  ::  MAXNUMBEROFTRACKS
INTEGER(I4)  ::  IOMETHOD
PARAMETER       (MAXNUMBEROFTRACKS = 254)
INTEGER(I4)  ::  MAXNUMBEROFCYCLES
PARAMETER       (MAXNUMBEROFCYCLES = 100)
INTEGER(I4)  ::  MAXNUMBEROFVARS
PARAMETER       (MAXNUMBEROFVARS = 10)

INTEGER(I4)      TRACKS(MAXNUMBEROFTRACKS)
INTEGER(I4)      NBDATATRACK(MAXNUMBEROFTRACKS)
INTEGER(I4)      CYCLES(MAXNUMBEROFCYCLES)
REAL(R8)         VALUES(MAXNUMBEROFCYCLES)
REAL(R8)         DATES(MAXNUMBEROFCYCLES)
CHARACTER*(40)  VARNAMES(MAXNUMBEROFVARS)
CHARACTER(LEN=16) :: CTRACK

INTEGER(I4)      IFILE
INTEGER(I4)      NPOBS, NPFIRST
!       MAX NUMBER OF TRACKS IN A FILE
INTEGER(I4)      MAXTRACKS
!       MAX NUMBER OF CYCLES IN A TRACK
INTEGER(I4)      MAXCYCLES
!       MAX NUMBER OF DATA FOR A TRACK
INTEGER(I4)      MAXDATA
!       NUMBER OF USER VARIABLES
INTEGER(I4)      NBVARS
!       NUMBER OF CYCLES IN A TRACK
INTEGER(I4)      NBCYCLES
!       NUMBER OF DATA IN A TRACK
INTEGER(I4)      NBDATA

REAL(R8) ::  LATITUDE, LONGITUDE         ! LAT LON OF A DATAPOINT

INTEGER(I4) :: INDEXTRACKS, INDEXCYCLES, INDEXVARS, INDEXDATA

INTEGER(I4) :: ISTATUS,JOBS

REAL(R8),ALLOCATABLE,DIMENSION(:) :: VALUESG,DATESG,LATSG,LONSG

!.............................................................................

!    IN SLA, TIME IS EXPRESSED AS (FRACTIONAL) DAYS SINCE 19500101 00:00

IOMETHOD=2

JOBS=0

WRITE(IONUMB,*)
WRITE(IONUMB,*) '*** READNETCDF_SLA_ATP'
WRITE(IONUMB,*) 'OPENING FILE ',TRIM(CINIFILE)

CALL FCC_CDFP_ATPOPEN(CINIFILE, IFILE)
IF (IFILE .EQ. 0) THEN
  WRITE(IONUMB, 1020) CINIFILE
1020      FORMAT ('PROBLEM OPENING ',A)
  GOTO 9999
ENDIF

! RETREIVE THE SIZES OF THIS FILE

CALL FCC_CDFP_ATPGETMAX(IFILE, MAXTRACKS, MAXCYCLES, MAXDATA, NBVARS,&
                      & ISTATUS)
IF (ISTATUS .NE. 0) GOTO 9998

WRITE(IONUMB, 1040) MAXTRACKS, MAXCYCLES, MAXDATA, NBVARS
1040    FORMAT ('MAXIMUM NUMBER OF TRACKS IN FILE        : ', I5,/,&
     &          'MAXIMUM NUMBER OF CYCLES BY TRACK       : ', I5,/,&
     &          'ACTUAL MAXIMUM NUMBER OF DATA FOR TRACKS: ', I5,/,&
     &          'NUMBER OF USER VARIABLES                : ', I5,/)

IF (MAXTRACKS .GT. MAXNUMBEROFTRACKS) THEN
  WRITE (*, 1050) MAXTRACKS, 'TRACKS'
1050      FORMAT ('NOT ENOUGH SPACE TO STORE ', I5, 1X, A)
  GOTO 9999
ENDIF
IF (MAXCYCLES .GT. MAXNUMBEROFCYCLES) THEN
  WRITE (*, 1050) MAXCYCLES, 'CYCLES'
  GOTO 9999
ENDIF
IF (NBVARS .GT. MAXNUMBEROFVARS) THEN
  WRITE (*, 1050) NBVARS, 'USER VARIABLES'
  GOTO 9999
ENDIF

! RETREIVE THE NAMES OF THE USER VARIABLES IN FILE

CALL FCC_CDFP_ATPREADVARIABLELIST(IFILE,&
     &                                    NBVARS,&
     &                                    VARNAMES,&
     &                                    NBVARS,&
     &                                    ISTATUS)

IF (ISTATUS .NE. 0) GOTO 9998

IF (NBVARS .GT. MAXNUMBEROFVARS) THEN
  WRITE (*, 1060) NBVARS, MAXNUMBEROFVARS
1060      FORMAT ('TOO MUCH VARS IN FILE',/&
     &            'NUMBER OF VARS                                :', I5,/,&
     &            'MAXIMUM NUMBER OF VARS THIS PROGRAM CAN HANDLE:', I5)
  GOTO 9999
ENDIF

WRITE(IONUMB, FMT=1070) (VARNAMES(INDEXVARS), INDEXVARS=1,NBVARS)
1070    FORMAT('USER VARIABLE NAMES:',100(/,30(' '),A))
WRITE(IONUMB, *)

! RETREIVE THE LIST OF TRACKS

CALL FCC_CDFP_ATPREADTRACKLIST(IFILE, TRACKS, MAXTRACKS, ISTATUS)
IF (ISTATUS .NE. 0) GOTO 9998

WRITE(IONUMB, 1080) MAXTRACKS
1080    FORMAT ('ACTUAL NUMBER OF TRACKS IN FILE: ', I5)

NBDATATRACK(:)=0

IF(IOMETHOD .EQ. 1 ) THEN

DO INDEXTRACKS=1,MAXTRACKS

!         RETREIVE THE NUMBER OF DATA FOR THE TRACK (THE NUMBER OF DATA
!         IS IDENTICAL FOR EACH CYCLE OF A GIVEN TRACK)

  CALL FCC_CDFP_ATPGETTRACKNBDATA(IFILE, TRACKS(INDEXTRACKS), NBDATA,&
       & ISTATUS)
  IF (ISTATUS .NE. 0) GOTO 9998

  NBDATATRACK(INDEXTRACKS) = NBDATA

!         RETREIVE THE LIST OF CYCLES IN A GIVEN TRACK

  CALL FCC_CDFP_ATPREADCYCLELIST(IFILE,&
     &                                   TRACKS(INDEXTRACKS),&
     &                                   CYCLES,&
     &                                   NBCYCLES,&
     &                                   ISTATUS)
  IF (ISTATUS .NE. 0) GOTO 9998

    DO INDEXDATA=0, NBDATA-1
      CALL FCC_CDFP_ATPGET1DATAFORCYCLES(IFILE,&
     &                                           VARNAME,&
     &                                           TRACKS(INDEXTRACKS),&
     &                                           INDEXDATA,&
     &                                           NBCYCLES,&
     &                                           CYCLES,&
     &                                           LATITUDE,&
     &                                           LONGITUDE,&
     &                                           VALUES,&
     &                                           DATES,&
     &                                           ISTATUS)

      IF (ISTATUS .NE. 0) GOTO 9998

      DO INDEXCYCLES=1, NBCYCLES

       IF(LONGITUDE.NE.DC_CDFP_DEFREAL8 .AND.&
        & VALUES(INDEXCYCLES).NE.DC_CDFP_DEFREAL8 .AND.&
        & DATES(INDEXCYCLES).GT.DATEST .AND.&
        & DATES(INDEXCYCLES).LT.DATEEN ) THEN

          JOBS=JOBS+1
          IF(JOBS.GT.IMAXO) &
          & CALL ABOR1('READNETCDF_SLA_ATP : MAXTOTALDATA TO INCREASE')
          IF(LONGITUDE .GT. 180._R8) THEN
             PLON(JOBS) = LONGITUDE -360._R8
          ELSE
             PLON(JOBS) = LONGITUDE
          ENDIF
          PLAT(JOBS) = LATITUDE
          PVAL(JOBS) = VALUES(INDEXCYCLES)
          PTIM(JOBS) = DATES(INDEXCYCLES)

       ENDIF

      ENDDO

  ENDDO
ENDDO


ELSEIF(IOMETHOD .EQ. 2 ) THEN

CTRACK='                '
DO INDEXTRACKS=1,MAXTRACKS


NPFIRST=JOBS

!... NAME THE TRACK

WRITE(CTRACK(1:16),'(A,I3.3)') TRIM(CINIFILE),TRACKS(INDEXTRACKS)

!         RETREIVE THE NUMBER OF DATA FOR THE TRACK (THE NUMBER OF DATA
!         IS IDENTICAL FOR EACH CYCLE OF A GIVEN TRACK)

  CALL FCC_CDFP_ATPGETTRACKNBDATA(IFILE, TRACKS(INDEXTRACKS), NBDATA,&
       & ISTATUS)
  IF (ISTATUS .NE. 0) GOTO 9998

  NBDATATRACK(INDEXTRACKS) = NBDATA

!         RETREIVE THE LIST OF CYCLES IN A GIVEN TRACK

  CALL FCC_CDFP_ATPREADCYCLELIST(IFILE,&
     &                                   TRACKS(INDEXTRACKS),&
     &                                   CYCLES,&
     &                                   NBCYCLES,&
     &                                   ISTATUS)
  IF (ISTATUS .NE. 0) GOTO 9998

  IF(ALLOCATED(VALUESG)) &
  &  DEALLOCATE(VALUESG,DATESG,LATSG,LONSG)

  ALLOCATE(VALUESG(NBDATA),DATESG(NBDATA),LATSG(NBDATA),LONSG(NBDATA))

    DO INDEXCYCLES=1, NBCYCLES

      CALL FCC_CDFP_ATPGETTRACKDATAFOR1CYCLE(  IFILE, &
     &                                           VARNAME,&
     &                                           TRACKS(INDEXTRACKS),&
     &                                           CYCLES(INDEXCYCLES),&
     &                                           NBDATA,&
     &                                           VALUESG,&
     &                                           LATSG,&
     &                                           LONSG,&
     &                                           DATESG,&
     &                                           ISTATUS)


      IF (ISTATUS .NE. 0) GOTO 9998

      DO IIJ=1,NBDATA

        IF(LONSG(IIJ).NE.DC_CDFP_DEFREAL8 .AND.&
        & DATESG(IIJ).GT.DATEST .AND.&
        & DATESG(IIJ).LT.DATEEN ) THEN

          JOBS=JOBS+1
          IF(JOBS.GT.IMAXO) &
          & CALL ABOR1('READNETCDF_SLA_ATP : MAXTOTALDATA TO INCREASE')
          IF(LONSG(IIJ).GT. 180._R8) THEN
             PLON(JOBS) = LONSG(IIJ)-360._R8
          ELSE
             PLON(JOBS) = LONSG(IIJ)
          ENDIF
          PLAT(JOBS) = LATSG(IIJ)
          PVAL(JOBS) = VALUESG(IIJ)
          PTIM(JOBS) = DATESG(IIJ)
          TRACK(JOBS) = TRACKS(INDEXTRACKS)

       ENDIF

      ENDDO

  ENDDO

  NPOBS=JOBS-NPFIRST
  IF(LLFILTERSLA .AND. NPOBS.GT.0) THEN
      CALL SFILTATSLAL(NPOBS,PLON(NPFIRST+1:JOBS),PLAT(NPFIRST+1:JOBS),&
      & PTIM(NPFIRST+1:JOBS),PVAL(NPFIRST+1:JOBS),NLANC_KN,ZLANC_CL)
  ENDIF

ENDDO

ELSE

 CALL ABOR1('WRONG IO METHOD IN READNETCDF OF SLA PRODUCTS')

ENDIF

CALL FCC_CDFP_ATPCLOSE(IFILE)

NTOTOBS=JOBS

WRITE(IONUMB,*) ' TOTAL OBS IN FILE            : ',SUM(NBDATATRACK(:))
WRITE(IONUMB,*) ' VALID OBS WITHIN TIME-WINDOW : ',NTOTOBS

GOTO 10000

9998    CONTINUE
WRITE(IONUMB, 2000) CINIFILE
2000    FORMAT ('ERROR ACCESSING FILE ',A)

9999    CONTINUE
CALL ABOR1('NETCDF ACCESS PROBLEMS')
10000   CONTINUE

END SUBROUTINE READNETCDF_SLA_ATP
