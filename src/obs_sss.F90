SUBROUTINE OBS_SSS

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR SSS DATA                            !
!                                                                      !
! VERSION 1: A.STORTO 2009                                             !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE READFG
 USE CTL_STR
 USE IOUNITS , ONLY : IOUNERR,IOUNLOG,IOUNOUT
 USE RUN, ONLY : RTIMES1950,NTSTEPS
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)   ::  I, J, D, KK,JTSTEP,KSTEP,FIRSTTS
 INTEGER(I4) :: JP

CALL MYFRTPROF_WALL('OBS_SSS: COMPUTE SSS BACKGROUND VALUES',0)

FIRST : DO JTSTEP=1,NTSTEPS
  IF( LL_FGASSIM(JTSTEP) ) THEN
      FIRSTTS=JTSTEP
      EXIT FIRST
  ENDIF
ENDDO FIRST

 WRITE(IOUNOUT,*) ' ENTERING OBS_SSS'

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(KK,KSTEP,JTSTEP,I,J,JP)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
 CYOBS : DO KK = 1,SSS%NO

  IF(SSS%FLC(KK).NE.1 ) CYCLE CYOBS

  KSTEP = MINLOC( ABS(RTIMES1950(1:NTSTEPS)-SSS%TIM(KK) ), DIM=1 ) - (FIRSTTS-1)
  IF(KSTEP .LT. 1 .OR. KSTEP .GT. NFGTSTEPS) THEN
     WRITE(IOUNOUT,*) ' OBS_SSS: FOUND KSTEP EQUAL TO ',KSTEP
     WRITE(IOUNOUT,*) ' OBS_SSS: TIME EQUAL TO ',SSS%TIM(KK)
     WRITE(IOUNOUT,*) ' LL_FGASSIM : ',LL_FGASSIM
     WRITE(IOUNOUT,*) ' RTIMES1950 : ',RTIMES1950(1:NTSTEPS)
     CALL ABOR1('OBS_SSS: KSTEP OUT OF RANGE')
  ENDIF

  SSS%BAC(KK) = 0._R8
  DO JP=1,NPQ
       SSS%BAC(KK) = SSS%BAC(KK) + SSS%PQ(KK,JP)*GRD%SALB(SSS%IB(KK,JP),SSS%JB(KK,JP),1,KSTEP)
  ENDDO

  SSS%RES(KK) = SSS%VAL(KK) - SSS%BAC(KK)

 ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) ' *** SSS OBSERVATIONS OBSOP COMPLETED'

CALL MYFRTPROF_WALL('OBS_SSS: COMPUTE SSS BACKGROUND VALUES',1)
END SUBROUTINE OBS_SSS
