#undef SHARED_MEMORY
SUBROUTINE INT_PAR_SST

!-----------------------------------------------------------------------
!                                                                      !
! GET INTERPOLATION PARAMETERS FOR A GRID                              !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
!-----------------------------------------------------------------------

 USE SET_KND
 USE GRD_STR
! USE PHINTERP
 USE PHINTERP2
 USE RUN
 USE OBS_STR
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE IOUNITS

 IMPLICIT NONE

  INTEGER(I4)   ::  K, N_TOPEX, N_ERS, II, JJ,I2
  INTEGER(I4)   ::  I1, J1, KK, I, J, ITER, IAUX, JP
  INTEGER(I4)   ::  MSCOUNT
  REAL(R8)      ::  P1, Q1, P, Q, SUMT, SUMI, NEWLON
  REAL(R8)      ::  MSK4, ZZSS

#include "obs_events.h"

 CALL MYFRTPROF_WALL('INT_PAR_SST: SST INTERPOLATION',0)

 IF(SST%NO.EQ.0) THEN
   CALL MYFRTPROF_WALL('INT_PAR_SST: SST INTERPOLATION',1)
   RETURN
 ENDIF

 SST%FLC = 0
 SST%IB  = 0
 SST%JB  = 0
 SST%TDIST(1:SST%NO)=SST%TIM(1:SST%NO) - ZANJUL1950


#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(PRIVATE), SHARED(SST)
!$OMP DO SCHEDULE(DYNAMIC)
#endif
CYOBS0 : DO KK = 1,SST%NO

      IF( SST%EVE(KK) .EQ. KEVE_PDOM ) CYCLE CYOBS0

!      IF( NN_INTERPM .EQ. 1 ) THEN
!         CALL PREPINTERP(SST%LON(KK),SST%LAT(KK),SST%IB(KK,:),SST%JB(KK,:),SST%PQ(KK,:))
!      ELSEIF ( NN_INTERPM .EQ. 2 ) THEN
         CALL PREPINTERP2(SST%LON(KK),SST%LAT(KK),SST%IB(KK,:),SST%JB(KK,:),SST%PQ(KK,:))
!      ENDIF

ENDDO CYOBS0
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED), PRIVATE(KK,ZZSS,JP,MSK4)
!$OMP DO SCHEDULE(DYNAMIC)
#endif
CYOBS : DO KK = 1,SST%NO
       IF( ALL(SST%IB(KK,:).GT.1 ) .AND. ALL(SST%JB(KK,:).GT.1 ) .AND. &
       & ALL(SST%IB(KK,:).LT.GRD%IM ) .AND. ALL(SST%JB(KK,:).LT.GRD%JM ) ) THEN
          SST%FLC(KK) = 1
       ELSE
          SST%FLC(KK) = 0
          SST%EVE(KK) = KEVE_INTE
          CYCLE CYOBS
       ENDIF

       SST%PQ(KK,1) = SST%PQ(KK,1) * GRD%MSK(SST%IB(KK,1),SST%JB(KK,1),1)
       SST%PQ(KK,2) = SST%PQ(KK,2) * GRD%MSK(SST%IB(KK,2),SST%JB(KK,2),1)
       SST%PQ(KK,3) = SST%PQ(KK,3) * GRD%MSK(SST%IB(KK,3),SST%JB(KK,3),1)
       SST%PQ(KK,4) = SST%PQ(KK,4) * GRD%MSK(SST%IB(KK,4),SST%JB(KK,4),1)

       ZZSS = 0._R8
       DO JP=1,NPQ
         ZZSS = ZZSS + GRD%MSK(SST%IB(KK,JP),SST%JB(KK,JP),1)
       ENDDO

       IF( ABS( ZZSS ) .LT. 1._R8 ) THEN
           SST%FLC(KK) = 0
           SST%EVE(KK) = KEVE_MASK
           CYCLE CYOBS
       ELSE
           SST%PQ(KK,:) = SST%PQ(KK,:) / SUM(SST%PQ(KK,:))
       ENDIF

ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

MSCOUNT=COUNT(SST%EVE(1:SST%NO).EQ.KEVE_INTE)
WRITE(IOUNLOG,*) &
& ' SST OBS FILTERED OUT FOR INTERP PROBLEMS: ',MSCOUNT

MSCOUNT=COUNT(SST%EVE(1:SST%NO).EQ.KEVE_MASK)
WRITE(IOUNLOG,*) &
& ' SST OBS FILTERED OUT FOR MASKING INCONSISTENCIES: ',MSCOUNT

! ---
! COUNT GOOD OBSERVATIONS
  SST%NC = COUNT(SST%FLC(1:SST%NO) .EQ. 1)

  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** SST OBS AFTER INTERPOLATION SET-UP'
  WRITE(IOUNLOG,*) ' TOTAL NUMBER OF SST OBS           :',SST%NO
  WRITE(IOUNLOG,*) ' TOTAL NUMBER OF RETAINED SST  OBS :',SST%NC

CALL MYFRTPROF_WALL('INT_PAR_SST: SST INTERPOLATION',1)
END SUBROUTINE INT_PAR_SST
