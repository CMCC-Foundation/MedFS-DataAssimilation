SUBROUTINE THINOUT_INS_B(ITER,ZIN1,ZIN2,ZIN3)

!.. THINNNING OF INS IN-SITU OBSERVATIONS
!..
!.. METHOD B (A TO BE PREFERRED!): FIND OBSERVATIONS
!.. IN A 3D BOX FOR EACH TYPE/PARAMETER
!.. AND RETAIN ONLY THE CLOSEST OBS
!.. TO ANALYSIS TIME.
!..
!.. ANDREA STORTO *INGV* 2009-04-6

USE SET_KND
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE IOUNITS, ONLY : IOUNLOG,IOUNOUT
USE MYFRTPROF

IMPLICIT NONE

 INTEGER(I4),INTENT(IN) :: ITER
 REAL(R8),INTENT(IN) :: ZIN1,ZIN2,ZIN3
 REAL(R8) :: XRES,YRES,ZAUX,ZM,ZRES,ZOFFX,ZOFFY,ZOFFZ
 INTEGER(I4)   ::  K,JOBS,IIND,JIND,KOKTH,KKOTH,NX,NY,NZ,MAXOBS,OIND
 INTEGER(I4)   ::  JX,JY,JZ,JTYPE,ZIND,JPAR,KEVE_HTHN
 REAL(DP), ALLOCATABLE :: ZDIST(:,:,:)
 INTEGER(I4), ALLOCATABLE :: KCOUNTTH(:,:,:),KGOOD(:,:,:),KOBS(:,:,:,:)

#include "obs_events.h"

 CALL MYFRTPROF_WALL('THINOUT_INS_B: INS DATA THINNING B',0)

  WRITE(IOUNOUT,*) ' *** INS THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' *** INS THINNING - ITERATION ',ITER

  KOKTH=0
  KKOTH=0
  XRES=ZIN1
  YRES=ZIN2
  ZRES=ZIN3

 IF(ITER.EQ.1) THEN
     ZOFFX=0._R8
     ZOFFY=0._R8
     ZOFFZ=0._R8
     KEVE_HTHN = KEVE_HTH1
  ELSEIF(ITER.EQ.2) THEN
     ZOFFX=0.5_R8*XRES
     ZOFFY=0.5_R8*YRES
     ZOFFZ=0.5_R8*YRES
     KEVE_HTHN = KEVE_HTH2
  ELSE
     CALL ABOR1('THINOUT: UNSUPPORTED NUMBER OF ITERATION')
  ENDIF

  ZM = REAL(GRD%IM)
  NX=INT( ZM / XRES ) + 1
  ZM = REAL(GRD%JM)
  NY=INT( ZM / YRES ) + 1
  ZM = REAL(GRD%KM)
  NZ=INT( ZM / ZRES ) + 1

  MAXOBS = 1000*INS%NO/(NX*NY*NZ)

  ALLOCATE(KCOUNTTH(NX,NY,NZ),  &
         & ZDIST(NX,NY,NZ),  &
         & KGOOD(NX,NY,NZ),  &
         & KOBS(NX,NY,NZ,MAXOBS) )

 INSTYPE : DO JTYPE=1,NOINSITU
  INSPAR : DO JPAR=1,NOPARAMS

  WRITE(IOUNOUT,*) 'THINNING FOR TYPE ',JTYPE,' PARAM ',JPAR
  WRITE(IOUNLOG,*) 'THINNING FOR TYPE ',JTYPE,' PARAM ',JPAR

  KCOUNTTH=0
  ZDIST = 1000000._DP
  KGOOD=0

  CYOBS5: DO K=1,INS%NO

       IF(INS%FLC(K).EQ.0 .OR. INS%KTY(K) .NE. JTYPE .OR. &
       &  INS%PAR(K) .NE. JPAR ) CYCLE CYOBS5

       IIND = INT((REAL(INS%IB(K,1),KIND=R8)+ZOFFX)/XRES) + 1
       JIND = INT((REAL(INS%JB(K,1),KIND=R8)+ZOFFY)/YRES) + 1
       ZIND = INT((REAL(INS%KB(K),KIND=R8)+INS%RB(K)+ZOFFZ)/ZRES) + 1


       KCOUNTTH(IIND,JIND,ZIND) = KCOUNTTH(IIND,JIND,ZIND) + 1
       OIND = KCOUNTTH(IIND,JIND,ZIND)
       IF(OIND.GT. MAXOBS) &
       & CALL ABOR1('THINOUT: MAXOBS MUST BE INCREASED')
       KOBS(IIND,JIND,ZIND,OIND) = K

       IF(ABS(INS%TDIST(K)) .LT. ZDIST(IIND,JIND,ZIND) ) THEN
              ZDIST(IIND,JIND,ZIND) = ABS(INS%TDIST(K))
              KGOOD(IIND,JIND,ZIND) = K
       ENDIF

  ENDDO CYOBS5

  DO JZ=1,NZ
   DO JY=1,NY
    CYX : DO JX=1,NX

      IF(KCOUNTTH(JX,JY,JZ) .GT. 0 ) THEN
          IF (KGOOD(JX,JY,JZ).EQ.0 ) &
          & CALL ABOR1('INS THINNING, PROBLEMS WITH MINIMUM DIST')

            DO K= 1, KCOUNTTH(JX,JY,JZ)
               IF( KGOOD(JX,JY,JZ) .NE. KOBS(JX,JY,JZ,K) ) THEN
                   JOBS = KOBS(JX,JY,JZ,K)
                   INS%FLC(JOBS) = 0
                   INS%EVE(JOBS) = KEVE_HTHN
                   KKOTH = KKOTH +1
               ELSE
                   KOKTH = KOKTH + 1
               ENDIF
            ENDDO

      ENDIF

    ENDDO CYX
   ENDDO
  ENDDO

 ENDDO INSPAR
 ENDDO INSTYPE

  WRITE(IOUNOUT,*) ' END INS THINNING - ITERATION ',ITER
  WRITE(IOUNLOG,*)
  WRITE(IOUNLOG,*) ' *** THINNING REPORT - ITERATION ',ITER
  WRITE(IOUNLOG,*) ' THINNING GRID : ',NX,' X',NY, ' X',NZ
  WRITE(IOUNLOG,*) ' FILTERED-OUT  : ',KKOTH
  WRITE(IOUNLOG,*) ' RETAINED      : ',KOKTH
  WRITE(IOUNLOG,*) ' TOTAL         : ',KOKTH + KKOTH
  WRITE(IOUNLOG,*) ' FLAGS COUNT   : ',SUM(INS%FLC(1:INS%NO))

  INS%NC = KOKTH

 CALL MYFRTPROF_WALL('THINOUT_INS_B: INS DATA THINNING B',1)
END SUBROUTINE THINOUT_INS_B
