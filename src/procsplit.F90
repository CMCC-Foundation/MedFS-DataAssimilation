SUBROUTINE PROCSPLIT(JPNI, IB, JB, SD1, SD2, MOI, MOJ)

!.. WRITE OBS IN BINARY OR NETCDF FORMAT

USE SET_KND
USE RUN , ONLY : NSUBDOMAINS, LL_NOPSABORT
USE OBS_STR
USE GRD_STR
USE READFG, ONLY : KDPF,KDPL,NONO,NOSO,NOEA,NOWE
USE IOUNITS

IMPLICIT NONE

INTEGER(I4), INTENT(IN) :: JPNI
INTEGER(I4), INTENT(INOUT) :: IB(NPQ), JB(NPQ)
INTEGER(I4), INTENT(OUT) :: SD1, SD2, MOI(NPQ), MOJ(NPQ)

INTEGER(I4) :: NPROCS, JP, I, J, I2, J2, MYPROC, IAUX
INTEGER(I4) :: NOTHP, JPQ

  NPROCS = NSUBDOMAINS

  NOTHP=0
  MOI=0
  MOJ=0

  IF(NPROCS.EQ.1) THEN
       SD1 = 1
       SD2 = 1
       RETURN
  ENDIF

  I=MINVAL(IB(:))
  J=MINVAL(JB(:))
  I2=MAXVAL(IB(:))
  J2=MAXVAL(JB(:))

  ! PROPER CHANGE OF BORDERLINE OBS 
  IF( I2.GE.GRD%IM-1 .AND. I .GE. 1 ) THEN
    WHERE( IB.GE.GRD%IM-1 ) IB = IB - (GRD%IM-2)
    I = MINVAL(IB(:))
    I2= MAXVAL(IB(:))
  ENDIF

  IF( I.LE.2 .AND. I2 .GE. GRD%IM-5 ) THEN
    WHERE( IB.LE.2 ) IB = IB + (GRD%IM-2)
    I = MINVAL(IB(:))
    I2= MAXVAL(IB(:))
  ENDIF

  MYPROC=0
  SPROC1 : DO JP=1,NPROCS
       IF(I.GE.KDPF(JP,1).AND.I2.LE.KDPL(JP,1)   .AND. &
          J.GE.KDPF(JP,2).AND.J2.LE.KDPL(JP,2)   ) THEN
             MYPROC=JP
             EXIT SPROC1
       ENDIF
  ENDDO SPROC1

  IF(MYPROC .NE. 0) THEN
     SD1=MYPROC
     SD2=MYPROC
     RETURN
  ENDIF

  MYPROC=0
  SPROC1A : DO JP=1,NPROCS
     IF(I.GE.KDPF(JP,1).AND.I.LE.KDPL(JP,1)   .AND. &
        J.GE.KDPF(JP,2).AND.J.LE.KDPL(JP,2)   ) THEN
             MYPROC=JP
             EXIT SPROC1A
       ENDIF
  ENDDO SPROC1A
  SD1=MYPROC

  MYPROC=0
  SPROC1B : DO JP=1,NPROCS
     IF(I2.GE.KDPF(JP,1).AND.I2.LE.KDPL(JP,1)   .AND. &
        J2.GE.KDPF(JP,2).AND.J2.LE.KDPL(JP,2)   ) THEN
             MYPROC=JP
             EXIT SPROC1B
       ENDIF
  ENDDO SPROC1B
  SD2=MYPROC

  ! HANDLE THE PERIODICITY CASE
  IF( (I2 - I) .GT. GRD%IM/2 ) THEN
      IAUX=SD1
      SD1=SD2
      SD2=IAUX
      IF( SD2 .NE. NOEA(SD1) ) THEN
          SD2 = NOEA(SD1)
      ENDIF
  ENDIF

  DO JPQ=1,NPQ
        I=IB(JPQ)
        J=JB(JPQ)
        IF(I.LT.KDPF(SD1,1).OR.I.GT.KDPL(SD1,1) .OR. &
           J.LT.KDPF(SD1,2).OR.J.GT.KDPL(SD1,2) ) THEN
           NOTHP=NOTHP+1
           MOI(NOTHP)=I
           MOJ(NOTHP)=J
           IF(I.LT.KDPF(SD2,1).OR.I.GT.KDPL(SD2,1) .OR. &
           J.LT.KDPF(SD2,2).OR.J.GT.KDPL(SD2,2) ) THEN
         
             WRITE(IOUNERR,*) 'PROBLEM IN DOMAIN ASSIGNEMENT - 1:', JPQ, I, J, SD1, SD2, &
             MINVAL(IB(:)), MINVAL(JB(:)), I2, J2
             IF( LL_NOPSABORT ) THEN
               SD1 = 0
               SD2 = 0
               RETURN
             ELSE
               CALL ABOR1('PROCSPLIT PROBLEM IN DOM ASSIGN')
             ENDIF
           ENDIF
        ENDIF
  ENDDO
  
  IF(SD2.NE.NOEA(SD1) .AND. SD2.NE.NONO(SD1)) THEN
     WRITE(IOUNERR,*) 'PROBLEM IN DOMAIN ASSIGNEMENT - 2:', JPQ, SD1, SD2, &
     MINVAL(IB(:)), MINVAL(JB(:)), I2, J2, IB(:),JB(:),NOTHP,MOI(1:NOTHP),&
     MOJ(1:NOTHP)
     IF( LL_NOPSABORT ) THEN
        SD1 = 0
        SD2 = 0
        RETURN
     ELSE
        CALL ABOR1('PROCSPLIT PROBLEM IN DOM ASSIGN')
     ENDIF
  ENDIF

END SUBROUTINE PROCSPLIT
