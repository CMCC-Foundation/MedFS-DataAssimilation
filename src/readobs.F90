SUBROUTINE READOBS

!.. INTERFACE FOR READING SLA AND INS FILES
!.. CALLING READSLA AND READINS, WHICH ARE INTENDED
!.. FOR A MORE GENERIC (READ: POTENTIALLY EXTERNAL)
!.. READING OF OBSERVATIONAL NETCDF FILES
!..
!.. ANDREA STORTO, *INGV* 2009-03-23

USE SET_KND
USE OBSDEF
USE OBS_STR, ONLY : SST,INS,SLA,OBS,SSS
USE GRD_STR
USE IOUNITS
USE OBSHANDLING , ONLY : INS_LQC_POS,INS_LQC_SAL,&
                       & INS_LQC_TEMP,INS_LQC_PROF_T,&
                       & INS_LQC_PROF_S,INS_LQC_TIME
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE RUN, ONLY : ZANJUL1950, CINS_FMT, LL_ICOADS, CICOADS_FMT, &
& LL_DENSLEVS, LL_DRIFTER_SST

IMPLICIT NONE

CHARACTER(LEN=20) :: CFILE
INTEGER(I4), PARAMETER :: NMAX =20000000,MAXPROFS = 50000
INTEGER(I4) :: NOBS , IFILE, KOSUM, JO, KR(4)
INTEGER(I4) :: STOBS,ENOBS,TOTOBS
INTEGER(I4), DIMENSION(NMAX) :: OTYPE, OPAR, OPROF
REAL   (DP), DIMENSION(NMAX) :: OTIME
REAL   (R8), DIMENSION(NMAX) :: ODEP,  OVAL
REAL   (R8), DIMENSION(NMAX) :: OLON,  OLAT
CHARACTER(LEN=8), DIMENSION(NMAX) :: OPLA
LOGICAL :: LPROCEED, LFLAGS(10), LDUPL, LDUPLA(MAXPROFS)
INTEGER(I4) :: KPRC, KDUPL, JOBS,JPROF,JPROF2, KPIND, KPIND2,UPPROF,K

#include "obs_events.h"

! LOAD SST OBSERVATIONS

IF ( OBS%SST .GT. 0 ) THEN
  WRITE(IOUNOUT,*) ' READING IN SST OBSERVATIONS'
     CALL READSST
ELSE
    SST%NO=0
ENDIF

! LOAD SSS OBSERVATIONS

IF ( OBS%SSS .GT. 0 ) THEN
  WRITE(IOUNOUT,*) ' READING IN SSS OBSERVATIONS'
     CALL READSSS
ELSE
    SSS%NO=0
ENDIF

! LOAD SLA OBSERVATIONS

IF ( OBS%SLA .GT. 0 ) THEN
  WRITE(IOUNOUT,*) ' READING IN SLA OBSERVATIONS'
     CALL READSLA
ELSE
    SLA%NO=0
ENDIF

! LOAD IN-SITU OBSERVATIONS
KOSUM = OBS%XBT + OBS%GLD + OBS%ARG

INS%NO = 0
TOTOBS = 0

IF ( KOSUM .GT. 0 ) THEN
  IF( LL_DENSLEVS .AND. CINS_FMT .NE. 'COR' ) THEN
     CALL ABOR1('ASSIMILIATION ON DENSITY LEVS SUPPORT ONLY CORA')
  ENDIF
  WRITE(IOUNOUT,*) ' READING IN IN-SITU OBSERVATIONS'
  IF( CINS_FMT .EQ. 'EN3' ) THEN
     CALL READEN3
  ELSEIF ( CINS_FMT .EQ. 'NRT' ) THEN
     CALL READNRT
  ELSEIF ( CINS_FMT .EQ. 'COR' ) THEN
     CALL READCORA
  ELSEIF ( CINS_FMT .EQ. 'WOD' ) THEN
     CALL READWOD
  ELSEIF ( CINS_FMT .EQ. 'CRT' ) THEN
     CALL READCRT
  ELSE
     CALL ABOR1('READOBS : CINS_FMT NOT SUPPORTED' )
  ENDIF
ELSE
  INS%NO=0
ENDIF

IF( LL_ICOADS ) THEN
   IF( CICOADS_FMT .EQ. 'NCDF' ) THEN
     CALL READICOADS_NC
   ELSEIF( CICOADS_FMT .EQ. 'ASCI' ) THEN
     CALL READICOADS
   ELSE
     CALL ABOR1('READOBS : CICOADS_FMT NOT SUPPORTED' )
   ENDIF
ENDIF

IF( LL_DRIFTER_SST ) CALL READ_DRIFTSST

IF ( OBS%AIR .GT. 0 ) THEN
  WRITE(IOUNOUT,*) ' READING IN AIR OBSERVATIONS'
  CALL READAIR
ENDIF

IF( .NOT. ALLOCATED (INS%PRIND) ) ALLOCATE( INS%PRIND(INS%NO) )

KR=0

IF ( SST%NO .GT. 0 ) THEN
  DO JO=1,SST%NO
    IF( SST%LON(JO) .LT. MIMA_LL(1) .OR. SST%LON(JO) .GT. MIMA_LL(2) .OR. &
       SST%LAT(JO) .LT. MIMA_LL(3) .OR. SST%LAT(JO) .GT. MIMA_LL(4) ) THEN
        SST%FLC(JO) = 0
        SST%EVE(JO) = KEVE_PDOM
        KR(3)=KR(3)+1
    ENDIF
  ENDDO
ENDIF

IF ( SSS%NO .GT. 0 ) THEN
  DO JO=1,SSS%NO
    IF( SSS%LON(JO) .LT. MIMA_LL(1) .OR. SSS%LON(JO) .GT. MIMA_LL(2) .OR. &
       SSS%LAT(JO) .LT. MIMA_LL(3) .OR. SSS%LAT(JO) .GT. MIMA_LL(4) ) THEN
        SSS%FLC(JO) = 0
        SSS%EVE(JO) = KEVE_PDOM
        KR(4)=KR(4)+1
    ENDIF
  ENDDO
ENDIF

IF ( SLA%NO .GT. 0 ) THEN
  DO JO=1,SLA%NO
    IF( SLA%LON(JO) .LT. MIMA_LL(1) .OR. SLA%LON(JO) .GT. MIMA_LL(2) .OR. &
       SLA%LAT(JO) .LT. MIMA_LL(3) .OR. SLA%LAT(JO) .GT. MIMA_LL(4) ) THEN
        SLA%FLC(JO) = 0
        SLA%EVE(JO) = KEVE_PDOM
        KR(2)=KR(2)+1
    ENDIF
  ENDDO
ENDIF

IF ( INS%NO .GT. 0 ) THEN
  DO JO=1,INS%NO
    IF( INS%LON(JO) .LT. MIMA_LL(1) .OR. INS%LON(JO) .GT. MIMA_LL(2) .OR. &
       INS%LAT(JO) .LT. MIMA_LL(3) .OR. INS%LAT(JO) .GT. MIMA_LL(4) ) THEN
        INS%FLC(JO) = 0
        INS%EVE(JO) = KEVE_PDOM
        KR(1)=KR(1)+1
    ENDIF
  ENDDO
ENDIF

WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) ' /// OUT OF DOMAIN REJECTION '
WRITE(IOUNLOG,*) ' INS : ', KR(1)
WRITE(IOUNLOG,*) ' SLA : ', KR(2)
WRITE(IOUNLOG,*) ' SST : ', KR(3)
WRITE(IOUNLOG,*) ' SSS : ', KR(4)
WRITE(IOUNLOG,*) 

END SUBROUTINE READOBS
