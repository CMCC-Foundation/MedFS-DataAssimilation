#undef SHARED_MEMORY
SUBROUTINE RDDNS2(I,J,K,DENS, MLD)

  USE SET_KND
  USE GRD_STR
  USE NETCDF
  USE OCEANTOOLS
  USE RUN
  USE IOUNITS
  USE READFG
  USE MYFRTPROF

  IMPLICIT NONE

  INTEGER(I4), INTENT(IN)    :: I, J, K
  REAL(R8), INTENT(OUT)      :: DENS(I,J,K), MLD(I,J)

  CHARACTER(LEN=12) :: CVARNAME1
  CHARACTER(LEN=12) :: CVARNAME2
  CHARACTER(LEN=12) :: CVARNAME3
  CHARACTER(LEN=30) :: CFNAME

  INTEGER(I4)             :: STAT,IDVAR,NCD
  REAL(R8)                ::  ZTT (I,J,K), ZSS(I,J,K)

  INTEGER(I4) :: MAXX,MAXY,ISTARTX,ISTARTY,&
               & IENDX,IENDY,ILX,ILY,JF,KREC,NRECS, JI, JJ, JK

  CALL MYFRTPROF_WALL('RDDNS2: READ AND COMPUTE DENSITY FROM GRID_T FILES',0)

MAXX=MAXVAL(KDSL(1:NSUBDOMAINS,1))
MAXY=MAXVAL(KDSL(1:NSUBDOMAINS,2))

CVARNAME1 = 'somxl010'
CVARNAME2 = 'votemper'
CVARNAME3 = 'vosaline'

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(JF,ISTARTX,ISTARTY,IENDX,IENDY,ILX,ILY,&
!$OMP & STAT,IDVAR,NCD,CFNAME,KREC,JI,JJ,JK)
!$OMP DO SCHEDULE(STATIC,1)
#endif
DO JF=1,NSUBDOMAINS

   ISTARTX=KDPF(JF,1)
   ISTARTY=KDPF(JF,2)
   IENDX=KDPL(JF,1)
   IENDY=KDPL(JF,2)
   ILX=KDSL(JF,1)
   ILY=KDSL(JF,2)

   WRITE(CFNAME,'(A,I4.4,A)' ) 'GRIDT_',JF-1,'.nc'

#ifndef SHARED_MEMORY
   WRITE(IOUNLOG,*) 'FILE ',CFNAME, &
   & ISTARTX, ISTARTY, IENDX, IENDY, ILX, ILY
   CALL FLUSH(IOUNLOG)
#endif

   STAT = NF90_OPEN(CFNAME, NF90_NOWRITE, NCD)
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

   WRITE(IOUNLOG,*)
   WRITE(IOUNLOG,*) ' DENSITY FILE READING '

   IF( NN_DENSREC .EQ. 0 ) THEN
       CALL ABOR1('RDDNS2 : REQUESTED RECORD HAS NOT BEEN SETUP')
   ELSEIF( NN_DENSREC .EQ. 1 ) THEN
       KREC = 1
       WRITE(IOUNLOG,*) ' NUMBER OF RECORD READ IN  : ', KREC
   ELSE
       STAT = NF90_INQ_DIMID (NCD, 'time_counter', IDVAR)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       STAT = NF90_INQUIRE_DIMENSION (NCD, IDVAR, LEN = NRECS)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
       IF( NN_DENSREC .LT. 0 ) THEN
           KREC = NRECS
       ELSE
           KREC = NN_DENSREC
           IF( KREC .GT. NRECS ) THEN
               WRITE(IOUNLOG,*) ' NN_DENSREC IS GREATER THAN NUMBER OF RECORDS IN FILE'
               WRITE(IOUNLOG,*) ' NN_DENSREC = ', NN_DENSREC
               WRITE(IOUNLOG,*) ' NRECS     = ', NRECS
               CALL ABOR1('RDDNS2 : REQUESTED RECORD IS GREATER THAN NO OF RECORDS')
           ENDIF
       ENDIF
       WRITE(IOUNLOG,*) ' NUMBER OF RECORDS IN FILE : ', NRECS
       WRITE(IOUNLOG,*) ' NUMBER OF RECORD READ IN  : ', KREC
   ENDIF

   STAT = NF90_INQ_VARID (NCD, CVARNAME1, IDVAR)
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
   STAT = NF90_GET_VAR (NCD,IDVAR,MLD(ISTARTX:IENDX,ISTARTY:IENDY),&
   & START=(/1,1,KREC/),COUNT=(/ILX,ILY,1/))
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

   STAT = NF90_INQ_VARID (NCD, CVARNAME2, IDVAR)
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
   STAT = NF90_GET_VAR (NCD,IDVAR,ZTT(ISTARTX:IENDX,ISTARTY:IENDY,1:K),&
   & START=(/1,1,1,KREC/),COUNT=(/ILX,ILY,K,1/))
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
   STAT = NF90_INQ_VARID (NCD, CVARNAME3, IDVAR)
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
   STAT = NF90_GET_VAR (NCD,IDVAR,ZSS(ISTARTX:IENDX,ISTARTY:IENDY,1:K),&
   & START=(/1,1,1,KREC/),COUNT=(/ILX,ILY,K,1/))
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

   DO JK=1,K
     DO JJ=ISTARTY,IENDY
       DO JI=ISTARTX,IENDX
         DENS(JI, JJ, JK) = RHO_UNESCO(MAX(ZSS(JI,JJ,JK),0._R8),ZTT(JI,JJ,JK),0._R8,.FALSE.)
       ENDDO
     ENDDO
   ENDDO

   STAT = NF90_CLOSE(NCD)
   IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
ENDDO
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

CALL MYFRTPROF_WALL('RDDNS2: READ AND COMPUTE DENSITY FROM GRID_T FILES',1)

! ----------------------------------------------------------------

END SUBROUTINE RDDNS2
