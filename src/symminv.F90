SUBROUTINE SYMMINV(A,N)
! INVERSE OF SYMMETRIC MATRIX VIA CHOLESKY DECOMP.
! USE LAPACK STANDARD ROUTINES
! A.S. - 2016
  USE SET_KND
  IMPLICIT NONE
  INTEGER(I4), INTENT(IN) :: N
  REAL(R8), INTENT(INOUT) :: A(N,N)
  INTEGER(I4) :: IERR,I,J
  IF (R8 == KIND(1._8)) THEN
     CALL DPOTRF('L',N,A,N,IERR)
     IF (IERR .NE. 0) THEN
        WRITE(IOUNERR,*) ' DPOTRF RETURNED ', IERR
        CALL ABOR1('SYMMINV: ERROR IN DPOTRF')
     END IF
     CALL DPOTRI('L',N,A,N,IERR)
     IF (IERR .NE. 0) THEN
        WRITE(IOUNERR,*) ' DPOTRI RETURNED ', IERR
        CALL ABOR1('SYMMINV: ERROR IN DPOTRI')
     END IF
  ELSE IF (R8 == KIND(1._4)) THEN
     CALL SPOTRF('L',N,A,N,IERR)
     IF (IERR .NE. 0) THEN
        WRITE(IOUNERR,*) ' SPOTRF RETURNED ', IERR
        CALL ABOR1('SYMMINV: ERROR IN SPOTRF')
     END IF
     CALL SPOTRI('L',N,A,N,IERR)
     IF (IERR .NE. 0) THEN
        WRITE(IOUNERR,*) ' SPOTRI RETURNED ', IERR
        CALL ABOR1('SYMMINV: ERROR IN SPOTRI')
     END IF
  ELSE
     CALL ABOR1('SYMMINV: UNRECOGNIZED KIND')
  ENDIF
  DO J=2,N
   DO I=1,J-1
     A(I,J) = A(J,I)
   ENDDO
  ENDDO
END SUBROUTINE SYMMINV
