SUBROUTINE SLA_BCORR

!... SEARCH OUT SLA ALONG-TRACK CO-LOCATIONS
!    FOR ERROR CORRELATION COMPUTATION
!... A.S.

USE SET_KND
USE OBSDEF
USE OBS_STR
USE GRD_STR
USE IOUNITS
USE MYFRTPROF
USE MYNETCDF
USE RUN
USE MPIREL

IMPLICIT NONE

 INTEGER(I4) :: NPRD_TOT
 INTEGER(I4) :: NXBC,NYBC,NSBC,NPBC
 INTEGER(I4) :: JOBS, I0, J0, I0R, J0R, IKS, NCKO, NOK
 REAL(R8), DIMENSION(:,:,:,:), ALLOCATABLE :: PMEANS, PVARS, PBCC
 REAL(R8), DIMENSION(:      ), ALLOCATABLE :: XX
 REAL(R8) :: ZBB
 CHARACTER(LEN=99), PARAMETER :: CFBC='slabiascorr.nc'

#include "obs_events.h"

 CALL MYFRTPROF_WALL('SLA_BCORR: SLA BIAS CORR',0)

 IF (SLA%NO .EQ. 0) THEN
     CALL MYFRTPROF_WALL('SLA_BCORR: SLA BIAS CORR',1)
     RETURN
 ENDIF

 NPRD_TOT = NPRD_SLA + 1

 CALL GETNCDIM(CFBC,'x',NXBC)
 CALL GETNCDIM(CFBC,'y',NYBC)
 CALL GETNCDIM(CFBC,'sats',NSBC)
 CALL GETNCDIM(CFBC,'prds',NPBC)

 ALLOCATE( PMEANS(NXBC,NYBC,NSBC,NPBC) )
 ALLOCATE( PVARS (NXBC,NYBC,NSBC,NPBC) )
 ALLOCATE( PBCC  (NXBC,NYBC,NSBC,NPBC) )

 CALL GETNCVAR( CFBC, 'pmeans',NXBC,NYBC,NSBC,NPBC,PMEANS )
 CALL GETNCVAR( CFBC, 'pvars',NXBC,NYBC,NSBC,NPBC,PVARS )
 CALL GETNCVAR( CFBC, 'nbcc',NXBC,NYBC,NSBC,NPBC,PBCC )

 PMEANS(:,:,:,1) = 0._R8
 PVARS(:,:,:,1) = 1._R8

 WRITE(IOUNLOG,*) ''
 WRITE(IOUNLOG,*) ' ===> SLA BIAS CORRECTION SCHEME'
 WRITE(IOUNLOG,*) ' DIMENSIONS OF MATRICES: ',NXBC,NYBC,NSBC,NPBC

 ALLOCATE( XX(NPBC) )
 XX(1) = 1._R8
 NCKO = 0
 NOK = 0

 CYO : DO JOBS=1,SLA%NO

     IF(SLA%FLC(JOBS) .NE. 1 ) CYCLE CYO

       NOK = NOK+1

       IKS=SLA%KSAT(JOBS)
       I0=SLA%IB(JOBS,1)
       J0=SLA%JB(JOBS,1)

       XX(2:10)=SLA%BCP(JOBS,1:9)
       XX(11:12)=SLA%BCP(JOBS,11:12)
       XX(NPRD_SLA+1)=OSUM( SLA%PQ(JOBS,:),&
       & GRD%HGT(SLA%IB(JOBS,:),SLA%JB(JOBS,:)) )

       I0R = ((I0+GRD%I0-1)-1)/NN_SLABC_BY + 1 
       J0R = ((J0+GRD%J0-1)-1)/NN_SLABC_BY + 1 

!       WRITE(7800+MYPROC,'(I9,5I6)') JOBS, IKS, I0, J0, I0R, J0R
!       WRITE(7800+MYPROC,'(14E12.3)') XX
       
       XX(2:NPRD_TOT) = ( XX(2:NPRD_TOT) - PMEANS(I0R,J0R,IKS,2:NPRD_TOT) ) / &
       & SQRT(PVARS(I0R,J0R,IKS,2:NPRD_TOT))

!       WRITE(7800+MYPROC,'(14E12.3)') XX

       ZBB = SUM(XX*PBCC(I0R,J0R,IKS,:))

       IF( .NOT. ABS(ZBB) .LT. 100._R8 ) THEN
            NCKO = NCKO +1
            SLA%FLC(JOBS) = 0
            SLA%EVE(JOBS) = KEVE_SBCR
       ENDIF

!       WRITE(7800+MYPROC,'(E12.3)') ZBB

       SLA%RES(JOBS) = SLA%RES(JOBS) - ZBB
       SLA%BIA(JOBS) = SLA%BIA(JOBS) + ZBB

 ENDDO CYO

 WRITE(IOUNLOG,*) ' TOTAL NO BIAS CORRECTED: ',NOK
 WRITE(IOUNLOG,*) ' TOTAL WRONG            : ',NCKO

 DEALLOCATE( PMEANS, PVARS, PBCC, XX )
 CALL MYFRTPROF_WALL('SLA_BCORR: SLA BIAS CORR',1)

END SUBROUTINE SLA_BCORR
