SUBROUTINE READNETCDF_SLA_TAP(CINIFILE,VARNAME,DATEST,DATEEN,IMAXO, &
                              & NTOTOBS, LL_RT,&
                              & PLON,PLAT,PTIM,PVAL,&
                              & IONUMB)

!... READ ALONGTRACK SLA PRODUCT IN NEW AVISO NETCDF FORMAT
!
!    ADAPTED FROM AVISO PUBLICREAD SOFTWARE
!    ANDREASTORTO  
!    JENNYPISTOIA 
!    open TAPAS file 

USE SET_KND
USE NETCDF
USE MYFRTPROF
USE OBSHANDLING, ONLY : LLFILTERSLA,NLANC_KN,ZLANC_CL

IMPLICIT NONE

CHARACTER (LEN=*),  INTENT(IN) :: CINIFILE        ! INPUT IFILE
CHARACTER (LEN=*),  INTENT(IN) :: VARNAME         ! VAR TO EXTRACT
INTEGER(I4),INTENT(IN)         :: IONUMB,IMAXO    ! UNIT FOR DIGNOSTIC OUTPUT
REAL(R8)         ,INTENT(IN)   :: DATEST, DATEEN  ! ASSIM TIME WINDOW
INTEGER(I4),INTENT(OUT)        :: NTOTOBS         ! VALID OBS
REAL(R8),DIMENSION(IMAXO),INTENT(OUT) :: PLON,PLAT,PTIM,PVAL
LOGICAL, INTENT(IN) :: LL_RT
REAL(R8), ALLOCATABLE, DIMENSION(:) :: LON, LAT,DAC,TIM
INTEGER(I4), ALLOCATABLE, DIMENSION(:) :: TRK, FLAG
INTEGER(I4) :: NOTR, NOR(2)
INTEGER(I4)  :: JOBS,JT,NT
INTEGER(I4)  :: STAT, NCID, IDVAR

!.............................................................................

    STAT = NF90_OPEN(CINIFILE, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    STAT = NF90_INQ_DIMID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    ALLOCATE( DAC(NT), LON(NT), LAT(NT), TRK(NT), TIM(NT) )

    STAT = NF90_INQ_VARID (NCID, VARNAME, IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,DAC)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'longitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LON)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'latitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LAT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'track', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TRK)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIM)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    IF ( LL_RT ) THEN
      ALLOCATE( FLAG(NT) )
      STAT = NF90_INQ_VARID (NCID, 'flag', IDVAR)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
      STAT = NF90_GET_VAR (NCID,IDVAR,FLAG)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ENDIF

    STAT = NF90_CLOSE (NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    JOBS=0
    NOTR=1
    NOR=0
    LON=LON*1.E-6_R8
    LAT=LAT*1.E-6_R8
    DAC=DAC*1.E-3_R8
    CYOBS : DO JT=1,NT
          IF( LL_RT .AND. FLAG(JT) .NE. 1 ) THEN
	     NOR(1)=NOR(1)+1
	     CYCLE CYOBS
	  ENDIF
          IF( TIM(JT) .LT. DATEST .OR. &
          & TIM(JT) .GT. DATEEN ) THEN
	     NOR(2)=NOR(2)+1
	     CYCLE CYOBS
	  ENDIF
          JOBS=JOBS+1
          IF(JOBS.GT.IMAXO) &
          & CALL ABOR1('READNETCDF_DAC_ATP : MAXTOTALDATA TO INCREASE')
          IF(LON(JT).GT. 180._R8) THEN
             PLON(JOBS) = LON(JT)-360._R8
          ELSE
             PLON(JOBS) = LON(JT)
          ENDIF
          PLAT(JOBS) = LAT(JT)
          PVAL(JOBS) = DAC(JT)
          PTIM(JOBS) = TIM(JT)
    ENDDO CYOBS

NTOTOBS=JOBS
DEALLOCATE( DAC, LON, LAT, TRK, TIM )
IF ( LL_RT ) DEALLOCATE( FLAG )

WRITE(IONUMB,*)
WRITE(IONUMB,*) ' -----'
WRITE(IONUMB,*) ' AVISO/SLA FILE ',TRIM(CINIFILE)
WRITE(IONUMB,*) ' NUMBER OF TRACKS', NOTR
WRITE(IONUMB,*) ' TOTAL OBS IN FILE : ',NT
WRITE(IONUMB,*) ' UNVALID FLAG (RT) : ',NOR(1)
WRITE(IONUMB,*) ' OUT OF TIME       : ',NOR(2)
WRITE(IONUMB,*) ' VALID OBS         : ',NTOTOBS
WRITE(IONUMB,*)

END SUBROUTINE READNETCDF_SLA_TAP
