MODULE OCEANTOOLS

!.... OCEANTOOLS :: COLLECTION OF VARIOUS ROUTINES
!
!     ANDREASTORTO, FOR USE IN 3D-VAR
!
!     SVAN : STERIC AND DENSITY ANOMALY CALCULATION
!     RHO_UNESCO : DENSITY
!     RHO_UNESCOTL : TANGENT-LINEAR OF DENSITY (USE IN SLA ASSIM)
!     RHO_UNESCOAD : ADJOINT OF DENSITY        (USE IN SLA ASSIM)
!     GRAVITY : G CALCULATION
!     CORIOL  : CORIOLIS PAR CALCULATION

USE SET_KND

IMPLICIT NONE

PRIVATE :: SVAN_GENERIC

#ifdef NO_DOUBLEPREC
INTERFACE SVAN
  MODULE PROCEDURE SVAN_R4_S,SVAN_R4_C
END INTERFACE SVAN
INTERFACE  RHO_UNESCO
  MODULE PROCEDURE RHO_UNESCO_R4
END INTERFACE RHO_UNESCO
INTERFACE  RHO_UNESCOTL
  MODULE PROCEDURE RHO_UNESCOTL_R4
END INTERFACE RHO_UNESCOTL
INTERFACE  GRAVITY
  MODULE PROCEDURE GRAVIT_R4,GRAVITY_R4
END INTERFACE GRAVITY
INTERFACE  CORIOL
  MODULE PROCEDURE CORIOL_R4
END INTERFACE CORIOL
INTERFACE  RHO_UNESCOAD
  MODULE PROCEDURE RHO_UNESCOAD_R4
END INTERFACE RHO_UNESCOAD
INTERFACE DIST
  MODULE PROCEDURE DIST_R4
END INTERFACE DIST
#else
INTERFACE SVAN
  MODULE PROCEDURE SVAN_R4_S,SVAN_R4_C,SVAN_R8_S,SVAN_R8_C
END INTERFACE SVAN
INTERFACE  RHO_UNESCO
  MODULE PROCEDURE RHO_UNESCO_R4,RHO_UNESCO_R8
END INTERFACE RHO_UNESCO
INTERFACE  RHO_UNESCOTL
  MODULE PROCEDURE RHO_UNESCOTL_R4,RHO_UNESCOTL_R8
END INTERFACE RHO_UNESCOTL
INTERFACE  GRAVITY
  MODULE PROCEDURE GRAVIT_R4,GRAVIT_R8,GRAVITY_R4,GRAVITY_R8
END INTERFACE GRAVITY
INTERFACE  CORIOL
  MODULE PROCEDURE CORIOL_R4,CORIOL_R8
END INTERFACE CORIOL
INTERFACE  RHO_UNESCOAD
  MODULE PROCEDURE RHO_UNESCOAD_R4,RHO_UNESCOAD_R8
END INTERFACE RHO_UNESCOAD
INTERFACE DIST
  MODULE PROCEDURE DIST_R4,DIST_R8
END INTERFACE DIST
#endif

CONTAINS
!.........
REAL (KIND=R8) FUNCTION SVAN_R8_S(S,T,SIGMA)
  IMPLICIT NONE
   REAL(KIND=R8) :: S,T,SIGMA
   SVAN_R8_S = SVAN_GENERIC(S,T,0.0_R8,SIGMA,.FALSE.)
RETURN
END FUNCTION SVAN_R8_S
!.........
REAL (KIND=R8) FUNCTION SVAN_R8_C(S,T,P0,SIGMA)
  IMPLICIT NONE
   REAL(KIND=R8) :: S,T,P0,SIGMA
   SVAN_R8_C = SVAN_GENERIC(S,T,P0,SIGMA,.TRUE.)
RETURN
END FUNCTION SVAN_R8_C

!.........
REAL (KIND=R4) FUNCTION SVAN_R4_S(S,T,SIGMA)

  IMPLICIT NONE

   REAL(KIND=R4) :: S,T,P0,SIGMA
   REAL(KIND=R8) :: S8,T8,SIGMA8,SVAN8

   S8=REAL(S,KIND=R8)
   T8=REAL(S,KIND=R8)
   SVAN8 = SVAN_GENERIC(S8,T8,0.0_R8,SIGMA8,.FALSE.)

   SIGMA = REAL(SIGMA8,KIND=R4)
   SVAN_R4_S = REAL(SVAN8,KIND=R4)

RETURN
END FUNCTION SVAN_R4_S
!.........
REAL (KIND=R4) FUNCTION SVAN_R4_C(S,T,P0,SIGMA)

  IMPLICIT NONE

   REAL(KIND=R4) :: S,T,P0,SIGMA
   REAL(KIND=R8) :: S8,T8,P08,SIGMA8,SVAN8

   S8=REAL(S,KIND=R8)
   T8=REAL(S,KIND=R8)
   P08=REAL(P0,KIND=R8)
   SVAN8 = SVAN_GENERIC(S8,T8,P08,SIGMA8,.TRUE.)

   SIGMA = REAL(SIGMA8,KIND=R4)
   SVAN_R4_C = REAL(SVAN8,KIND=R4)

RETURN
END FUNCTION SVAN_R4_C
!.........


!******************************************************************************
      REAL(KIND=R8) FUNCTION SVAN_GENERIC(S,T,P0,SIGMA,LCONT)
!******************************************************************************
!
!  MODIFIED RCM
!
! ******************************************************
! SPECIFIC VOLUME ANOMALY (STERIC ANOMALY) BASED ON 1980 EQUATION
! OF STATE FOR SEAWATER AND 1978 PRACTICAL SALINITY SCALE.
! REFERENCES
! MILLERO, ET AL (1980) DEEP-SEA RES.,27A,255-264
! MILLERO & POISSON 1981,DEEP-SEA RES.,28A PP 625-629.
! BOTH ABOVE REFERENCES ARE ALSO FOUND IN UNESCO REPORT 38 (1981)
! UNITS:
!       PRESSURE        P0       DECIBARS
!       TEMPERATURE     T        DEG CELSIUS (IPTS-68)
!       SALINITY        S        PSU (IPSS-78)
!       SPEC. VOL. ANA. SVAN     M**3/KG *1.0E-8
!       DENSITY ANA.    SIGMA    KG/M**3
! CHECK VALUE: SVAN = 981.30190 M**3/KG FOR S = 40 PSU,
! T = 40 DEG C, P0= 10000 DECIBARS.
! ******************************************************************
! *****************************
! CHECK VALUE: SIGMA = 59.82037  KG/M**3 FOR S = 40 PSU,
! T = 40 DEG C, P0= 10000 DECIBARS.
! *******************************************************
IMPLICIT NONE

      REAL(R8) ::  P0,SIGMA
      REAL(R8) ::  P,T,S,SIG,SR,R1,R2,R3,R4
      REAL(R8) ::  A,B,C,D,E,A1,B1,AW,BW,K,K0,KW,K35
      REAL(R8) ::  R3500,DR350
      REAL(R8) ::  DVAN,DR35P,D350,PK,GAM,DK,SVA,V350P
      LOGICAL  :: LCONT
! EQUIV
      EQUIVALENCE (E,D,B1),(BW,B,R3),(C,A1,R2)
      EQUIVALENCE (AW,A,R1),(KW,K0,K)
! ********************
! DATA
      DATA R3500,R4/1028.1063,4.8314E-4/
      DATA DR350/28.106331/
!   R4 IS REFERED TO AS  C  IN MILLERO & POISSON 1981
! CONVERT PRESSURE TO BARS AND TAKE SQUARE ROOT SALINITY.
      P=P0/10.
      SR = SQRT(ABS(S))
! *********************************************************
! PURE WATER DENSITY AT ATMOSPHERIC PRESSURE
!   BIGG P.H.,(1967) BR. J. APPLIED PHYSICS 8 PP 521-537.
!
      R1 = ((((6.536332E-9*T-1.120083E-6)*T+1.001685E-4)*T           &
      -9.095290E-3)*T+6.793952E-2)*T-28.263737
! SEAWATER DENSITY ATM PRESS.
!  COEFFICIENTS INVOLVING SALINITY
!  R2 = A   IN NOTATION OF MILLERO & POISSON 1981
      R2 = (((5.3875E-9*T-8.2467E-7)*T+7.6438E-5)*T-4.0899E-3)*T     &
      +8.24493E-1
!  R3 = B  IN NOTATION OF MILLERO & POISSON 1981
      R3 = (-1.6546E-6*T+1.0227E-4)*T-5.72466E-3
!  INTERNATIONAL ONE-ATMOSPHERE EQUATION OF STATE OF SEAWATER
      SIG = (R4*S + R3*SR + R2)*S + R1
! SPECIFIC VOLUME AT ATMOSPHERIC PRESSURE
      V350P = 1.0/R3500
      SVA = -SIG*V350P/(R3500+SIG)
      SIGMA=DR350-(SVA/(V350P*(V350P+SVA)))
!  SCALE SPECIFIC VOL. ANAMOLY TO NORMALLY REPORTED UNITS
      SVAN_GENERIC=SVA*1.0E+8
      IF(P.LT.0.000000001 .OR. .NOT. LCONT) RETURN
! ******************************************************************
! ******  NEW HIGH PRESSURE EQUATION OF STATE FOR SEAWATER ********
! ******************************************************************
!        MILLERO, ET AL , 1980 DSR 27A, PP 255-264
!               CONSTANT NOTATION FOLLOWS ARTICLE
!********************************************************
! COMPUTE COMPRESSION TERMS
      E = (9.1697E-10*T+2.0816E-8)*T-9.9348E-7
      BW = (5.2787E-8*T-6.12293E-6)*T+3.47718E-5
      B = BW + E*S
!
      D = 1.91075E-4
      C = (-1.6078E-6*T-1.0981E-5)*T+2.2838E-3
      AW = ((-5.77905E-7*T+1.16092E-4)*T+1.43713E-3)*T             &
      -0.1194975
      A = (D*SR + C)*S + AW
!
      B1 = (-5.3009E-4*T+1.6483E-2)*T+7.944E-2
      A1 = ((-6.1670E-5*T+1.09987E-2)*T-0.603459)*T+54.6746
      KW = (((-5.155288E-5*T+1.360477E-2)*T-2.327105)*T            &
      +148.4206)*T-1930.06
      K0 = (B1*SR + A1)*S + KW
! EVALUATE PRESSURE POLYNOMIAL
! ***********************************************
!   K EQUALS THE SECANT BULK MODULUS OF SEAWATER
!   DK=K(S,T,P)-K(35,0,P)
!  K35=K(35,0,P)
! ***********************************************
      DK = (B*P + A)*P + K0
      K35  = (5.03217E-5*P+3.359406)*P+21582.27
      GAM=P/K35
      PK = 1.0 - GAM
      SVA = SVA*PK + (V350P+SVA)*P*DK/(K35*(K35+DK))
!  SCALE SPECIFIC VOL. ANAMOLY TO NORMALLY REPORTED UNITS
      SVAN_GENERIC=SVA*1.0E+8
      V350P = V350P*PK
!  ****************************************************
! COMPUTE DENSITY ANAMOLY WITH RESPECT TO 1000.0 KG/M**3
!  1) DR350: DENSITY ANAMOLY AT 35 PSU 0 DEG. C & ATMOSPHERIC PRES.
!  2) DR35P: DENSITY ANAMOLY 35 PSU 0 DEG. C , WITH PRES. VARIATION
!  3) DVAN : DENSITY ANAMOLY VARIATIONS INVOLVING SPECFIC VOL. ANAMOLY
! ********************************************************************
! CHECK VALUE: SIGMA = 59.82037  KG/M**3 FOR S = 40 PSU,
! T = 40 DEG C, P0= 10000 DECIBARS.
! *******************************************************
      D350=GAM/PK
      DR35P=R3500*D350
      DVAN=SVA/(V350P*(V350P+SVA))
      SIGMA=DR350+DR35P-DVAN
      RETURN
END FUNCTION SVAN_GENERIC



REAL(KIND=R8) FUNCTION RHO_UNESCO_R8(SALINITY, TEMPERATURE, PRESSURE,LCONT)
IMPLICIT NONE

!
!
!  THIS SOFTWARE WAS DEVELOPED BY THE THERMAL MODELING AND ANALYSIS
!  PROJECT(TMAP) OF THE NATIONAL OCEANOGRAPHIC AND ATMOSPHERIC
!  ADMINISTRATION'S (NOAA) PACIFIC MARINE ENVIRONMENTAL LAB(PMEL),
!  HEREAFTER REFERRED TO AS NOAA/PMEL/TMAP.
!
!  ACCESS AND USE OF THIS SOFTWARE SHALL IMPOSE THE FOLLOWING
!  OBLIGATIONS AND UNDERSTANDINGS ON THE USER. THE USER IS GRANTED THE
!  RIGHT, WITHOUT ANY FEE OR COST, TO USE, COPY, MODIFY, ALTER, ENHANCE
!  AND DISTRIBUTE THIS SOFTWARE, AND ANY DERIVATIVE WORKS THEREOF, AND
!  ITS SUPPORTING DOCUMENTATION FOR ANY PURPOSE WHATSOEVER, PROVIDED
!  THAT THIS ENTIRE NOTICE APPEARS IN ALL COPIES OF THE SOFTWARE,
!  DERIVATIVE WORKS AND SUPPORTING DOCUMENTATION.  FURTHER, THE USER
!  AGREES TO CREDIT NOAA/PMEL/TMAP IN ANY PUBLICATIONS THAT RESULT FROM
!  THE USE OF THIS SOFTWARE OR IN ANY PRODUCT THAT INCLUDES THIS
!  SOFTWARE. THE NAMES TMAP, NOAA AND/OR PMEL, HOWEVER, MAY NOT BE USED
!  IN ANY ADVERTISING OR PUBLICITY TO ENDORSE OR PROMOTE ANY PRODUCTS
!  OR COMMERCIAL ENTITY UNLESS SPECIFIC WRITTEN PERMISSION IS OBTAINED
!  FROM NOAA/PMEL/TMAP. THE USER ALSO UNDERSTANDS THAT NOAA/PMEL/TMAP
!  IS NOT OBLIGATED TO PROVIDE THE USER WITH ANY SUPPORT, CONSULTING,
!  TRAINING OR ASSISTANCE OF ANY KIND WITH REGARD TO THE USE, OPERATION
!  AND PERFORMANCE OF THIS SOFTWARE NOR TO PROVIDE THE USER WITH ANY
!  UPDATES, REVISIONS, NEW VERSIONS OR "BUG FIXES".
!
!  THIS SOFTWARE IS PROVIDED BY NOAA/PMEL/TMAP "AS IS" AND ANY EXPRESS
!  OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
!  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
!  ARE DISCLAIMED. IN NO EVENT SHALL NOAA/PMEL/TMAP BE LIABLE FOR ANY
!  SPECIAL,
!  INDIRECT OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER
!  RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF
!  CONTRACT, NEGLIGENCE OR OTHER TORTUOUS ACTION, ARISING OUT OF OR IN
!  CONNECTION WITH THE ACCESS, USE OR PERFORMANCE OF THIS SOFTWARE.
!
!
! THE 1980 UNESCO INTERNATIONAL EQUATION OF STATE (IES80)
! *SH* 8/5/92 - ADAPTED FROM C VERSION SUPPLIED BY JOHN OSBORNE, PMEL

!       THIS CALCULATES RHO (DENSITY KG/M^3) AT SALINITY,
!       TEMPERATURE, AND PRESSURE.  THIS WILL BE POTENTIAL DENSITY IF
!       TEMPERATURE IS POTENTIAL TEMPERATURE.  THE ROUTINE USES THE HIGH
!       PRESSURE EQUATION OF STATE FROM MILLERO ET AL. 1980 AND THE ONE-
!       ATMOSPHERE EQUATION OF STATE FROM MILLERO AND POISSON 1981 AS
!       REPORTED IN GILL 1982.  THE NOTATION FOLLOWS MILLERO ET AL. 1980
!       AND MILLERO AND POISSON 1981.
!
!       NOTE: THE ROUTINE TAKES P IN DECIBARS AND CONVERTS TO BARS FOR
!       THE CALCULATIONS.
!
! REFERENCES:   MILLERO, F.J., ET AL., 1980, DEEP-SEA RES., 27A,
! 255-264.
!  MILLERO, F.J. AND ALAIN POISSON, 1981, DEEP-SEA RES., 28A, 625-629.
!  GILL, A.E., 1982, ATMOSPHERE-OCEAN DYNAMICS, ACADEMIC PRESS, INC.,
!  662 PP.
!
!       INPUT UNITS:
!                       S: PSU  T: DEG. C       P: DECIBARS.
!
!       OUTPUT UNITS:
!                       RHO: KG/M^3
!
!       CHECK VALUES:
!                       RHO = 999.96675         FOR S = 0,  T = 5,  P =
!                       0
!                       RHO = 1027.675465       FOR S = 35, T = 5,  P =
!                       0
!                       RHO = 1062.538172       FOR S = 35, T = 25, P =
!                       10000.

! CALLING ARGUMENT DECLARAIONS:
        REAL(R8) ::  SALINITY, TEMPERATURE, PRESSURE

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R8) ::  S, T, P, ROOTS
        REAL(R8) ::  A, B, C, D, E
        REAL(R8) ::  AW, BW, KW
        REAL(R8) ::  A2, B2, C2, KZERO, K
        REAL(R8) ::  RHOW, RHOZERO
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        T = TEMPERATURE
        P = PRESSURE / 10.
        ROOTS = SQRT (S)

! RHOW IS THE DENSITY OF PURE WATER AT TEMPERATURE T.
        RHOW = ((((6.536332D-09 * T - 1.120083D-06) * T + 1.001685D-04) &
     &          * T - 9.095290D-03) * T + 6.793952D-02) * T + 999.842594

        A2 = (((5.3875D-09 * T - 8.2467D-07) * T + 7.6438D-05) &
     &          * T - 4.0899D-03) * T + 8.24493D-01

        B2 = (-1.6546D-06 * T + 1.0227D-04) * T - 5.72466D-03

        C2 = 4.8314D-04

! RHOZERO IS THE ONE-ATMOSPHERE DENSITY OF SEAWATER
        RHOZERO = (C2 * S + B2 * ROOTS + A2) * S + RHOW

        IF(P.LT.0.000000001 .OR. .NOT. LCONT) THEN
           RHO_UNESCO_R8 = RHOZERO
           RETURN
        ENDIF

        A = ((-6.1670D-05 * T + 1.09987D-02) * T - 6.03459D-01) * T &
     &          + 54.6746

        B = (-5.3009D-04 * T + 1.6483D-02) * T + 7.944D-02

        C = (-1.6078D-06 * T - 1.0981D-05) * T + 2.2838D-03

        D = 1.91075D-04

        E = (9.1697E-10 * T + 2.0816D-08) * T - 9.9348D-07

        AW = ((-5.77905D-07 * T + 1.16092D-04) * T + 1.43713D-03) * T &
     &         + 3.239908

        BW = (5.2787D-08 * T - 6.12293D-06) * T + 8.50935D-05

! KW IS THE SECANT BULK MODULUS OF PURE WATER AT TEMPERATURE T.
        KW = (((-5.155288D-05 * T + 1.360477D-02) * T - 2.327105) &
     &          * T + 148.4206) * T + 19652.21

! KZERO IS THE SECANT BULK MODULUS OF SEAWATER AT ONE ATMOSPHERE.
        KZERO = (B * ROOTS + A) * S + KW

! K IS THE SECANT BULK MODULUS OF SEAWATER AT (S, T, P).
        K = ((E * P + D * ROOTS + C) * S + BW * P + AW) * P + KZERO

        RHO_UNESCO_R8 = RHOZERO / (1.0 - (P / K))

        RETURN
        END FUNCTION RHO_UNESCO_R8
!=========================================================================
REAL(KIND=R4) FUNCTION RHO_UNESCO_R4(SALINITY, TEMPERATURE, PRESSURE,LCONT)
IMPLICIT NONE
!
!
!  THIS SOFTWARE WAS DEVELOPED BY THE THERMAL MODELING AND ANALYSIS
!  PROJECT(TMAP) OF THE NATIONAL OCEANOGRAPHIC AND ATMOSPHERIC
!  ADMINISTRATION'S (NOAA) PACIFIC MARINE ENVIRONMENTAL LAB(PMEL),
!  HEREAFTER REFERRED TO AS NOAA/PMEL/TMAP.
!
!  ACCESS AND USE OF THIS SOFTWARE SHALL IMPOSE THE FOLLOWING
!  OBLIGATIONS AND UNDERSTANDINGS ON THE USER. THE USER IS GRANTED THE
!  RIGHT, WITHOUT ANY FEE OR COST, TO USE, COPY, MODIFY, ALTER, ENHANCE
!  AND DISTRIBUTE THIS SOFTWARE, AND ANY DERIVATIVE WORKS THEREOF, AND
!  ITS SUPPORTING DOCUMENTATION FOR ANY PURPOSE WHATSOEVER, PROVIDED
!  THAT THIS ENTIRE NOTICE APPEARS IN ALL COPIES OF THE SOFTWARE,
!  DERIVATIVE WORKS AND SUPPORTING DOCUMENTATION.  FURTHER, THE USER
!  AGREES TO CREDIT NOAA/PMEL/TMAP IN ANY PUBLICATIONS THAT RESULT FROM
!  THE USE OF THIS SOFTWARE OR IN ANY PRODUCT THAT INCLUDES THIS
!  SOFTWARE. THE NAMES TMAP, NOAA AND/OR PMEL, HOWEVER, MAY NOT BE USED
!  IN ANY ADVERTISING OR PUBLICITY TO ENDORSE OR PROMOTE ANY PRODUCTS
!  OR COMMERCIAL ENTITY UNLESS SPECIFIC WRITTEN PERMISSION IS OBTAINED
!  FROM NOAA/PMEL/TMAP. THE USER ALSO UNDERSTANDS THAT NOAA/PMEL/TMAP
!  IS NOT OBLIGATED TO PROVIDE THE USER WITH ANY SUPPORT, CONSULTING,
!  TRAINING OR ASSISTANCE OF ANY KIND WITH REGARD TO THE USE, OPERATION
!  AND PERFORMANCE OF THIS SOFTWARE NOR TO PROVIDE THE USER WITH ANY
!  UPDATES, REVISIONS, NEW VERSIONS OR "BUG FIXES".
!
!  THIS SOFTWARE IS PROVIDED BY NOAA/PMEL/TMAP "AS IS" AND ANY EXPRESS
!  OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
!  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
!  ARE DISCLAIMED. IN NO EVENT SHALL NOAA/PMEL/TMAP BE LIABLE FOR ANY
!  SPECIAL,
!  INDIRECT OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER
!  RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF
!  CONTRACT, NEGLIGENCE OR OTHER TORTUOUS ACTION, ARISING OUT OF OR IN
!  CONNECTION WITH THE ACCESS, USE OR PERFORMANCE OF THIS SOFTWARE.
!
!
! THE 1980 UNESCO INTERNATIONAL EQUATION OF STATE (IES80)
! *SH* 8/5/92 - ADAPTED FROM C VERSION SUPPLIED BY JOHN OSBORNE, PMEL

!       THIS CALCULATES RHO (DENSITY KG/M^3) AT SALINITY,
!       TEMPERATURE, AND PRESSURE.  THIS WILL BE POTENTIAL DENSITY IF
!       TEMPERATURE IS POTENTIAL TEMPERATURE.  THE ROUTINE USES THE HIGH
!       PRESSURE EQUATION OF STATE FROM MILLERO ET AL. 1980 AND THE ONE-
!       ATMOSPHERE EQUATION OF STATE FROM MILLERO AND POISSON 1981 AS
!       REPORTED IN GILL 1982.  THE NOTATION FOLLOWS MILLERO ET AL. 1980
!       AND MILLERO AND POISSON 1981.
!
!       NOTE: THE ROUTINE TAKES P IN DECIBARS AND CONVERTS TO BARS FOR
!       THE CALCULATIONS.
!
! REFERENCES:   MILLERO, F.J., ET AL., 1980, DEEP-SEA RES., 27A,
! 255-264.
!  MILLERO, F.J. AND ALAIN POISSON, 1981, DEEP-SEA RES., 28A, 625-629.
!  GILL, A.E., 1982, ATMOSPHERE-OCEAN DYNAMICS, ACADEMIC PRESS, INC.,
!  662 PP.
!
!       INPUT UNITS:
!                       S: PSU  T: DEG. C       P: DECIBARS.
!
!       OUTPUT UNITS:
!                       RHO: KG/M^3
!
!       CHECK VALUES:
!                       RHO = 999.96675         FOR S = 0,  T = 5,  P =
!                       0
!                       RHO = 1027.675465       FOR S = 35, T = 5,  P =
!                       0
!                       RHO = 1062.538172       FOR S = 35, T = 25, P =
!                       10000.

! CALLING ARGUMENT DECLARAIONS:
        REAL(R4) ::  SALINITY, TEMPERATURE, PRESSURE

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R4) ::  S, T, P, ROOTS
        REAL(R4) ::  A, B, C, D, E
        REAL(R4) ::  AW, BW, KW
        REAL(R4) ::  A2, B2, C2, KZERO, K
        REAL(R4) ::  RHOW, RHOZERO
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        T = TEMPERATURE
        P = PRESSURE / 10.
        ROOTS = SQRT (S)

! RHOW IS THE DENSITY OF PURE WATER AT TEMPERATURE T.
        RHOW = ((((6.536332E-09 * T - 1.120083E-06) * T + 1.001685E-04) &
     &          * T - 9.095290E-03) * T + 6.793952E-02) * T + 999.842594

        A2 = (((5.3875E-09 * T - 8.2467E-07) * T + 7.6438E-05) &
     &          * T - 4.0899E-03) * T + 8.24493E-01

        B2 = (-1.6546E-06 * T + 1.0227E-04) * T - 5.72466E-03

        C2 = 4.8314E-04

! RHOZERO IS THE ONE-ATMOSPHERE DENSITY OF SEAWATER
        RHOZERO = (C2 * S + B2 * ROOTS + A2) * S + RHOW

        IF(P.LT.0.000000001 .OR. .NOT. LCONT) THEN
           RHO_UNESCO_R4 = RHOZERO
           RETURN
        ENDIF

        A = ((-6.1670E-05 * T + 1.09987E-02) * T - 6.03459E-01) * T &
     &          + 54.6746

        B = (-5.3009E-04 * T + 1.6483E-02) * T + 7.944E-02

        C = (-1.6078E-06 * T - 1.0981E-05) * T + 2.2838E-03

        D = 1.91075E-04

        E = (9.1697E-10 * T + 2.0816E-08) * T - 9.9348E-07

        AW = ((-5.77905E-07 * T + 1.16092E-04) * T + 1.43713E-03) * T &
     &         + 3.239908

        BW = (5.2787E-08 * T - 6.12293E-06) * T + 8.50935E-05

! KW IS THE SECANT BULK MODULUS OF PURE WATER AT TEMPERATURE T.
        KW = (((-5.155288E-05 * T + 1.360477E-02) * T - 2.327105) &
     &          * T + 148.4206) * T + 19652.21

! KZERO IS THE SECANT BULK MODULUS OF SEAWATER AT ONE ATMOSPHERE.
        KZERO = (B * ROOTS + A) * S + KW

! K IS THE SECANT BULK MODULUS OF SEAWATER AT (S, T, P).
        K = ((E * P + D * ROOTS + C) * S + BW * P + AW) * P + KZERO

        RHO_UNESCO_R4 = RHOZERO / (1.0 - (P / K))

        RETURN
        END FUNCTION RHO_UNESCO_R4



!^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
!
!....... TL OF RHO_UNESCO - ADDED FOR SLA ASSIMILATION
!
!        TANGENT-LINEAR VERSION OF RHO_UNESCO
!        CODED FOR THE CASE DRHO/DP=0
!        ORIGINAL : ANDREA STORTO, 20.03.2009
!
!^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

REAL(KIND=R8) FUNCTION RHO_UNESCOTL_R8(SALINITY, TEMPERATURE,&
& SALINITY_TL,TEMPERATURE_TL,PRESSURE,LCONT)
IMPLICIT NONE

! CALLING ARGUMENT DECLARAIONS:
        REAL(R8) ::  SALINITY, TEMPERATURE, PRESSURE
        REAL(R8) ::  SALINITY_TL, TEMPERATURE_TL

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R8) ::  S, T, P, ROOTS
        REAL(R8) ::  S_TL, T_TL, ROOTS_TL
        REAL(R8) ::  A, B, C, D, E
        REAL(R8) ::  AW, BW, KW
        REAL(R8) ::  A2, B2, C2, KZERO, K
        REAL(R8) ::  A2_TL, B2_TL
        REAL(R8) ::  RHOW, RHOZERO
        REAL(R8) ::  RHOW_TL, RHOZERO_TL
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        S_TL = SALINITY_TL
        T = TEMPERATURE
        T_TL = TEMPERATURE_TL
        P = PRESSURE / 10.
        ROOTS = SQRT (S)

        RHOW_TL = ( 5._R8*6.536332D-09 * T**4 -4._R8*1.120083D-06*T**3 + &
                & 3._R8*1.001685D-04*T**2 - 2._R8*9.095290D-03*T + &
                & 6.793952D-02 )

        A2 = (((5.3875D-09 * T - 8.2467D-07) * T + 7.6438D-05) &
     &          * T - 4.0899D-03) * T + 8.24493D-01

        A2_TL = (4._R8*5.3875D-09 * T**3 - 3._R8*8.2467D-07 * T**2 + &
              &  2._R8*7.6438D-05*T - 4.0899D-03)

        B2 = (-1.6546D-06 * T + 1.0227D-04) * T - 5.72466D-03
        B2_TL = (-2._R8*1.6546D-06 * T + 1.0227D-04)

        C2 = 4.8314D-04

        RHOZERO_TL = 2._R8*C2*S*S_TL + B2_TL*ROOTS*S*T_TL + &
                   & 1.5_R8*B2*ROOTS*S_TL + A2_TL*S*T_TL + A2*S_TL + &
                   & RHOW_TL*T_TL

        IF(P.LT.0.000000001 .OR. .NOT. LCONT) THEN
           RHO_UNESCOTL_R8 = RHOZERO_TL
           RETURN
        ELSE
           CALL ABOR1('NOT SUPPORTED USE OF RHO_UNESCOTL')
        ENDIF

END FUNCTION RHO_UNESCOTL_R8
!.............................................................
REAL(KIND=R4) FUNCTION RHO_UNESCOTL_R4(SALINITY, TEMPERATURE,&
& SALINITY_TL,TEMPERATURE_TL,PRESSURE,LCONT)
IMPLICIT NONE

! CALLING ARGUMENT DECLARAIONS:
        REAL(R4) ::  SALINITY, TEMPERATURE, PRESSURE
        REAL(R4) ::  SALINITY_TL, TEMPERATURE_TL

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R4) ::  S, T, P, ROOTS
        REAL(R4) ::  S_TL, T_TL, ROOTS_TL
        REAL(R4) ::  A, B, C, D, E
        REAL(R4) ::  AW, BW, KW
        REAL(R4) ::  A2, B2, C2, KZERO, K
        REAL(R4) ::  A2_TL, B2_TL
        REAL(R4) ::  RHOW, RHOZERO
        REAL(R4) ::  RHOW_TL, RHOZERO_TL
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        S_TL = SALINITY_TL
        T = TEMPERATURE
        T_TL = TEMPERATURE_TL
        P = PRESSURE / 10.
        ROOTS = SQRT (S)

        RHOW_TL = ( 5._R4*6.536332E-09 * T**4 -4._R4*1.120083E-06*T**3 + &
                & 3._R4*1.001685E-04*T**2 - 2._R4*9.095290E-03*T + &
                & 6.793952E-02 )

        A2 = (((5.3875E-09 * T - 8.2467E-07) * T + 7.6438E-05) &
     &          * T - 4.0899E-03) * T + 8.24493E-01

        A2_TL = (4._R4*5.3875E-09 * T**3 - 3._R4*8.2467E-07 * T**2 + &
              &  2._R4*7.6438E-05*T - 4.0899E-03)

        B2 = (-1.6546E-06 * T + 1.0227E-04) * T - 5.72466E-03
        B2_TL = (-2._R4*1.6546E-06 * T + 1.0227E-04)

        C2 = 4.8314E-04

        RHOZERO_TL = 2._R4*C2*S*S_TL + B2_TL*ROOTS*S*T_TL + &
                   & 1.5_R4*B2*ROOTS*S_TL + A2_TL*S*T_TL + A2*S_TL + &
                   & RHOW_TL*T_TL

        IF(P.LT.0.000000001 .OR. .NOT. LCONT) THEN
           RHO_UNESCOTL_R4 = RHOZERO_TL
           RETURN
        ELSE
           CALL ABOR1('NOT SUPPORTED USE OF RHO_UNESCOTL')
        ENDIF

        END FUNCTION RHO_UNESCOTL_R4

!........ GRAVITY FROM LATITUDE AND PRESSURE (ANON70 BULLETIN GEODESIQUE) A.S.

REAL(KIND=R4) FUNCTION GRAVIT_R4(XLAT,XP)
! XLAT : DEGREES ; XP : PRESSURE DECIBARS
IMPLICIT NONE
REAL(KIND=R4) :: XLAT,XP,XWORK
XWORK = SIN(XLAT/57.29578_R4)
XWORK = XWORK*XWORK
GRAVIT_R4 = 9.780318_R4*(1._R4+(5.2788E-3+2.36E-5*XWORK)*XWORK) + 1.092E-6*XP
END FUNCTION GRAVIT_R4
!
REAL(KIND=R8) FUNCTION GRAVIT_R8(XLAT,XP)
! XLAT : DEGREES ; XP : PRESSURE DECIBARS
IMPLICIT NONE
REAL(KIND=R8) :: XLAT,XP,XWORK
XWORK = SIN(XLAT/57.29578_R8)
XWORK = XWORK*XWORK
GRAVIT_R8 = 9.780318_R8*(1._R8+(5.2788D-3+2.36D-5*XWORK)*XWORK) + 1.092D-6*XP
END FUNCTION GRAVIT_R8

!........ GRAVITY FROM LATITUDE
REAL(KIND=R4) FUNCTION GRAVITY_R4(XLAT)
IMPLICIT NONE
REAL(KIND=R4) :: XLAT, XPI,XW
XPI=2._R4*ASIN(1._R4)
XW=ABS(XLAT*XPI/180._R4)
GRAVITY_R4=978.0318_R4*(1._R4+5.3024E-3*SIN(XW)**2-5.9E-6*SIN(2._R4*XW)**2)
END FUNCTION GRAVITY_R4
!...
REAL(KIND=R8) FUNCTION GRAVITY_R8(XLAT)
IMPLICIT NONE
REAL(KIND=R8) :: XLAT, XPI,XW
XPI=2._R8*ASIN(1._R8)
XW=ABS(XLAT*XPI/180._R8)
GRAVITY_R8=978.0318_R8*(1._R8+5.3024D-3*SIN(XW)**2-5.9D-6*SIN(2._R8*XW)**2)
END FUNCTION GRAVITY_R8
!........ CORIOLIS PARAM
REAL(KIND=R4) FUNCTION CORIOL_R4(XLAT)
IMPLICIT NONE
REAL(KIND=R4) :: XLAT, XPI,XW
XPI=2._R4*ASIN(1._R4)
XW=ABS(XLAT*XPI/180._R4)
CORIOL_R4 = 14.5842E-5*SIN(XW)
END FUNCTION CORIOL_R4
!...
REAL(KIND=R8) FUNCTION CORIOL_R8(XLAT)
IMPLICIT NONE
REAL(KIND=R8) :: XLAT, XPI,XW
XPI=2._R8*ASIN(1._R8)
XW=ABS(XLAT*XPI/180._R8)
CORIOL_R8 = 14.5842D-5*SIN(XW)
END FUNCTION CORIOL_R8

!^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
!
!....... AD OF RHO_UNESCO - ADDED FOR SLA ASSIMILATION
!
!        ADJOINT VERSION OF RHO_UNESCO
!        CODED FOR THE CASE DRHO/DP=0
!        ORIGINAL : ANDREA STORTO, 20.03.2009
!
!^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
SUBROUTINE RHO_UNESCOAD_R4(RHO_AD,SALINITY, TEMPERATURE,&
& S_AD,T_AD)
IMPLICIT NONE

! CALLING ARGUMENT DECLARAIONS:
        REAL(R4) ::  RHO_AD
        REAL(R4) ::  SALINITY, TEMPERATURE, PRESSURE
        REAL(R4) ::  S_AD, T_AD

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R4) ::  S, T, P, ROOTS
        REAL(R4) ::  S_TL, T_TL, ROOTS_TL
        REAL(R4) ::  A, B, C, D, E
        REAL(R4) ::  AW, BW, KW
        REAL(R4) ::  A2, B2, C2, KZERO, K
        REAL(R4) ::  A2_TL, B2_TL
        REAL(R4) ::  RHOW, RHOZERO
        REAL(R4) ::  RHOW_TL, RHOZERO_TL
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        T = TEMPERATURE
        P = PRESSURE / 10.
        ROOTS = SQRT (S)

        RHOW_TL = ( 5._R4*6.536332E-09 * T**4 -4._R4*1.120083E-06*T**3 + &
                & 3._R4*1.001685E-04*T**2 - 2._R4*9.095290E-03*T + &
                & 6.793952E-02 )

        A2 = (((5.3875E-09 * T - 8.2467E-07) * T + 7.6438E-05) &
     &          * T - 4.0899E-03) * T + 8.24493E-01

        A2_TL = (4._R4*5.3875E-09 * T**3 - 3._R4*8.2467E-07 * T**2 + &
              &  2._R4*7.6438E-05*T - 4.0899E-03)

        B2 = (-1.6546E-06 * T + 1.0227E-04) * T - 5.72466E-03
        B2_TL = (-2._R4*1.6546E-06 * T + 1.0227E-04)

        C2 = 4.8314E-04

        S_AD = (2._R4*C2*S + 1.5_R4*B2*ROOTS + A2 )*RHO_AD

        T_AD = (B2_TL*ROOTS*S + A2_TL*S + RHOW_TL )*RHO_AD

END SUBROUTINE RHO_UNESCOAD_R4

!...
SUBROUTINE RHO_UNESCOAD_R8(RHO_AD,SALINITY, TEMPERATURE,&
& S_AD,T_AD)
IMPLICIT NONE

! CALLING ARGUMENT DECLARAIONS:
        REAL(R8),INTENT(IN) ::  RHO_AD
        REAL(R8),INTENT(IN) ::  SALINITY, TEMPERATURE
        REAL(R8),INTENT(OUT) ::  S_AD, T_AD
        REAL(R8) ::  PRESSURE

! INTERNAL VARIABLE DECLARATIONS:
        REAL(R8) ::  S, T, P, ROOTS
        REAL(R8) ::  S_TL, T_TL, ROOTS_TL
        REAL(R8) ::  A, B, C, D, E
        REAL(R8) ::  AW, BW, KW
        REAL(R8) ::  A2, B2, C2, KZERO, K
        REAL(R8) ::  A2_TL, B2_TL
        REAL(R8) ::  RHOW, RHOZERO
        REAL(R8) ::  RHOW_TL, RHOZERO_TL
        LOGICAL  :: LCONT

! INITIALIZE
        S = SALINITY
        T = TEMPERATURE
!        P = PRESSURE / 10.
        ROOTS = SQRT (S)

        RHOW_TL = ( 5._R8*6.536332E-09 * T**4 -4._R8*1.120083E-06*T**3 + &
                & 3._R8*1.001685E-04*T**2 - 2._R8*9.095290E-03*T + &
                & 6.793952E-02 )

        A2 = (((5.3875E-09 * T - 8.2467E-07) * T + 7.6438E-05) &
     &          * T - 4.0899E-03) * T + 8.24493E-01

        A2_TL = (4._R8*5.3875E-09 * T**3 - 3._R8*8.2467E-07 * T**2 + &
              &  2._R8*7.6438E-05*T - 4.0899E-03)

        B2 = (-1.6546E-06 * T + 1.0227E-04) * T - 5.72466E-03
        B2_TL = (-2._R8*1.6546E-06 * T + 1.0227E-04)

        C2 = 4.8314E-04


        S_AD = (2._R8*C2*S + 1.5_R8*B2*ROOTS + A2 )*RHO_AD

        T_AD = (B2_TL*ROOTS*S + A2_TL*S + RHOW_TL )*RHO_AD

END SUBROUTINE RHO_UNESCOAD_R8

!=================================================================
SUBROUTINE DIST_R4(XXLAT,XXLAT2,XXLON,XXLON2,XDIST4)
IMPLICIT NONE
REAL(R4),INTENT(IN) ::  XXLAT,XXLON,XXLAT2,XXLON2
REAL(R4),INTENT(OUT)::  XDIST4
REAL(R4) ::  XLAT,XLON,XLAT2,XLON2,DIST1,DIST2,DIST3,DIST4,AAA
XLAT=XXLAT
XLAT2=XXLAT2
XLON=XXLON
XLON2=XXLON2
AAA=2._R4*ASIN(1._R4)/180._R4
IF (XLON .GT. 180._R4) XLON=XLON-360._R4
IF (XLON2 .GT. 180._R4) XLON2=XLON2-360._R4
DIST1 = SIN(XLAT*AAA) * SIN(XLAT2*AAA) + &
&  COS(XLAT*AAA) * COS(XLAT2*AAA) * &
&  COS(XLON2*AAA - XLON*AAA)
DIST2 = ACOS(DIST1) / AAA
DIST3 = DIST2 * 60._R4
DIST4=DIST3*1853.32_R4
XDIST4=DIST4
RETURN
END SUBROUTINE DIST_R4
!=================================================================
SUBROUTINE DIST_R8(XXLAT,XXLAT2,XXLON,XXLON2,XDIST4)
IMPLICIT NONE
REAL(R8),INTENT(IN) ::  XXLAT,XXLON,XXLAT2,XXLON2
REAL(R8),INTENT(OUT)::  XDIST4
REAL(R8) ::  XLAT,XLON,XLAT2,XLON2,DIST1,DIST2,DIST3,DIST4,AAA
XLAT=XXLAT
XLAT2=XXLAT2
XLON=XXLON
XLON2=XXLON2
AAA=2._R8*ASIN(1._R8)/180._R8
IF (XLON .GT. 180._R8) XLON=XLON-360._R8
IF (XLON2 .GT. 180._R8) XLON2=XLON2-360._R8
DIST1 = SIN(XLAT*AAA) * SIN(XLAT2*AAA) + &
&  COS(XLAT*AAA) * COS(XLAT2*AAA) * &
&  COS(XLON2*AAA - XLON*AAA)
DIST2 = ACOS(DIST1) / AAA
DIST3 = DIST2 * 60._R8
DIST4=DIST3*1853.32_R8
XDIST4=DIST4
RETURN
END SUBROUTINE DIST_R8
!=================================================================
END MODULE OCEANTOOLS
