MODULE READOBAS

! MODULE FOR READING OBSERVATIONS
! FROM ASCII FILE
!
! A.STORTO 01.10.2010

USE IOUNITS
USE SET_KND
USE OBSDEF
USE OBS_STR
USE RUN

IMPLICIT NONE

INTEGER(I4), PARAMETER :: IOBASUN = 219
REAL   (R8), PARAMETER :: PNOERR = -999._R8

CONTAINS

SUBROUTINE GETOASN(IOBS)

! GET THE OBSERVATIONS AMOUNT BY TYPE

IMPLICIT NONE

INTEGER(I4), INTENT(OUT) :: IOBS(NOFAMS)
INTEGER(I4) :: IERR, IFAILED, IFAM
INTEGER(I4) :: KK, ITYPE, IINST, IPAR, INO
REAL(R8)    :: ZLON, ZLAT, ZDPT, ZVAL, ZERR
REAL(DP)    :: ZTIM

IOBS = 0
IFAILED = 0

OPEN(IOBASUN,FILE='OBSIN.DAT',STATUS='OLD',IOSTAT=IERR)
REWIND(IOBASUN)
IF(IERR .NE. 0) THEN
   WRITE(IOUNERR,*) 'OPENING OBSIN.DAT RETURNED ',IERR
   CALL ABOR1('PROBLEMS OPENING OBSIN.DAT')
ENDIF

DO WHILE (IERR.EQ.0)
   READ(IOBASUN,*,IOSTAT=IERR) IFAM, ITYPE, IINST, IPAR, INO, &
   & ZLON, ZLAT, ZDPT, ZTIM, ZVAL, ZERR
   WRITE(IOUNLOG,*) 'OBS_ASCII: ',IERR,IFAM, ITYPE, IINST, IPAR, INO, &
   & ZLON, ZLAT, ZDPT, ZTIM, ZVAL, ZERR
   IF(IERR.EQ.0) THEN
      IF(IFAM.GT.0 .AND. IFAM .LE. NOFAMS) THEN
        IOBS(IFAM) = IOBS(IFAM) + 1
      ELSE
        IFAILED = IFAILED + 1
      ENDIF
   ENDIF
ENDDO

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' *** REPORT FROM GETOASN'
WRITE(IOUNLOG,*) '-----------------------------------'
WRITE(IOUNLOG,*) ' NUMBER OF INSITU OBS ',IOBS(NNINS)
WRITE(IOUNLOG,*) ' NUMBER OF SLA    OBS ',IOBS(NNSLA)
WRITE(IOUNLOG,*) ' NUMBER OF SST    OBS ',IOBS(NNSST)
WRITE(IOUNLOG,*) ' NUMBER OF SSS    OBS ',IOBS(NNSSS)
WRITE(IOUNLOG,*) '-----------------------------------'
WRITE(IOUNLOG,*) ' NUMBER OF FAILED OBS ',IFAILED
WRITE(IOUNLOG,*) ' TOTAL NUMBER OF  OBS ',SUM(IOBS)
WRITE(IOUNLOG,*)

CLOSE(IOBASUN)
END SUBROUTINE GETOASN

SUBROUTINE GETOASOBS

! GET THE OBSERVATIONAL DATA

IMPLICIT NONE

INTEGER(I4) :: IERR, IFAILED, IFAM
INTEGER(I4) :: IOBS(NOFAMS)
INTEGER(I4) :: KK, ITYPE, IINST, IPAR, INO
REAL(R8)    :: ZLON, ZLAT, ZDPT, ZVAL, ZERR
REAL(DP)    :: ZTIM

IOBS = 0

OPEN(IOBASUN,FILE='OBSIN.DAT',STATUS='OLD',IOSTAT=IERR)
IF(IERR .NE. 0) THEN
   WRITE(IOUNERR,*) 'OPENING OBSIN.DAT RETURNED ',IERR
   CALL ABOR1('PROBLEMS OPENING OBSIN.DAT')
ENDIF
REWIND(IOBASUN)

DO WHILE (IERR.EQ.0)
   READ(IOBASUN,*,IOSTAT=IERR) IFAM, ITYPE, IINST, IPAR, INO, &
   & ZLON, ZLAT, ZDPT, ZTIM, ZVAL, ZERR
   IF(IERR.EQ.0) THEN
      IF(IFAM.GT.0 .AND. IFAM .LE. NOFAMS) THEN
        IOBS(IFAM) = IOBS(IFAM) + 1
        KK = IOBS(IFAM)

        SELECT CASE(IFAM)

          CASE(NNINS)
             INS%OTYPE(KK) = ITYPE
             INS%INST (KK) = IINST
             INS%PAR  (KK) = IPAR
             INS%INO  (KK) = INO
             INS%LON  (KK) = ZLON
             INS%LAT  (KK) = ZLAT
             INS%DPT  (KK) = ZDPT
             INS%TIM  (KK) = ZTIM
             INS%VAL  (KK) = ZVAL
             IF( ZERR .GT. 0._R8 ) THEN
               INS%ERR  (KK) = ZERR
             ELSE
               INS%ERR  (KK) = PNOERR
             ENDIF
             INS%TDIST(KK) = INS%TIM(KK) - ZANJUL1950
             INS%DSRC(KK)='EN3'
             IF(IPAR.EQ.KKT2M.OR.IPAR.EQ.KKT2M) INS%DSRC(KK)='BUF'

          CASE(NNSLA)
             SLA%KSAT (KK) = IINST
             SLA%INO  (KK) = INO
             SLA%LON  (KK) = ZLON
             SLA%LAT  (KK) = ZLAT
             SLA%TIM  (KK) = ZTIM
             SLA%VAL  (KK) = ZVAL
             IF( ZERR .GT. 0._R8 ) THEN
               SLA%ERR  (KK) = ZERR
             ELSE
               SLA%ERR  (KK) = PNOERR
             ENDIF
             SLA%TDIST(KK) = SLA%TIM(KK) - ZANJUL1950

          CASE(NNSST)
             SST%KSAT (KK) = IINST
             SST%LON  (KK) = ZLON
             SST%LAT  (KK) = ZLAT
             SST%TIM  (KK) = ZTIM
             SST%VAL  (KK) = ZVAL
             IF( ZERR .GT. 0._R8 ) THEN
               SST%ERR  (KK) = ZERR
             ELSE
               SST%ERR  (KK) = PNOERR
             ENDIF
             SST%TDIST(KK) = SST%TIM(KK) - ZANJUL1950

          CASE(NNSSS)
             SSS%KSAT (KK) = IINST
             SSS%LON  (KK) = ZLON
             SSS%LAT  (KK) = ZLAT
             SSS%TIM  (KK) = ZTIM
             SSS%VAL  (KK) = ZVAL
             IF( ZERR .GT. 0._R8 ) THEN
               SSS%ERR  (KK) = ZERR
             ELSE
               SSS%ERR  (KK) = PNOERR
             ENDIF
             SSS%TDIST(KK) = SSS%TIM(KK) - ZANJUL1950

        ENDSELECT
      ELSE
        IFAILED = IFAILED + 1
      ENDIF
   ENDIF
ENDDO

CLOSE(IOBASUN)

END SUBROUTINE GETOASOBS

END MODULE READOBAS
