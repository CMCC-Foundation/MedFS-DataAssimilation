SUBROUTINE READ_NOAA_SST_NC (CFILE,MAXOBS,NTOBS,ZDATA,&
          & ZLLON,ZLLAT,ZERR)

USE SET_KND
USE NETCDF
USE IOUNITS

IMPLICIT NONE

CHARACTER(LEN=*), INTENT(IN) :: CFILE
INTEGER(I4), INTENT(IN) :: MAXOBS
INTEGER(I4), INTENT(OUT) :: NTOBS
REAL   (R8), DIMENSION(MAXOBS), INTENT(OUT) :: ZDATA,&
          & ZLLON,ZLLAT,ZERR

INTEGER(I4), PARAMETER :: NX=1440, NY=720
REAL(DP) :: ZOTIM(NX,NY)
REAL(R8) :: ZOLON(NX),ZOLAT(NY),ZOSST(NX,NY),ZOERR(NX,NY)
INTEGER(I4) :: STAT,IDVAR,IDF,JX,JY,JT

WRITE(IOUNOUT,*) ' :: OPENING NOAA SST FILE '//TRIM(CFILE)
WRITE(IOUNLOG,*) ' :: OPENING NOAA SST FILE '//TRIM(CFILE)

STAT=NF90_OPEN(CFILE,NF90_NOWRITE, IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'lon', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLON)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

WHERE ( ZOLON .GT. 180._R8 ) ZOLON = ZOLON - 360._R8

STAT = NF90_INQ_VARID (IDF, 'lat', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOLAT)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'sst', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOSST)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'err', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOERR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_VARID (IDF, 'ice', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR (IDF,IDVAR,ZOTIM)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT=NF90_CLOSE(IDF)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

NTOBS=0

DO JY=1,NY
  DO JX=1,NX
        IF(ZOSST(JX,JY).GE.-2._R8 .AND. ZOSST(JX,JY).LT.35._R8 .AND. &
        & ZOERR(JX,JY) .GT. 0._R8 .AND. ZOERR(JX,JY).LT. 1.5_R8 .AND. &
        & ZOTIM(JX,JY).LT.0.05_R8) THEN
          NTOBS=NTOBS+1
          IF(NTOBS.GT.MAXOBS) &
          & CALL ABOR1('READ_AVHRR_NC   : MAXOBS TO INCREASE')
          ZDATA(NTOBS) = ZOSST(JX,JY)
          ZLLON(NTOBS) = ZOLON(JX)
          ZLLAT(NTOBS) = ZOLAT(JY)
          ZERR(NTOBS)  = ZOERR(JX,JY)*1.5_R8
        ENDIF
  ENDDO
ENDDO

WRITE(IOUNLOG,*) ' --> FOUND ',NTOBS,' OBSERVATIONS'

END SUBROUTINE READ_NOAA_SST_NC
