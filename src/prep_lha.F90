SUBROUTINE PREP_LHA

!.. PREPARATION FOR ASSIMILATING SLA
!.. THROUGH LOCAL HYDROSTATIC ADJUSTMENT
!..
!.. A.STORTO, 2009-03-18


 USE SET_KND
 USE GRD_STR
 USE OBS_STR
 USE NETCDF
 USE RUN, ONLY : RHO0, KLEVNM, ZNOML, LL_VARLNM, CGRIDPATH, NGRID_ID
 USE IOUNITS, ONLY : IOUNLOG, IOUNOUT
 USE OCEANTOOLS , ONLY : RHO_UNESCO
 USE MYFRTPROF,ONLY : MYFRTPROF_WALL

 IMPLICIT NONE
 REAL(R8) :: RTDIFF,MSK4
 CHARACTER (LEN=10) :: CDATENEW
 INTEGER(I4)   ::  I, J, K,JTSTEP, JX,JY,I2,JP, &
                & KBGQC,KNO(5),KCE1,KCTP,JVL,IERR,JKLEV
 INTEGER(I4)   ::  XIND1

 ! Variables for full domain mask
 INTEGER(I4) :: NCID, STAT, IDVAR               ! Netcdf management
 INTEGER(I4) :: dim_X, dim_Y, dim_Z             ! Domain dimensions
 REAL(R8), DIMENSION(:,:,:), ALLOCATABLE :: FULL_MASK  ! Mask over full domain


#include "grids.h"
#include "obs_events.h"

 CALL MYFRTPROF_WALL('PREP_LHA: PREPARING LHA ALGORITHM',0)

 KNO=0
 RHO0=RHO_UNESCO(35.0_R8,0.0_R8 ,0.0_R8 ,.FALSE.)
 WRITE(IOUNLOG,*) ' CHECK: RHO_0 IS ',RHO0



!... READ THE FULL DOMAIN BATHYMETRY
 ! Open file
 STAT = NF90_OPEN(TRIM(CGRIDPATH)//TRIM(CNFILE(NGRID_ID)), NF90_NOWRITE,NCID)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

 ! Get dimensions
 STAT = NF90_INQ_DIMID (NCID, 'x', IDVAR)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = dim_X)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_INQ_DIMID (NCID, 'y', IDVAR)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = dim_Y)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_INQ_DIMID (NCID, 'z', IDVAR)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = dim_Z)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

 ! Allocate array
 ALLOCATE ( FULL_MASK(dim_X,dim_Y,dim_Z))

 ! Read bathymetry data
 STAT = NF90_INQ_VARID (NCID, 'tmsk', IDVAR)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
 STAT = NF90_GET_VAR(NCID,IDVAR,FULL_MASK,START=(/1,1,1/),COUNT=(/dim_X,dim_Y,dim_Z/))
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

 ! Close file
 STAT = NF90_CLOSE(NCID)
 IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)



 
!... CHECK LEVEL OF NO MOTION FOR EACH OBS
 DO JVL=1,GRD%KM
    IF(GRD%DEP(JVL) .GT. ZNOML) THEN
       KLEVNM = JVL
       EXIT
    ENDIF
 ENDDO
 WRITE(IOUNOUT,*) ' NO-MOTION LEVEL IS',ZNOML,&
               & ' M CORRESPONDING TO MODEL LEVEL',KLEVNM
 WRITE(IOUNLOG,*) ' NO-MOTION LEVEL IS',ZNOML,&
               & ' M CORRESPONDING TO MODEL LEVEL',KLEVNM

CYOBS4: DO K=1,SLA%NO

    ! Total number of observations
    KNO(3) = KNO(3) + 1
  
    ! Number of already rejected observations
    IF(SLA%FLC(K).EQ.0) THEN
       KNO(2) = KNO(2) + 1
       CYCLE CYOBS4
    ENDIF

    ! Number of ocean points around obs at no-motion depth
    MSK4=0._R8
    DO JP=1,NPQ
       MSK4=MSK4+FULL_MASK(SLA%IB(K,JP),SLA%JB(K,JP),KLEVNM)
    ENDDO
    
    ! If not 4 (one land point), reject obs
    IF(MSK4 .LT. 3.9_R8) THEN
       KNO(4) = KNO(4) +1
       SLA%FLC(K) = 0
       SLA%EVE(K) = KEVE_LONM
       CYCLE CYOBS4
    ENDIF
  
    ! Variable no-motion depth (model depth if > namelist value)
    IF( LL_VARLNM ) THEN
       SLA%BOT(K) = GRD%KM
       BOTTOM : DO JKLEV=KLEVNM,GRD%KM
          MSK4=0._R8
          DO JP=1,NPQ
             MSK4=MSK4+FULL_MASK(SLA%IB(K,JP),SLA%JB(K,JP),JKLEV)
          ENDDO
          IF(MSK4 .LT. 3.9_R8) THEN
             SLA%BOT(K) = JKLEV-1
             EXIT BOTTOM
          ENDIF
       ENDDO BOTTOM
    ELSE
       SLA%BOT(K) = KLEVNM
    ENDIF
  
    ! Reject observation at -3 < lat < 3 degrees (equator)
    IF(ABS(SLA%LAT(K)).LT.3._R8) THEN
       KNO(5) = KNO(5) +1
       SLA%FLC(K) = 0
       SLA%EVE(K) = KEVE_GEOR
       CYCLE CYOBS4
    ENDIF
  
    KNO(1) = KNO(1) + 1

 ENDDO CYOBS4

 DEALLOCATE(FULL_MASK)

 WRITE(IOUNLOG,*) ' TOTAL OBSERVATIONS      : ',KNO(3)
 WRITE(IOUNLOG,*) ' OBS ALREADY REJECTED    : ',KNO(2)
 WRITE(IOUNLOG,*) ' OBS IN TOO SHALLOW AREAS: ',KNO(4)
 WRITE(IOUNLOG,*) ' OBS TOO CLOSE TO EQUATOR: ',KNO(5)
 WRITE(IOUNLOG,*) ' GOOD OBSERVATIONS       : ',KNO(1)
 WRITE(IOUNOUT,*) ' GOOD SLA OBSERVATIONS       : ',KNO(1)

 CALL MYFRTPROF_WALL('PREP_LHA: PREPARING LHA ALGORITHM',1)
END SUBROUTINE PREP_LHA
