SUBROUTINE REARRALPHASRZ
USE MYFRTPROF
USE RECFILTER
USE MYNETCDF

!-- RECOMPUTE ALPHAS ACCORDING TO EXTENDED GRID
!   THIS ROUTINE IS SUITABLE TO BE RECALLED WITHIN RF ROUTINES

USE GRD_STR

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY
INTEGER(KIND=I4) :: I,J,KK

CALL MYFRTPROF_WALL('REARRALPHASRZ: REARRANGE RF COEFFS',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// REARRANGE ALPHA FOR SSH'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF( .NOT.ALLOCATED(GRD%DX).OR..NOT.ALLOCATED(GRD%DY).OR.&
  & .NOT.ALLOCATED(GRD%MSR)) THEN
   WRITE(IOUNERR,*) 'REARRALPHA CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN REARRALPHA, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('REARRALPHA CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM

ALLOCATE( RF_AEXZ(NY,IMAX),&
        & RF_BEXZ(NY,IMAX),&
        & RF_AEYZ(NX,JMAX),&
        & RF_BEYZ(NX,JMAX) )


RF_AEXZ = 0._R8
RF_AEYZ = 0._R8
RF_BEXZ = 0._R8
RF_BEYZ = 0._R8

   DO J = 1, NY
      KK = ISTPNSZ(1,J)
      IF(LLMSR2(1,J)) THEN
          KK = KK + 1
          RF_AEXZ(J,1:KK) = SR_GALXZ(1,J)
          RF_BEXZ(J,1:KK) = SR_GBTXZ(1,J)
      ENDIF
      DO I = 2,NX
         IF(.NOT.LLMSR2(I,J) .AND. LLMSR2(I-1,J)) THEN
                 RF_AEXZ(J,KK+1:KK+ISTPNSZ(I,J)) = SR_GALXZ(I-1,J)
                 RF_BEXZ(J,KK+1:KK+ISTPNSZ(I,J)) = SR_GBTXZ(I-1,J)
                 KK = KK + ISTPNS(I,J,1)
         ELSEIF(LLMSR2(I,J) .AND. .NOT.LLMSR2(I-1,J)) THEN
                 RF_AEXZ(J,KK+1:KK+ISTPNSZ(I,J)+1) = SR_GALXZ(I,J)
                 RF_BEXZ(J,KK+1:KK+ISTPNSZ(I,J)+1) = SR_GBTXZ(I,J)
                 KK = KK + ISTPNS(I,J,1) + 1
         ELSEIF(LLMSR2(I,J)) THEN
                 RF_AEXZ(J,KK+1) = SR_GALXZ(I,J)
                 RF_BEXZ(J,KK+1) = SR_GBTXZ(I,J)
                 KK = KK + 1
         ENDIF
      ENDDO
   ENDDO

   IF (LL_PRINT_RECFILT) THEN
      WRITE(IOUNLOG,*) 'RF_AEXZ: ',SUM(RF_AEXZ(:,1:KK))/(NY*KK),&
      & MINVAL(RF_AEXZ(:,1:KK)),MAXVAL(RF_AEXZ(:,1:KK))
      WRITE(IOUNLOG,*) 'RF_BEXZ: ',SUM(RF_BEXZ(:,1:KK))/(NY*KK),&
      & MINVAL(RF_BEXZ(:,1:KK)),MAXVAL(RF_BEXZ(:,1:KK))
   ENDIF

   CALL FLUSH(IOUNLOG)

   DO I = 1, NX
      KK = JSTPNSZ(I,1)
      IF(LLMSR2(I,1)) THEN
          KK = KK + 1
          RF_AEYZ(I,1:KK) = SR_GALYZ(I,1)
          RF_BEYZ(I,1:KK) = SR_GBTYZ(I,1)
      ENDIF
      DO J = 2, NY
         IF(.NOT.LLMSR2(I,J) .AND. LLMSR2(I,J-1)) THEN
                 RF_AEYZ(I,KK+1:KK+JSTPNSZ(I,J)) = SR_GALYZ(I,J-1)
                 RF_BEYZ(I,KK+1:KK+JSTPNSZ(I,J)) = SR_GBTYZ(I,J-1)
                 KK = KK + JSTPNS(I,J,1)
         ELSEIF(LLMSR2(I,J) .AND. .NOT.LLMSR2(I,J-1)) THEN
                 RF_AEYZ(I,KK+1:KK+JSTPNSZ(I,J)+1) = SR_GALYZ(I,J)
                 RF_BEYZ(I,KK+1:KK+JSTPNSZ(I,J)+1) = SR_GBTYZ(I,J)
                 KK = KK + JSTPNS(I,J,1) + 1
         ELSEIF(LLMSR2(I,J)) THEN
                 RF_AEYZ(I,KK+1) = SR_GALYZ(I,J)
                 RF_BEYZ(I,KK+1) = SR_GBTYZ(I,J)
                 KK = KK + 1
         ENDIF
      ENDDO
   ENDDO

   IF (LL_PRINT_RECFILT) THEN
      WRITE(IOUNLOG,*) 'RF_AEYZ: ',SUM(RF_AEYZ(:,1:KK))/(NY*KK),&
      & MINVAL(RF_AEYZ(:,1:KK)),MAXVAL(RF_AEYZ(:,1:KK))
      WRITE(IOUNLOG,*) 'RF_BEYZ: ',SUM(RF_BEYZ(:,1:KK))/(NY*KK),&
      & MINVAL(RF_BEYZ(:,1:KK)),MAXVAL(RF_BEYZ(:,1:KK))
   ENDIF

   CALL FLUSH(IOUNLOG)

! SCALE FACTORS

ALLOCATE(SCXNZ(NX,NY), SCYNZ(NX,NY))

SCXNZ(:,:)       = SR_SCXZ
SCYNZ(:,:)       = SR_SCYZ

WRITE(IOUNLOG,*) 'SCXNZ: ',SUM(SCXNZ(:,:))/(NY*NX),&
& MINVAL(SCXNZ(:,:)),MAXVAL(SCXNZ(:,:))
WRITE(IOUNLOG,*) 'SCYNZ: ',SUM(SCYNZ(:,:))/(NY*NX),&
& MINVAL(SCYNZ(:,:)),MAXVAL(SCYNZ(:,:))

CALL FLUSH(IOUNLOG)

CALL MYFRTPROF_WALL('REARRALPHASRZ: REARRANGE RF COEFFS',1)
RETURN
END SUBROUTINE REARRALPHASRZ
