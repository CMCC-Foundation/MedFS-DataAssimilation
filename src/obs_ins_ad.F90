SUBROUTINE OBS_INS_AD

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR INS DATA, ADJOINT VERSION           !
!                                                                      !
! VERSION 1: A.STORTO 2009                                             !
! VERSION 2: A.STORTO 2009, OPENMP PARALLELIZATION                     !
!-----------------------------------------------------------------------

 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE CTL_STR
 USE IOUNITS , ONLY : IOUNERR
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL
 USE TLAD_VARS
 USE WEAKLY

 IMPLICIT NONE

 INTEGER(I4)   ::  I, JP, D, KK,I2,OBS_K,NO(INS%NO)
 INTEGER(I4)   ::  XIND1

CALL MYFRTPROF_WALL('OBS_INS_AD: ADJOINT OF INS OPERATOR',0)

!... WATCHOUT : KK FOLLOWS INS/SLA/ ETC ARRAYS
!               K  FOLLOWS GLOBAL OBSERVATION VECTOR

CYOBS : DO KK = 1,INS%NO

 IF( .NOT. LL_4DVAR .OR. ( LL_4DVAR .AND. INS%TATS(KK) .EQ. TLAD_TS )) THEN

  IF(INS%FLC(KK).EQ.0) CYCLE CYOBS

  OBS%K = OBS%K + 1
  OBS_K = OBS%K
  IF( LL_4DVAR ) OBS_K = INS%OBIN(KK)
  D=INS%KB(KK)

  IF( INS%PAR(KK).EQ.KKTEMP)THEN

    DO JP=1,NPQ
      GRD%TEM_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) = &
      & GRD%TEM_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) + INS%PQ(KK,JP) *OBS%GRA(OBS_K)
    ENDDO
    DO JP=1,NPQ
      GRD%TEM_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) = &
      & GRD%TEM_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) + INS%PQ(KK,JP+NPQ)*OBS%GRA(OBS_K)
    ENDDO
    IF(LL_WEAKLY) THEN
     DO JP=1,NPQ
      TERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) = &
      & TERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) + INS%PQ(KK,JP) *OBS%GRA(OBS_K)
     ENDDO
     DO JP=1,NPQ
      TERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) = &
      & TERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) + INS%PQ(KK,JP+NPQ)*OBS%GRA(OBS_K)
     ENDDO
    ENDIF

  ELSE IF(INS%PAR(KK).EQ.KKSAL )THEN

    DO JP=1,NPQ
      GRD%SAL_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) = &
      & GRD%SAL_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) + INS%PQ(KK,JP) *OBS%GRA(OBS_K)
    ENDDO
    DO JP=1,NPQ
      GRD%SAL_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) = &
      & GRD%SAL_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) + INS%PQ(KK,JP+NPQ)*OBS%GRA(OBS_K)
    ENDDO
    IF(LL_WEAKLY) THEN
     DO JP=1,NPQ
      SERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) = &
      & SERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D) + INS%PQ(KK,JP) *OBS%GRA(OBS_K)
     ENDDO
     DO JP=1,NPQ
      SERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) = &
      & SERR_AD(INS%IB(KK,JP),INS%JB(KK,JP),D+1) + INS%PQ(KK,JP+NPQ)*OBS%GRA(OBS_K)
     ENDDO
    ENDIF

  ELSE IF(INS%PAR(KK).EQ.KKT2M )THEN

    DO JP=1,NPQ
      GRD%T2M_AD(INS%IB(KK,JP),INS%JB(KK,JP)) = &
      & GRD%T2M_AD(INS%IB(KK,JP),INS%JB(KK,JP)) + INS%PQ(KK,JP)*OBS%GRA(OBS_K)
    ENDDO

  ELSE IF(INS%PAR(KK).EQ.KKQ2M )THEN

    DO JP=1,NPQ
      GRD%Q2M_AD(INS%IB(KK,JP),INS%JB(KK,JP)) = &
      & GRD%Q2M_AD(INS%IB(KK,JP),INS%JB(KK,JP)) + INS%PQ(KK,JP)*OBS%GRA(OBS_K)
    ENDDO

  ELSE

     WRITE(IOUNERR,*) 'PARAMETER ',INS%PAR(KK),' NOT SUPPORTED'
     CALL ABOR1('OBS_INS : UNKNOWN PARAMETER')

  ENDIF

 ENDIF

ENDDO CYOBS

CALL MYFRTPROF_WALL('OBS_INS_AD: ADJOINT OF INS OPERATOR',1)
END SUBROUTINE OBS_INS_AD
