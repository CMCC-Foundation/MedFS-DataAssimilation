SUBROUTINE REARRALPHA_LVM

USE RECFILTER

!-- RECOMPUTE ALPHAS ACCORDING TO EXTENDED GRID
!   THIS ROUTINE IS SUITABLE TO BE RECALLED WITHIN RF ROUTINES

USE GRD_STR
USE MYNETCDF
USE RUN
USE MYFRTPROF

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY
INTEGER(KIND=I4) :: I,J,K,KK

CALL MYFRTPROF_WALL('REARRALPHA_LVM: REARRANGE ALPHA',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// REARRANGE ALPHA'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF( .NOT.ALLOCATED(GRD%DX).OR..NOT.ALLOCATED(GRD%DY).OR.&
  & .NOT.ALLOCATED(GRD%MSR)) THEN
   WRITE(IOUNERR,*) 'REARRALPHA CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN REARRALPHA, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('REARRALPHA CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM

ALLOCATE( RF_AEXZ(NY,IMAXZ),&
        & RF_BEXZ(NY,IMAXZ),&
        & RF_AEYZ(NX,JMAXZ),&
        & RF_BEYZ(NX,JMAXZ) )

RF_AEXZ = 0._R8
RF_AEYZ = 0._R8
RF_BEXZ = 0._R8
RF_BEYZ = 0._R8

   DO J = 1, NY
      KK = ISTPNSZ(1,J)
      IF(LLMSRM(1,J)) THEN
          KK = KK + 1
          RF_AEXZ(J,1:KK) = ALXNZ(1,J)
          RF_BEXZ(J,1:KK) = BTXNZ(1,J)
      ENDIF
      DO I = 2,NX
         IF(.NOT.LLMSRM(I,J) .AND. LLMSRM(I-1,J)) THEN
                 RF_AEXZ(J,KK+1:KK+ISTPNSZ(I,J)) = ALXNZ(I,J)
                 RF_BEXZ(J,KK+1:KK+ISTPNSZ(I,J)) = BTXNZ(I,J)
                 KK = KK + ISTPNSZ(I,J)
         ELSEIF(LLMSRM(I,J) .AND. .NOT.LLMSRM(I-1,J)) THEN
                 RF_AEXZ(J,KK+1:KK+ISTPNSZ(I,J)+1) = ALXNZ(I,J)
                 RF_BEXZ(J,KK+1:KK+ISTPNSZ(I,J)+1) = BTXNZ(I,J)
                 KK = KK + ISTPNSZ(I,J) + 1
         ELSEIF(LLMSRM(I,J)) THEN
                 RF_AEXZ(J,KK+1) = ALXNZ(I,J)
                 RF_BEXZ(J,KK+1) = BTXNZ(I,J)
                 KK = KK + 1
         ENDIF
      ENDDO
   ENDDO

   DO I = 1, NX
      KK = JSTPNSZ(I,1)
      IF(LLMSRM(I,1)) THEN
          KK = KK + 1
          RF_AEYZ(I,1:KK) = ALYNZ(I,1)
          RF_BEYZ(I,1:KK) = BTYNZ(I,1)
      ENDIF
      DO J = 2, NY
         IF(.NOT.LLMSRM(I,J) .AND. LLMSRM(I,J-1)) THEN
                 RF_AEYZ(I,KK+1:KK+JSTPNSZ(I,J)) = ALYNZ(I,J)
                 RF_BEYZ(I,KK+1:KK+JSTPNSZ(I,J)) = BTYNZ(I,J)
                 KK = KK + JSTPNSZ(I,J)
         ELSEIF(LLMSRM(I,J) .AND. .NOT.LLMSRM(I,J-1)) THEN
                 RF_AEYZ(I,KK+1:KK+JSTPNSZ(I,J)+1) = ALYNZ(I,J)
                 RF_BEYZ(I,KK+1:KK+JSTPNSZ(I,J)+1) = BTYNZ(I,J)
                 KK = KK + JSTPNSZ(I,J) + 1
         ELSEIF(LLMSRM(I,J)) THEN
                 RF_AEYZ(I,KK+1) = ALYNZ(I,J)
                 RF_BEYZ(I,KK+1) = BTYNZ(I,J)
                 KK = KK + 1
         ENDIF
      ENDDO
   ENDDO

IF( NCONF .EQ. 202 ) THEN
  ALLOCATE( FCT(GRD%IM, GRD%JM, 1) )
  FCT = 0._R8
  WHERE( LLMSRM ) FCT(:,:,1) = 1._R8
ENDIF

CALL MYFRTPROF_WALL('REARRALPHA_LVM: REARRANGE ALPHA',1)

END SUBROUTINE REARRALPHA_LVM
