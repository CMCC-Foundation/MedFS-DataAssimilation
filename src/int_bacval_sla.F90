SUBROUTINE INT_BACVAL_SLA

!-----------------------------------------------------------------------
!                                                                      !
! APPLY OBSERVATIONAL OPERATOR FOR SLA                                 !
!                                                                      !
! VERSION 1: S.DOBRICIC 2006                                           !
!-----------------------------------------------------------------------


 USE SET_KND
 USE GRD_STR
 USE OBSDEF
 USE OBS_STR
 USE READFG
 USE RUN, ONLY : CDATE, RTIMES1950,RHO0,KLEVNM,NTSTEPS
 USE IOUNITS, ONLY : IOUNLOG,IOUNOUT
 USE OCEANTOOLS , ONLY : RHO_UNESCO
 USE MYFRTPROF, ONLY : MYFRTPROF_WALL

 IMPLICIT NONE

 INTEGER(I4)   :: J
 REAL(R8) :: MSR4,MSK4
 CHARACTER (LEN=10) :: CDATENEW
 INTEGER(I4)   ::  K,JTSTEP,KSTEP,IDIFFER1,IDIFFER2,IRET,KNO(5),JP,FIRSTTS
 LOGICAL :: LLPRINT

#include "obs_events.h"

 CALL MYFRTPROF_WALL('INT_BACVAL_SLA: SLA BGVALUES',0)

 IF(SLA%NO==0) THEN
   CALL MYFRTPROF_WALL('INT_BACVAL_SLA: SLA BGVALUES',1)
   RETURN
 ENDIF

 LLPRINT = .FALSE.

 FIRST : DO JTSTEP=1,NTSTEPS
  IF( LL_FGASSIM(JTSTEP) ) THEN
      FIRSTTS=JTSTEP
      EXIT FIRST
  ENDIF
 ENDDO FIRST

 KNO=0

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) '+++++++++++++++ COMPUTE BG VALUES FOR SLA +++++++++++++++'
 WRITE(IOUNLOG,*)

 CALL FLUSH(IOUNLOG)

#ifdef SHARED_MEMORY
!$OMP PARALLEL DEFAULT(SHARED),PRIVATE(K,KSTEP,JTSTEP,JP,J)
!$OMP DO SCHEDULE(DYNAMIC,1)
#endif
 CYOBS: DO K=1,SLA%NO

  IF (SLA%FLC(K).NE.1) CYCLE CYOBS

  KSTEP = MINLOC( ABS(RTIMES1950(1:NTSTEPS)-SLA%TIM(K) ), DIM=1 ) - (FIRSTTS-1)
  IF(KSTEP .LT. 1 .OR. KSTEP .GT. NFGTSTEPS) THEN
     WRITE(IOUNOUT,*) ' INT_BACVAL_SLA: FOUND KSTEP EQUAL TO ',KSTEP
     WRITE(IOUNOUT,*) ' INT_BACVAL_SLA: TIME EQUAL TO ',SLA%TIM(K)
     WRITE(IOUNOUT,*) ' LL_FGASSIM : ',LL_FGASSIM
     WRITE(IOUNOUT,*) ' RTIMES1950 : ',RTIMES1950(1:NTSTEPS)
     CALL ABOR1('INT_BACVAL_SLA: KSTEP OUT OF RANGE')
  ENDIF

  IF(KSTEP .EQ. 0 ) THEN
      SLA%FLC(K)=0
      SLA%EVE(K) =KEVE_TIME
      CYCLE CYOBS
  ENDIF
  
  DO JP=1,NPQ
     IF( ABS(GRD%MDT(SLA%IB(K,JP),SLA%JB(K,JP))) .GT. 10._R8 ) THEN
       SLA%FLC(K)=0
       SLA%EVE(K) = KEVE_MDTU
       CYCLE CYOBS
     ENDIF
  ENDDO

  SLA%BAC(K) = 0._R8
  DO JP=1,NPQ
     SLA%BAC(K)=SLA%BAC(K)+SLA%PQ(K,JP)*(GRD%ETAB(SLA%IB(K,JP),SLA%JB(K,JP),KSTEP)-&
                GRD%MDT(SLA%IB(K,JP),SLA%JB(K,JP)) )
  ENDDO
  
  DO J=1,GRD%KM
   SLA%TB(K,J) = 0._R8
   SLA%SB(K,J) = 0._R8
   DO JP=1,NPQ
    SLA%TB(K,J)=SLA%TB(K,J)+SLA%PQ(K,JP)*GRD%TEMB(SLA%IB(K,JP),SLA%JB(K,JP),J,KSTEP)
    SLA%SB(K,J)=SLA%SB(K,J)+SLA%PQ(K,JP)*GRD%SALB(SLA%IB(K,JP),SLA%JB(K,JP),J,KSTEP)
   ENDDO
  ENDDO

  SLA%RES(K) = SLA%VAL(K) - SLA%BAC(K)

 ENDDO CYOBS
#ifdef SHARED_MEMORY
!$OMP END DO
!$OMP END PARALLEL
#endif

KNO(1)=COUNT(SLA%EVE(1:SLA%NO).EQ.KEVE_TIME)
KNO(2)=COUNT(SLA%FLC(1:SLA%NO).EQ.0)
KNO(3)=COUNT(SLA%EVE(1:SLA%NO).EQ.KEVE_MDTU)

 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) '=========== GLOBAL STATISTICS FOR SLA ==========='
 WRITE(IOUNLOG,*)
 WRITE(IOUNLOG,*) ' SLA OBSERVATIONS : ',SLA%NO
 WRITE(IOUNLOG,*) ' OBS W/O FG TSTEP : ',KNO(1)
 WRITE(IOUNLOG,*) ' OBS ALREADY REJ. : ',KNO(2)
 WRITE(IOUNLOG,*) ' OBS W/0 VALID MDT: ',KNO(3)
 WRITE(IOUNLOG,*)
 CALL FLUSH(IOUNLOG)

 CALL MYFRTPROF_WALL('INT_BACVAL_SLA: SLA BGVALUES',1)
END SUBROUTINE INT_BACVAL_SLA
