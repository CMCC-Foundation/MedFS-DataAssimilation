SUBROUTINE READICOADSFILE(CFIN,IOUN,NMAX,NOB,KTYP,PAR,DEP,VAL,TIM,LON,LAT,&
& PRF,PLA)

! READ OBSERVATION IN REFORMATTED ICOADS FORMAT
!
! A.S. - 26.11.2013

  USE SET_KND
  USE CALENDAR
  USE OBSDEF
  USE RUN , ONLY : ZJULSTART, ZJULEND

  IMPLICIT NONE

  INTEGER(I4), INTENT(IN) :: IOUN,NMAX
  CHARACTER(LEN=*), INTENT(IN) :: CFIN
  INTEGER(I4), INTENT(OUT) :: NOB
  INTEGER(I4), DIMENSION(NMAX),INTENT(OUT) :: KTYP, PAR, PRF
  REAL   (R8), DIMENSION(NMAX),INTENT(OUT) :: DEP,  VAL, LON, LAT
  REAL   (DP), DIMENSION(NMAX),INTENT(OUT) :: TIM
  CHARACTER(LEN=8 ), DIMENSION(NMAX),INTENT(OUT) :: PLA

  INTEGER(I4), PARAMETER :: MAXVARS=14
  INTEGER(I4) :: KOBS, NOTW, NFLR, KKTP, KKOP
  INTEGER(I4) :: IERR, JK, J, KPAR, HH, MM, TI, LI, II, ISI
  REAL(DP)    :: ZTIME, TIME
  REAL(R8)    :: RLAT, RLON, VALT
  INTEGER(I4) :: ICRUISE, IY, IM, ID, JJ, NLEVS, NVARS
  LOGICAL     :: LL_INIT, LLTIME, LCOO
  CHARACTER(LEN=9) :: CID, CTMP
 
  WRITE(IOUN,*) ' ^^^ READICOADSFILE PROCESSING FILE ',TRIM(CFIN)

  KOBS=0
  KKOP=0
  NOTW=0
  NFLR=0

  IERR=0

800  FORMAT(I4,X,I2.2,X,I2.2,X,I2.2,X,I2.2,X,&
     & 2F9.3,X,3I3,X,A9,X,I3,F8.3) 

  OPEN(71,FILE=CFIN,STATUS='OLD',IOSTAT=IERR)
  IF( IERR .NE. 0 ) CALL ABOR1('READ_ICOADSFILE: OPEN FAILED')
  PROFILES : DO WHILE (IERR .EQ. 0)
     READ(71,800,IOSTAT=IERR) &
     & IY,IM,ID,HH,MM,RLON,RLAT,TI,LI,&
     & II,CID,ISI,VALT
     IF( IERR .NE. 0 ) EXIT PROFILES
     LCOO = .TRUE.
     IF(RLON .LT. -180._R8 .OR. RLON .GT. 360._R8 &
     .OR. RLAT .LT. -90._R8 .OR. RLAT .GT. 180.-R8 ) THEN
         KKOP=KKOP+1
         LCOO = .FALSE.
     ENDIF
     TIME = REAL(HH,DP) + REAL(MM,DP)/60._DP
     IF( TIME < 0 ) TIME = 12._R8
     CALL YMDS2JU(IY,IM,ID,TIME/24._R8,ZTIME)
     LLTIME = (ZTIME .GE. ZJULSTART .AND. ZTIME .LE. ZJULEND)
     IF(.NOT.LLTIME) NOTW = NOTW + 1
     IF( RLON .GT. 180._R8 ) RLON = RLON -360._R8
     IF(.NOT.LLTIME .OR. .NOT.LCOO) THEN
        CYCLE PROFILES
     ENDIF
     KKTP = 109
     IF( ISI .GE. 0 .AND. ISI .LE. 12 ) KKTP =  ISI + 100
     ! GET OBS
     KOBS=KOBS+1 
     LON(KOBS) = RLON
     LAT(KOBS) = RLAT
     PAR(KOBS) = KKTEMP
     KTYP(KOBS) = KKTP
     DEP(KOBS) = 0._R8
     VAL(KOBS) = VALT
     TIM(KOBS) = ZTIME
     PRF(KOBS) = KOBS
     IF(II>0) THEN
        CTMP = ADJUSTR(CID)
        PLA(KOBS)(1:8) = CTMP(2:9)
     ELSE
        PLA(KOBS)(1:8) = '00000000'
     ENDIF
  ENDDO PROFILES
  CLOSE(71)

  NOB = KOBS

  WRITE(IOUN,*)
  WRITE(IOUN,*) ' ^^^ REPORT '
  WRITE(IOUN,*) ' NUMBER OF BAD  OBS COO  :', KKOP
  WRITE(IOUN,*) ' NUMBER OF GOOD OBSERV.  :', KOBS
  WRITE(IOUN,*) ' NUMBER OF P OUT OF TIME :', NOTW
  WRITE(IOUN,*) ' NUMBER OF BAD FLAGS OBS :', NFLR
  WRITE(IOUN,*) 

END SUBROUTINE READICOADSFILE
