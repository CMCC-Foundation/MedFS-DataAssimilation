SUBROUTINE HAVERSINE(LATS,LONS,LATE,LONE,DIST)

! A.S. 20.01.2010
! Haversine formula to compute distance (km)
! between points in latlon coordinates
!
! Use latitudinally-dependent Earth radius

USE SET_KND
IMPLICIT NONE

REAL(R8),INTENT(IN)  :: LATS,LONS,LATE,LONE
REAL(R8),INTENT(OUT) :: DIST
REAL(R8)             :: ZLATS,ZLONS,ZLATE,ZLONE,DLAT,DLON
REAL(R8)             :: PI, RPD, ZA, ZC, ZR
REAL(R8),PARAMETER   :: ZA1 = 6378.1370_R8
REAL(R8),PARAMETER   :: ZB1 = 6356.7523_R8

PI=2._R8*ASIN(1._R8)
RPD=PI/180._R8

ZLATS=LATS*RPD
ZLATE=LATE*RPD
ZLONS=LONS*RPD
ZLONE=LONE*RPD

DLAT=ABS(ZLATS-ZLATE)
DLON=ABS(ZLONS-ZLONE)

ZA = SIN( DLAT/2._R8 )*SIN( DLAT/2._R8 ) + COS(ZLATS)*COS(ZLATE)*&
& SIN(DLON/2._R8)*SIN(DLON/2._R8)
ZC = 2._R8 * ATAN2(SQRT(ZA),SQRT(1._R8-ZA))
ZR = SQRT( ZA1**4*COS(ZLATS)**2 + ZB1**4*SIN(ZLATS)**2 ) / &
   & SQRT( ZA1**2*COS(ZLATS)**2 + ZB1**2*SIN(ZLATS)**2 )
DIST = ZC*ZR

END SUBROUTINE HAVERSINE
