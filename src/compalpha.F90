SUBROUTINE COMPALPHA

USE SET_KND
USE RECFILTER
USE GRD_STR

!-- COMPUTE ALPHA COEFFICIENTS FOR RF

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY,NZ
INTEGER(KIND=I4) :: I,J,JLEV
REAL(KIND=R8) :: E,DST

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// COMPUTE ALPHA COEFFICIENTS'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF(.NOT.ALLOCATED(GRD%DX).OR..NOT.ALLOCATED(GRD%DY)) THEN
   WRITE(IOUNERR,*) 'COMPALPHA CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN COMPALPHA, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('COMPALPHA CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM
NZ=GRD%KM

ALLOCATE(ALXNS(NX,NY,NTOTV),&
       & BTXNS(NX,NY,NTOTV),&
       & ALYNS(NX,NY,NTOTV),&
       & BTYNS(NX,NY,NTOTV))

DO JLEV=1,NTOTV
   DO J=1,NY
      DO I=2,NX
         DST = (GRD%DX(I-1,J) + GRD%DX(I,J)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *ZGA_CRAD(JLEV)**2)
         ALXNS(I,J,JLEV) = 1._R8 + E - SQRT(E*(E+2._R8))
      ENDDO
      DO I=1,NX-1
         DST = (GRD%DX(I,J) + GRD%DX(I+1,J)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *ZGA_CRAD(JLEV)**2)
         BTXNS(I,J,JLEV) = 1._R8 + E - SQRT(E*(E+2._R8))
      ENDDO
   ENDDO
   DO J=2,NY
      DO I=1,NX
         DST = (GRD%DY(I,J-1) + GRD%DY(I,J)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *ZGA_CRAD(JLEV)**2)
         ALYNS(I,J,JLEV) = 1._R8 + E - SQRT(E*(E+2._R8))
      ENDDO
   ENDDO
   DO J=1,NY-1
      DO I=1,NX
         DST = (GRD%DY(I,J) + GRD%DY(I,J+1)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *ZGA_CRAD(JLEV)**2)
         BTYNS(I,J,JLEV) = 1._R8 + E - SQRT(E*(E+2._R8))
      ENDDO
   ENDDO
ENDDO

ALXNS( 1,:,:) = ALXNS(  2 ,:   ,:)
BTXNS(NX,:,:) = BTXNS(NX-1,:   ,:)
ALYNS(:, 1,:) = ALYNS(:   ,2   ,:)
BTYNS(:,NY,:) = BTYNS(:   ,NY-1,:)

END SUBROUTINE COMPALPHA
