SUBROUTINE READNETCDF_SLA_NEW(CINIFILE,VARNAME,DATEST,DATEEN, &
                              & IMAXO,NTOTOBS,LL_RT,&
                              & PLON,PLAT,PTIM,PVAL,TRACK,&
                              & IONUMB)

!... READ ALONGTRACK SLA PRODUCT IN NEW AVISO NETCDF FORMAT
!
!    ADAPTED FROM AVISO PUBLICREAD SOFTWARE
!    ANDREASTORTO

USE SET_KND
USE NETCDF
USE MYFRTPROF
USE OBSHANDLING, ONLY : LLFILTERSLA,NLANC_KN,ZLANC_CL

IMPLICIT NONE

CHARACTER (LEN=*),  INTENT(IN) :: CINIFILE        ! INPUT IFILE
CHARACTER (LEN=*),  INTENT(IN) :: VARNAME         ! VAR TO EXTRACT
INTEGER(I4),INTENT(IN)         :: IONUMB,IMAXO    ! UNIT FOR DIGNOSTIC OUTPUT
INTEGER(I4),INTENT(OUT)        :: NTOTOBS         ! VALID OBS
REAL(R8)         ,INTENT(IN)   :: DATEST, DATEEN  ! ASSIM TIME WINDOW
REAL(R8),DIMENSION(IMAXO),INTENT(OUT) :: PLON,PLAT,PTIM,PVAL
INTEGER(I4)      ,INTENT(OUT) :: TRACK(IMAXO)

LOGICAL, INTENT(IN) :: LL_RT
REAL(R8), ALLOCATABLE, DIMENSION(:) :: LON, LAT, SLA, TIM
INTEGER(I4), ALLOCATABLE, DIMENSION(:) :: TRK, FLAG
INTEGER(I4) :: NOTR, NOR(2)
INTEGER(I4)  :: JOBS,JT,NT
INTEGER(I4)  :: STAT, NCID, IDVAR
REAL(R8):: SCFACT,SCFACTLON,SCFACTLAT,FILLVAL
!.............................................................................

    ! Open netCDF file
    STAT = NF90_OPEN(CINIFILE, NF90_NOWRITE, NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    ! Get number of observations
    STAT = NF90_INQ_DIMID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    ! Allocate arrays
    ALLOCATE( SLA(NT), LON(NT), LAT(NT), TRK(NT), TIM(NT), FLAG(NT) )

    ! Get sla values
    STAT = NF90_INQ_VARID (NCID, VARNAME, IDVAR)
    IF (STAT /= NF90_NOERR) THEN
       STAT = NF90_INQ_VARID (NCID, 'sla_filtered', IDVAR)
       IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ENDIF
    STAT = NF90_GET_ATT (NCID,IDVAR,'scale_factor',SCFACT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID,IDVAR,'_FillValue',FILLVAL)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ! Read the sla values
    STAT = NF90_GET_VAR (NCID,IDVAR,SLA)

    ! Get longitude values
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_INQ_VARID (NCID, 'longitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LON)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID,IDVAR,'scale_factor',SCFACTLON)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ! Get latitude values
    STAT = NF90_INQ_VARID (NCID, 'latitude', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,LAT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_ATT (NCID,IDVAR,'scale_factor',SCFACTLAT)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ! Get track values
    STAT = NF90_INQ_VARID (NCID, 'track', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TRK)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ! Get time values
    STAT = NF90_INQ_VARID (NCID, 'time', IDVAR)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    STAT = NF90_GET_VAR (NCID,IDVAR,TIM)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    ! Get missing/bad values
    IF ( LL_RT ) THEN
      STAT = NF90_INQ_VARID (NCID, 'flag', IDVAR)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
      STAT = NF90_GET_VAR (NCID,IDVAR,FLAG)
      IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
    ELSE
      FLAG(1:NT)=1
      DO JT=1,NT
         IF( SLA(JT) .EQ. FILLVAL) FLAG(JT)=0
      ENDDO
    ENDIF

    STAT = NF90_CLOSE (NCID)
    IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

    JOBS=0
    NOTR=1
    NOR=0
    LON=LON*SCFACTLON
    LAT=LAT*SCFACTLAT
    SLA=SLA*SCFACT
    CYOBS : DO JT=1,NT
          ! Remove missing/bad values
          IF( FLAG(JT) .NE. 1 ) THEN
             NOR(1)=NOR(1)+1
             CYCLE CYOBS
          ENDIF
          ! Remove values out of time window
          IF( TIM(JT) .LT. DATEST .OR. &
          & TIM(JT) .GT. DATEEN ) THEN
             NOR(2)=NOR(2)+1
             CYCLE CYOBS
          ENDIF
          ! Save the rest of values
          JOBS=JOBS+1
          IF(JOBS.GT.IMAXO) &
          & CALL ABOR1('READNETCDF_SLA_ATP : MAXTOTALDATA TO INCREASE')
          IF(LON(JT).GT. 180._R8) THEN
             PLON(JOBS) = LON(JT)-360._R8
          ELSE
             PLON(JOBS) = LON(JT)
          ENDIF
          PLAT(JOBS) = LAT(JT)
          PVAL(JOBS) = SLA(JT)
          PTIM(JOBS) = TIM(JT)
          TRACK(JOBS) = TRK(JT)
          IF(JOBS .GT. 1) THEN
             IF( TRACK(JOBS).NE.TRACK(JOBS-1) ) NOTR = NOTR+1
          ENDIF
    ENDDO CYOBS

NTOTOBS=JOBS
DEALLOCATE( SLA, LON, LAT, TRK, TIM, FLAG )

WRITE(IONUMB,*)
WRITE(IONUMB,*) ' -----'
WRITE(IONUMB,*) ' AVISO/SLA FILE ',TRIM(CINIFILE)
WRITE(IONUMB,*) ' NUMBER OF TRACKS', NOTR
WRITE(IONUMB,*) ' TOTAL OBS IN FILE : ',NT
WRITE(IONUMB,*) ' UNVALID FLAG      : ',NOR(1)
WRITE(IONUMB,*) ' OUT OF TIME       : ',NOR(2)
WRITE(IONUMB,*) ' VALID OBS         : ',NTOTOBS
WRITE(IONUMB,*)

END SUBROUTINE READNETCDF_SLA_NEW
