SUBROUTINE WRANAL(IM,JM,KM,LON,LAT,DEP,ASAL,ATEM,BSAL,BTEM,ZMV)
USE SET_KND
USE NETCDF
IMPLICIT NONE
INTEGER(I4), INTENT(IN) :: IM, JM, KM
REAL(R8), INTENT(IN) :: LON(IM,JM),LAT(IM,JM),DEP(KM)
REAL(R8), INTENT(INOUT) :: ASAL(IM,JM,KM),ATEM(IM,JM,KM)
REAL(R8), INTENT(INOUT) :: BSAL(IM,JM,KM),BTEM(IM,JM,KM),ZMV
INTEGER(I4) :: NCID,DIMIDS1(1),DIMIDS2(2),DIMIDS3(3)
INTEGER(I4) :: VARID(7),X_DIMID,Y_DIMID,Z_DIMID

  CALL CHECK( NF90_CREATE('ANALYSIS.NC', NF90_CLOBBER, NCID) )
  CALL CHECK( NF90_DEF_DIM(NCID, 'x', IM, X_DIMID) )
  CALL CHECK( NF90_DEF_DIM(NCID, 'y', JM, Y_DIMID) )
  CALL CHECK( NF90_DEF_DIM(NCID, 'z', KM, Z_DIMID) )
  DIMIDS3=  (/X_DIMID, Y_DIMID, Z_DIMID /)
  DIMIDS2=  (/X_DIMID, Y_DIMID /)
  DIMIDS1=  (/Z_DIMID /)
  CALL CHECK( NF90_DEF_VAR(NCID, 'nav_lon',NF90_REAL, DIMIDS2, VARID(1)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'nav_lat',NF90_REAL, DIMIDS2, VARID(2)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'deptht',NF90_REAL, DIMIDS1, VARID(3)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'ANASALINE',NF90_REAL, DIMIDS3, VARID(4)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'ANATEMPER',NF90_REAL, DIMIDS3, VARID(5)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'BGSALINE',NF90_REAL, DIMIDS3, VARID(6)) )
  CALL CHECK( NF90_DEF_VAR(NCID, 'BGTEMPER',NF90_REAL, DIMIDS3, VARID(7)) )

  CALL CHECK( NF90_PUT_ATT(NCID, VARID(1), 'units','degrees_east') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(2), 'units','degrees_north') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(3), 'units','m') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(4), 'units','psu') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(5), 'units','degc') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(6), 'units','psu') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(7), 'units','degc') )

  CALL CHECK( NF90_PUT_ATT(NCID, VARID(1), 'longname','Longitude') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(2), 'longname','Latitude') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(3), 'longname','Depth') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(4), 'longname','Analysis Salinity') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(5), 'longname','Analysis Temperature') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(6), 'longname','Background Salinity') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(7), 'longname','Background Temperature') )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(4), 'missing_value',ZMV) )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(5), 'missing_value',ZMV) )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(6), 'missing_value',ZMV) )
  CALL CHECK( NF90_PUT_ATT(NCID, VARID(7), 'missing_value',ZMV) )
  CALL CHECK( NF90_PUT_ATT(NCID, NF90_GLOBAL, 'Product',&
  & 'Global OceanVar Variational Analysis') )
  CALL CHECK( NF90_ENDDEF(NCID) )
  WHERE( ABS(ATEM).GT. 1000._R8 ) ATEM = ZMV
  WHERE( ABS(BTEM).GT. 1000._R8 ) BTEM = ZMV
  WHERE( ABS(ASAL).GT. 1000._R8 ) ASAL = ZMV
  WHERE( ABS(BSAL).GT. 1000._R8 ) BSAL = ZMV
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(1), LON) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(2), LAT) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(3), DEP) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(4), ASAL) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(5), ATEM) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(6), BSAL) )
  CALL CHECK( NF90_PUT_VAR(NCID, VARID(7), BTEM) )
  CALL CHECK( NF90_CLOSE(NCID) )

END SUBROUTINE WRANAL 
