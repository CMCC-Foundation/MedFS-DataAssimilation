SUBROUTINE COMPALPHA_LVM

USE SET_KND
USE RECFILTER
USE GRD_STR
USE LBCLNK
USE MYFRTPROF

!-- COMPUTE ALPHA COEFFICIENTS FOR RF

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY
INTEGER(KIND=I4) :: I,J,IERR
REAL(KIND=R8) :: E,DST, TCR, TCRM

CALL MYFRTPROF_WALL('COMPALPHA_LVM: COMPUTE ALPHA COEFFS',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// COMPUTE ALPHA COEFFICIENTS'
WRITE(IOUNLOG,*)

CALL FLUSH(IOUNLOG)

#ifndef USE_POINTERS
IF(.NOT.ALLOCATED(NEWDX).OR..NOT.ALLOCATED(NEWDY)) THEN
   WRITE(IOUNERR,*) 'COMPALPHA CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN COMPALPHA, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('COMPALPHA CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM

WRITE(IOUNLOG,*) 'DIMENSIONS ARE ', NX, NY
CALL FLUSH(IOUNLOG)

ALLOCATE(ALXNZ(NX,NY),&
       & ALYNZ(NX,NY),&
       & BTXNZ(NX,NY),&
       & BTYNZ(NX,NY),STAT=IERR)

IF(IERR .NE. 0 ) THEN
   CALL ABOR1('ALLOCATION PROBLEMS IN COMPALPHA_LVM')
ENDIF

WRITE(IOUNLOG,*) 'ALLOCATION DONE'
CALL FLUSH(IOUNLOG)

ALXNZ = 0._R8
BTXNZ = 0._R8
ALYNZ = 0._R8
BTYNZ = 0._R8

   DO J=1,NY
      DO I=2,NX

         TCR = CRMX(I,J)
         TCRM= CRMX(I-1,J)

         DST = (NEWDX(I-1,J) + NEWDX(I,J)) * 0.5_R8
         TCR = (TCR + TCRM) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *TCR**2)
         ALXNZ(I,J) = 1._R8 + E - SQRT(E*(E+2._R8))

      ENDDO

      DO I=1,NX-1

         TCR = CRMX(I,J)
         TCRM= CRMX(I+1,J)

         TCR = (TCR + TCRM) * 0.5_R8
         DST = (NEWDX(I,J) + NEWDX(I+1,J)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *TCR**2)
         BTXNZ(I,J) = 1._R8 + E - SQRT(E*(E+2._R8))

      ENDDO

   ENDDO

   DO J=2,NY
      DO I=1,NX

         TCR = CRMY(I,J)
         TCRM= CRMY(I,J-1)

         TCR = (TCR + TCRM) * 0.5_R8
         DST = (NEWDY(I,J-1) + NEWDY(I,J)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *TCR**2)
         ALYNZ(I,J) = 1._R8 + E - SQRT(E*(E+2._R8))

      ENDDO
   ENDDO

   DO J=1,NY-1
      DO I=1,NX

         TCR = CRMY(I,J)
         TCRM= CRMY(I,J+1)

         TCR = (TCR + TCRM) * 0.5_R8
         DST = (NEWDY(I,J) + NEWDY(I,J+1)) * 0.5_R8
         E   = (2._R8 * RF_NTR) * DST**2 / (4._R8 *TCR**2)
         BTYNZ(I,J) = 1._R8 + E - SQRT(E*(E+2._R8))

      ENDDO
   ENDDO

CALL LBC_LNK( ALXNZ )
CALL LBC_LNK( BTXNZ )
CALL LBC_LNK( ALYNZ )
CALL LBC_LNK( BTYNZ )

CALL MYFRTPROF_WALL('COMPALPHA_LVM: COMPUTE ALPHA COEFFS',1)

END SUBROUTINE COMPALPHA_LVM
