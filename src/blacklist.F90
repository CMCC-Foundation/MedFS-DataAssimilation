SUBROUTINE BLACKLIST

!.. BLACKLIST SUSPICIOUS STATIONS/INSTRUMENTS
!..
!.. ANDREA STORTO *INGV* 2009-04-02

USE SET_KND
USE OBSDEF
USE OBS_STR
USE IOUNITS, ONLY : IOUNLOG, IOUNBLACK
USE MYFRTPROF, ONLY : MYFRTPROF_WALL

IMPLICIT NONE

LOGICAL :: LLEXIST
INTEGER(I4), PARAMETER :: MAXIT=100
CHARACTER(LEN=60) :: CFILE
INTEGER(I4) :: IOERR,NITEMS
INTEGER(I4) :: IOBSFAM,IOBSPAR,IOBSPAR2
INTEGER(I4) :: NOBS(NOFAMS), NPARAM(NOFAMS,MAXIT)
INTEGER(I4) :: NDONE(NOFAMS)
INTEGER(I4) :: NPARAM2(NOFAMS,MAXIT)
INTEGER(I4) :: JOBS,JBLA

#include "obs_events.h"

CALL MYFRTPROF_WALL('BLACKLIST: BLACKLISTING OBS',0)

CFILE='BLACKLIST.DAT'
NITEMS=0
NDONE=0
NOBS=0

WRITE(IOUNLOG,*) ' *** BLACKLISTING'
WRITE(IOUNLOG,*)

INQUIRE(FILE=CFILE,EXIST=LLEXIST)

IF(LLEXIST) THEN
     OPEN(IOUNBLACK,FILE=CFILE)
     IOERR=0
     DO WHILE(IOERR.EQ.0)
        READ(IOUNBLACK,*,IOSTAT=IOERR) IOBSFAM,IOBSPAR,IOBSPAR2
         IF(IOERR.EQ.0.AND.IOBSFAM.GT.0.AND.IOBSFAM.LE.NOFAMS) THEN
          NITEMS=NITEMS+1
          NOBS(IOBSFAM)=NOBS(IOBSFAM)+1
          NPARAM(IOBSFAM,NOBS(IOBSFAM)) = IOBSPAR
          IF(IOBSFAM.EQ.1) NPARAM2(IOBSFAM,NOBS(IOBSFAM)) = IOBSPAR2
        ENDIF
     ENDDO
     CLOSE(IOUNBLACK)
     IF(NOBS(1).GT.0) THEN
      DO JBLA=1,NOBS(1)
       DO JOBS=1,INS%NO
        IF(INS%FLC(JOBS).EQ.1 .AND.&
        & (INS%OTYPE(JOBS).EQ.NPARAM(1,JBLA) .OR. NPARAM(1,JBLA).EQ.0).AND. &
        & (NPARAM2(1,JBLA).EQ.0 .OR. NPARAM2(1,JBLA).EQ.INS%PAR(JOBS)) ) THEN
           INS%FLC(JOBS)=0
           INS%EVE(JOBS)= KEVE_BLAC
           NDONE(1)=NDONE(1)+1
        ENDIF
       ENDDO
      ENDDO
     ENDIF
     IF(NOBS(2).GT.0) THEN
      DO JBLA=1,NOBS(2)
       DO JOBS=1,SLA%NO
        IF(SLA%FLC(JOBS).EQ.1 .AND.SLA%KSAT(JOBS).EQ.NPARAM(2,JBLA)) THEN
           SLA%FLC(JOBS)=0
           SLA%EVE(JOBS)= KEVE_BLAC
           NDONE(2)=NDONE(2)+1
        ENDIF
       ENDDO
      ENDDO
     ENDIF
     IF(NOBS(3).GT.0) THEN
      DO JBLA=1,NOBS(3)
       DO JOBS=1,SST%NO
        IF(SST%FLC(JOBS).EQ.1 .AND.SST%KSAT(JOBS).EQ.NPARAM(3,JBLA)) THEN
           SST%FLC(JOBS)=0
           SST%EVE(JOBS)= KEVE_BLAC
           NDONE(3)=NDONE(3)+1
        ENDIF
       ENDDO
      ENDDO
     ENDIF
     IF(NOBS(4).GT.0) THEN
      DO JBLA=1,NOBS(4)
       DO JOBS=1,SSS%NO
        IF(SSS%FLC(JOBS).EQ.1 .AND.SSS%KSAT(JOBS).EQ.NPARAM(4,JBLA)) THEN
           SSS%FLC(JOBS)=0
           SSS%EVE(JOBS)= KEVE_BLAC
           NDONE(4)=NDONE(4)+1
        ENDIF
       ENDDO
      ENDDO
     ENDIF

ELSE
     WRITE(IOUNLOG,*) ' -> BLACKLIST DID NOT FIND ANY BLACKLIST.DAT FILE'
ENDIF

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' -> BLACKLIST FOUND ',NITEMS,' ITEMS'
WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) '     BLACKLISTED INS :',NDONE(1)
WRITE(IOUNLOG,*) '     BLACKLISTED SLA :',NDONE(2)
WRITE(IOUNLOG,*) '     BLACKLISTED SST :',NDONE(3)
WRITE(IOUNLOG,*) '     BLACKLISTED SSS :',NDONE(4)
WRITE(IOUNLOG,*) '     -----------------------------'
WRITE(IOUNLOG,*) '     TOT BLACKLISTED :',SUM(NDONE)
WRITE(IOUNLOG,*)


CALL MYFRTPROF_WALL('BLACKLIST: BLACKLISTING OBS',1)
END SUBROUTINE BLACKLIST
