MODULE TLAD

USE SET_KND
USE GRD_STR
USE TLAD_VARS
USE TRAADV_CEN2H
USE TRAADV_CEN2V
USE TRALDF_LAP
USE TRAZDF_EXP
USE TRAZDF_IMP
USE OBS_STR
USE MYNETCDF
USE RUN, ONLY : IANTW, LL_TWCENTERED, ZJULSTART, ZJUL1950
USE CALENDAR, ONLY : TIME_DIFF, TIME_ADD
USE IOUNITS
USE MYFRTPROF
USE MPIREL
USE BLKOCE_CORE
USE WEAKLY

IMPLICIT NONE

CONTAINS

SUBROUTINE TLAD_INIT_OBS

IF( INS%NO .GT. 0 ) THEN
  ALLOCATE ( INS%TATS(INS%NO) )
  ALLOCATE ( INS%OBIN(INS%NO) )
  INS%TATS = 0
  INS%OBIN = 0
ENDIF
IF( SLA%NO .GT. 0 ) THEN
  ALLOCATE ( SLA%TATS(SLA%NO) )
  ALLOCATE ( SLA%OBIN(SLA%NO) )
  SLA%TATS = 0
  SLA%OBIN = 0
ENDIF
IF( SST%NO .GT. 0 ) THEN
  ALLOCATE ( SST%TATS(SST%NO) )
  ALLOCATE ( SST%OBIN(SST%NO) )
  SST%TATS = 0
  SST%OBIN = 0
ENDIF
IF( SSS%NO .GT. 0 ) THEN
  ALLOCATE ( SSS%TATS(SSS%NO) )
  ALLOCATE ( SSS%OBIN(SSS%NO) )
  SSS%TATS = 0
  SSS%OBIN = 0
ENDIF

IF( .NOT. LL_4DVAR .AND. .NOT. LL_TLMODEL .AND. .NOT. LL_ADTEST) RETURN

CALL PREP_OBS_4DVAR

END SUBROUTINE TLAD_INIT_OBS

SUBROUTINE TLAD_INIT

IMPLICIT NONE

REAL(R8) :: ZDM, ZA00, ZEUMAX, ZEVMAX
INTEGER(I4) :: JI,JJ,JTSTEP,JK,JT
INTEGER(I4) :: IFGYY, IFGMM, IFGDD, IFGHH
INTEGER(I4) :: ITTYY,ITTMM,ITTDD,NTS0,KINX
INTEGER(I4), PARAMETER :: MS=999
REAL(R8) :: SEC(MS), ZTTSS, ZTS, DZM
CHARACTER(LEN=99) :: CF_MOD = 'meshmask.nc'

IF( .NOT. LL_4DVAR .AND. .NOT. LL_TLMODEL .AND. .NOT. LL_ADTEST) RETURN

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' INITIALIZING TANGENT LINEAR/ADJOINT MODEL'

JPI= GRD%IM; JPJ=GRD%JM ; JPK=GRD%KM
JPIM1=JPI-1 ; JPJM1=JPJ-1 ; JPKM1 = JPK-1

ALLOCATE( E1T(JPI,JPJ) )
ALLOCATE( E2T(JPI,JPJ) )
ALLOCATE( E1U(JPI,JPJ) )
ALLOCATE( E1V(JPI,JPJ) )
ALLOCATE( E2U(JPI,JPJ) )
ALLOCATE( E2V(JPI,JPJ) )
ALLOCATE( E1UR(JPI,JPJ) )
ALLOCATE( E2VR(JPI,JPJ) )
ALLOCATE( TMASK(JPI,JPJ,JPK) )
ALLOCATE( UMASK(JPI,JPJ,JPK) )
ALLOCATE( VMASK(JPI,JPJ,JPK) )
ALLOCATE( AHTU(JPI,JPJ) )
ALLOCATE( AHTV(JPI,JPJ) )
ALLOCATE( E3T(JPK) )
ALLOCATE( E3U(JPK) )
ALLOCATE( E3V(JPK) )
ALLOCATE( E3W(JPK) )

TS_MOD   = INT(3600._R8 * IANTW / RDT )+1
NTS_TRAJ = MAX(1,INT( (TS_MOD * RDT ) / RDT_TRAJ )+1)

WRITE(IOUNLOG,*) ' GRID DIMENSION OF TLMODEL : ',JPI,JPJ,JPK
WRITE(IOUNLOG,*) ' NUMBER OF MODEL TIMESTEPS : ',TS_MOD
WRITE(IOUNLOG,*) ' NUMBER OF TRAJ  TIMESTEPS : ',NTS_TRAJ

ALLOCATE(TLAD_TIMES(TS_MOD))
DO JI=1,TS_MOD
 TLAD_TIMES(JI)=(REAL(JI-1,R8)+0.5_R8)*RDT/86400+ZJULSTART-ZJUL1950
 WRITE(IOUNLOG,*) ' TLAD TIMESTEP ',JI,' TIME: ',TLAD_TIMES(JI)
ENDDO

ALLOCATE( UB(JPI,JPJ,JPK,NTS_TRAJ) )
ALLOCATE( VB(JPI,JPJ,JPK,NTS_TRAJ) )
ALLOCATE( WB(JPI,JPJ,JPK,NTS_TRAJ) )

ALLOCATE( UBT(JPI,JPJ,JPK) )
ALLOCATE( VBT(JPI,JPJ,JPK) )
ALLOCATE( WBT(JPI,JPJ,JPK) )
ALLOCATE( HDIVN(JPI,JPJ,JPK) )

UBT=0._WP
VBT=0._WP
WBT=0._WP
HDIVN=0._WP

ALLOCATE( RTIMES1950TL( NTS_TRAJ ) )
CALL FINDFGTIMES_TL('UB.nc',NTS0, IFGYY, IFGMM, IFGDD, IFGHH, MS, SEC)
IF(NTS0.NE.NTS_TRAJ) THEN
   WRITE(IOUNERR,*) ' NTS0 / NTS_TRAJ MISMATCH :',NTS0,NTS_TRAJ
   CALL ABOR1('TLAD : MISMATCH IN MOD TRAJ RECORDS')
ENDIF

DO JTSTEP=1,NTS_TRAJ
    CALL TIME_ADD(IFGYY,IFGMM,IFGDD,REAL(IFGHH*3600,KIND=DP),&
                & SEC(JTSTEP),ITTYY,ITTMM,ITTDD,ZTTSS)
    CALL TIME_DIFF(1950,1,1,0._DP,ITTYY,ITTMM,ITTDD,ZTTSS,RTIMES1950TL(JTSTEP))
    RTIMES1950TL(JTSTEP) = RTIMES1950TL(JTSTEP) / 86400._DP
    WRITE(IOUNLOG,*) JTSTEP,RTIMES1950TL(JTSTEP)
ENDDO

ALLOCATE( KBG1(TS_MOD), KBG2(TS_MOD), RBG1(TS_MOD) )
WRITE(IOUNLOG,*) 'MODEL TRAJ SETTINGS'
DO JI=1,TS_MOD
    KINX = MINLOC( ABS(TLAD_TIMES(JI)-RTIMES1950TL), DIM=1 )
    IF( TLAD_TIMES(JI) .GT. RTIMES1950TL(NTS_TRAJ) ) THEN
        KBG1(JI)=NTS_TRAJ
        KBG2(JI)=NTS_TRAJ+1
    ELSEIF( TLAD_TIMES(JI) .LT. RTIMES1950TL(1) ) THEN
        KBG1(JI)=0
        KBG2(JI)=1
    ELSEIF( TLAD_TIMES(JI) .GT. RTIMES1950TL(KINX) ) THEN
        KBG1(JI)=KINX-1
        KBG2(JI)=KINX
    ELSE
        KBG1(JI)=KINX
        KBG2(JI)=KINX+1
    ENDIF
    IF(KBG1(JI).LT.1) THEN
       KBG1(JI)=1
       RBG1(JI)=1._R8
    ELSEIF (KBG2(JI).GT.NTS_TRAJ) THEN
       KBG2(JI)=NTS_TRAJ
       RBG1(JI)=1._R8
    ELSE
       RBG1(JI) = ABS( TLAD_TIMES(JI)-RTIMES1950TL(KBG2(JI)) ) /&
       & (RDT_TRAJ/86400._R8)
    ENDIF
    WRITE(IOUNLOG,'(A,I5,F12.3,2I5,F8.3)') '*** ',JI,TLAD_TIMES(JI),KBG1(JI),KBG2(JI),RBG1(JI)
ENDDO

WRITE(IOUNLOG,*) ' READING BG VELOCITY'
WRITE(IOUNLOG,*) '                U-COMP,',JPI,JPJ,JPK,NTS_TRAJ
CALL GETNCVAR('UB.nc','vozocrtx',JPI,JPJ,JPK,NTS_TRAJ,UB)
WRITE(IOUNLOG,*) '                V-COMP,',JPI,JPJ,JPK,NTS_TRAJ
CALL GETNCVAR('VB.nc','vomecrty',JPI,JPJ,JPK,NTS_TRAJ,VB)
WRITE(IOUNLOG,*) '                W-COMP,',JPI,JPJ,JPK,NTS_TRAJ
CALL GETNCVAR('WB.nc','vovecrtz',JPI,JPJ,JPK,NTS_TRAJ,WB)

IF(LL_VDIFF.OR.LL_ADTEST) THEN
  WRITE(IOUNLOG,*) ' READING BG VERTICAL DIFFUSIVITY'
  ALLOCATE( AVTB(JPI,JPJ,JPK,NTS_TRAJ) )
  CALL GETNCVAR('AVTB.nc','voavtnoe',JPI,JPJ,JPK,NTS_TRAJ,AVTB)
  WHERE( .NOT. ABS(AVTB) .LT. 1.E12_R8 ) AVTB = 0._R8
  WHERE( AVTB .GT. 1.E-3_R8 ) AVTB = 1.E-3_R8
  ALLOCATE( AVBT(JPI,JPJ,JPK) )
  IF(ZZ_VMIXC .GE. 1.E-6_R8 .AND. &
  &  ZZ_VMIXC .LE. 1.E+6_R8 ) AVTB = ZZ_VMIXC * AVTB
ENDIF

IF(LL_SRFFLX.OR.LL_ADTEST) THEN
  WRITE(IOUNLOG,*) ' READING BG SURFACE FLUXES'
  ALLOCATE( SSTB(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( SSSB(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( CEB(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( CHB(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( Q10B(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( WNDB(JPI,JPJ,NTS_TRAJ) )
  ALLOCATE( EMPB(JPI,JPJ,NTS_TRAJ) )
  CALL GETNCVAR('SSTB.nc','sosstsst',JPI,JPJ,NTS_TRAJ,SSTB)
  CALL GETNCVAR('SSSB.nc','sosaline',JPI,JPJ,NTS_TRAJ,SSSB)
  CALL GETNCVAR('CEB.nc','sotrcfce',JPI,JPJ,NTS_TRAJ,CEB)
  CALL GETNCVAR('CHB.nc','sotrcfch',JPI,JPJ,NTS_TRAJ,CHB)
  CALL GETNCVAR('Q10B.nc','sotrcfq1',JPI,JPJ,NTS_TRAJ,Q10B)
  CALL GETNCVAR('WNDB.nc','sowindsp',JPI,JPJ,NTS_TRAJ,WNDB)
  CALL GETNCVAR('EMPB.nc','sowaflup',JPI,JPJ,NTS_TRAJ,EMPB)
  WHERE( .NOT. ABS(SSTB) .LT. 1.E12_R8 ) SSTB = 0._R8
  WHERE( .NOT. ABS(SSSB) .LT. 1.E12_R8 ) SSSB = 0._R8
  WHERE( .NOT. ABS(CEB) .LT. 1.E12_R8 )  CEB = 0._R8
  WHERE( .NOT. ABS(CHB) .LT. 1.E12_R8 )  CHB = 0._R8
  WHERE( .NOT. ABS(Q10B) .LT. 1.E12_R8 ) Q10B = 0._R8
  WHERE( .NOT. ABS(WNDB) .LT. 1.E12_R8 ) WNDB = 0._R8
  WHERE( .NOT. ABS(EMPB) .LT. 1.E12_R8 ) EMPB = 0._R8
  ALLOCATE( TEM0(JPI,JPJ) )
  ALLOCATE( SAL0(JPI,JPJ) )
  ALLOCATE( CE(JPI,JPJ) )
  ALLOCATE( CH(JPI,JPJ) )
  ALLOCATE( Q10(JPI,JPJ) )
  ALLOCATE( WND(JPI,JPJ) )
  ALLOCATE( EMP(JPI,JPJ) )
ENDIF

WRITE(IOUNLOG,*) ' READING GEOMETRY FIELDS'
CALL GETNCVAR(CF_MOD,'e1t',JPI,JPJ,E1T)
CALL GETNCVAR(CF_MOD,'e2t',JPI,JPJ,E2T)
CALL GETNCVAR(CF_MOD,'e1u',JPI,JPJ,E1U)
CALL GETNCVAR(CF_MOD,'e2u',JPI,JPJ,E2U)
CALL GETNCVAR(CF_MOD,'e1v',JPI,JPJ,E1V)
CALL GETNCVAR(CF_MOD,'e2v',JPI,JPJ,E2V)
CALL GETNCVAR(CF_MOD,'e3t_0',JPK,E3T)
CALL GETNCVAR(CF_MOD,'e3w_0',JPK,E3W)
E3U=E3T
E3V=E3T

E1UR = E2U / E1U
E2VR = E1V / E2V

WRITE(IOUNLOG,*) ' READING MASKS'
CALL GETNCVAR(CF_MOD,'tmask',JPI,JPJ,JPK,TMASK)
CALL GETNCVAR(CF_MOD,'umask',JPI,JPJ,JPK,UMASK)
CALL GETNCVAR(CF_MOD,'vmask',JPI,JPJ,JPK,VMASK)

AVT   = 0._R8

ZDM = MAX( MAXVAL( E1T(:,:) ), MAXVAL( E2T(:,:) ) )
! MPI COMMAND HERE

WRITE(IOUNLOG,*) ' MAX RES : ',ZDM

ZA00 = AHT0 / ZDM
DO JJ = 1, JPJ
   DO JI = 1, JPI
      ZEUMAX = MAX( E1U(JI,JJ), E2U(JI,JJ) )
      ZEVMAX = MAX( E1V(JI,JJ), E2V(JI,JJ) )
      AHTU(JI,JJ) = ZA00 * ZEUMAX ! SET AHTU = AHTV AT U- AND V-POINTS,
      AHTV(JI,JJ) = ZA00 * ZEVMAX ! AND AHTW AT W-POINT (IDEM T-POINT)
   END DO
END DO

WRITE(IOUNLOG,*) ' ADJUSTING VELOCITIES'
WRITE(IOUNLOG,*) ' START : ',MAXVAL(ABS(UB)),MAXVAL(ABS(VB)),MAXVAL(ABS(WB))

WHERE( .NOT. ABS(UB) .LT. 1.E12_R8 ) UB = 0._R8
WHERE( .NOT. ABS(VB) .LT. 1.E12_R8 ) VB = 0._R8
WHERE( .NOT. ABS(WB) .LT. 1.E12_R8 ) WB = 0._R8

WRITE(IOUNLOG,*) ' INTER : ',MAXVAL(ABS(UB)),MAXVAL(ABS(VB)),MAXVAL(ABS(WB))

DO JT=1,NTS_TRAJ
  DO JK=1,JPKM1
    DO JJ=1,JPJ
      DO JI=1,JPI

        ZTS = RDT
        DZM=(E3T(JK)+E3T(JK+1))*0.5_R8
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,MAX(1.E-9_R8,ABS(WB(JI,JJ,JK  ,JT)))))
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,MAX(1.E-9_R8,ABS(WB(JI,JJ,JK+1,JT)))))

        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK  ,JT) .GT. 0._R8) THEN
            WB(JI,JJ,JK,JT) = MIN( 0.45_R8*DZM/RDT, WB(JI,JJ,JK,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK  ,JT) .LT. 0._R8) THEN
            WB(JI,JJ,JK,JT) = MAX( -0.45_R8*DZM/RDT, WB(JI,JJ,JK,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK+1,JT) .GT. 0._R8) THEN
            WB(JI,JJ,JK+1,JT) = MIN( 0.45_R8*DZM/RDT, WB(JI,JJ,JK+1,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK+1,JT) .LT. 0._R8) THEN
            WB(JI,JJ,JK+1,JT) = MAX( -0.45_R8*DZM/RDT, WB(JI,JJ,JK+1,JT) )
        ENDIF


        ZTS = RDT
        DZM=(E3W(JK)+E3W(JK+1))*0.5_R8
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,MAX(1.E-9_R8,ABS(WB(JI,JJ,JK,  JT)))))
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,MAX(1.E-9_R8,ABS(WB(JI,JJ,JK+1,JT)))))

        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK  ,JT) .GT. 0._R8) THEN
            WB(JI,JJ,JK,JT) = MIN( 0.45_R8*DZM/RDT, WB(JI,JJ,JK,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK  ,JT) .LT. 0._R8) THEN
            WB(JI,JJ,JK,JT) = MAX( -0.45_R8*DZM/RDT, WB(JI,JJ,JK,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK+1,JT) .GT. 0._R8) THEN
            WB(JI,JJ,JK+1,JT) = MIN( 0.45_R8*DZM/RDT, WB(JI,JJ,JK+1,JT) )
        ENDIF
        IF( ZTS .LT. RDT .AND. WB(JI,JJ,JK+1,JT) .LT. 0._R8) THEN
            WB(JI,JJ,JK+1,JT) = MAX( -0.45_R8*DZM/RDT, WB(JI,JJ,JK+1,JT) )
        ENDIF

      ENDDO
    ENDDO
  ENDDO
ENDDO

WRITE(IOUNLOG,*) ' END   : ',MAXVAL(ABS(UB)),MAXVAL(ABS(VB)),MAXVAL(ABS(WB))

DO JT=1,NTS_TRAJ
  DO JK=1,JPK
    DO JJ=1,JPJ
      DO JI=1,JPI

        ZTS = 0.5_R8*E3W(JK)/RDT
        IF( WB(JI,JJ,JK,JT) .GT.  ZTS ) WB(JI,JJ,JK,JT) = ZTS
        IF( WB(JI,JJ,JK,JT) .LT. -ZTS ) WB(JI,JJ,JK,JT) = -ZTS

      ENDDO
    ENDDO
  ENDDO
ENDDO

WRITE(IOUNLOG,*) ' FINAL : ',MAXVAL(ABS(UB)),MAXVAL(ABS(VB)),MAXVAL(ABS(WB))

END SUBROUTINE TLAD_INIT

SUBROUTINE TLMOD(IMODE)

IMPLICIT NONE

INTEGER,INTENT(IN) :: IMODE

REAL(R8), DIMENSION(JPI,JPJ,JPK) :: GAT, GAS
!! REAL(R8), DIMENSION(JPI,JPJ,JPK) :: TBK, SBK
REAL(R8) :: R2DT
INTEGER :: JT, JO

 CALL MYFRTPROF_WALL('TLMOD: TANGENT LINEAR DRIVER',0)

GAT = 0._R8
GAS = 0._R8

IF( IMODE .EQ. 1 .AND. ANY(TS_WRITE(1:NTS_WRITE).EQ.0) ) THEN
       CALL WRANINCTL(JPI,JPJ,JPK,0,GRD%LON,GRD%LAT,GRD%DEP,GRD%SAL,GRD%TEM)
ENDIF

!!IF( LL_VDIFF ) THEN
!!  TBK=GRD%TEM
!!  SBK=GRD%SAL
!!ENDIF

DO JT=1,TS_MOD

   TLAD_TS = JT

   R2DT=RDT
   IF(JT.LE.2) R2DT=2*RDT

   ! ADD MODEL ERROR 
   IF(LL_WEAKLY ) THEN
       CALL LOW2HI_TL(JT)
       GRD%TEM = GRD%TEM + TERR
       GRD%SAL = GRD%SAL + SERR
   ENDIF

   IF(IMODE.EQ.0) CALL OBSOP

   ! SUBTRACT MODEL ERROR
   IF(LL_WEAKLY ) THEN
       GRD%TEM = GRD%TEM - TERR
       GRD%SAL = GRD%SAL - SERR
   ENDIF

   WRITE(IOUNLOG,*) ' TL TIMESTEP : ',JT

   IF(MOD(JT-1,4).EQ.0) THEN
     CALL SET_BGF1(JT)
     IF(LL_VDIFF) CALL SET_BGF2(JT)
     IF(LL_SRFFLX)CALL SET_BGF3(JT)
   ENDIF

   IF( LL_TLDEB ) THEN
     WRITE(IOUNLOG,*) ' XVEL: ',MINVAL(UBT),MAXVAL(UBT),UBT(IDB_COO(1),IDB_COO(2),IDB_COO(3))
     WRITE(IOUNLOG,*) ' YVEL: ',MINVAL(VBT),MAXVAL(VBT),VBT(IDB_COO(1),IDB_COO(2),IDB_COO(3))
     WRITE(IOUNLOG,*) ' ZVEL: ',MINVAL(WBT),MAXVAL(WBT),WBT(IDB_COO(1),IDB_COO(2),IDB_COO(3))
     IF(LL_VDIFF) WRITE(IOUNLOG,*) ' AVT : ',MINVAL(AVTB),MAXVAL(AVBT),AVBT(IDB_COO(1),IDB_COO(2),IDB_COO(3))
     WRITE(IOUNLOG,*) ' TEM : ',MINVAL(GRD%TEM),MAXVAL(GRD%TEM),GRD%TEM(IDB_COO(1),IDB_COO(2),IDB_COO(3))
   ENDIF

   !... SURFACE FORCING
   IF( LL_SRFFLX ) THEN
     CALL BLK_OCE_CORE(JPI,JPJ,GRD%TEM(:,:,1),GRD%SAL(:,:,1),GAT(:,:,1),GAS(:,:,1))
     IF( LL_TLDEB ) &
     & WRITE(IOUNLOG,*) ' FLX : ',MINVAL(GAT),MAXVAL(GAT),&
     & MAX(ABS(MINVAL(GAT)),MAXVAL(GAT))*RDT,GAT(IDB_COO(1),IDB_COO(2),IDB_COO(3))*RDT
   ENDIF

   IF( LL_TLDEB ) &
   & WRITE(IOUNLOG,*) ' MIN/MAX UBT ',MINVAL(UBT),MAXVAL(UBT),MINVAL(WBT),MAXVAL(WBT)

   !... TRACER ADVECTION
   IF( LL_HADV ) THEN
     CALL TRA_ADV_CEN2H(JPI,JPJ,JPK,UBT,VBT,GRD%TEM,GAT)
     CALL TRA_ADV_CEN2H(JPI,JPJ,JPK,UBT,VBT,GRD%SAL,GAS)
   ENDIF

   IF( LL_VADV ) THEN
     CALL TRA_ADV_CEN2V(JPI,JPJ,JPK,WBT,GRD%TEM,GAT)
     CALL TRA_ADV_CEN2V(JPI,JPJ,JPK,WBT,GRD%SAL,GAS)
   ENDIF

   IF( LL_TLDEB ) &
   & WRITE(IOUNLOG,*) ' ADV : ',MINVAL(GAT),MAXVAL(GAT),&
   & MAX(ABS(MINVAL(GAT)),MAXVAL(GAT))*RDT,GAT(IDB_COO(1),IDB_COO(2),IDB_COO(3))*RDT

   !... TRACER HORIZONTAL DIFFUSION
   IF( LL_HDIFF ) THEN
     CALL TRA_LDF_LAP(JPI,JPJ,JPK,GRD%TEM,GAT)
     CALL TRA_LDF_LAP(JPI,JPJ,JPK,GRD%SAL,GAS)
   ENDIF

   IF( LL_TLDEB ) &
   & WRITE(IOUNLOG,*) ' LAP : ',MINVAL(GAT),MAXVAL(GAT),&
   & MAX(ABS(MINVAL(GAT)),MAXVAL(GAT))*RDT,GAT(IDB_COO(1),IDB_COO(2),IDB_COO(3))*RDT

   !... TRACER VERTICAL DIFFUSION AND TIMESTEPPING
   IF(LL_VDIFF) THEN
     IF(NN_VDIFF .EQ. 1 ) THEN
       CALL ABOR1('TLAD : EXPLICIT VERT MIXING IS NOT SUPPORTED IN TL TIMESTEPPING')
     ELSEIF(NN_VDIFF .EQ. 2) THEN
       CALL TRA_ZDF_IMP(R2DT,GRD%TEM,GAT,AVBT)
       CALL TRA_ZDF_IMP(R2DT,GRD%SAL,GAS,AVBT)
     ENDIF
     IF( LL_TLDEB ) THEN
       WRITE(IOUNLOG,*) ' TOT     : ',MINVAL(GRD%TEM), MAXVAL(GRD%TEM)
       WRITE(IOUNLOG,*) ' TOT     : ',MINVAL(GAT), MAXVAL(GAT)
       WRITE(IOUNLOG,*) ' TOT LOC : ',MAXLOC(GRD%TEM)
       WRITE(IOUNLOG,*) ' TOT LOC : ',MAXLOC(GAT)
       WRITE(IOUNLOG,*) ' POINT   : ',GAT(IDB_COO(1),IDB_COO(2),IDB_COO(3)), GRD%TEM(IDB_COO(1),IDB_COO(2),IDB_COO(3))
     ENDIF
     CALL LBC_LNK(GAT)
     CALL LBC_LNK(GAS)
!!     TBK=GRD%TEM ; GRD%TEM = GAT ; GAT = 0._R8
!!     SBK=GRD%SAL ; GRD%SAL = GAS ; GAS = 0._R8
     GRD%TEM = GAT ; GAT = 0._R8
     GRD%SAL = GAS ; GAS = 0._R8
   ELSE
     ! CALL TLMOD_TS(JT,GBT,GAT,GBS,GAS,GRD%TEM,GRD%SAL,RDT)
     CALL LBC_LNK(GAT)
     CALL LBC_LNK(GAS)
     GRD%TEM = GRD%TEM + RDT*GAT ; GAT = 0._R8
     GRD%SAL = GRD%SAL + RDT*GAS ; GAS = 0._R8
   ENDIF
   IF( LL_TLDEB ) THEN
     WRITE(IOUNLOG,*) ' TOT     : ',MINVAL(GRD%TEM), MAXVAL(GRD%TEM)
     WRITE(IOUNLOG,*) ' TOT LOC : ',MAXLOC(GRD%TEM)
     WRITE(IOUNLOG,*) ' POINT   : ',GRD%TEM(IDB_COO(1),IDB_COO(2),IDB_COO(3))
   ENDIF

   IF( IMODE .EQ. 1 .AND. ANY(TS_WRITE(1:NTS_WRITE).EQ.JT) ) THEN
       CALL WRANINCTL(JPI,JPJ,JPK,JT,GRD%LON,GRD%LAT,GRD%DEP,GRD%SAL,GRD%TEM)
   ENDIF
   IF( IMODE .EQ. 2 ) WRITE(IOUNLOG,*) ' TIMESTEP :', JT, &
   & MAXVAL(ABS(GRD%TEM)),MAXVAL(ABS(GRD%SAL))

ENDDO

 CALL MYFRTPROF_WALL('TLMOD: TANGENT LINEAR DRIVER',1)

END SUBROUTINE TLMOD

SUBROUTINE ADMOD(IMODE)

IMPLICIT NONE

INTEGER,INTENT(IN) :: IMODE
REAL(R8) :: R2DT
REAL(R8), DIMENSION(JPI,JPJ,JPK) :: GAT, GAS
!!REAL(R8), DIMENSION(JPI,JPJ,JPK) :: TBK, SBK
INTEGER :: JT, JO, JP

 CALL MYFRTPROF_WALL('ADMOD: ADJOINT MODEL DRIVER',0)

GAT = 0._R8
GAS = 0._R8
!!TBK = 0._R8
!!SBK = 0._R8

DO JT=TS_MOD,1,-1

   TLAD_TS = JT

   IF( IMODE .EQ. 1 .AND. ANY(TS_WRITE(1:NTS_WRITE).EQ.JT) ) THEN
       CALL WRANINCAD(JPI,JPJ,JPK,JT,GRD%LON,GRD%LAT,GRD%DEP,GRD%SAL_AD,GRD%TEM_AD)
   ENDIF

   R2DT=RDT
   IF(JT.LE.2) R2DT=2*RDT

   IF(MOD(JT-1,4).EQ.0) THEN
     CALL SET_BGF1(JT)
     IF(LL_VDIFF) CALL SET_BGF2(JT)
     IF(LL_SRFFLX)CALL SET_BGF3(JT)
   ENDIF

   IF(LL_VDIFF) THEN
     !!GAT=GAT+GRD%TEM_AD
     !!GAS=GAS+GRD%SAL_AD
     GAT=GRD%TEM_AD
     GAS=GRD%SAL_AD
       GRD%TEM_AD = 0._R8
       GRD%SAL_AD = 0._R8
!O     GRD%TEM_AD = TBK
!O     GRD%SAL_AD = SBK
!!     GRD%TEM_AD = TBK
!!     GRD%SAL_AD = SBK
     CALL LBC_LNK_AD(GAT)
     CALL LBC_LNK_AD(GAS)
     IF(NN_VDIFF .EQ. 1 ) THEN
!!       CALL TRA_ZDF_EXP_AD(KN_ZDFEXP,TBK,GAT,AVBT)
!!       CALL TRA_ZDF_EXP_AD(KN_ZDFEXP,SBK,GAS,AVBT)
     WRITE(*,*) 'TEMP NOT'
     ELSEIF(NN_VDIFF .EQ. 2 ) THEN
       CALL TRA_ZDF_IMP_AD(R2DT,GRD%TEM_AD,GAT,AVBT)
       CALL TRA_ZDF_IMP_AD(R2DT,GRD%SAL_AD,GAS,AVBT)
!N       CALL TRA_ZDF_IMP_AD(R2DT,GRD%TEM_AD,GAT,AVBT)
!N       CALL TRA_ZDF_IMP_AD(R2DT,GRD%SAL_AD,GAS,AVBT)
     ENDIF
   ELSE
     GAT = RDT*GRD%TEM_AD
     GAS = RDT*GRD%SAL_AD
     CALL LBC_LNK_AD(GAT)
     CALL LBC_LNK_AD(GAS)
   ENDIF

   IF( LL_HDIFF ) THEN
     CALL TRA_LDF_LAP_AD(JPI,JPJ,JPK,GRD%SAL_AD,GAS)
     CALL TRA_LDF_LAP_AD(JPI,JPJ,JPK,GRD%TEM_AD,GAT)
   ENDIF

   IF( LL_VADV ) THEN
     CALL TRA_ADV_CEN2V_AD(JPI,JPJ,JPK,WBT,GRD%SAL_AD,GAS)
     CALL TRA_ADV_CEN2V_AD(JPI,JPJ,JPK,WBT,GRD%TEM_AD,GAT)
   ENDIF

   IF( LL_HADV ) THEN
     CALL TRA_ADV_CEN2H_AD(JPI,JPJ,JPK,UBT,VBT,GRD%SAL_AD,GAS)
     CALL TRA_ADV_CEN2H_AD(JPI,JPJ,JPK,UBT,VBT,GRD%TEM_AD,GAT)
   ENDIF

   IF( LL_SRFFLX ) THEN
     CALL BLK_OCE_CORE_AD(JPI,JPJ,GRD%TEM_AD(:,:,1),GRD%SAL_AD(:,:,1),GAT(:,:,1),GAS(:,:,1))
   ENDIF

   IF(IMODE.EQ.0) CALL OBSOP_AD

   IF(LL_WEAKLY ) THEN
     WRITE(IOUNLOG,*) 'LHT',MINVAL(TERR_AD), MAXVAL(TERR_AD)
     WRITE(IOUNLOG,*) 'LHS',MINVAL(SERR_AD), MAXVAL(SERR_AD)
     CALL FLUSH(IOUNLOG)
     CALL LOW2HI_AD(JT)
   ENDIF

ENDDO

!O IF( LL_VDIFF ) THEN
!O   GRD%TEM_AD = GRD%TEM_AD + TBK
!O   GRD%SAL_AD = GRD%SAL_AD + SBK
! OENDIF

!... TLAD_TS = 0
!... CALL OBSOP_AD

IF( IMODE .EQ. 1 .AND. ANY(TS_WRITE(1:NTS_WRITE).EQ.0) ) THEN
       CALL WRANINCAD(JPI,JPJ,JPK,0,GRD%LON,GRD%LAT,GRD%DEP,GRD%SAL_AD,GRD%TEM_AD)
ENDIF

 CALL MYFRTPROF_WALL('ADMOD: ADJOINT MODEL DRIVER',1)

END SUBROUTINE ADMOD

SUBROUTINE TLMOD_TS(J,GBT,GNT,GBS,GNS,XAT,XAS,DT)

IMPLICIT NONE

INTEGER, INTENT(IN) :: J
REAL(R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT) :: GBT,GNT,XAT
REAL(R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT) :: GBS,GNS,XAS
REAL(R8), INTENT(IN) :: DT
REAL(R8), PARAMETER :: ABEPS      = 0.01_R8
REAL(R8) :: AB15, AB05

 CALL MYFRTPROF_WALL('TLMOD_TS: TL MODEL TIMESTEPPING',0)

!IF( J .EQ. 1 ) THEN      
    AB15 = 1.0_R8
    AB05 = 0.0_R8
!ELSE
!    AB15 =    1.5_R8 + ABEPS
!    AB05 = -( 0.5_R8 + ABEPS )
!ENDIF

!GB =  AB15*GN + AB05*GB
!XA = XA + DT*GB
!GB = GN
!GN = 0._R8

XAT = XAT+DT*GNT
GNT = 0._R8
XAS = XAS+DT*GNS
GNS = 0._R8

 CALL MYFRTPROF_WALL('TLMOD_TS: TL MODEL TIMESTEPPING',1)
END SUBROUTINE TLMOD_TS

SUBROUTINE ADMOD_TS(J,GBT,GNT,GBS,GNS,XAT,XAS,DT)

IMPLICIT NONE

INTEGER, INTENT(IN) :: J
REAL(R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT) :: GBT,GNT,XAT
REAL(R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT) :: GBS,GNS,XAS
REAL(R8), INTENT(IN) :: DT
REAL(R8), PARAMETER :: ABEPS      = 0.01_R8
REAL(R8) :: AB15, AB05

 CALL MYFRTPROF_WALL('ADMOD_TS: AD MODEL TIMESTEPPING',0)
!IF( J .EQ. 1 ) THEN
    AB15 = 1._R8
    AB05 = 0._R8
!ELSE
!    AB15 =    1.5 + ABEPS
!    AB05 = -( 0.5 + ABEPS )
!ENDIF

!GN = 0._R8
!GN = GB
!GB = 0._R8
!GB = DT*XA
!GN = GN + AB15*GB
!GB = AB05*GB

GNT = 0._R8
GNT = DT*XAT
GNS = 0._R8
GNS = DT*XAS
 CALL MYFRTPROF_WALL('ADMOD_TS: AD MODEL TIMESTEPPING',1)

END SUBROUTINE ADMOD_TS

SUBROUTINE SET_BGF1(IT)

IMPLICIT NONE

INTEGER, INTENT(IN) :: IT
INTEGER :: JI,JJ,KT1,KT2,JK
REAL(R8) :: RT1, ZTS, DZM

CALL MYFRTPROF_WALL('SETBGF1: SETBGF - 1',0)

KT1 = KBG1(IT)
KT2 = KBG2(IT)
RT1 = RBG1(IT)

UBT = RT1*UB(:,:,:,KT1) + (1._R8-RT1)*UB(:,:,:,KT2)
VBT = RT1*VB(:,:,:,KT1) + (1._R8-RT1)*VB(:,:,:,KT2)
IF(LL_VVEL_NEMO) THEN
  WBT = RT1*WB(:,:,:,KT1) + (1._R8-RT1)*WB(:,:,:,KT2)
ELSE
  DO JK=1,JPKM1
    DO JJ=2,JPJM1
      DO JI=2,JPIM1
        HDIVN(JI,JJ,JK) = &
        (E2U(JI,JJ)*E3U(JK)*UBT(JI,JJ,JK)-E2U(JI-1,JJ)*E3U(JK)*UBT(JI-1,JJ,JK) &
        +E1V(JI,JJ)*E3V(JK)*VBT(JI,JJ,JK)-E1V(JI,JJ-1)*E3V(JK)*VBT(JI,JJ-1,JK) ) &
        /(E1T(JI,JJ)*E2T(JI,JJ)*E3T(JK))
      ENDDO
    ENDDO
  ENDDO
  WBT = 0._WP
  DO JK = JPKM1, 1, -1 
    WBT(:,:,JK) = WBT(:,:,JK+1) - ( E3T(JK) * HDIVN(:,:,JK) ) * TMASK(:,:,JK)
  END DO
ENDIF

UBT(:,:,JPK) = 0._WP
VBT(:,:,JPK) = 0._WP
WBT(:,:,JPK) = 0._WP

IF( .FALSE. ) THEN
  ZTS = RDT
  DO JK=1,JPKM1
    DO JJ=1,JPJ
      DO JI=1,JPI
        DZM=(E3T(JK)+E3T(JK+1))*0.5_R8
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,ABS(WBT(JI,JJ,JK))))
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,ABS(WBT(JI,JJ,JK+1))))
        DZM=(E3W(JK)+E3W(JK+1))*0.5_R8
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,ABS(WBT(JI,JJ,JK))))
        ZTS=MIN(ZTS,DZM/MAX(DZM/RDT,ABS(WBT(JI,JJ,JK+1))))
      ENDDO
    ENDDO
  ENDDO
  WRITE(IOUNLOG,'(A,2I4,F6.3,A,F10.3)') '  SETTING BG : ',KT1,KT2,RT1, 'MIN TS : ',ZTS
ELSE
  WRITE(IOUNLOG,*) ' SETTING BG : ',KT1,KT2,RT1
ENDIF

 CALL MYFRTPROF_WALL('SETBGF1: SETBGF - 1',1)

END SUBROUTINE SET_BGF1

SUBROUTINE SET_BGF2(IT)

IMPLICIT NONE

INTEGER, INTENT(IN) :: IT
INTEGER :: JI,JJ,KT1,KT2
REAL(R8) :: RT1
CALL MYFRTPROF_WALL('SETBGF2: SETBGF - 2',0)

KT1 = KBG1(IT)
KT2 = KBG2(IT)
RT1 = RBG1(IT)

AVBT = RT1*AVTB(:,:,:,KT1) + (1._R8-RT1)*AVTB(:,:,:,KT2)

CALL MYFRTPROF_WALL('SETBGF2: SETBGF - 2',1)
END SUBROUTINE SET_BGF2

SUBROUTINE SET_BGF3(IT)

IMPLICIT NONE

INTEGER, INTENT(IN) :: IT
INTEGER :: JI,JJ,KT1,KT2
REAL(R8) :: RT1
CALL MYFRTPROF_WALL('SETBGF3: SETBGF - 3',0)

KT1 = KBG1(IT)
KT2 = KBG2(IT)
RT1 = RBG1(IT)

TEM0 = RT1*SSTB(:,:,KT1) + (1._R8-RT1)*SSTB(:,:,KT2)
SAL0 = RT1*SSSB(:,:,KT1) + (1._R8-RT1)*SSSB(:,:,KT2)
CH  = RT1*CHB(:,:,KT1)  + (1._R8-RT1)*CHB(:,:,KT2)
CE  = RT1*CEB(:,:,KT1)  + (1._R8-RT1)*CEB(:,:,KT2)
Q10 = RT1*Q10B(:,:,KT1) + (1._R8-RT1)*Q10B(:,:,KT2)
WND = RT1*WNDB(:,:,KT1) + (1._R8-RT1)*WNDB(:,:,KT2)
EMP = RT1*EMPB(:,:,KT1) + (1._R8-RT1)*EMPB(:,:,KT2)
TEM0 = TEM0 + RT0

CALL MYFRTPROF_WALL('SETBGF3: SETBGF - 3',1)
END SUBROUTINE SET_BGF3


SUBROUTINE LBC_LNK(VAR)
IMPLICIT NONE
REAL (R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT)  :: VAR
VAR(1,:,:) = 0._R8
VAR(JPI,:,:) = 0._R8
VAR(:,1,:) = 0._R8
VAR(:,JPJ,:) = 0._R8
VAR = VAR * GRD%MSK
END SUBROUTINE LBC_LNK

SUBROUTINE LBC_LNK_AD(VAR)
IMPLICIT NONE
REAL (R8), DIMENSION(JPI,JPJ,JPK), INTENT(INOUT)  :: VAR
VAR(1,:,:) = 0._R8
VAR(JPI,:,:) = 0._R8
VAR(:,1,:) = 0._R8
VAR(:,JPJ,:) = 0._R8
VAR = VAR * GRD%MSK
END SUBROUTINE LBC_LNK_AD

SUBROUTINE PREP_OBS_4DVAR

INTEGER(I4) :: JO

! INSITU
IF( INS%NO .GT. 0 ) THEN
 DO JO=1,INS%NO
   INS%TATS(JO) = MINLOC( ABS( INS%TIM(JO) - TLAD_TIMES ), DIM=1 )
 ENDDO
 IF ( ANY(INS%TATS(1:INS%NO).LT.0) .OR. ANY(INS%TATS(1:INS%NO).GT.TS_MOD) )&
 & CALL ABOR1('PREP_OBS_4DVAR: INS OUT OF TIME RANGE')
ENDIF

! SLA
IF( SLA%NO .GT. 0 ) THEN
 DO JO=1,SLA%NO
   SLA%TATS(JO) = MINLOC( ABS( SLA%TIM(JO) - TLAD_TIMES ), DIM=1 )
 ENDDO
 IF ( ANY(SLA%TATS(1:SLA%NO).LT.0) .OR. ANY(SLA%TATS(1:SLA%NO).GT.TS_MOD) )&
 & CALL ABOR1('PREP_OBS_4DVAR: SLA OUT OF TIME RANGE')
ENDIF

! SST
IF( SST%NO .GT. 0 ) THEN
 DO JO=1,SST%NO
   SST%TATS(JO) = MINLOC( ABS( SST%TIM(JO) - TLAD_TIMES ), DIM=1 )
 ENDDO
 IF ( ANY(SST%TATS(1:SST%NO).LT.0) .OR. ANY(SST%TATS(1:SST%NO).GT.TS_MOD) )&
 & CALL ABOR1('PREP_OBS_4DVAR: SST OUT OF TIME RANGE')
ENDIF

! SSS
IF( SSS%NO .GT. 0 ) THEN
 DO JO=1,SSS%NO
   SSS%TATS(JO) = MINLOC( ABS( SSS%TIM(JO) - TLAD_TIMES ), DIM=1 )
 ENDDO
 IF ( ANY(SSS%TATS(1:SSS%NO).LT.0) .OR. ANY(SSS%TATS(1:SSS%NO).GT.TS_MOD) )&
 & CALL ABOR1('PREP_OBS_4DVAR: SSS OUT OF TIME RANGE')
ENDIF

END SUBROUTINE PREP_OBS_4DVAR

SUBROUTINE PREP301

IMPLICIT NONE

WRITE(IOUNLOG,*) ' PREPARATION FOR CONFIGURATION 301 '
WRITE(IOUNLOG,*) 
WRITE(IOUNLOG,*) ' READING PERTURBATIONS'

CALL GETNCVAR('PERTUR.NC','INCTEMPER',JPI,JPJ,JPK,GRD%TEM)
CALL GETNCVAR('PERTUR.NC','INCSALINE',JPI,JPJ,JPK,GRD%SAL)

WRITE(IOUNLOG,*) ' DONE'

END SUBROUTINE PREP301

END MODULE TLAD 
