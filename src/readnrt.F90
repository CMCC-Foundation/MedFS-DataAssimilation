SUBROUTINE READNRT

!.. INTERFACE FOR READING ARGO AND GTSPP FILES
!..
!.. ANDREA STORTO, *CMCC* 2012-03-13

USE SET_KND
USE OBS_STR, ONLY : SST,INS,SLA,OBS,SSS
USE GRD_STR
USE IOUNITS
USE ALLOCOBS
USE OBSHANDLING , ONLY : INS_LQC_POS,INS_CHK_TIME,INS_LQC_SAL,&
                       & INS_LQC_TEMP,INS_LQC_PROF_T,INS_LQC_PRE,&
                       & INS_LQC_PROF_S,INS_LQC_PROF_P,INS_LQC_TIME,&
                       & INS_ARG_GREYL
USE MYFRTPROF, ONLY : MYFRTPROF_WALL
USE RUN, ONLY : ZANJUL1950

IMPLICIT NONE

CHARACTER(LEN=20) :: CFILE
INTEGER(I4), PARAMETER :: NMAX = 1500000,MAXPROFS = 20000
INTEGER(I4) :: NOBS , IFILE, KOSUM, NLEVS
INTEGER(I4) :: STOBS,ENOBS,TOTOBS
INTEGER(I4), DIMENSION(NMAX) :: OTYPE, OPAR, OPROF
REAL   (R8), DIMENSION(NMAX) :: ODEP,  OVAL
REAL   (DP), DIMENSION(NMAX) :: OTIME
REAL   (R8), DIMENSION(NMAX) :: OLON,  OLAT
CHARACTER(LEN=8), DIMENSION(NMAX) :: OPLA
LOGICAL :: LPROCEED, LFLAGS(10), LDUPL, LDUPLA(MAXPROFS)
INTEGER(I4) :: KPRC, KDUPL, JOBS,JPROF,JPROF2, KPIND, KPIND2,UPPROF,K

  WRITE(IOUNOUT,*) ' READING IN IN-SITU OBSERVATIONS'

  INS%NO = NMAX
  INS%NC = NMAX

  CALL ALLOCINS(INS%NO,.TRUE.)
  INS%FLC(:)=0
  INS%FLG(:)=1

  !... SET FLAGS FOR EXTRACTION OF OBS
  LFLAGS( 1) = INS_LQC_POS
  LFLAGS( 2) = INS_LQC_TIME
  LFLAGS( 3) = INS_LQC_PRE
  LFLAGS( 4) = INS_LQC_SAL
  LFLAGS( 5) = INS_LQC_TEMP
  LFLAGS( 6) = INS_LQC_PROF_T
  LFLAGS( 7) = INS_LQC_PROF_S
  LFLAGS( 8) = INS_LQC_PROF_P
  LFLAGS( 9) = INS_CHK_TIME
  LFLAGS(10) = INS_ARG_GREYL

!=========  *** ARGO SECTION ***

  LPROCEED=.TRUE.
  IFILE=1
  TOTOBS=0
  UPPROF=0

  !... LOOP OVER EXISTING INS FILES
  DO WHILE(LPROCEED)
      WRITE(CFILE,'(A,I2.2)' ) 'ARGO_',IFILE
      INQUIRE(FILE=CFILE,EXIST=LPROCEED)
      IF(LPROCEED) THEN
          WRITE(IOUNLOG,*)
          WRITE(IOUNLOG,*) ' /// READING ARGO FILE ',TRIM(CFILE)
          WRITE(IOUNLOG,*)

          CALL READ_ARGLEVS(CFILE,NLEVS)

          CALL READ_ARGO(CFILE,NLEVS,IOUNLOG,NMAX,&
          & LFLAGS(1),LFLAGS(2),LFLAGS(3),LFLAGS(4),LFLAGS(5),&
          & LFLAGS(6),LFLAGS(7),LFLAGS(8),LFLAGS(9),LFLAGS(10),&
          & NOBS,OPAR,ODEP,OVAL,OTIME,OLON,OLAT,OPROF,OPLA)

   !... STORE OBS

          IF(NOBS.GT.0) THEN
            IF(TOTOBS+NOBS .GT. NMAX ) &
            & CALL ABOR1('READINSITU : NMAX MUST BE INCREASED')

            STOBS = TOTOBS +1
            ENOBS = TOTOBS + NOBS

            INS%OTYPE(STOBS:ENOBS) = KKARGO
            INS%  PAR(STOBS:ENOBS) = OPAR (1:NOBS)
            INS%  TIM(STOBS:ENOBS) = OTIME(1:NOBS)
            INS%  DPT(STOBS:ENOBS) = ODEP (1:NOBS)
            INS%  VAL(STOBS:ENOBS) = OVAL (1:NOBS)
            INS%  LON(STOBS:ENOBS) = OLON (1:NOBS)
            INS%  LAT(STOBS:ENOBS) = OLAT (1:NOBS)
            INS% PROF(STOBS:ENOBS) = OPROF(1:NOBS) + UPPROF
            INS% PLNO(STOBS:ENOBS)(1:8) = OPLA (1:NOBS)(1:8)
            INS% DSRC(STOBS:ENOBS)(1:3) = 'ARG'

            TOTOBS = TOTOBS + NOBS
            UPPROF = INS% PROF(ENOBS)

          ENDIF
      ELSE

          IF(IFILE.EQ.1) &
          & WRITE(IOUNERR,*) '*** WARNING : NO ARGO FILE'

      ENDIF
      IFILE=IFILE+1
  ENDDO

!=========  *** GTSPP SECTION ***

  LPROCEED=.TRUE.
  IFILE=1

  !... LOOP OVER EXISTING INS FILES
  DO WHILE(LPROCEED)
      WRITE(CFILE,'(A,I2.2)' ) 'GTSPP_',IFILE
      INQUIRE(FILE=CFILE,EXIST=LPROCEED)
      IF(LPROCEED) THEN
          WRITE(IOUNLOG,*)
          WRITE(IOUNLOG,*) ' /// READING GTSPP FILE ',TRIM(CFILE)
          WRITE(IOUNLOG,*)

          CALL READ_GTSPP(CFILE,IOUNLOG,NMAX,&
          & NOBS,OTYPE,OPAR,ODEP,OVAL,OTIME,OLON,OLAT,OPROF,OPLA)

          WRITE(IOUNLOG,*) ' FILE ',TRIM(CFILE)
          WRITE(IOUNLOG,*) ' NO VALID PROFILES ', OPROF(NOBS)
          WRITE(IOUNLOG,*) ' NO VALID OBS      ', NOBS

   !... STORE OBS

          IF(NOBS.GT.0) THEN
            IF(TOTOBS+NOBS .GT. NMAX ) &
            & CALL ABOR1('READINSITU : NMAX MUST BE INCREASED')

            STOBS = TOTOBS +1
            ENOBS = TOTOBS + NOBS

            INS%OTYPE(STOBS:ENOBS) = OTYPE(1:NOBS)
            INS%  PAR(STOBS:ENOBS) = OPAR (1:NOBS)
            INS%  TIM(STOBS:ENOBS) = OTIME(1:NOBS)
            INS%  DPT(STOBS:ENOBS) = ODEP (1:NOBS)
            INS%  VAL(STOBS:ENOBS) = OVAL (1:NOBS)
            INS%  LON(STOBS:ENOBS) = OLON (1:NOBS)
            INS%  LAT(STOBS:ENOBS) = OLAT (1:NOBS)
            INS% PROF(STOBS:ENOBS) = OPROF(1:NOBS) + UPPROF
            INS% PLNO(STOBS:ENOBS)(1:8) = OPLA (1:NOBS)(1:8)
            INS% DSRC(STOBS:ENOBS)(1:3) = 'GTS'

            TOTOBS = TOTOBS + NOBS
            UPPROF = INS% PROF(ENOBS)

          ENDIF
      ELSE

          IF(IFILE.EQ.1) &
          & WRITE(IOUNERR,*) '*** WARNING : NO GTSPP FILE'

      ENDIF
      IFILE=IFILE+1
  ENDDO

!=========  *** COMMON SECTION ***

 INS%NO=TOTOBS
 INS%FLC(1:INS%NO) = 1

 CALL PREPPROF

END SUBROUTINE READNRT
