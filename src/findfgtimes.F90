SUBROUTINE FINDFGTIMES(NTIMES,FGY,FGM,FGD,FGH,MT,RT)

!... READ NUMBER OF STEPS IN THE (FIRST) FG FILE

USE SET_KND
USE IOUNITS, ONLY : IOUNERR,IOUNOUT,IOUNLOG
USE NETCDF
USE READFG
USE RUN, ONLY : LLONLYOBSPP

IMPLICIT NONE

INTEGER(I4)  ,  INTENT(OUT)   :: NTIMES, FGY,FGM,FGD,FGH
INTEGER(I4)  ,  INTENT(IN )   :: MT
REAL   (R8)  ,  INTENT(OUT)   :: RT(MT)
INTEGER(I4)                   :: STAT, NCID, IDVAR,K,ITIME
CHARACTER(LEN=99) :: CATTR

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// READING FG TIMESTEPS FROM FILE ',TRIM(CFGFILES(1))

STAT = NF90_OPEN(TRIM(CFGFILES(1)), NF90_NOWRITE, NCID)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_INQ_DIMID (NCID, 'time_counter', IDVAR)
IF (STAT /= NF90_NOERR) THEN
  IF(LLONLYOBSPP) THEN
    NTIMES=1
  ELSE
    CALL HANDLE_ERR(STAT)
  ENDIF
ELSE
  STAT = NF90_INQUIRE_DIMENSION (NCID, IDVAR, LEN = NTIMES)
  IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
ENDIF

STAT = NF90_INQ_VARID (NCID, 'time_counter', IDVAR)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_VAR   (NCID, IDVAR, RT(1:NTIMES) )
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_INQUIRE_ATTRIBUTE (NCID, IDVAR, 'units', LEN = K)
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)
STAT = NF90_GET_ATT (NCID, IDVAR, 'units', CATTR(1:K))
IF (STAT /= NF90_NOERR) CALL HANDLE_ERR(STAT)

STAT = NF90_CLOSE(NCID)

WRITE(IOUNLOG,*) ' FOUND TIMESTEPS :', NTIMES

IF( CATTR(1:13) .NE. 'seconds since' ) &
CALL ABOR1('UNRECOGNIZED TIME UNIT IN FINDFGTIMES :'//CATTR(1:13))

READ(CATTR(15:18),*) FGY
READ(CATTR(20:21),*) FGM
READ(CATTR(23:24),*) FGD
READ(CATTR(26:27),*) FGH

WRITE(IOUNLOG,*) ' PREVIOUS INTEGRATION BEGINNING :',FGY,FGM,FGD,FGH

CALL FLUSH(IOUNLOG)
RETURN
END SUBROUTINE FINDFGTIMES
