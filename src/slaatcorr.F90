SUBROUTINE SLAATCORR

!... SEARCH OUT SLA ALONG-TRACK CO-LOCATIONS
!    FOR ERROR CORRELATION COMPUTATION
!... A.S.

USE SET_KND
USE OBS_STR, ONLY : SLA
USE GRD_STR
USE IOUNITS
USE MOD_GCDIST, ONLY : GCDIST

IMPLICIT NONE

INTEGER(I4), PARAMETER :: NSAT_TPN=8, NSAT_J1=5
INTEGER(I4), PARAMETER :: MAXCT=4000
INTEGER(I4) :: XIND1
INTEGER(I4) :: JS, JS2, NCT, NTA, NTB, JC, I1, I2, J1
INTEGER(I4) :: NLISTA(MAXCT),NLISTB(MAXCT)
INTEGER(I4) :: MYOBS, MYOBS2, KCOUNT, KCOUNTTOT
INTEGER(I4) :: KTRACKA(MAXCT),KTRACKB(MAXCT)
REAL (R8), PARAMETER :: ZMAXDIST=80._R8
REAL (DP), PARAMETER :: ZMINMINS=20._DP
REAL (R8), PARAMETER :: ZMINDIST=120._R8
REAL (R8) :: ZCDIST, ZDC1, ZDC2
REAL (R8) :: ZKTRA(MAXCT)
REAL (DP) :: ZTTRA(MAXCT), ZMINTIM
REAL (R8), ALLOCATABLE :: ZDISTS(:,:)
REAL (R8) :: ZTEMP1, ZTEMP2, ZTEMP3, MYDIFF1, MYDIFF2, ZTEMPOLD
LOGICAL :: LLDONE

ZMINTIM=ZMINMINS/REAL(60*24,KIND=DP)
WRITE(IOUNOUT,*) 'ZMINTIM =',ZMINTIM
NCT=0

OUTER : DO JS=1,SLA%NO

   IF(SLA%KSAT(JS)==NSAT_TPN .AND. ABS(SLA%LAT(JS)).LT.60._R8) THEN

     IF(NCT.GT.0) THEN
        IF( ANY(SLA%TRACK.EQ.KTRACKA(1:NCT)) ) CYCLE OUTER
     ENDIF

     DO JS2=1,SLA%NO

        IF(SLA%KSAT(JS2)==NSAT_J1) THEN

           IF( ABS(SLA%TIM(JS2)-SLA%TIM(JS)) .LT. ZMINTIM) THEN
             CALL GCDIST(SLA%LAT(JS),SLA%LON(JS),&
                  & SLA%LAT(JS2),SLA%LON(JS2),ZCDIST)
             IF(ZCDIST.LT.ZMAXDIST) THEN
                NCT=NCT+1
                KTRACKA(NCT) = SLA%TRACK(JS)
                KTRACKB(NCT) = SLA%TRACK(JS2)
                ZTTRA(NCT)=SLA%TIM(JS2)-SLA%TIM(JS)
                ZKTRA(NCT)=ZCDIST
                CYCLE OUTER
             ENDIF
           ENDIF

        ENDIF

     ENDDO

   ENDIF

ENDDO OUTER

WRITE(IOUNOUT,*) 'NUMBER OF JASON-1/TOPEXPOSEIDON COLOCATED TRACKS: ',NCT
DO JS=1,NCT
   WRITE(999,*) KTRACKA(JS),KTRACKB(JS),ZTTRA(JS),ZKTRA(JS)
ENDDO

KCOUNTTOT=0
DO JC=1,NCT
   WRITE(IOUNOUT,*) ' TREATING TRACK NO ',JC,' OVER ',NCT, &
   & ' ',KTRACKA(JC),KTRACKB(JC)
   NTA=0
   NTB=0
   KCOUNT=0
   PREP : DO JS=1,SLA%NO
      IF(SLA%FLC(JS).EQ.0) CYCLE PREP
      IF(KTRACKA(JC).EQ.SLA%TRACK(JS))THEN
         NTA=NTA+1
         NLISTA(NTA)=JS
      ENDIF
      IF(KTRACKB(JC).EQ.SLA%TRACK(JS))THEN
         NTB=NTB+1
         NLISTB(NTB)=JS
      ENDIF
   ENDDO PREP
   WRITE(IOUNOUT,*) ' (NTA,NTB) =',NTA,NTB
   ALLOCATE(ZDISTS(NTA,NTB))
   DO JS=1,NTB
     DO JS2=1,NTA
        CALL GCDIST(SLA%LAT(NLISTA(JS2)),SLA%LON(NLISTA(JS2)),&
        & SLA%LAT(NLISTB(JS)),SLA%LON(NLISTB(JS)),ZDISTS(JS2,JS))
     ENDDO
   ENDDO
   OUTER2 : DO JS=1,NTB
      ZDC1=GRD%DISTC(SLA%IB(NLISTB(JS),1) , SLA%JB(NLISTB(JS),1) )
      ZTEMP1=MINVAL(ZDISTS(:,JS),DIM=1)
      IF(ZDC1.LT.150._R8) CYCLE OUTER2
      IF(ZTEMP1.LT.ZMAXDIST) THEN
        MYOBS=MINLOC(ZDISTS(:,JS),DIM=1)
        IF(ABS(SLA%TIM(NLISTB(JS))-SLA%TIM(NLISTA(MYOBS))).GT.ZMINTIM) &
        & CYCLE OUTER2
        MYDIFF1=SLA%VAL(NLISTB(JS))-SLA%VAL(NLISTA(MYOBS))
        INNER2 : DO JS2=JS,NTB
          ZDC2=GRD%DISTC(SLA%IB(NLISTB(JS2),1) , SLA%JB(NLISTB(JS2),1) )
          IF(ZDC2.LT.150._R8) CYCLE INNER2
          ZTEMP2=MINVAL(ZDISTS(:,JS2),DIM=1)
          CALL GCDIST(SLA%LAT(NLISTB(JS2)),SLA%LON(NLISTB(JS2)),&
          & SLA%LAT(NLISTB(JS)),SLA%LON(NLISTB(JS)),ZTEMP3)
          IF(ABS(ZTEMPOLD-ZTEMP3).GT.2000._R8.AND.JS2.GT.1) CYCLE OUTER2
          IF(ZTEMP3.GT.30000._R8) CYCLE OUTER2
          IF(ZTEMP2.LT.ZMAXDIST .AND.ZTEMP3.GT.ZMINDIST) THEN
            MYOBS2=MINLOC(ZDISTS(:,JS2),DIM=1)
            IF(ABS(SLA%TIM(NLISTB(JS2))-SLA%TIM(NLISTA(MYOBS2))).GT.ZMINTIM) &
            & CYCLE INNER2
            WRITE(266,*) SLA%TIM(NLISTB(JS)),SLA%TIM(NLISTA(MYOBS)),&
            & SLA%TIM(NLISTB(JS2)),SLA%TIM(NLISTA(MYOBS2))
            MYDIFF2=SLA%VAL(NLISTB(JS2))-SLA%VAL(NLISTA(MYOBS2))
            WRITE(265,*) SLA%LON(NLISTB(JS)),SLA%LAT(NLISTB(JS)),&
            & SLA%LON(NLISTB(JS2)),SLA%LAT(NLISTB(JS2)),&
            & SLA%IB(NLISTB(JS),1),SLA%JB(NLISTB(JS),1),&
            & SLA%IB(NLISTB(JS2),1),SLA%JB(NLISTB(JS2),1),&
            & ZTEMP1,ZTEMP2,ZTEMP3,MYDIFF1,MYDIFF2
            KCOUNT=KCOUNT+1
          ENDIF
          ZTEMPOLD=ZTEMP3
        ENDDO INNER2
      ENDIF
   ENDDO OUTER2
   DEALLOCATE(ZDISTS)
   WRITE(*,*) ' DATA FOUND =',KCOUNT
   KCOUNTTOT=KCOUNTTOT+KCOUNT
ENDDO

WRITE(*,*) ' TOTAL DATA FOUND =',KCOUNTTOT

RETURN
END SUBROUTINE SLAATCORR
