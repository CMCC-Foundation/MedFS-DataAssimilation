SUBROUTINE SRFBAL_TL(RDT,TEMB,TEM_TL,CE,CH,BLH,Q10,WND,T_AML,Q_AML)

USE SET_KND
USE GRD_STR

IMPLICIT NONE

REAL(R8), INTENT(IN)  :: RDT
REAL(R8), DIMENSION(GRD%IM,GRD%JM), INTENT(IN)  :: TEMB, TEM_TL
REAL(R8), DIMENSION(GRD%IM,GRD%JM), INTENT(IN)  :: CE, CH, BLH, Q10, WND
REAL(R8), DIMENSION(GRD%IM,GRD%JM), INTENT(INOUT) :: T_AML, Q_AML

REAL(R8), ALLOCATABLE, DIMENSION(:,:) :: ZQLW_TL, ZQSATW, ZQSATW_TL
REAL(R8), ALLOCATABLE, DIMENSION(:,:) :: ZEVAP_TL, ZAUX
REAL(R8), ALLOCATABLE, DIMENSION(:,:) :: ZQSB_TL, ZQLA_TL, TEM0

REAL(R8), PARAMETER :: STEF =5.67E-8_R8
REAL(R8), PARAMETER :: RHOA =    1.22_R8
REAL(R8), PARAMETER :: CPA  = 1000.5_R8
REAL(R8), PARAMETER :: LV   =    2.5E6_R8
REAL(R8), PARAMETER :: RT0  =  273.15_R8
REAL(R8), PARAMETER :: ZCOEF_QSATW = 0.98_R8 * 640380._R8 / RHOA

ALLOCATE( ZQLW_TL(GRD%IM,GRD%JM) )
ALLOCATE( ZQSATW (GRD%IM,GRD%JM) )
ALLOCATE( ZQSATW_TL (GRD%IM,GRD%JM) )
ALLOCATE( ZEVAP_TL (GRD%IM,GRD%JM) )
ALLOCATE( ZAUX   (GRD%IM,GRD%JM) )
ALLOCATE( ZQSB_TL(GRD%IM,GRD%JM) )
ALLOCATE( ZQLA_TL(GRD%IM,GRD%JM) )
ALLOCATE( TEM0   (GRD%IM,GRD%JM) )

TEM0 = TEMB + RT0

! UPWELLING LONGWAVE
ZQLW_TL(:,:) = (4._R8*STEF*TEM0(:,:)*TEM0(:,:)*TEM0(:,:)*TEM_TL(:,:))*GRD%MSK(:,:,1)

!EVAPORATION
ZQSATW(:,:) = ZCOEF_QSATW*EXP(-5107.4_R8/TEM0(:,:))

ZQSATW_TL(:,:) = ZCOEF_QSATW*5107.4_R8*TEM_TL(:,:)*EXP(-5107.4_R8/TEM0(:,:))/TEM0(:,:)**2
ZEVAP_TL = 0._R8
ZAUX   = RHOA  *CE(:,:)* ( ZQSATW(:,:) - Q10(:,:) ) * WND(:,:)
WHERE ( ZAUX .GT. 0._R8 ) ZEVAP_TL = RHOA *CE(:,:) *  ZQSATW_TL(:,:) * WND(:,:)

! SENSIBLE HEAT
ZQSB_TL (:,:) =  RHOA*CPA*CH(:,:)*TEM_TL(:,:) * WND(:,:)

! LATENT HEAT
ZQLA_TL (:,:) = LV * ZEVAP_TL(:,:)

! 2M PARAMETERS
T_AML   =  T_AML + ( RDT * (ZQLW_TL + ZQSB_TL) / (RHOA*CPA*BLH) ) * GRD%MSK(:,:,1)
Q_AML   =  Q_AML + ( RDT * ZEVAP_TL / (RHOA*BLH) ) * GRD%MSK(:,:,1)

DEALLOCATE( ZQLW_TL, ZQSATW, ZQSATW_TL, ZEVAP_TL, ZAUX, ZQSB_TL, ZQLA_TL, TEM0)

END SUBROUTINE SRFBAL_TL
