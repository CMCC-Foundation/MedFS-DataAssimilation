SUBROUTINE DEFEXTGRDSRZ
USE RECFILTER
USE MYFRTPROF

!-- DEFINE EXTENDED GRID

USE GRD_STR

IMPLICIT  NONE

INTEGER(KIND=I4) :: NX,NY,NZ,IERR
INTEGER(KIND=I4) :: I,J,K,KK,K2,JLEV
REAL(KIND=R8), ALLOCATABLE, DIMENSION(:,:) :: &
& CRXZ,CRYZ

CALL MYFRTPROF_WALL('DEFEXTGRDSRZ: DEFINE EXTENDED GRID',0)

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// DEFINE EXTENDED GRID'
WRITE(IOUNLOG,*)

#ifndef USE_POINTERS
IF( .NOT.ALLOCATED(GRD%DX).OR..NOT.ALLOCATED(GRD%DY).OR.&
  & .NOT.ALLOCATED(GRD%MSR)) THEN
   WRITE(IOUNERR,*) 'DEFEXTGRD CALLED BEFORE GRID INITIALIZATION'
   CALL ABOR1('ERROR IN DEFEXTGRD, CANNOT PROCEED')
ENDIF
#endif

IF(.NOT.LLRFINIT) &
& CALL ABOR1('DEFEXTGRDSR CALLED BEFORE RF INITIALIZATION')

NX=GRD%IM
NY=GRD%JM

ALLOCATE(ISTPNSZ(NX,NY),JSTPNSZ(NX,NY),STAT=IERR)
IF(IERR.NE.0) THEN
   WRITE(IOUNERR,*) 'CANNOT ALLOCATE ISTPNSZ,JSTPNSZ'
   CALL ABOR1('DEFEXTGRDSRZ: ALLOCATION PROBLEMS')
ENDIF
ALLOCATE(INXZ(NX,NY),JNXZ(NX,NY),STAT=IERR)
IF(IERR.NE.0) THEN
   WRITE(IOUNERR,*) 'CANNOT ALLOCATE INXZ,JNXZ'
   CALL ABOR1('DEFEXTGRDSRZ: ALLOCATION PROBLEMS')
ENDIF

WRITE(IOUNLOG,*) 'VECTORS HAVE BEEN ALLOCATED' ; CALL FLUSH(IOUNLOG)

!... READ CR AND SET SAFETY DISTANCE

ALLOCATE( CRXZ(NX,NY),CRYZ(NX,NY) )
CALL READ_CRZ(NX,NY,CRXZ,CRYZ)

IMAXZ   = 0
JMAXZ   = 0

     ISTPNSZ(:,:) = INT(CRXZ(:,:)*RF_EFC / NEWDX(:,:) )+1
     JSTPNSZ(:,:) = INT(CRYZ(:,:)*RF_EFC / NEWDY(:,:) )+1

   IMXZ = 0
   DO J=1,NY
      KK=ISTPNSZ(1,J)
      IF( LLMSR2(1,J) ) KK=KK+1
      INXZ(1,J) = KK
      DO I=2,NX
         IF(.NOT.LLMSR2(I,J).AND.LLMSR2(I-1,J)) THEN
            KK=KK+ISTPNSZ(I-1,J)
         ELSEIF(LLMSR2(I,J).AND..NOT.LLMSR2(I-1,J))THEN
            KK=KK+ISTPNSZ(I,J)+1
         ELSEIF(LLMSR2(I,J)) THEN
            KK=KK+1
         ENDIF
         INXZ(I,J) = KK
      ENDDO
      IMXZ = MAX( IMXZ, KK+ISTPNSZ(NX,J))
   ENDDO

   JMXZ = 0
   DO I=1,NX
      KK=JSTPNSZ(I,1)
      IF( LLMSR2(I,1) ) KK=KK+1
      JNXZ(I,1) = KK
      DO J=2,NY
         IF(.NOT.LLMSR2(I,J).AND.LLMSR2(I,J-1)) THEN
            KK=KK+JSTPNSZ(I,J-1)
         ELSEIF(LLMSR2(I,J).AND..NOT.LLMSR2(I,J-1))THEN
            KK=KK+JSTPNSZ(I,J)+1
         ELSEIF(LLMSR2(I,J)) THEN
            KK=KK+1
         ENDIF
         JNXZ(I,J) = KK
      ENDDO
      JMXZ = MAX( JMXZ, KK+JSTPNSZ(I,NY))
   ENDDO

IMAXZ = IMXZ
JMAXZ = JMXZ

WRITE(IOUNLOG,*) 'IMAXZ,JMAXZ = ',IMAXZ,JMAXZ

DEALLOCATE(CRXZ,CRYZ)
CALL FLUSH(IOUNLOG)
CALL MYFRTPROF_WALL('DEFEXTGRDSRZ: DEFINE EXTENDED GRID',1)
END SUBROUTINE DEFEXTGRDSRZ
