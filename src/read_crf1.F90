SUBROUTINE READ_CRF1
USE RECFILTER

!-- READ GLOBAL CR

IMPLICIT NONE

LOGICAL :: LLEXIST
CHARACTER(LEN=30) CFILE,CTT
INTEGER(KIND=I4) :: IIUN,IAUX,IERR,JTOT

IF(.NOT.LLRFINIT) &
& CALL ABOR1('READ_CRF1 CALLED BEFORE RF INITIALIZATION')

WRITE(IOUNLOG,*)
WRITE(IOUNLOG,*) ' /// READING VERTICAL CRF '
WRITE(IOUNLOG,*)

IF(LLVVCR .AND. LLTSAPART) THEN
      CFILE='vert_cr_ts.dat'
      CTT='CR FOR T AND S BY LEVELS'
ELSEIF(LLVVCR .AND. .NOT. LLTSAPART) THEN
      CFILE='vert_cr.dat'
      CTT='CR BY LEVELS'
ELSEIF(.NOT.LLVVCR .AND. LLTSAPART) THEN
      CFILE='cr_ts.dat'
      CTT='CR FOR T AND S'
ENDIF

IIUN=74

IF(LLVVCR.OR.LLTSAPART) THEN
  OPEN(IIUN,FILE=CFILE,STATUS='OLD',IOSTAT=IERR)
  IF(IERR.NE.0) THEN
    WRITE(IOUNERR,*) 'CANNOT OPEN '//TRIM(CFILE)//' NEEDED BY READ_CRF1'
    CALL ABOR1('READ_CRF1 ERROR OPENING FILE')
  ENDIF
  WRITE(IOUNLOG,*) TRIM(CTT)
  DO JTOT=1,NTOTV
    READ(IIUN,*,IOSTAT=IERR) IAUX, ZGA_CRAD(JTOT)
    IF(IERR.NE.0) THEN
       WRITE(IOUNERR,*) 'CANNOT READ '//TRIM(CFILE)//' IN READ_CRF1'
       CALL ABOR1('READ_CRF1 ERROR READING FILE')
    ENDIF
  ENDDO
  CLOSE(IIUN)
ELSE
  ZGA_CRAD(:) = RF_L
ENDIF

ZGA_CRAD = ZGA_CRAD*RF_CORRAD_MFACT

DO JTOT=1,NTOTV
    WRITE(IOUNLOG,*) 'CR FOR RF LEVEL',JTOT,' =',ZGA_CRAD(JTOT)
ENDDO

RETURN
END SUBROUTINE READ_CRF1
