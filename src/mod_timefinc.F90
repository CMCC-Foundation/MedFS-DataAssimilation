MODULE TIMEFINC

CONTAINS

!......

FUNCTION CHECK_TIME(IDATE,ICONF) RESULT(IRET)

IMPLICIT NONE

INTEGER,INTENT(IN) :: IDATE(6),ICONF
INTEGER :: IRET
CHARACTER(LEN=120) CCONF,CSTR
CCONF=' '
CSTR=' '
IF(ICONF.EQ.1) CCONF='DIFFTIME'
IF(ICONF.EQ.2) CCONF='ADDTIME'
CSTR='*** FROM CHECKTIME CALLED BY '//TRIM(CCONF)
IF(ICONF.EQ.3) CSTR='        '
IRET=0

IF(IDATE(1) .LT. 1900 .OR. IDATE(1) .GT. 2100 ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'YEAR OUT OF THE RANGE (1900 TO 2100): ',IDATE(1)
        IRET=1
ELSEIF(IDATE(2) .LT. 1 .OR. IDATE(2) .GT. 12 ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'MONTH OUT OF THE RANGE: ',IDATE(2)
        IRET=2
ELSEIF(IDATE(3) .LT. 1 .OR. OUTOFMONTH(IDATE(1),IDATE(2),IDATE(3)) ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'DAY OUT OF THE RANGE: ',IDATE(3)
        IRET=3
ELSEIF(IDATE(4) .LT. 0 .OR. IDATE(4) .GT. 23 ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'HOURS OUT OF THE RANGE: ',IDATE(4)
        IRET=4
ELSEIF(IDATE(5) .LT. 0 .OR. IDATE(5) .GT. 59 ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'MINUTES OUT OF THE RANGE: ',IDATE(5)
        IRET=5
ELSEIF(IDATE(6) .LT. 0 .OR. IDATE(6) .GT. 59 ) THEN
        WRITE(0,*) TRIM(CSTR)
        WRITE(0,*) 'SECONDS OUT OF THE RANGE: ',IDATE(6)
        IRET=6
ENDIF

END FUNCTION CHECK_TIME

!............

SUBROUTINE WRLNG(LNOEX,ICONF,IRETCD)
IMPLICIT NONE
LOGICAL,INTENT(IN)::LNOEX
INTEGER,INTENT(IN)::ICONF
INTEGER,INTENT(OUT)::IRETCD
CHARACTER(LEN=120) CCONF
CCONF=' '
IF(ICONF.EQ.1) CCONF='DIFFTIME'
IF(ICONF.EQ.2) CCONF='ADDTIME'
WRITE(0,*) '*** FROM WRLNG CALLED BY '//TRIM(CCONF)
WRITE(0,*) 'WRONG LENGTH OF DATES'
IRETCD=-2
IF(.NOT.LNOEX) STOP
END SUBROUTINE WRLNG

!............

SUBROUTINE MINUSDAY(DATE)

IMPLICIT NONE

INTEGER,INTENT(INOUT) :: DATE
CHARACTER(LEN=8) :: CSTRING
INTEGER :: YY,MM,DD

WRITE(CSTRING,'(I8.8)') DATE
READ(CSTRING(1:4),*) YY
READ(CSTRING(5:6),*) MM
READ(CSTRING(7:8),*) DD

DD=DD-1

IF (DD.EQ.0) THEN

   MM=MM-1
   IF (MM.EQ.0) THEN
      MM=12
      YY=YY-1
   END IF

   DD=31

   DO WHILE (OUTOFMONTH(YY,MM,DD))

      DD=DD-1

   END DO

END IF

DATE=DD+MM*100+YY*10000

END SUBROUTINE MINUSDAY

!............

SUBROUTINE PLUSDAY(DATE)

IMPLICIT NONE

INTEGER, INTENT(INOUT) :: DATE
CHARACTER(LEN=8) :: CSTRING
INTEGER :: YY,MM,DD

WRITE(CSTRING,'(I8.8)') DATE
READ(CSTRING(1:4),*) YY
READ(CSTRING(5:6),*) MM
READ(CSTRING(7:8),*) DD

DD=DD+1

IF(OUTOFMONTH(YY,MM,DD)) THEN

  DD=1
  MM=MM+1
  IF (MM .GT.12 ) THEN
     MM=1
     YY=YY+1
  END IF

END IF

DATE=DD+MM*100+YY*10000

END SUBROUTINE PLUSDAY

!............

FUNCTION OUTOFMONTH(YEAR,MONTH,DAY)

IMPLICIT NONE

LOGICAL :: OUTOFMONTH
LOGICAL :: LLEAP
INTEGER :: YEAR,MONTH,DAY
INTEGER :: MDD1(12),MDD2(12)
DATA MDD1/31,28,31,30,31,30,31,31,30,31,30,31/
DATA MDD2/31,29,31,30,31,30,31,31,30,31,30,31/

OUTOFMONTH=.FALSE.
LLEAP=.FALSE.
IF (MOD(YEAR,4).EQ.0) THEN
        LLEAP=.TRUE.
        IF (MOD(YEAR,100).EQ.0.AND.MOD(YEAR,400).NE.0) LLEAP=.FALSE.
END IF

IF (.NOT.LLEAP) THEN
        IF (DAY.GT.MDD1(MONTH)) OUTOFMONTH=.TRUE.
ELSE
        IF (DAY.GT.MDD2(MONTH)) OUTOFMONTH=.TRUE.
END IF

END FUNCTION OUTOFMONTH

!............

END MODULE TIMEFINC
